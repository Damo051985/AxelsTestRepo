OBJECT Codeunit 12 Gen. Jnl.-Post Line
{
  OBJECT-PROPERTIES
  {
    Date=12/11/19;
    Time=10:14:04 AM;
    Modified=Yes;
    Version List=NAVW111.00.00.34561,NAVAPAC11.00.00.23572LPC-BIR,CWT,JOPS-TODO;
  }
  PROPERTIES
  {
    TableNo=81;
    Permissions=TableData 15=r,
                TableData 17=rimd,
                TableData 21=imd,
                TableData 25=imd,
                TableData 45=imd,
                TableData 253=rimd,
                TableData 254=imd,
                TableData 271=imd,
                TableData 272=imd,
                TableData 379=imd,
                TableData 380=imd,
                TableData 1053=rim,
                TableData 5222=imd,
                TableData 5223=imd,
                TableData 5601=rimd,
                TableData 5617=imd,
                TableData 5625=rimd;
    OnRun=BEGIN
            GetGLSetup;
            RunWithCheck(Rec);
          END;

  }
  CODE
  {
    VAR
      NeedsRoundingErr@1000 : TextConst 'ENU=%1 needs to be rounded';
      PurchaseAlreadyExistsErr@1003 : TextConst '@@@="%1 = Document Type; %2 = Document No.";ENU=Purchase %1 %2 already exists for this vendor.';
      BankPaymentTypeMustNotBeFilledErr@1004 : TextConst 'ENU=Bank Payment Type must not be filled if Currency Code is different in Gen. Journal Line and Bank Account.';
      DocNoMustBeEnteredErr@1005 : TextConst 'ENU=Document No. must be entered when Bank Payment Type is %1.';
      CheckAlreadyExistsErr@1006 : TextConst 'ENU=Check %1 already exists for this Bank Account.';
      GLSetup@1009 : Record 98;
      GlobalGLEntry@1014 : Record 17;
      TempGLEntryBuf@1010 : TEMPORARY Record 17;
      TempGLEntryVAT@1016 : TEMPORARY Record 17;
      GenJnlLine1@1106000000 : Record 81;
      GenJnlLine2@1106000001 : Record 81;
      VendLedgEntry@1500001 : Record 25;
      GLReg@1029 : Record 45;
      AddCurrency@1033 : Record 4;
      CurrExchRate@1035 : Record 330;
      VATEntry@1038 : Record 254;
      TaxDetail@1046 : Record 322;
      UnrealizedCustLedgEntry@1084 : Record 21;
      UnrealizedVendLedgEntry@1085 : Record 25;
      GLEntryVATEntryLink@1087 : Record 253;
      TempVATEntry@1088 : TEMPORARY Record 254;
      SourceCode@1240003 : Record 230;
      GenJnlCheckLine@1001 : Codeunit 11;
      PaymentToleranceMgt@1002 : Codeunit 426;
      WHTManagement@1240002 : Codeunit 50000;
      DeferralUtilities@1031 : Codeunit 1720;
      DeferralDocType@1039 : 'Purchase,Sales,G/L';
      gTextdan : Text //Damo
      LastDocType@1025 : ' ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder';
      AddCurrencyCode@1117 : Code[10];
      GLSourceCode@1040 : Code[10];
      LastDocNo@1023 : Code[20];
      FiscalYearStartDate@1011 : Date;
      CurrencyDate@1020 : Date;
      LastDate@1021 : Date;
      BalanceCheckAmount@1056 : Decimal;
      BalanceCheckAmount2@1057 : Decimal;
      BalanceCheckAddCurrAmount@1058 : Decimal;
      BalanceCheckAddCurrAmount2@1059 : Decimal;
      CurrentBalance@1060 : Decimal;
      TotalAddCurrAmount@1062 : Decimal;
      TotalAmount@1063 : Decimal;
      UnrealizedRemainingAmountCust@1086 : Decimal;
      UnrealizedRemainingAmountVend@1074 : Decimal;
      AmountRoundingPrecision@1012 : Decimal;
      AddCurrGLEntryVATAmt@1017 : Decimal;
      CurrencyFactor@1019 : Decimal;
      FirstEntryNo@1042 : Integer;
      NextEntryNo@1022 : Integer;
      NextVATEntryNo@1064 : Integer;
      FirstNewVATEntryNo@1065 : Integer;
      FirstTransactionNo@1089 : Integer;
      NextTransactionNo@1066 : Integer;
      NextConnectionNo@1067 : Integer;
      NextCheckEntryNo@1028 : Integer;
      InsertedTempGLEntryVAT@1027 : Integer;
      GLEntryNo@1026 : Integer;
      UseCurrFactorOnly@1078 : Boolean;
      NonAddCurrCodeOccured@1079 : Boolean;
      FADimAlreadyChecked@1080 : Boolean;
      ResidualRoundingErr@1008 : TextConst 'ENU=Residual caused by rounding of %1';
      DimensionUsedErr@1007 : TextConst '@@@=Comment;ENU=A dimension used in %1 %2, %3, %4 has caused an error. %5.';
      OverrideDimErr@1018 : Boolean;
      JobLine@1036 : Boolean;
      CheckUnrealizedCust@1082 : Boolean;
      CheckUnrealizedVend@1083 : Boolean;
      GLSetupRead@1015 : Boolean;
      TempWHTEntry@1500010 : Record 50004;
      WHTEntry@1500009 : Record 50004;
      NextWHTEntryNo@1500015 : Integer;
      CurrFactor@1500026 : Decimal;
      KeepWHTEntryNo@1500025 : Integer;
      HadWHTEntryNo@1500022 : Boolean;
      NextNo@1500019 : Integer;
      UseVendExchRate@1500027 : Boolean;
      EntryType@1500029 : Option;
      Journal@1500030 : Record 230;
      Text28000@1500031 : TextConst 'ENU=No Matching Document';
      CheckRem@1500032 : Boolean;
      IsReversal@1500039 : Boolean;
      Text016@1500034 : TextConst '@@@=%1 : WHT Realized Type;ENU=Cannot post the payment journal because one or more journal lines must be applied to an invoice line when the WHT Realized Type %1';
      InvalidPostingDateErr@1034 : TextConst '@@@="%1=The date passed in for the posting date.";ENU=%1 is not within the range of posting dates for your company.';
      DescriptionMustNotBeBlankErr@1030 : TextConst '@@@=%1: Field Omit Default Descr. in Jnl., %2 G/L Account No, %3 Description;ENU=When %1 is selected for %2, %3 must have a value.';
      NoDeferralScheduleErr@1037 : TextConst '@@@="%1=The line number of the general ledger transaction, %2=The Deferral Template Code";ENU=You must create a deferral schedule if a deferral template is selected. Line: %1, Deferral Template: %2.';
      ZeroDeferralAmtErr@1041 : TextConst '@@@="%1=The line number of the general ledger transaction, %2=The Deferral Template Code";ENU=Deferral amounts cannot be 0. Line: %1, Deferral Template: %2.';
      IsGLRegInserted@1013 : Boolean;

    [External]
    PROCEDURE GetGLReg@10(VAR NewGLReg@1000 : Record 45);
    BEGIN
      NewGLReg := GLReg;
    END;

    [External]
    PROCEDURE RunWithCheck@45(VAR GenJnlLine2@1000 : Record 81) : Integer;
    VAR
      GenJnlLine@1001 : Record 81;
    BEGIN
      GenJnlLine.COPY(GenJnlLine2);
      Code(GenJnlLine,TRUE);
      OnAfterRunWithCheck(GenJnlLine);
      GenJnlLine2 := GenJnlLine;
      EXIT(GLEntryNo);
    END;

    [Internal]
    PROCEDURE RunWithoutCheck@21(VAR GenJnlLine2@1000 : Record 81) : Integer;
    VAR
      GenJnlLine@1001 : Record 81;
    BEGIN
      GenJnlLine.COPY(GenJnlLine2);
      Code(GenJnlLine,FALSE);
      OnAfterRunWithoutCheck(GenJnlLine);
      GenJnlLine2 := GenJnlLine;
      EXIT(GLEntryNo);
    END;

    LOCAL PROCEDURE Code@9(VAR GenJnlLine@1003 : Record 81;CheckLine@1000 : Boolean);
    VAR
      Balancing@1002 : Boolean;
      IsTransactionConsistent@1001 : Boolean;
    BEGIN
      OnBeforeCode(GenJnlLine,CheckLine);

      GetGLSourceCode;

      WITH GenJnlLine DO BEGIN
        IF EmptyLine THEN BEGIN
          InitLastDocDate(GenJnlLine);
          EXIT;
        END;

        SourceCode.GET("Source Code");
        IF SourceCode.Simulation THEN
          EntryType := "Entry Type"::Simulation
        ELSE
          EntryType := "Entry Type"::Definitive;

        IF CheckLine THEN BEGIN
          IF OverrideDimErr THEN
            GenJnlCheckLine.SetOverDimErr;
          GenJnlCheckLine.RunCheck(GenJnlLine);
        END;

        AmountRoundingPrecision := InitAmounts(GenJnlLine);

        IF "Bill-to/Pay-to No." = '' THEN
          CASE TRUE OF
            "Account Type" IN ["Account Type"::Customer,"Account Type"::Vendor]:
              "Bill-to/Pay-to No." := "Account No.";
            "Bal. Account Type" IN ["Bal. Account Type"::Customer,"Bal. Account Type"::Vendor]:
              "Bill-to/Pay-to No." := "Bal. Account No.";
          END;
        IF "Document Date" = 0D THEN
          "Document Date" := "Posting Date";
        IF "Due Date" = 0D THEN
          "Due Date" := "Posting Date";

        JobLine := ("Job No." <> '');

        IF NextEntryNo = 0 THEN
          StartPosting(GenJnlLine)
        ELSE
          ContinuePosting(GenJnlLine);

        IF "Account No." <> '' THEN BEGIN
          IF ("Bal. Account No." <> '') AND
             (NOT "System-Created Entry") AND
             ("Account Type" IN
              ["Account Type"::Customer,
               "Account Type"::Vendor,
               "Account Type"::Employee, //JOPS
               "Account Type"::"Fixed Asset"])
          THEN BEGIN
            CODEUNIT.RUN(CODEUNIT::"Exchange Acc. G/L Journal Line",GenJnlLine);
            Balancing := TRUE;
          END;

          PostGenJnlLine(GenJnlLine,Balancing);
        END;

        IF "Bal. Account No." <> '' THEN BEGIN
          CODEUNIT.RUN(CODEUNIT::"Exchange Acc. G/L Journal Line",GenJnlLine);
          PostGenJnlLine(GenJnlLine,NOT Balancing);
        END;

        CheckPostUnrealizedVAT(GenJnlLine,TRUE);

        CreateDeferralScheduleFromGL(GenJnlLine,Balancing);

        IsTransactionConsistent := FinishPosting;
      END;

      OnAfterGLFinishPosting(GlobalGLEntry,GenJnlLine,IsTransactionConsistent,FirstTransactionNo);
    END;

    LOCAL PROCEDURE PostGenJnlLine@47(VAR GenJnlLine@1000 : Record 81;Balancing@1001 : Boolean);
    BEGIN
      OnBeforePostGenJnlLine(GenJnlLine);

      WITH GenJnlLine DO
        CASE "Account Type" OF
          "Account Type"::"G/L Account":
            PostGLAcc(GenJnlLine,Balancing);
          "Account Type"::Customer:
            IF "Entry Type" = "Entry Type"::Definitive THEN
              PostCust(GenJnlLine,Balancing);
          "Account Type"::Vendor:
            IF "Entry Type" = "Entry Type"::Definitive THEN
              PostVend(GenJnlLine,Balancing);
          "Account Type"::Employee:
            PostEmployee(GenJnlLine);
          "Account Type"::"Bank Account":
            IF "Entry Type" = "Entry Type"::Definitive THEN
              PostBankAcc(GenJnlLine,Balancing);
          "Account Type"::"Fixed Asset":
            IF "Entry Type" = "Entry Type"::Definitive THEN
              PostFixedAsset(GenJnlLine);
          "Account Type"::"IC Partner":
            PostICPartner(GenJnlLine);
        END;
    END;

    LOCAL PROCEDURE InitAmounts@186(VAR GenJnlLine@1000 : Record 81) : Decimal;
    VAR
      Currency@1001 : Record 4;
    BEGIN
      WITH GenJnlLine DO BEGIN
        IF "Currency Code" = '' THEN BEGIN
          Currency.InitRoundingPrecision;
          "Amount (LCY)" := Amount;
          "VAT Amount (LCY)" := "VAT Amount";
          "VAT Base Amount (LCY)" := "VAT Base Amount";
        END ELSE BEGIN
          Currency.GET("Currency Code");
          Currency.TESTFIELD("Amount Rounding Precision");
          IF NOT "System-Created Entry" THEN BEGIN
            "Source Currency Code" := "Currency Code";
            "Source Currency Amount" := Amount;
            "Source Curr. VAT Base Amount" := "VAT Base Amount";
            "Source Curr. VAT Amount" := "VAT Amount";
          END;
        END;
        IF "Additional-Currency Posting" = "Additional-Currency Posting"::None THEN BEGIN
          IF Amount <> ROUND(Amount,Currency."Amount Rounding Precision") THEN
            FIELDERROR(
              Amount,
              STRSUBSTNO(NeedsRoundingErr,Amount));
          IF "Amount (LCY)" <> ROUND("Amount (LCY)") THEN
            FIELDERROR(
              "Amount (LCY)",
              STRSUBSTNO(NeedsRoundingErr,"Amount (LCY)"));
        END;
        EXIT(Currency."Amount Rounding Precision");
      END;
    END;

    LOCAL PROCEDURE InitLastDocDate@23(GenJnlLine@1000 : Record 81);
    BEGIN
      WITH GenJnlLine DO BEGIN
        LastDocType := "Document Type";
        LastDocNo := "Document No.";
        LastDate := "Posting Date";
      END;
    END;

    LOCAL PROCEDURE InitNextEntryNo@230();
    VAR
      GLEntry@1000 : Record 17 SECURITYFILTERING(Ignored);
    BEGIN
      GLEntry.LOCKTABLE;
      IF EntryType = GLEntry."Entry Type"::Definitive THEN BEGIN
        IF GLEntry.FINDLAST THEN BEGIN
          NextEntryNo := GLEntry."Entry No." + 1;
          NextTransactionNo := GLEntry."Transaction No." + 1;
        END ELSE BEGIN
          NextEntryNo := 1;
          NextTransactionNo := 1;
        END;
        FirstTransactionNo := NextTransactionNo
      END ELSE
        IF GLEntry.FINDFIRST THEN BEGIN
          NextEntryNo := GLEntry."Entry No." - 1;
          IF NextEntryNo = 0 THEN
            NextEntryNo := -1;
          NextTransactionNo := GLEntry."Transaction No." - 1;
        END ELSE BEGIN
          NextEntryNo := -1;
          NextTransactionNo := -1;
        END;
    END;

    LOCAL PROCEDURE InitVAT@33(VAR GenJnlLine@1001 : Record 81;VAR GLEntry@1002 : Record 17;VAR VATPostingSetup@1003 : Record 325);
    VAR
      LCYCurrency@1000 : Record 4;
      SalesTaxCalculate@1004 : Codeunit 398;
    BEGIN
      LCYCurrency.InitRoundingPrecision;
      WITH GenJnlLine DO
        IF ("Gen. Posting Type" <> 0)
        THEN BEGIN
          VATPostingSetup.GET("VAT Bus. Posting Group","VAT Prod. Posting Group");
          TESTFIELD("VAT Calculation Type",VATPostingSetup."VAT Calculation Type");
          CASE "VAT Posting" OF
            "VAT Posting"::"Automatic VAT Entry":
              BEGIN
                GLEntry.CopyPostingGroupsFromGenJnlLine(GenJnlLine);
                CASE "VAT Calculation Type" OF
                  "VAT Calculation Type"::"Normal VAT":
                    IF ("VAT Difference" <> 0) OR ("VAT Difference (ACY)" <> 0) THEN BEGIN
                      GLEntry.Amount := "VAT Base Amount (LCY)";
                      GLEntry."VAT Amount" := "Amount (LCY)" - GLEntry.Amount;
                      IF "VAT Base (ACY)" = 0 THEN
                        GLEntry."Additional-Currency Amount" := "Source Curr. VAT Base Amount"
                      ELSE
                        GLEntry."Additional-Currency Amount" := "VAT Base (ACY)";
                      IF "VAT Base (ACY)" <> 0 THEN
                        AddCurrGLEntryVATAmt := "Amount Including VAT (ACY)" - "VAT Base (ACY)"
                      ELSE BEGIN
                        IF "Source Currency Code" = AddCurrencyCode THEN
                          AddCurrGLEntryVATAmt := "Source Curr. VAT Amount"
                        ELSE
                          AddCurrGLEntryVATAmt := CalcLCYToAddCurr(GLEntry."VAT Amount");
                      END;
                    END ELSE BEGIN
                      GLEntry."VAT Amount" :=
                        ROUND(
                          "Amount (LCY)" * VATPostingSetup."VAT %" / (100 + VATPostingSetup."VAT %"),
                          LCYCurrency."Amount Rounding Precision",LCYCurrency.VATRoundingDirection);
                      GLEntry.Amount := "Amount (LCY)" - GLEntry."VAT Amount";
                      IF "VAT Base (ACY)" = 0 THEN
                        GLEntry."Additional-Currency Amount" :=
                          ROUND(
                            "Source Currency Amount" / (1 + VATPostingSetup."VAT %" / 100),
                            AddCurrency."Amount Rounding Precision")
                      ELSE
                        GLEntry."Additional-Currency Amount" := "VAT Base (ACY)";
                      IF "VAT Base (ACY)" <> 0 THEN
                        AddCurrGLEntryVATAmt := "Amount Including VAT (ACY)" - "VAT Base (ACY)"
                      ELSE BEGIN
                        IF "Source Currency Code" = AddCurrencyCode THEN
                          AddCurrGLEntryVATAmt :=
                            ROUND(
                              "Source Currency Amount" * VATPostingSetup."VAT %" / (100 + VATPostingSetup."VAT %"),
                              AddCurrency."Amount Rounding Precision",AddCurrency.VATRoundingDirection)
                        ELSE
                          AddCurrGLEntryVATAmt := CalcLCYToAddCurr(GLEntry."VAT Amount");
                        GLEntry."Additional-Currency Amount" := "Source Currency Amount" - AddCurrGLEntryVATAmt;
                      END;
                    END;
                  "VAT Calculation Type"::"Reverse Charge VAT":
                    CASE "Gen. Posting Type" OF
                      "Gen. Posting Type"::Purchase:
                        IF "VAT Difference" <> 0 THEN BEGIN
                          GLEntry."VAT Amount" := "VAT Amount (LCY)";
                          IF "Source Currency Code" = AddCurrencyCode THEN
                            AddCurrGLEntryVATAmt := "Source Curr. VAT Amount"
                          ELSE
                            AddCurrGLEntryVATAmt := CalcLCYToAddCurr(GLEntry."VAT Amount");
                        END ELSE BEGIN
                          GLEntry."VAT Amount" :=
                            ROUND(
                              GLEntry.Amount * VATPostingSetup."VAT %" / 100,
                              LCYCurrency."Amount Rounding Precision",LCYCurrency.VATRoundingDirection);
                          IF "Source Currency Code" = AddCurrencyCode THEN
                            AddCurrGLEntryVATAmt :=
                              ROUND(
                                GLEntry."Additional-Currency Amount" * VATPostingSetup."VAT %" / 100,
                                AddCurrency."Amount Rounding Precision",AddCurrency.VATRoundingDirection)
                          ELSE
                            AddCurrGLEntryVATAmt := CalcLCYToAddCurr(GLEntry."VAT Amount");
                        END;
                      "Gen. Posting Type"::Sale:
                        BEGIN
                          GLEntry."VAT Amount" := 0;
                          AddCurrGLEntryVATAmt := 0;
                        END;
                    END;
                  "VAT Calculation Type"::"Full VAT":
                    BEGIN
                      CASE "Gen. Posting Type" OF
                        "Gen. Posting Type"::Sale:
                          TESTFIELD("Account No.",VATPostingSetup.GetSalesAccount(FALSE));
                        "Gen. Posting Type"::Purchase:
                          TESTFIELD("Account No.",VATPostingSetup.GetPurchAccount(FALSE));
                      END;
                      GLEntry.Amount := 0;
                      GLEntry."Additional-Currency Amount" := 0;
                      GLEntry."VAT Amount" := "Amount (LCY)";
                      IF "Source Currency Code" = AddCurrencyCode THEN
                        AddCurrGLEntryVATAmt := "Source Currency Amount"
                      ELSE
                        AddCurrGLEntryVATAmt := CalcLCYToAddCurr("Amount (LCY)");
                    END;
                  "VAT Calculation Type"::"Sales Tax":
                    BEGIN
                      IF ("Gen. Posting Type" = "Gen. Posting Type"::Purchase) AND
                         "Use Tax"
                      THEN BEGIN
                        GLEntry."VAT Amount" :=
                          ROUND(
                            SalesTaxCalculate.CalculateTax(
                              "Tax Area Code","Tax Group Code","Tax Liable",
                              "Posting Date","Amount (LCY)",Quantity,0));
                        GLEntry.Amount := "Amount (LCY)";
                      END ELSE BEGIN
                        GLEntry.Amount :=
                          ROUND(
                            SalesTaxCalculate.ReverseCalculateTax(
                              "Tax Area Code","Tax Group Code","Tax Liable",
                              "Posting Date","Amount (LCY)",Quantity,0));
                        GLEntry."VAT Amount" := "Amount (LCY)" - GLEntry.Amount;
                      END;
                      GLEntry."Additional-Currency Amount" := "Source Currency Amount";
                      IF "Source Currency Code" = AddCurrencyCode THEN
                        AddCurrGLEntryVATAmt := "Source Curr. VAT Amount"
                      ELSE
                        AddCurrGLEntryVATAmt := CalcLCYToAddCurr(GLEntry."VAT Amount");
                    END;
                END;
              END;
            "VAT Posting"::"Manual VAT Entry":
              IF "Gen. Posting Type" <> "Gen. Posting Type"::Settlement THEN BEGIN
                GLEntry.CopyPostingGroupsFromGenJnlLine(GenJnlLine);
                GLEntry."VAT Amount" := "VAT Amount (LCY)";
                IF ("Vendor Exchange Rate (ACY)" <> 0) OR
                   (("Vendor Exchange Rate (ACY)" = 1) AND
                    ("Source Currency Code" <> AddCurrencyCode))
                THEN BEGIN
                  GLEntry."Additional-Currency Amount" :=
                    "VAT Base (ACY)" + "Line Discount Amt. (ACY)" + "Inv. Discount Amt. (ACY)";
                  AddCurrGLEntryVATAmt := "Amount Including VAT (ACY)" - "VAT Base (ACY)";
                END ELSE BEGIN
                  IF "Source Currency Code" = AddCurrencyCode THEN
                    AddCurrGLEntryVATAmt := "Source Curr. VAT Amount"
                  ELSE
                    AddCurrGLEntryVATAmt := CalcLCYToAddCurr("VAT Amount (LCY)");
                END;
              END;
          END;

          UseVendExchRate := "VAT Base (ACY)" <> 0;
        END;
      GLEntry."Additional-Currency Amount" :=
        GLCalcAddCurrency(GLEntry.Amount,GLEntry."Additional-Currency Amount",GLEntry."Additional-Currency Amount",TRUE,GenJnlLine,UseVendExchRate);
    END;

    LOCAL PROCEDURE PostVAT@34(GenJnlLine@1010 : Record 81;VAR GLEntry@1015 : Record 17;VATPostingSetup@1012 : Record 325);
    VAR
      TaxDetail2@1008 : Record 322;
      SalesTaxCalculate@1013 : Codeunit 398;
      VATAmount@1000 : Decimal;
      VATAmount2@1003 : Decimal;
      VATBase@1001 : Decimal;
      VATBase2@1004 : Decimal;
      SrcCurrVATAmount@1002 : Decimal;
      SrcCurrVATBase@1009 : Decimal;
      SrcCurrSalesTaxBaseAmount@1005 : Decimal;
      RemSrcCurrVATAmount@1007 : Decimal;
      SalesTaxBaseAmount@1014 : Decimal;
      TaxDetailFound@1006 : Boolean;
    BEGIN
      WITH GenJnlLine DO
        // Post VAT
        // VAT for VAT entry
        CASE "VAT Calculation Type" OF
          "VAT Calculation Type"::"Normal VAT",
          "VAT Calculation Type"::"Reverse Charge VAT",
          "VAT Calculation Type"::"Full VAT":
            BEGIN
              IF "VAT Posting" = "VAT Posting"::"Automatic VAT Entry" THEN
                "VAT Base Amount (LCY)" := GLEntry.Amount;
              IF "Gen. Posting Type" = "Gen. Posting Type"::Settlement THEN
                AddCurrGLEntryVATAmt := "Source Curr. VAT Amount";
              InsertVAT(
                GenJnlLine,VATPostingSetup,
                GLEntry.Amount,GLEntry."VAT Amount","VAT Base Amount (LCY)","Source Currency Code",
                GLEntry."Additional-Currency Amount",AddCurrGLEntryVATAmt,"Source Curr. VAT Base Amount");
              NextConnectionNo := NextConnectionNo + 1;
            END;
          "VAT Calculation Type"::"Sales Tax":
            BEGIN
              CASE "VAT Posting" OF
                "VAT Posting"::"Automatic VAT Entry":
                  SalesTaxBaseAmount := GLEntry.Amount;
                "VAT Posting"::"Manual VAT Entry":
                  SalesTaxBaseAmount := "VAT Base Amount (LCY)";
              END;
              IF ("VAT Posting" = "VAT Posting"::"Manual VAT Entry") AND
                 ("Gen. Posting Type" = "Gen. Posting Type"::Settlement)
              THEN
                InsertVAT(
                  GenJnlLine,VATPostingSetup,
                  GLEntry.Amount,GLEntry."VAT Amount","VAT Base Amount (LCY)","Source Currency Code",
                  "Source Curr. VAT Base Amount","Source Curr. VAT Amount","Source Curr. VAT Base Amount")
              ELSE BEGIN
                CLEAR(SalesTaxCalculate);
                SalesTaxCalculate.InitSalesTaxLines(
                  "Tax Area Code","Tax Group Code","Tax Liable",
                  SalesTaxBaseAmount,Quantity,"Posting Date",GLEntry."VAT Amount");
                SrcCurrVATAmount := 0;
                SrcCurrSalesTaxBaseAmount := CalcLCYToAddCurr(SalesTaxBaseAmount);
                RemSrcCurrVATAmount := AddCurrGLEntryVATAmt;
                TaxDetailFound := FALSE;
                WHILE SalesTaxCalculate.GetSalesTaxLine(TaxDetail2,VATAmount,VATBase) DO BEGIN
                  RemSrcCurrVATAmount := RemSrcCurrVATAmount - SrcCurrVATAmount;
                  IF TaxDetailFound THEN
                    InsertVAT(
                      GenJnlLine,VATPostingSetup,
                      SalesTaxBaseAmount,VATAmount2,VATBase2,"Source Currency Code",
                      SrcCurrSalesTaxBaseAmount,SrcCurrVATAmount,SrcCurrVATBase);
                  TaxDetailFound := TRUE;
                  TaxDetail := TaxDetail2;
                  VATAmount2 := VATAmount;
                  VATBase2 := VATBase;
                  SrcCurrVATAmount := CalcLCYToAddCurr(VATAmount);
                  SrcCurrVATBase := CalcLCYToAddCurr(VATBase);
                END;
                IF TaxDetailFound THEN
                  InsertVAT(
                    GenJnlLine,VATPostingSetup,
                    SalesTaxBaseAmount,VATAmount2,VATBase2,"Source Currency Code",
                    SrcCurrSalesTaxBaseAmount,RemSrcCurrVATAmount,SrcCurrVATBase);
                InsertSummarizedVAT(GenJnlLine);
              END;
            END;
        END;
    END;

    LOCAL PROCEDURE InsertVAT@30(GenJnlLine@1015 : Record 81;VATPostingSetup@1018 : Record 325;GLEntryAmount@1000 : Decimal;GLEntryVATAmount@1001 : Decimal;GLEntryBaseAmount@1002 : Decimal;SrcCurrCode@1004 : Code[10];SrcCurrGLEntryAmt@1005 : Decimal;SrcCurrGLEntryVATAmt@1006 : Decimal;SrcCurrGLEntryBaseAmt@1007 : Decimal);
    VAR
      TaxJurisdiction@1003 : Record 320;
      VATAmount@1008 : Decimal;
      VATBase@1009 : Decimal;
      SrcCurrVATAmount@1011 : Decimal;
      SrcCurrVATBase@1012 : Decimal;
      VATDifferenceLCY@1013 : Decimal;
      SrcCurrVATDifference@1014 : Decimal;
      UnrealizedVAT@1019 : Boolean;
    BEGIN
      WITH GenJnlLine DO BEGIN
        // Post VAT
        // VAT for VAT entry
        VATEntry.INIT;
        VATEntry.CopyFromGenJnlLine(GenJnlLine);
        VATEntry."Entry No." := NextVATEntryNo;
        VATEntry."EU Service" := VATPostingSetup."EU Service";
        VATEntry."Transaction No." := NextTransactionNo;
        VATEntry."Sales Tax Connection No." := NextConnectionNo;
        VATEntry."Sett. Unrealized Amount" := "VAT Amount";
        VATEntry."Sett. Unrealized Base" := "VAT Base Amount";
        VATEntry."Sett. Unrealised Amount (FCY)" := "Source Curr. VAT Amount";
        VATEntry."Sett. Unrealised Base (FCY)" := "Source Curr. VAT Base Amount";
        VATEntry.Adjustment := Adjustment;
        VATEntry."VAT Classification" := VATPostingSetup."VAT Classification"; //BIR
        // --- #001 --- Start
        {
        IF TIN <> '' THEN BEGIN
          VATEntry."VAT Registration No." := TIN;
          IF PettyCashVend.GET(TIN) THEN
            VATEntry.Name := PettyCashVend.Name;
          VATEntry.Payee := Name; //Petty Cash Payee
        END;
        }
        // --- #001 --- End
        IF "VAT Difference" = 0 THEN
          VATDifferenceLCY := 0
        ELSE
          IF "Currency Code" = '' THEN
            VATDifferenceLCY := "VAT Difference"
          ELSE
            VATDifferenceLCY :=
              ROUND(
                CurrExchRate.ExchangeAmtFCYToLCY(
                  "Posting Date","Currency Code","VAT Difference",
                  CurrExchRate.ExchangeRate("Posting Date","Currency Code")));

        IF "VAT Calculation Type" = "VAT Calculation Type"::"Sales Tax" THEN BEGIN
          IF TaxDetail."Tax Jurisdiction Code" <> '' THEN
            TaxJurisdiction.GET(TaxDetail."Tax Jurisdiction Code");
          IF "Gen. Posting Type" <> "Gen. Posting Type"::Settlement THEN BEGIN
            VATEntry."Tax Group Used" := TaxDetail."Tax Group Code";
            VATEntry."Tax Type" := TaxDetail."Tax Type";
            VATEntry."Tax on Tax" := TaxDetail."Calculate Tax on Tax";
          END;
          VATEntry."Tax Jurisdiction Code" := TaxDetail."Tax Jurisdiction Code";
        END;

        IF ("Vendor Exchange Rate (ACY)" <> 0) OR
           (("Vendor Exchange Rate (ACY)" = 1) AND
            ("Source Currency Code" <> AddCurrencyCode))
        THEN
          SrcCurrGLEntryBaseAmt := "VAT Base (ACY)";

        IF AddCurrencyCode <> '' THEN
          IF AddCurrencyCode <> SrcCurrCode THEN BEGIN
            IF "VAT Base (ACY)" = 0 THEN BEGIN
              SrcCurrGLEntryAmt := ExchangeAmtLCYToFCY2(GLEntryAmount);
              SrcCurrGLEntryVATAmt := ExchangeAmtLCYToFCY2(GLEntryVATAmount);
              SrcCurrGLEntryBaseAmt := ExchangeAmtLCYToFCY2(GLEntryBaseAmount);
              SrcCurrVATDifference := ExchangeAmtLCYToFCY2(VATDifferenceLCY);
            END ELSE BEGIN
              SrcCurrGLEntryAmt := "VAT Base (ACY)";
              SrcCurrGLEntryVATAmt := "Amount Including VAT (ACY)" - "VAT Base (ACY)";
              SrcCurrGLEntryBaseAmt := "VAT Base (ACY)";
              SrcCurrVATDifference := "VAT Difference (ACY)";
            END;
          END ELSE
            SrcCurrVATDifference := "VAT Difference";

        UnrealizedVAT :=
          (((VATPostingSetup."Unrealized VAT Type" > 0) AND
            (VATPostingSetup."VAT Calculation Type" IN
             [VATPostingSetup."VAT Calculation Type"::"Normal VAT",
              VATPostingSetup."VAT Calculation Type"::"Reverse Charge VAT",
              VATPostingSetup."VAT Calculation Type"::"Full VAT"])) OR
           ((TaxJurisdiction."Unrealized VAT Type" > 0) AND
            (VATPostingSetup."VAT Calculation Type" IN
             [VATPostingSetup."VAT Calculation Type"::"Sales Tax"]))) AND
          IsNotPayment("Document Type");
        IF GLSetup."Prepayment Unrealized VAT" AND NOT GLSetup."Unrealized VAT" AND
           (VATPostingSetup."Unrealized VAT Type" > 0)
        THEN
          UnrealizedVAT := Prepayment;

        // VAT for VAT entry
        IF "Gen. Posting Type" <> 0 THEN BEGIN
          CASE "VAT Posting" OF
            "VAT Posting"::"Automatic VAT Entry":
              BEGIN
                VATAmount := GLEntryVATAmount;
                VATBase := GLEntryBaseAmount;
                SrcCurrVATAmount := SrcCurrGLEntryVATAmt;
                SrcCurrVATBase := SrcCurrGLEntryBaseAmt;
              END;
            "VAT Posting"::"Manual VAT Entry":
              BEGIN
                IF "Gen. Posting Type" = "Gen. Posting Type"::Settlement THEN BEGIN
                  VATAmount := GLEntryAmount;
                  SrcCurrVATAmount := SrcCurrGLEntryVATAmt;
                  VATEntry.Closed := TRUE;
                END ELSE BEGIN
                  VATAmount := GLEntryVATAmount;
                  SrcCurrVATAmount := SrcCurrGLEntryVATAmt;
                END;
                VATBase := GLEntryBaseAmount;
                SrcCurrVATBase := SrcCurrGLEntryBaseAmt;
              END;
          END;

          IF UnrealizedVAT THEN BEGIN
            VATEntry.Amount := 0;
            VATEntry.Base := 0;
            VATEntry."Unrealized Amount" := VATAmount;
            VATEntry."Unrealized Base" := VATBase;
            VATEntry."Remaining Unrealized Amount" := VATEntry."Unrealized Amount";
            VATEntry."Remaining Unrealized Base" := VATEntry."Unrealized Base";
          END ELSE BEGIN
            VATEntry.Amount := VATAmount;
            VATEntry.Base := VATBase;
            VATEntry."Unrealized Amount" := 0;
            VATEntry."Unrealized Base" := 0;
            VATEntry."Remaining Unrealized Amount" := 0;
            VATEntry."Remaining Unrealized Base" := 0;
          END;

          IF AddCurrencyCode = '' THEN BEGIN
            VATEntry."Additional-Currency Base" := 0;
            VATEntry."Additional-Currency Amount" := 0;
            VATEntry."Add.-Currency Unrealized Amt." := 0;
            VATEntry."Add.-Currency Unrealized Base" := 0;
          END ELSE
            IF UnrealizedVAT THEN BEGIN
              VATEntry."Additional-Currency Base" := 0;
              VATEntry."Additional-Currency Amount" := 0;
              VATEntry."Add.-Currency Unrealized Base" := SrcCurrVATBase;
              VATEntry."Add.-Currency Unrealized Amt." := SrcCurrVATAmount;
            END ELSE BEGIN
              VATEntry."Additional-Currency Base" := SrcCurrVATBase;
              VATEntry."Additional-Currency Amount" := SrcCurrVATAmount;
              VATEntry."Add.-Currency Unrealized Base" := 0;
              VATEntry."Add.-Currency Unrealized Amt." := 0;
            END;
          VATEntry."Add.-Curr. Rem. Unreal. Amount" := VATEntry."Add.-Currency Unrealized Amt.";
          VATEntry."Add.-Curr. Rem. Unreal. Base" := VATEntry."Add.-Currency Unrealized Base";
          VATEntry."VAT Difference" := VATDifferenceLCY;
          VATEntry."Add.-Curr. VAT Difference" := SrcCurrVATDifference;

          VATEntry."VAT Classification" := VATPostingSetup."VAT Classification"; //BIR

          VATEntry.INSERT(TRUE);
          GLEntryVATEntryLink.InsertLink(TempGLEntryBuf."Entry No.",VATEntry."Entry No.");
          NextVATEntryNo := NextVATEntryNo + 1;
        END;

        // VAT for G/L entry/entries
        IF (GLEntryVATAmount <> 0) OR
           ((SrcCurrGLEntryVATAmt <> 0) AND (SrcCurrCode = AddCurrencyCode))
        THEN
          CASE "Gen. Posting Type" OF
            "Gen. Posting Type"::Purchase:
              CASE VATPostingSetup."VAT Calculation Type" OF
                VATPostingSetup."VAT Calculation Type"::"Normal VAT",
                VATPostingSetup."VAT Calculation Type"::"Full VAT":
                  BEGIN
                    UseVendExchRate := "VAT Base (ACY)" <> 0;
                    CreateGLEntry(GenJnlLine,VATPostingSetup.GetPurchAccount(UnrealizedVAT),
                      GLEntryVATAmount,SrcCurrGLEntryVATAmt,TRUE);
                  END;
                VATPostingSetup."VAT Calculation Type"::"Reverse Charge VAT":
                  BEGIN
                    CreateGLEntry(GenJnlLine,VATPostingSetup.GetPurchAccount(UnrealizedVAT),
                      GLEntryVATAmount,SrcCurrGLEntryVATAmt,TRUE);
                    CreateGLEntry(GenJnlLine,VATPostingSetup.GetRevChargeAccount(UnrealizedVAT),
                      -GLEntryVATAmount,-SrcCurrGLEntryVATAmt,TRUE);
                  END;
                VATPostingSetup."VAT Calculation Type"::"Sales Tax":
                  IF "Use Tax" THEN BEGIN
                    InitGLEntryVAT(GenJnlLine,TaxJurisdiction.GetPurchAccount(UnrealizedVAT),'',
                      GLEntryVATAmount,SrcCurrGLEntryVATAmt,TRUE);
                    InitGLEntryVAT(GenJnlLine,TaxJurisdiction.GetRevChargeAccount(UnrealizedVAT),'',
                      -GLEntryVATAmount,-SrcCurrGLEntryVATAmt,TRUE);
                  END ELSE
                    InitGLEntryVAT(GenJnlLine,TaxJurisdiction.GetPurchAccount(UnrealizedVAT),'',
                      GLEntryVATAmount,SrcCurrGLEntryVATAmt,TRUE);
              END;
            "Gen. Posting Type"::Sale:
              CASE VATPostingSetup."VAT Calculation Type" OF
                VATPostingSetup."VAT Calculation Type"::"Normal VAT",
                VATPostingSetup."VAT Calculation Type"::"Full VAT":
                  BEGIN
                    UseVendExchRate := FALSE;
                    CreateGLEntry(GenJnlLine,VATPostingSetup.GetSalesAccount(UnrealizedVAT),
                      GLEntryVATAmount,SrcCurrGLEntryVATAmt,TRUE);
                  END;
                VATPostingSetup."VAT Calculation Type"::"Reverse Charge VAT":
                  ;
                VATPostingSetup."VAT Calculation Type"::"Sales Tax":
                  BEGIN
                    UseVendExchRate := FALSE;
                    InitGLEntryVAT(GenJnlLine,TaxJurisdiction.GetSalesAccount(UnrealizedVAT),'',
                      GLEntryVATAmount,SrcCurrGLEntryVATAmt,TRUE);
                  END;
              END;
          END;
      END;
    END;

    LOCAL PROCEDURE SummarizeVAT@31(SummarizeGLEntries@1000 : Boolean;GLEntry@1001 : Record 17);
    VAR
      InsertedTempVAT@1004 : Boolean;
    BEGIN
      InsertedTempVAT := FALSE;
      IF SummarizeGLEntries THEN
        IF TempGLEntryVAT.FINDSET THEN
          REPEAT
            IF (TempGLEntryVAT."G/L Account No." = GLEntry."G/L Account No.") AND
               (TempGLEntryVAT."Bal. Account No." = GLEntry."Bal. Account No.")
            THEN BEGIN
              TempGLEntryVAT.Amount := TempGLEntryVAT.Amount + GLEntry.Amount;
              TempGLEntryVAT."Additional-Currency Amount" :=
                TempGLEntryVAT."Additional-Currency Amount" + GLEntry."Additional-Currency Amount";
              TempGLEntryVAT.MODIFY;
              InsertedTempVAT := TRUE;
            END;
          UNTIL (TempGLEntryVAT.NEXT = 0) OR InsertedTempVAT;
      IF NOT InsertedTempVAT OR NOT SummarizeGLEntries THEN BEGIN
        TempGLEntryVAT := GLEntry;
        TempGLEntryVAT."Entry No." :=
          TempGLEntryVAT."Entry No." + InsertedTempGLEntryVAT;
        TempGLEntryVAT.INSERT;
        InsertedTempGLEntryVAT := InsertedTempGLEntryVAT + 1;
      END;
    END;

    LOCAL PROCEDURE InsertSummarizedVAT@37(GenJnlLine@1000 : Record 81);
    BEGIN
      IF TempGLEntryVAT.FINDSET THEN BEGIN
        REPEAT
          InsertGLEntry(GenJnlLine,TempGLEntryVAT,TRUE);
        UNTIL TempGLEntryVAT.NEXT = 0;
        TempGLEntryVAT.DELETEALL;
        InsertedTempGLEntryVAT := 0;
      END;
      NextConnectionNo := NextConnectionNo + 1;
    END;

    LOCAL PROCEDURE PostGLAcc@11(GenJnlLine@1001 : Record 81;Balancing@1004 : Boolean);
    VAR
      GLAcc@1000 : Record 15;
      GLEntry@1002 : Record 17;
      VATPostingSetup@1003 : Record 325;
      WHTPostingSetup@1240000 : Record 50003;
      WHTAmountLCY@1240001 : Decimal;
      WHTAmount@1240002 : Decimal;
    BEGIN
      WITH GenJnlLine DO BEGIN
        GLAcc.GET("Account No.");
        // G/L entry
        CalcGLAccWHT(GenJnlLine,WHTPostingSetup,WHTAmountLCY,WHTAmount);
        // G/L entry
        InitGLEntry(GenJnlLine,GLEntry,
          "Account No.","Amount (LCY)" + WHTAmountLCY,
          "Source Currency Amount" + WHTAmountLCY,TRUE,"System-Created Entry");
        IF NOT "System-Created Entry" THEN
          IF "Posting Date" = NORMALDATE("Posting Date") THEN
            GLAcc.TESTFIELD("Direct Posting",TRUE);
        IF GLAcc."Omit Default Descr. in Jnl." THEN
          IF DELCHR(Description,'=',' ') = '' THEN
            ERROR(
              DescriptionMustNotBeBlankErr,
              GLAcc.FIELDCAPTION("Omit Default Descr. in Jnl."),
              GLAcc."No.",
              FIELDCAPTION(Description));
        GLEntry."Gen. Posting Type" := "Gen. Posting Type";
        GLEntry."Bal. Account Type" := "Bal. Account Type";
        GLEntry."Bal. Account No." := "Bal. Account No.";
        GLEntry."No. Series" := "Posting No. Series";
        IF "Additional-Currency Posting" =
           "Additional-Currency Posting"::"Additional-Currency Amount Only"
        THEN BEGIN
          GLEntry."Additional-Currency Amount" := Amount;
          GLEntry.Amount := 0;
        END;
        // Store Entry No. to global variable for return:
        GLEntryNo := GLEntry."Entry No.";
        InitVAT(GenJnlLine,GLEntry,VATPostingSetup);
        InsertGLEntry(GenJnlLine,GLEntry,TRUE);
        IF EntryType = GLEntry."Entry Type"::Definitive THEN BEGIN
          PostJob(GenJnlLine,GLEntry);
          PostVAT(GenJnlLine,GLEntry,VATPostingSetup);
        END;
        PostGLAccWHT(GenJnlLine,WHTPostingSetup,WHTAmountLCY);
        DeferralPosting("Deferral Code","Source Code","Account No.",GenJnlLine,Balancing);
        OnMoveGenJournalLine(GLEntry.RECORDID);
      END;

      OnAfterPostGLAcc(GenJnlLine);
    END;

    LOCAL PROCEDURE PostCust@12(VAR GenJnlLine@1007 : Record 81;Balancing@1010 : Boolean);
    VAR
      LineFeeNoteOnReportHist@1008 : Record 1053;
      Cust@1005 : Record 18;
      CustPostingGr@1006 : Record 92;
      CustLedgEntry@1000 : Record 21;
      CVLedgEntryBuf@1002 : Record 382;
      TempDtldCVLedgEntryBuf@1003 : TEMPORARY Record 383;
      DtldCustLedgEntry@1004 : Record 379;
      WHTPostingSetup@1240000 : Record 50003;
      CustLedgerEntryNo@150001 : Integer;
      WHTAmount@1106000002 : Decimal;
      WHTAmountLCY@1240001 : Decimal;
      ReceivablesAccount@1009 : Code[20];
      DtldLedgEntryInserted@1001 : Boolean;
    BEGIN
      WITH GenJnlLine DO BEGIN
        Cust.GET("Account No.");
        Cust.CheckBlockedCustOnJnls(Cust,"Document Type",TRUE);

        IF "Posting Group" = '' THEN BEGIN
          IF NOT "Cylinder Posting" THEN  // LPC-CM
            Cust.TESTFIELD("Customer Posting Group");
          "Posting Group" := Cust."Customer Posting Group";
        END;
        //LPC-CM  LPC-CM
        IF "Cylinder Posting" THEN
          "Posting Group" := Cust."Customer Posting Group 2";
        //LPC-CM
        CustPostingGr.GET("Posting Group");
        ReceivablesAccount := CustPostingGr.GetReceivablesAccount;

        DtldCustLedgEntry.LOCKTABLE;
        CustLedgEntry.LOCKTABLE;

        InitCustLedgEntry(GenJnlLine,CustLedgEntry);

        CalcCustWHT(GenJnlLine,WHTPostingSetup,WHTAmountLCY,WHTAmount);
        CustLedgEntry."Amount to Apply" := Amount;

        IF NOT Cust."Block Payment Tolerance" THEN
          CalcPmtTolerancePossible(
            GenJnlLine,CustLedgEntry."Pmt. Discount Date",CustLedgEntry."Pmt. Disc. Tolerance Date",
            CustLedgEntry."Max. Payment Tolerance");

        TempDtldCVLedgEntryBuf.DELETEALL;
        TempDtldCVLedgEntryBuf.INIT;
        TempDtldCVLedgEntryBuf.CopyFromGenJnlLine(GenJnlLine);
        TempDtldCVLedgEntryBuf."CV Ledger Entry No." := CustLedgEntry."Entry No.";
        TempDtldCVLedgEntryBuf.Amount := TempDtldCVLedgEntryBuf.Amount - ExchangeAmtLCYToFCY2(WHTAmount) ;
        TempDtldCVLedgEntryBuf."Amount (LCY)" := TempDtldCVLedgEntryBuf."Amount (LCY)" - WHTAmountLCY;
        TempDtldCVLedgEntryBuf."Additional-Currency Amount" := Amount;
        CVLedgEntryBuf.CopyFromCustLedgEntry(CustLedgEntry);
        TempDtldCVLedgEntryBuf.InsertDtldCVLedgEntry(TempDtldCVLedgEntryBuf,CVLedgEntryBuf,TRUE);
        CVLedgEntryBuf.Open := CVLedgEntryBuf."Remaining Amount" <> 0;
        CVLedgEntryBuf.Positive := CVLedgEntryBuf."Remaining Amount" > 0;

        CalcPmtDiscPossible(GenJnlLine,CVLedgEntryBuf);

        IF "Currency Code" <> '' THEN BEGIN
          TESTFIELD("Currency Factor");
          CVLedgEntryBuf."Original Currency Factor" := "Currency Factor"
        END ELSE
          CVLedgEntryBuf."Original Currency Factor" := 1;
        CVLedgEntryBuf."Adjusted Currency Factor" := CVLedgEntryBuf."Original Currency Factor";

        // Check the document no.
        IF "Recurring Method" = 0 THEN
          IF IsNotPayment("Document Type") THEN BEGIN
            GenJnlCheckLine.CheckSalesDocNoIsNotUsed("Document Type","Document No.");
            CheckSalesExtDocNo(GenJnlLine);
          END;

        // Post application
        ApplyCustLedgEntry(CVLedgEntryBuf,TempDtldCVLedgEntryBuf,GenJnlLine,Cust);

        // Post customer entry
        CustLedgEntry.CopyFromCVLedgEntryBuffer(CVLedgEntryBuf);
        CustLedgEntry."Amount to Apply" := 0;
        CustLedgEntry."Applies-to Doc. No." := '';
        CustLedgEntry."Collection Date" := "Collection Date"; //<<CA-04-008
        CustLedgEntry.INSERT(TRUE);

        // Post detailed customer entries
        DtldLedgEntryInserted := PostDtldCustLedgEntries(GenJnlLine,TempDtldCVLedgEntryBuf,CustPostingGr,TRUE);

        // Post Reminder Terms - Note About Line Fee on Report
        LineFeeNoteOnReportHist.Save(CustLedgEntry);

        IF DtldLedgEntryInserted THEN
          IF IsTempGLEntryBufEmpty THEN
            DtldCustLedgEntry.SetZeroTransNo(NextTransactionNo);

        PostCustWHT(GenJnlLine,CustLedgEntry,WHTPostingSetup,WHTAmountLCY);

        DeferralPosting("Deferral Code","Source Code",ReceivablesAccount,GenJnlLine,Balancing);
        OnMoveGenJournalLine(CustLedgEntry.RECORDID);
      END;
    END;

    LOCAL PROCEDURE PostVend@13(GenJnlLine@1007 : Record 81;Balancing@1009 : Boolean);
    VAR
      Vend@1005 : Record 23;
      VendPostingGr@1006 : Record 93;
      VendLedgEntry@1000 : Record 25;
      CVLedgEntryBuf@1002 : Record 382;
      TempDtldCVLedgEntryBuf@1003 : TEMPORARY Record 383;
      DtldVendLedgEntry@1004 : Record 380;
      WHTPostingSetup@1240000 : Record 50003;
      WHTAmount@1106000002 : Decimal;
      WHTAmountLCY@1240001 : Decimal;
      PayablesAccount@1008 : Code[20];
      DtldLedgEntryInserted@1001 : Boolean;
      CheckExtDocNoHandled@1010 : Boolean;
    BEGIN
      WITH GenJnlLine DO BEGIN
        Vend.GET("Account No.");
        Vend.CheckBlockedVendOnJnls(Vend,"Document Type",TRUE);

        IF "Posting Group" = '' THEN BEGIN
          Vend.TESTFIELD("Vendor Posting Group");
          "Posting Group" := Vend."Vendor Posting Group";
        END;
        VendPostingGr.GET("Posting Group");
        PayablesAccount := VendPostingGr.GetPayablesAccount;

        DtldVendLedgEntry.LOCKTABLE;
        VendLedgEntry.LOCKTABLE;

        InitVendLedgEntry(GenJnlLine,VendLedgEntry);

        CalcVendWHT(GenJnlLine,WHTPostingSetup,WHTAmountLCY,WHTAmount);
        VendLedgEntry."Amount to Apply" := Amount;

        IF NOT Vend."Block Payment Tolerance" THEN
          CalcPmtTolerancePossible(
            GenJnlLine,VendLedgEntry."Pmt. Discount Date",VendLedgEntry."Pmt. Disc. Tolerance Date",
            VendLedgEntry."Max. Payment Tolerance");

        TempDtldCVLedgEntryBuf.DELETEALL;
        TempDtldCVLedgEntryBuf.INIT;
        TempDtldCVLedgEntryBuf.CopyFromGenJnlLine(GenJnlLine);
        TempDtldCVLedgEntryBuf."CV Ledger Entry No." := VendLedgEntry."Entry No.";
        TempDtldCVLedgEntryBuf.Amount := TempDtldCVLedgEntryBuf.Amount - "Interest Amount" - ExchangeAmtLCYToFCY2(WHTAmount);
        TempDtldCVLedgEntryBuf."Amount (LCY)" := TempDtldCVLedgEntryBuf."Amount (LCY)" - "Interest Amount (LCY)" - WHTAmountLCY;
        IF "Vendor Exchange Rate (ACY)" <> 0 THEN
          TempDtldCVLedgEntryBuf."Additional-Currency Amount" := "Amount Including VAT (ACY)" - "Interest Amount"
        ELSE
          TempDtldCVLedgEntryBuf."Additional-Currency Amount" := Amount;
        CVLedgEntryBuf.CopyFromVendLedgEntry(VendLedgEntry);
        TempDtldCVLedgEntryBuf.InsertDtldCVLedgEntry(TempDtldCVLedgEntryBuf,CVLedgEntryBuf,TRUE);
        CVLedgEntryBuf.Open := CVLedgEntryBuf."Remaining Amount" <> 0;
        CVLedgEntryBuf.Positive := CVLedgEntryBuf."Remaining Amount" > 0;

        CalcPmtDiscPossible(GenJnlLine,CVLedgEntryBuf);

        IF "Currency Code" <> '' THEN BEGIN
          TESTFIELD("Currency Factor");
          CVLedgEntryBuf."Adjusted Currency Factor" := "Currency Factor"
        END ELSE
          CVLedgEntryBuf."Adjusted Currency Factor" := 1;
        CVLedgEntryBuf."Original Currency Factor" := CVLedgEntryBuf."Adjusted Currency Factor";

        // Check the document no.
        IF "Recurring Method" = 0 THEN
          IF IsNotPayment("Document Type") THEN BEGIN
            GenJnlCheckLine.CheckPurchDocNoIsNotUsed("Document Type","Document No.");
            OnBeforeCheckPurchExtDocNo(GenJnlLine,VendLedgEntry,CVLedgEntryBuf,CheckExtDocNoHandled);
            IF NOT CheckExtDocNoHandled THEN
              CheckPurchExtDocNo(GenJnlLine);
          END;

        // Post application
        ApplyVendLedgEntry(CVLedgEntryBuf,TempDtldCVLedgEntryBuf,GenJnlLine,Vend);

        // Post vendor entry
        VendLedgEntry.CopyFromCVLedgEntryBuffer(CVLedgEntryBuf);
        VendLedgEntry."Amount to Apply" := 0;
        VendLedgEntry."Applies-to Doc. No." := '';
        VendLedgEntry.INSERT(TRUE);

        // Post detailed vendor entries
        DtldLedgEntryInserted := PostDtldVendLedgEntries(GenJnlLine,TempDtldCVLedgEntryBuf,VendPostingGr,TRUE);

        IF DtldLedgEntryInserted THEN
          IF IsTempGLEntryBufEmpty THEN
            DtldVendLedgEntry.SetZeroTransNo(NextTransactionNo);

        PostVendWHT(GenJnlLine,VendLedgEntry,WHTPostingSetup,WHTAmountLCY,WHTAmount);
        DeferralPosting("Deferral Code","Source Code",PayablesAccount,GenJnlLine,Balancing);
        OnMoveGenJournalLine(VendLedgEntry.RECORDID);
      END;
    END;

    LOCAL PROCEDURE PostEmployee@86(GenJnlLine@1007 : Record 81);
    VAR
      Employee@1005 : Record 5200;
      EmployeePostingGr@1006 : Record 5221;
      EmployeeLedgerEntry@1000 : Record 5222;
      CVLedgEntryBuf@1008 : Record 382;
      TempDtldCVLedgEntryBuf@1004 : TEMPORARY Record 383;
      DtldEmplLedgEntry@1001 : Record 5223;
      DtldLedgEntryInserted@1010 : Boolean;
    BEGIN
      WITH GenJnlLine DO BEGIN
        Employee.GET("Account No.");
        Employee.CheckBlockedEmployeeOnJnls(TRUE);

        IF "Posting Group" = '' THEN BEGIN
          Employee.TESTFIELD("Employee Posting Group");
          "Posting Group" := Employee."Employee Posting Group";
        END;
        EmployeePostingGr.GET("Posting Group");

        DtldEmplLedgEntry.LOCKTABLE;
        EmployeeLedgerEntry.LOCKTABLE;

        InitEmployeeLedgerEntry(GenJnlLine,EmployeeLedgerEntry);

        TempDtldCVLedgEntryBuf.DELETEALL;
        TempDtldCVLedgEntryBuf.INIT;
        TempDtldCVLedgEntryBuf.CopyFromGenJnlLine(GenJnlLine);
        TempDtldCVLedgEntryBuf."CV Ledger Entry No." := EmployeeLedgerEntry."Entry No.";
        CVLedgEntryBuf.CopyFromEmplLedgEntry(EmployeeLedgerEntry);
        TempDtldCVLedgEntryBuf.InsertDtldCVLedgEntry(TempDtldCVLedgEntryBuf,CVLedgEntryBuf,TRUE);
        CVLedgEntryBuf.Open := CVLedgEntryBuf."Remaining Amount" <> 0;
        CVLedgEntryBuf.Positive := CVLedgEntryBuf."Remaining Amount" > 0;

        //<<LPC-CA  *** new 0925
        IF "Currency Code" <> '' THEN BEGIN
          TESTFIELD("Currency Factor");
          CVLedgEntryBuf."Original Currency Factor" := "Currency Factor"
        END ELSE
          CVLedgEntryBuf."Original Currency Factor" := 1;
        CVLedgEntryBuf."Adjusted Currency Factor" := CVLedgEntryBuf."Original Currency Factor";
        //>>LPC-CA  *** new 0925

        // Post application
        ApplyEmplLedgEntry(CVLedgEntryBuf,TempDtldCVLedgEntryBuf,GenJnlLine,Employee);

        // Post vendor entry
        EmployeeLedgerEntry.CopyFromCVLedgEntryBuffer(CVLedgEntryBuf);
        EmployeeLedgerEntry."Amount to Apply" := 0;
        EmployeeLedgerEntry."Applies-to Doc. No." := '';
        EmployeeLedgerEntry.INSERT(TRUE);

        // Post detailed employee entries
        DtldLedgEntryInserted := PostDtldEmplLedgEntries(GenJnlLine,TempDtldCVLedgEntryBuf,EmployeePostingGr,TRUE);

        // Posting GL Entry
        IF DtldLedgEntryInserted THEN
          IF IsTempGLEntryBufEmpty THEN
            DtldEmplLedgEntry.SetZeroTransNo(NextTransactionNo);

        OnMoveGenJournalLine(EmployeeLedgerEntry.RECORDID);
      END;
    END;

    LOCAL PROCEDURE PostBankAcc@14(GenJnlLine@1005 : Record 81;Balancing@1006 : Boolean);
    VAR
      BankAcc@1000 : Record 270;
      BankAccLedgEntry@1004 : Record 271;
      CheckLedgEntry@1003 : Record 272;
      CheckLedgEntry2@1002 : Record 272;
      BankAccPostingGr@1001 : Record 277;
      WHTPostingSetup@1500000 : Record 50003;
      WHTAmountLCY@1500001 : Decimal;
      WHTAmount@1500002 : Decimal;
    BEGIN
      WITH GenJnlLine DO BEGIN
        BankAcc.GET("Account No.");
        BankAcc.TESTFIELD(Blocked,FALSE);
        IF "Currency Code" = '' THEN
          BankAcc.TESTFIELD("Currency Code",'')
        ELSE
          IF BankAcc."Currency Code" <> '' THEN
            TESTFIELD("Currency Code",BankAcc."Currency Code");

        BankAcc.TESTFIELD("Bank Acc. Posting Group");
        BankAccPostingGr.GET(BankAcc."Bank Acc. Posting Group");

        BankAccLedgEntry.LOCKTABLE;

        InitBankAccLedgEntry(GenJnlLine,BankAccLedgEntry);

        BankAccLedgEntry."Bank Acc. Posting Group" := BankAcc."Bank Acc. Posting Group";
        BankAccLedgEntry."Currency Code" := BankAcc."Currency Code";
        CalcBankAccWHT(GenJnlLine,WHTPostingSetup,WHTAmountLCY,WHTAmount);
        IF BankAcc."Currency Code" <> '' THEN
          BankAccLedgEntry.Amount := Amount + WHTAmount
        ELSE
          BankAccLedgEntry.Amount := "Amount (LCY)" + WHTAmountLCY;
        BankAccLedgEntry."Amount (LCY)" := "Amount (LCY)" + WHTAmountLCY;
        BankAccLedgEntry."User ID" := USERID;
        IF BankAccLedgEntry.Amount <> 0 THEN BEGIN
          BankAccLedgEntry.Open := TRUE;
          BankAccLedgEntry."Remaining Amount" := BankAccLedgEntry.Amount;
        END;
        BankAccLedgEntry.Positive := BankAccLedgEntry.Amount > 0;
        BankAccLedgEntry.UpdateDebitCredit(Correction);
        BankAccLedgEntry."Collection Date" := "Collection Date"; //<<CA-04-008
        BankAccLedgEntry.INSERT(TRUE);

        IF ((Amount <= 0) AND ("Bank Payment Type" = "Bank Payment Type"::"Computer Check") AND "Check Printed") OR
           ((Amount < 0) AND ("Bank Payment Type" = "Bank Payment Type"::"Manual Check"))
        THEN BEGIN
          IF BankAcc."Currency Code" <> "Currency Code" THEN
            ERROR(BankPaymentTypeMustNotBeFilledErr);
          CASE "Bank Payment Type" OF
            "Bank Payment Type"::"Computer Check":
              BEGIN
                TESTFIELD("Check Printed",TRUE);
                CheckLedgEntry.LOCKTABLE;
                CheckLedgEntry.RESET;
                CheckLedgEntry.SETCURRENTKEY("Bank Account No.","Entry Status","Check No.");
                CheckLedgEntry.SETRANGE("Bank Account No.","Account No.");
                CheckLedgEntry.SETRANGE("Entry Status",CheckLedgEntry."Entry Status"::Printed);
                CheckLedgEntry.SETRANGE("Check No.","Document No.");
                IF CheckLedgEntry.FINDSET THEN
                  REPEAT
                    CheckLedgEntry2 := CheckLedgEntry;
                    CheckLedgEntry2."Entry Status" := CheckLedgEntry2."Entry Status"::Posted;
                    CheckLedgEntry2."Bank Account Ledger Entry No." := BankAccLedgEntry."Entry No.";
                    CheckLedgEntry2.MODIFY;
                  UNTIL CheckLedgEntry.NEXT = 0;
              END;
            "Bank Payment Type"::"Manual Check":
              BEGIN
                IF "Document No." = '' THEN
                  ERROR(DocNoMustBeEnteredErr,"Bank Payment Type");
                CheckLedgEntry.RESET;
                IF NextCheckEntryNo = 0 THEN BEGIN
                  CheckLedgEntry.LOCKTABLE;
                  IF CheckLedgEntry.FINDLAST THEN
                    NextCheckEntryNo := CheckLedgEntry."Entry No." + 1
                  ELSE
                    NextCheckEntryNo := 1;
                END;

                CheckLedgEntry.SETRANGE("Bank Account No.","Account No.");
                CheckLedgEntry.SETFILTER(
                  "Entry Status",'%1|%2|%3',
                  CheckLedgEntry."Entry Status"::Printed,
                  CheckLedgEntry."Entry Status"::Posted,
                  CheckLedgEntry."Entry Status"::"Financially Voided");
                CheckLedgEntry.SETRANGE("Check No.","Document No.");
                IF NOT CheckLedgEntry.ISEMPTY THEN
                  ERROR(CheckAlreadyExistsErr,"Document No.");

                InitCheckLedgEntry(BankAccLedgEntry,CheckLedgEntry);
                CheckLedgEntry."Bank Payment Type" := CheckLedgEntry."Bank Payment Type"::"Manual Check";

                GenJnlLine1.RESET;
                GenJnlLine1.COPY(GenJnlLine);
                IF GLSetup."Enable WHT" THEN
                  IF NOT "Skip WHT" THEN
                    CheckLedgEntry."WHT Amount" := ABS(-WHTManagement.WHTAmountJournal(GenJnlLine1,FALSE));
                CheckLedgEntry."Interest Amount" := "Interest Amount";
                IF BankAcc."Currency Code" <> '' THEN
                  CheckLedgEntry.Amount := -Amount - CheckLedgEntry."WHT Amount"
                ELSE BEGIN
                  IF WHTAmountLCY <> 0 THEN
                    CheckLedgEntry."WHT Amount" := WHTAmountLCY;
                  CheckLedgEntry.Amount := -"Amount (LCY)" - CheckLedgEntry."WHT Amount";
                END;
                CheckLedgEntry.INSERT(TRUE);
                NextCheckEntryNo := NextCheckEntryNo + 1;
              END;
          END;
        END;

        BankAccPostingGr.TESTFIELD("G/L Bank Account No.");
        CreateGLEntryBalAcc(
          GenJnlLine,BankAccPostingGr."G/L Bank Account No.",
          "Amount (LCY)" + WHTAmountLCY,"Source Currency Amount" + WHTAmount,
          "Bal. Account Type","Bal. Account No.");

        PostBankAccWHT(GenJnlLine,WHTPostingSetup,WHTAmountLCY);
        DeferralPosting("Deferral Code","Source Code",BankAccPostingGr."G/L Bank Account No.",GenJnlLine,Balancing);
        OnMoveGenJournalLine(BankAccLedgEntry.RECORDID);
      END;
    END;

    LOCAL PROCEDURE PostFixedAsset@29(GenJnlLine@1009 : Record 81);
    VAR
      GLEntry@1010 : Record 17;
      GLEntry2@1000 : Record 17;
      TempFAGLPostBuf@1001 : TEMPORARY Record 5637;
      FAGLPostBuf@1011 : Record 5637;
      VATPostingSetup@1012 : Record 325;
      FAJnlPostLine@1013 : Codeunit 5632;
      FAAutomaticEntry@1003 : Codeunit 5607;
      ShortcutDim1Code@1004 : Code[20];
      ShortcutDim2Code@1005 : Code[20];
      Correction2@1006 : Boolean;
      NetDisposalNo@1007 : Integer;
      DimensionSetID@1008 : Integer;
      VATEntryGLEntryNo@1002 : Integer;
    BEGIN
      WITH GenJnlLine DO BEGIN
        InitGLEntry(GenJnlLine,GLEntry,'',"Amount (LCY)","Source Currency Amount",TRUE,"System-Created Entry");
      //IF (GenJnlLine."Location Code" <> '') AND (GenJnlLine."Bin Code" <> '') THEN
            IF (NOT "System-Created Entry") AND ("Cylinder Disposal") THEN
              CreateFAInventory(GenJnlLine); //RKY
        GLEntry."Gen. Posting Type" := "Gen. Posting Type";
        GLEntry."Bal. Account Type" := "Bal. Account Type";
        GLEntry."Bal. Account No." := "Bal. Account No.";
        InitVAT(GenJnlLine,GLEntry,VATPostingSetup);
        GLEntry2 := GLEntry;
        FAJnlPostLine.GenJnlPostLine(
          GenJnlLine,GLEntry2.Amount,GLEntry2."VAT Amount",NextTransactionNo,NextEntryNo,GLReg."No.");
        ShortcutDim1Code := "Shortcut Dimension 1 Code";
        ShortcutDim2Code := "Shortcut Dimension 2 Code";
        DimensionSetID := "Dimension Set ID";
        Correction2 := Correction;
      END;
      WITH TempFAGLPostBuf DO
        IF FAJnlPostLine.FindFirstGLAcc(TempFAGLPostBuf) THEN
          REPEAT
            GenJnlLine."Shortcut Dimension 1 Code" := "Global Dimension 1 Code";
            GenJnlLine."Shortcut Dimension 2 Code" := "Global Dimension 2 Code";
            GenJnlLine."Dimension Set ID" := "Dimension Set ID";
            GenJnlLine.Correction := Correction;
            FADimAlreadyChecked := "FA Posting Group" <> '';
            CheckDimValueForDisposal(GenJnlLine,"Account No.");
            IF "Original General Journal Line" THEN
              InitGLEntry(GenJnlLine,GLEntry,"Account No.",Amount,GLEntry2."Additional-Currency Amount",TRUE,TRUE)
            ELSE BEGIN
              CheckNonAddCurrCodeOccurred('');
              InitGLEntry(GenJnlLine,GLEntry,"Account No.",Amount,0,FALSE,TRUE);
            END;
            FADimAlreadyChecked := FALSE;
            GLEntry.CopyPostingGroupsFromGLEntry(GLEntry2);
            GLEntry."VAT Amount" := GLEntry2."VAT Amount";
            GLEntry."Bal. Account Type" := GLEntry2."Bal. Account Type";
            GLEntry."Bal. Account No." := GLEntry2."Bal. Account No.";
            GLEntry."FA Entry Type" := "FA Entry Type";
            GLEntry."FA Entry No." := "FA Entry No.";
            IF "Net Disposal" THEN
              NetDisposalNo := NetDisposalNo + 1
            ELSE
              NetDisposalNo := 0;
            IF "Automatic Entry" AND NOT "Net Disposal" THEN
              FAAutomaticEntry.AdjustGLEntry(GLEntry);
            IF NetDisposalNo > 1 THEN
              GLEntry."VAT Amount" := 0;
            IF "FA Posting Group" <> '' THEN BEGIN
              FAGLPostBuf := TempFAGLPostBuf;
              FAGLPostBuf."Entry No." := NextEntryNo;
              FAGLPostBuf.INSERT;
            END;
            InsertGLEntry(GenJnlLine,GLEntry,TRUE);
            IF (VATEntryGLEntryNo = 0) AND (GLEntry."Gen. Posting Type" <> GLEntry."Gen. Posting Type"::" ") THEN
              VATEntryGLEntryNo := GLEntry."Entry No.";
          UNTIL FAJnlPostLine.GetNextGLAcc(TempFAGLPostBuf) = 0;
      GenJnlLine."Shortcut Dimension 1 Code" := ShortcutDim1Code;
      GenJnlLine."Shortcut Dimension 2 Code" := ShortcutDim2Code;
      GenJnlLine."Dimension Set ID" := DimensionSetID;
      GenJnlLine.Correction := Correction2;
      GLEntry := GLEntry2;
      IF VATEntryGLEntryNo = 0 THEN
        VATEntryGLEntryNo := GLEntry."Entry No.";
      TempGLEntryBuf."Entry No." := VATEntryGLEntryNo; // Used later in InsertVAT(): GLEntryVATEntryLink.InsertLink(TempGLEntryBuf."Entry No.",VATEntry."Entry No.")
      PostVAT(GenJnlLine,GLEntry,VATPostingSetup);

      FAJnlPostLine.UpdateRegNo(GLReg."No.");
      GenJnlLine.OnMoveGenJournalLine(GLEntry.RECORDID);
    END;

    LOCAL PROCEDURE PostICPartner@63(GenJnlLine@1002 : Record 81);
    VAR
      ICPartner@1001 : Record 413;
      AccountNo@1000 : Code[20];
    BEGIN
      WITH GenJnlLine DO BEGIN
        IF "Account No." <> ICPartner.Code THEN
          ICPartner.GET("Account No.");
        IF NOT IsReversal THEN BEGIN
          IF ("Document Type" = "Document Type"::"Credit Memo") XOR (Amount > 0) THEN BEGIN
            ICPartner.TESTFIELD("Receivables Account");
            AccountNo := ICPartner."Receivables Account";
          END ELSE BEGIN
            ICPartner.TESTFIELD("Payables Account");
            AccountNo := ICPartner."Payables Account";
          END;
        END ELSE
          IF Amount < 0 THEN BEGIN
            ICPartner.TESTFIELD("Receivables Account");
            AccountNo := ICPartner."Receivables Account";
          END ELSE BEGIN
            ICPartner.TESTFIELD("Payables Account");
            AccountNo := ICPartner."Payables Account";
          END;

        CreateGLEntryBalAcc(
          GenJnlLine,AccountNo,"Amount (LCY)","Source Currency Amount",
          "Bal. Account Type","Bal. Account No.");
      END;
    END;

    LOCAL PROCEDURE PostJob@78(GenJnlLine@1000 : Record 81;GLEntry@1002 : Record 17);
    VAR
      JobPostLine@1001 : Codeunit 1001;
    BEGIN
      IF JobLine THEN BEGIN
        JobLine := FALSE;
        JobPostLine.PostGenJnlLine(GenJnlLine,GLEntry);
      END;
    END;

    [Internal]
    PROCEDURE StartPosting@24(GenJnlLine@1001 : Record 81);
    VAR
      GenJnlTemplate@1000 : Record 80;
      AccountingPeriod@1002 : Record 50;
    BEGIN
      OnBeforeStartPosting(GenJnlLine);

      WITH GenJnlLine DO BEGIN
        InitNextEntryNo;

        InitLastDocDate(GenJnlLine);
        CurrentBalance := 0;

        AccountingPeriod.RESET;
        AccountingPeriod.SETCURRENTKEY(Closed);
        AccountingPeriod.SETRANGE(Closed,FALSE);
        AccountingPeriod.FINDFIRST;
        FiscalYearStartDate := AccountingPeriod."Starting Date";

        GetGLSetup;

        IF NOT GenJnlTemplate.GET("Journal Template Name") THEN
          GenJnlTemplate.INIT;

        VATEntry.LOCKTABLE;
        IF VATEntry.FINDLAST THEN
          NextVATEntryNo := VATEntry."Entry No." + 1
        ELSE
          NextVATEntryNo := 1;
        NextConnectionNo := 1;
        FirstNewVATEntryNo := NextVATEntryNo;

        WHTEntry.LOCKTABLE;
        IF WHTEntry.FINDLAST THEN
          NextWHTEntryNo := WHTEntry."Entry No." + 1
        ELSE
          NextWHTEntryNo := 1;
        IF NOT GLSetup."Enable WHT" THEN
          NextWHTEntryNo := 0;

        GLReg.LOCKTABLE;
        IF EntryType = GlobalGLEntry."Entry Type"::Definitive THEN BEGIN
          IF GLReg.FINDLAST THEN
            GLReg."No." := GLReg."No." + 1
          ELSE
            GLReg."No." := 1;
        END ELSE BEGIN
          IF GLReg.FINDFIRST THEN BEGIN
            GLReg."No." := GLReg."No." - 1;
            IF GLReg."No." = 0 THEN
              GLReg."No." := -1;
          END ELSE
            GLReg."No." := -1;
        END;
        GLReg.INIT;
        GLReg."From Entry No." := NextEntryNo;
        IF EntryType = GlobalGLEntry."Entry Type"::Definitive THEN
          GLReg."From VAT Entry No." := NextVATEntryNo;
        GLReg."From WHT Entry No." := NextWHTEntryNo;
        GLReg."Creation Date" := TODAY;
        GLReg."Source Code" := "Source Code";
        GLReg."Journal Batch Name" := "Journal Batch Name";
        GLReg."User ID" := USERID;
        IsGLRegInserted := FALSE;

        OnAfterInitGLRegister(GLReg,GenJnlLine);

        GetCurrencyExchRate(GenJnlLine);
        TempGLEntryBuf.DELETEALL;
        CalculateCurrentBalance(
          "Account No.","Bal. Account No.",IncludeVATAmount,"Amount (LCY)","VAT Amount");
      END;
    END;

    [Internal]
    PROCEDURE ContinuePosting@155(GenJnlLine@1000 : Record 81);
    BEGIN
      OnBeforeContinuePosting(GenJnlLine);

      IF NextTransactionNoNeeded(GenJnlLine) THEN BEGIN
        CheckPostUnrealizedVAT(GenJnlLine,FALSE);
        IF EntryType = GlobalGLEntry."Entry Type"::Definitive THEN
          NextTransactionNo := NextTransactionNo + 1
        ELSE
          NextTransactionNo := NextTransactionNo - 1;
        InitLastDocDate(GenJnlLine);
        FirstNewVATEntryNo := NextVATEntryNo;
      END;

      GetCurrencyExchRate(GenJnlLine);
      TempGLEntryBuf.DELETEALL;
      CalculateCurrentBalance(
        GenJnlLine."Account No.",GenJnlLine."Bal. Account No.",GenJnlLine.IncludeVATAmount,
        GenJnlLine."Amount (LCY)",GenJnlLine."VAT Amount");
    END;

    LOCAL PROCEDURE NextTransactionNoNeeded@152(GenJnlLine@1000 : Record 81) : Boolean;
    VAR
      NewTransaction@1001 : Boolean;
    BEGIN
      WITH GenJnlLine DO BEGIN
        NewTransaction :=
          (LastDocType <> "Document Type") OR (LastDocNo <> "Document No.") OR
          (LastDate <> "Posting Date") OR ((CurrentBalance = 0) AND (TotalAddCurrAmount = 0)) AND NOT "System-Created Entry";
        IF NOT NewTransaction THEN
          OnNextTransactionNoNeeded(GenJnlLine,LastDocType,LastDocNo,LastDate,CurrentBalance,TotalAddCurrAmount,NewTransaction);
        EXIT(NewTransaction);
      END;
    END;

    [Internal]
    PROCEDURE FinishPosting@25() IsTransactionConsistent : Boolean;
    VAR
      CostAccSetup@1003 : Record 1108;
      TransferGlEntriesToCA@1004 : Codeunit 1105;
    BEGIN
      IsTransactionConsistent :=
        (BalanceCheckAmount = 0) AND (BalanceCheckAmount2 = 0) AND
        (BalanceCheckAddCurrAmount = 0) AND (BalanceCheckAddCurrAmount2 = 0);

      IF SourceCode.Simulation THEN BEGIN
        IF TempGLEntryBuf.FINDLAST THEN BEGIN
          REPEAT
            GlobalGLEntry := TempGLEntryBuf;
            IF AddCurrencyCode = '' THEN BEGIN
              GlobalGLEntry."Additional-Currency Amount" := 0;
              GlobalGLEntry."Add.-Currency Debit Amount" := 0;
              GlobalGLEntry."Add.-Currency Credit Amount" := 0;
            END;
            GlobalGLEntry.INSERT(TRUE);
          UNTIL TempGLEntryBuf.NEXT(-1) = 0;
          UpdateGLRegister(GlobalGLEntry,GLReg,IsTransactionConsistent);
        END
      END ELSE
        IF TempGLEntryBuf.FINDSET THEN BEGIN
          REPEAT
            GlobalGLEntry := TempGLEntryBuf;
            IF AddCurrencyCode = '' THEN BEGIN
              GlobalGLEntry."Additional-Currency Amount" := 0;
              GlobalGLEntry."Add.-Currency Debit Amount" := 0;
              GlobalGLEntry."Add.-Currency Credit Amount" := 0;
            END;
            GlobalGLEntry."Prior-Year Entry" := GlobalGLEntry."Posting Date" < FiscalYearStartDate;
            GlobalGLEntry.INSERT(TRUE);
            OnAfterInsertGlobalGLEntry(GlobalGLEntry);
          UNTIL TempGLEntryBuf.NEXT = 0;
          UpdateGLRegister(GlobalGLEntry,GLReg,IsTransactionConsistent);
        END;
      GlobalGLEntry.CONSISTENT(IsTransactionConsistent);

      IF CostAccSetup.GET THEN
        IF CostAccSetup."Auto Transfer from G/L" THEN
          TransferGlEntriesToCA.GetGLEntries;

      FirstEntryNo := 0;
    END;

    LOCAL PROCEDURE PostUnrealizedVAT@64(GenJnlLine@1000 : Record 81);
    BEGIN
      IF CheckUnrealizedCust THEN BEGIN
        CustUnrealizedVAT(GenJnlLine,UnrealizedCustLedgEntry,UnrealizedRemainingAmountCust);
        CheckUnrealizedCust := FALSE;
      END;
      IF CheckUnrealizedVend THEN BEGIN
        VendUnrealizedVAT(GenJnlLine,UnrealizedVendLedgEntry,UnrealizedRemainingAmountVend);
        CheckUnrealizedVend := FALSE;
      END;
    END;

    LOCAL PROCEDURE CheckPostUnrealizedVAT@41(GenJnlLine@1000 : Record 81;CheckCurrentBalance@1001 : Boolean);
    BEGIN
      IF CheckCurrentBalance AND (CurrentBalance = 0) OR NOT CheckCurrentBalance THEN
        PostUnrealizedVAT(GenJnlLine)
    END;

    LOCAL PROCEDURE InitGLEntry@3(GenJnlLine@1008 : Record 81;VAR GLEntry@1009 : Record 17;GLAccNo@1000 : Code[20];Amount@1001 : Decimal;AmountAddCurr@1002 : Decimal;UseAmountAddCurr@1003 : Boolean;SystemCreatedEntry@1004 : Boolean);
    VAR
      GLAcc@1007 : Record 15;
    BEGIN
      IF GLAccNo <> '' THEN BEGIN
        GLAcc.GET(GLAccNo);
        GLAcc.TESTFIELD(Blocked,FALSE);
        GLAcc.TESTFIELD("Account Type",GLAcc."Account Type"::Posting);

        // Check the Value Posting field on the G/L Account if it is not checked already in Codeunit 11
        IF (NOT
            ((GLAccNo = GenJnlLine."Account No.") AND
             (GenJnlLine."Account Type" = GenJnlLine."Account Type"::"G/L Account")) OR
            ((GLAccNo = GenJnlLine."Bal. Account No.") AND
             (GenJnlLine."Bal. Account Type" = GenJnlLine."Bal. Account Type"::"G/L Account"))) AND
           NOT FADimAlreadyChecked
        THEN
          CheckGLAccDimError(GenJnlLine,GLAccNo);
      END;

      GLEntry.INIT;
      GLEntry."Entry Type" := EntryType;
      GLEntry.CopyFromGenJnlLine(GenJnlLine);
      //Damo
      GLEntry."Responsibility Center LG" := GenJnlLine."Responsibility Center LG";
      //Damo
      GLEntry."Entry No." := NextEntryNo;
      GenJnlLine."Entry No." := NextEntryNo;
      GLEntry."Transaction No." := NextTransactionNo;
      GLEntry."G/L Account No." := GLAccNo;
      GLEntry."System-Created Entry" := SystemCreatedEntry;
      GLEntry.Amount := Amount;
      UseVendExchRate := FALSE;
      GLEntry."Additional-Currency Amount" :=
        GLCalcAddCurrency(Amount,AmountAddCurr,GLEntry."Additional-Currency Amount",UseAmountAddCurr,GenJnlLine,UseVendExchRate);
    END;

    LOCAL PROCEDURE InitGLEntryVAT@113(GenJnlLine@1004 : Record 81;AccNo@1003 : Code[20];BalAccNo@1008 : Code[20];Amount@1002 : Decimal;AmountAddCurr@1001 : Decimal;UseAmtAddCurr@1007 : Boolean);
    VAR
      GLEntry@1005 : Record 17;
    BEGIN
      IF UseAmtAddCurr THEN
        InitGLEntry(GenJnlLine,GLEntry,AccNo,Amount,AmountAddCurr,TRUE,TRUE)
      ELSE BEGIN
        InitGLEntry(GenJnlLine,GLEntry,AccNo,Amount,0,FALSE,TRUE);
        GLEntry."Additional-Currency Amount" := AmountAddCurr;
        GLEntry."Bal. Account No." := BalAccNo;
      END;
      SummarizeVAT(GLSetup."Summarize G/L Entries",GLEntry);
    END;

    LOCAL PROCEDURE InitGLEntryVATCopy@116(GenJnlLine@1001 : Record 81;AccNo@1003 : Code[20];BalAccNo@1007 : Code[20];Amount@1004 : Decimal;AmountAddCurr@1005 : Decimal;VATEntry@1008 : Record 254) : Integer;
    VAR
      GLEntry@1000 : Record 17;
    BEGIN
      InitGLEntry(GenJnlLine,GLEntry,AccNo,Amount,0,FALSE,TRUE);
      GLEntry."Additional-Currency Amount" := AmountAddCurr;
      GLEntry."Bal. Account No." := BalAccNo;
      GLEntry.CopyPostingGroupsFromVATEntry(VATEntry);
      SummarizeVAT(GLSetup."Summarize G/L Entries",GLEntry);

      EXIT(GLEntry."Entry No.");
    END;

    [External]
    PROCEDURE InsertGLEntry@2(GenJnlLine@1001 : Record 81;GLEntry@1002 : Record 17;CalcAddCurrResiduals@1000 : Boolean);
    BEGIN
      WITH GLEntry DO BEGIN
        TESTFIELD("G/L Account No.");

        IF Amount <> ROUND(Amount) THEN
          FIELDERROR(
            Amount,
            STRSUBSTNO(NeedsRoundingErr,Amount));

        UpdateCheckAmounts(
          "Posting Date",Amount,"Additional-Currency Amount",
          BalanceCheckAmount,BalanceCheckAmount2,BalanceCheckAddCurrAmount,BalanceCheckAddCurrAmount2);

        UpdateDebitCredit(GenJnlLine.Correction);
        "Entry Type" := EntryType;
      END;

      TempGLEntryBuf := GLEntry;

      OnBeforeInsertGLEntryBuffer(TempGLEntryBuf,GenJnlLine);

      TempGLEntryBuf.INSERT;

      IF FirstEntryNo = 0 THEN
        FirstEntryNo := TempGLEntryBuf."Entry No.";
      IF EntryType = GLEntry."Entry Type"::Definitive THEN
        NextEntryNo := NextEntryNo + 1
      ELSE
        NextEntryNo := NextEntryNo - 1;

      IF CalcAddCurrResiduals THEN
        HandleAddCurrResidualGLEntry(GenJnlLine,GLEntry.Amount,GLEntry."Additional-Currency Amount");
    END;

    LOCAL PROCEDURE CreateGLEntry@112(GenJnlLine@1005 : Record 81;AccNo@1004 : Code[20];Amount@1003 : Decimal;AmountAddCurr@1002 : Decimal;UseAmountAddCurr@1006 : Boolean);
    VAR
      GLEntry@1000 : Record 17;
    BEGIN
      IF UseAmountAddCurr THEN
        InitGLEntry(GenJnlLine,GLEntry,AccNo,Amount,AmountAddCurr,TRUE,TRUE)
      ELSE BEGIN
        InitGLEntry(GenJnlLine,GLEntry,AccNo,Amount,0,FALSE,TRUE);
        GLEntry."Additional-Currency Amount" := AmountAddCurr;
      END;
      InsertGLEntry(GenJnlLine,GLEntry,TRUE);
    END;

    LOCAL PROCEDURE CreateGLEntryBalAcc@126(GenJnlLine@1005 : Record 81;AccNo@1004 : Code[20];Amount@1003 : Decimal;AmountAddCurr@1002 : Decimal;BalAccType@1008 : Option;BalAccNo@1007 : Code[20]);
    VAR
      GLEntry@1000 : Record 17;
    BEGIN
      InitGLEntry(GenJnlLine,GLEntry,AccNo,Amount,AmountAddCurr,TRUE,TRUE);
      GLEntry."Bal. Account Type" := BalAccType;
      GLEntry."Bal. Account No." := BalAccNo;
      InsertGLEntry(GenJnlLine,GLEntry,TRUE);
      GenJnlLine.OnMoveGenJournalLine(GLEntry.RECORDID);
    END;

    LOCAL PROCEDURE CreateGLEntryGainLoss@26(GenJnlLine@1005 : Record 81;AccNo@1004 : Code[20];Amount@1003 : Decimal;UseAmountAddCurr@1006 : Boolean);
    VAR
      GLEntry@1000 : Record 17;
    BEGIN
      InitGLEntry(GenJnlLine,GLEntry,AccNo,Amount,0,UseAmountAddCurr,TRUE);
      InsertGLEntry(GenJnlLine,GLEntry,TRUE);
    END;

    LOCAL PROCEDURE CreateGLEntryVAT@117(GenJnlLine@1004 : Record 81;AccNo@1003 : Code[20];Amount@1002 : Decimal;AmountAddCurr@1001 : Decimal;VATAmount@1005 : Decimal;DtldCVLedgEntryBuf@1006 : Record 383);
    VAR
      GLEntry@1000 : Record 17;
    BEGIN
      InitGLEntry(GenJnlLine,GLEntry,AccNo,Amount,0,FALSE,TRUE);
      GLEntry."Additional-Currency Amount" := AmountAddCurr;
      GLEntry."VAT Amount" := VATAmount;
      GLEntry.CopyPostingGroupsFromDtldCVBuf(DtldCVLedgEntryBuf,DtldCVLedgEntryBuf."Gen. Posting Type");
      InsertGLEntry(GenJnlLine,GLEntry,TRUE);
      InsertVATEntriesFromTemp(DtldCVLedgEntryBuf,GLEntry);
    END;

    LOCAL PROCEDURE CreateGLEntryVATCollectAdj@110(GenJnlLine@1004 : Record 81;AccNo@1003 : Code[20];Amount@1002 : Decimal;AmountAddCurr@1001 : Decimal;VATAmount@1005 : Decimal;DtldCVLedgEntryBuf@1006 : Record 383;VAR AdjAmount@1007 : ARRAY [4] OF Decimal);
    VAR
      GLEntry@1000 : Record 17;
    BEGIN
      InitGLEntry(GenJnlLine,GLEntry,AccNo,Amount,0,FALSE,TRUE);
      GLEntry."Additional-Currency Amount" := AmountAddCurr;
      GLEntry."VAT Amount" := VATAmount;
      GLEntry.CopyPostingGroupsFromDtldCVBuf(DtldCVLedgEntryBuf,DtldCVLedgEntryBuf."Gen. Posting Type");
      InsertGLEntry(GenJnlLine,GLEntry,TRUE);
      CollectAdjustment(AdjAmount,GLEntry.Amount,GLEntry."Additional-Currency Amount");
      InsertVATEntriesFromTemp(DtldCVLedgEntryBuf,GLEntry);
    END;

    LOCAL PROCEDURE CreateGLEntryFromVATEntry@22(GenJnlLine@1000 : Record 81;VATAccNo@1002 : Code[20];Amount@1003 : Decimal;AmountAddCurr@1004 : Decimal;VATEntry@1005 : Record 254) : Integer;
    VAR
      GLEntry@1001 : Record 17;
    BEGIN
      InitGLEntry(GenJnlLine,GLEntry,VATAccNo,Amount,0,FALSE,TRUE);
      GLEntry."Additional-Currency Amount" := AmountAddCurr;
      GLEntry.CopyPostingGroupsFromVATEntry(VATEntry);
      InsertGLEntry(GenJnlLine,GLEntry,TRUE);
      EXIT(GLEntry."Entry No.");
    END;

    LOCAL PROCEDURE CreateDeferralScheduleFromGL@386(VAR GenJournalLine@1000 : Record 81;IsBalancing@1001 : Boolean);
    BEGIN
      WITH GenJournalLine DO
        IF ("Account No." <> '') AND ("Deferral Code" <> '') THEN
          IF (("Account Type" IN ["Account Type"::Customer,"Account Type"::Vendor]) AND ("Source Code" = GLSourceCode)) OR
             ("Account Type" IN ["Account Type"::"G/L Account","Account Type"::"Bank Account"])
          THEN BEGIN
            IF NOT IsBalancing THEN
              CODEUNIT.RUN(CODEUNIT::"Exchange Acc. G/L Journal Line",GenJournalLine);
            DeferralUtilities.CreateScheduleFromGL(GenJournalLine,FirstEntryNo);
          END;
    END;

    LOCAL PROCEDURE UpdateCheckAmounts@98(PostingDate@1000 : Date;Amount@1005 : Decimal;AddCurrAmount@1006 : Decimal;VAR BalanceCheckAmount@1001 : Decimal;VAR BalanceCheckAmount2@1002 : Decimal;VAR BalanceCheckAddCurrAmount@1003 : Decimal;VAR BalanceCheckAddCurrAmount2@1004 : Decimal);
    BEGIN
      IF PostingDate = NORMALDATE(PostingDate) THEN BEGIN
        BalanceCheckAmount :=
          BalanceCheckAmount + Amount * ((PostingDate - 01010000D) MOD 99 + 1);
        BalanceCheckAmount2 :=
          BalanceCheckAmount2 + Amount * ((PostingDate - 01010000D) MOD 98 + 1);
      END ELSE BEGIN
        BalanceCheckAmount :=
          BalanceCheckAmount + Amount * ((NORMALDATE(PostingDate) - 01010000D + 50) MOD 99 + 1);
        BalanceCheckAmount2 :=
          BalanceCheckAmount2 + Amount * ((NORMALDATE(PostingDate) - 01010000D + 50) MOD 98 + 1);
      END;

      IF AddCurrencyCode <> '' THEN
        IF PostingDate = NORMALDATE(PostingDate) THEN BEGIN
          BalanceCheckAddCurrAmount :=
            BalanceCheckAddCurrAmount + AddCurrAmount * ((PostingDate - 01010000D) MOD 99 + 1);
          BalanceCheckAddCurrAmount2 :=
            BalanceCheckAddCurrAmount2 + AddCurrAmount * ((PostingDate - 01010000D) MOD 98 + 1);
        END ELSE BEGIN
          BalanceCheckAddCurrAmount :=
            BalanceCheckAddCurrAmount +
            AddCurrAmount * ((NORMALDATE(PostingDate) - 01010000D + 50) MOD 99 + 1);
          BalanceCheckAddCurrAmount2 :=
            BalanceCheckAddCurrAmount2 +
            AddCurrAmount * ((NORMALDATE(PostingDate) - 01010000D + 50) MOD 98 + 1);
        END
      ELSE BEGIN
        BalanceCheckAddCurrAmount := 0;
        BalanceCheckAddCurrAmount2 := 0;
      END;
    END;

    LOCAL PROCEDURE CalcPmtDiscPossible@71(GenJnlLine@1000 : Record 81;VAR CVLedgEntryBuf@1001 : Record 382);
    BEGIN
      WITH GenJnlLine DO
        IF "Amount (LCY)" <> 0 THEN BEGIN
          IF (CVLedgEntryBuf."Pmt. Discount Date" >= CVLedgEntryBuf."Posting Date") OR
             (CVLedgEntryBuf."Pmt. Discount Date" = 0D)
          THEN BEGIN
            IF GLSetup."Pmt. Disc. Excl. VAT" THEN BEGIN
              IF "Sales/Purch. (LCY)" = 0 THEN
                CVLedgEntryBuf."Original Pmt. Disc. Possible" :=
                  ("Amount (LCY)" + TotalVATAmountOnJnlLines(GenJnlLine)) * Amount / "Amount (LCY)"
              ELSE
                CVLedgEntryBuf."Original Pmt. Disc. Possible" := "Sales/Purch. (LCY)" * Amount / "Amount (LCY)"
            END ELSE
              CVLedgEntryBuf."Original Pmt. Disc. Possible" := Amount;
            CVLedgEntryBuf."Original Pmt. Disc. Possible" :=
              ROUND(
                CVLedgEntryBuf."Original Pmt. Disc. Possible" * "Payment Discount %" / 100,AmountRoundingPrecision);
          END;
          CVLedgEntryBuf."Remaining Pmt. Disc. Possible" := CVLedgEntryBuf."Original Pmt. Disc. Possible";
        END;
    END;

    LOCAL PROCEDURE CalcPmtTolerancePossible@72(GenJnlLine@1003 : Record 81;PmtDiscountDate@1001 : Date;VAR PmtDiscToleranceDate@1002 : Date;VAR MaxPaymentTolerance@1000 : Decimal);
    BEGIN
      WITH GenJnlLine DO
        IF "Document Type" IN ["Document Type"::Invoice,"Document Type"::"Credit Memo"] THEN BEGIN
          IF PmtDiscountDate <> 0D THEN
            PmtDiscToleranceDate :=
              CALCDATE(GLSetup."Payment Discount Grace Period",PmtDiscountDate)
          ELSE
            PmtDiscToleranceDate := PmtDiscountDate;

          CASE "Account Type" OF
            "Account Type"::Customer:
              PaymentToleranceMgt.CalcMaxPmtTolerance(
                "Document Type","Currency Code",Amount,"Amount (LCY)",1,MaxPaymentTolerance);
            "Account Type"::Vendor:
              PaymentToleranceMgt.CalcMaxPmtTolerance(
                "Document Type","Currency Code",Amount,"Amount (LCY)",-1,MaxPaymentTolerance);
            //<<LPC-CA JOPS102419
            "Account Type"::Employee:
              PaymentToleranceMgt.CalcMaxPmtTolerance(
                "Document Type","Currency Code",Amount,"Amount (LCY)",-1,MaxPaymentTolerance);
            //>>LPC-CA JOPS102419
          END;
        END;
    END;

    LOCAL PROCEDURE CalcPmtTolerance@61(VAR NewCVLedgEntryBuf@1008 : Record 382;VAR OldCVLedgEntryBuf@1007 : Record 382;VAR OldCVLedgEntryBuf2@1006 : Record 382;VAR DtldCVLedgEntryBuf@1005 : Record 383;GenJnlLine@1004 : Record 81;VAR PmtTolAmtToBeApplied@1012 : Decimal;NextTransactionNo@1001 : Integer;FirstNewVATEntryNo@1000 : Integer);
    VAR
      PmtTol@1011 : Decimal;
      PmtTolLCY@1010 : Decimal;
      PmtTolAddCurr@1009 : Decimal;
    BEGIN
      IF OldCVLedgEntryBuf2."Accepted Payment Tolerance" = 0 THEN
        EXIT;

      PmtTol := -OldCVLedgEntryBuf2."Accepted Payment Tolerance";
      PmtTolAmtToBeApplied := PmtTolAmtToBeApplied + PmtTol;
      PmtTolLCY :=
        ROUND(
          (NewCVLedgEntryBuf."Original Amount" + PmtTol) / NewCVLedgEntryBuf."Original Currency Factor") -
        NewCVLedgEntryBuf."Original Amt. (LCY)";

      OldCVLedgEntryBuf."Accepted Payment Tolerance" := 0;
      OldCVLedgEntryBuf."Pmt. Tolerance (LCY)" := -PmtTolLCY;

      IF NewCVLedgEntryBuf."Currency Code" = AddCurrencyCode THEN
        PmtTolAddCurr := PmtTol
      ELSE
        PmtTolAddCurr := CalcLCYToAddCurr(PmtTolLCY);

      IF NOT GLSetup."Pmt. Disc. Excl. VAT" AND GLSetup."Adjust for Payment Disc." AND (PmtTolLCY <> 0) THEN
        CalcPmtDiscIfAdjVAT(
          NewCVLedgEntryBuf,OldCVLedgEntryBuf2,DtldCVLedgEntryBuf,GenJnlLine,PmtTolLCY,PmtTolAddCurr,
          NextTransactionNo,FirstNewVATEntryNo,DtldCVLedgEntryBuf."Entry Type"::"Payment Tolerance (VAT Excl.)");

      DtldCVLedgEntryBuf.InitDtldCVLedgEntryBuf(
        GenJnlLine,NewCVLedgEntryBuf,DtldCVLedgEntryBuf,
        DtldCVLedgEntryBuf."Entry Type"::"Payment Tolerance",PmtTol,PmtTolLCY,PmtTolAddCurr,0,0,0);
    END;

    LOCAL PROCEDURE CalcPmtDisc@50(VAR NewCVLedgEntryBuf@1000 : Record 382;VAR OldCVLedgEntryBuf@1001 : Record 382;VAR OldCVLedgEntryBuf2@1002 : Record 382;VAR DtldCVLedgEntryBuf@1003 : Record 383;GenJnlLine@1004 : Record 81;PmtTolAmtToBeApplied@1012 : Decimal;ApplnRoundingPrecision@1006 : Decimal;NextTransactionNo@1007 : Integer;FirstNewVATEntryNo@1008 : Integer);
    VAR
      PmtDisc@1009 : Decimal;
      PmtDiscLCY@1010 : Decimal;
      PmtDiscAddCurr@1011 : Decimal;
      MinimalPossibleLiability@1014 : Decimal;
      PaymentExceedsLiability@1005 : Boolean;
      ToleratedPaymentExceedsLiability@1013 : Boolean;
    BEGIN
      MinimalPossibleLiability := ABS(OldCVLedgEntryBuf2."Remaining Amount" - OldCVLedgEntryBuf2."Remaining Pmt. Disc. Possible");
      PaymentExceedsLiability := ABS(OldCVLedgEntryBuf2."Amount to Apply") >= MinimalPossibleLiability;
      ToleratedPaymentExceedsLiability := ABS(NewCVLedgEntryBuf."Remaining Amount" + PmtTolAmtToBeApplied) >= MinimalPossibleLiability;

      IF (PaymentToleranceMgt.CheckCalcPmtDisc(NewCVLedgEntryBuf,OldCVLedgEntryBuf2,ApplnRoundingPrecision,TRUE,TRUE) AND
          ((OldCVLedgEntryBuf2."Amount to Apply" = 0) OR PaymentExceedsLiability) OR
          (PaymentToleranceMgt.CheckCalcPmtDisc(NewCVLedgEntryBuf,OldCVLedgEntryBuf2,ApplnRoundingPrecision,FALSE,FALSE) AND
           (OldCVLedgEntryBuf2."Amount to Apply" <> 0) AND PaymentExceedsLiability AND ToleratedPaymentExceedsLiability))
      THEN BEGIN
        PmtDisc := -OldCVLedgEntryBuf2."Remaining Pmt. Disc. Possible";
        PmtDiscLCY :=
          ROUND(
            (NewCVLedgEntryBuf."Original Amount" + PmtDisc) / NewCVLedgEntryBuf."Original Currency Factor") -
          NewCVLedgEntryBuf."Original Amt. (LCY)";

        OldCVLedgEntryBuf."Pmt. Disc. Given (LCY)" := -PmtDiscLCY;

        IF (NewCVLedgEntryBuf."Currency Code" = AddCurrencyCode) AND (AddCurrencyCode <> '') THEN
          PmtDiscAddCurr := PmtDisc
        ELSE
          PmtDiscAddCurr := CalcLCYToAddCurr(PmtDiscLCY);

        IF NOT GLSetup."Pmt. Disc. Excl. VAT" AND GLSetup."Adjust for Payment Disc." AND
           (PmtDiscLCY <> 0)
        THEN
          CalcPmtDiscIfAdjVAT(
            NewCVLedgEntryBuf,OldCVLedgEntryBuf2,DtldCVLedgEntryBuf,GenJnlLine,PmtDiscLCY,PmtDiscAddCurr,
            NextTransactionNo,FirstNewVATEntryNo,DtldCVLedgEntryBuf."Entry Type"::"Payment Discount (VAT Excl.)");

        DtldCVLedgEntryBuf.InitDtldCVLedgEntryBuf(
          GenJnlLine,NewCVLedgEntryBuf,DtldCVLedgEntryBuf,
          DtldCVLedgEntryBuf."Entry Type"::"Payment Discount",PmtDisc,PmtDiscLCY,PmtDiscAddCurr,0,0,0);
      END;
    END;

    LOCAL PROCEDURE CalcPmtDiscIfAdjVAT@49(VAR NewCVLedgEntryBuf@1000 : Record 382;VAR OldCVLedgEntryBuf@1001 : Record 382;VAR DtldCVLedgEntryBuf@1002 : Record 383;GenJnlLine@1003 : Record 81;VAR PmtDiscLCY2@1005 : Decimal;VAR PmtDiscAddCurr2@1006 : Decimal;NextTransactionNo@1007 : Integer;FirstNewVATEntryNo@1008 : Integer;EntryType@1009 : Integer);
    VAR
      VATEntry2@1011 : Record 254;
      VATPostingSetup@1012 : Record 325;
      TaxJurisdiction@1013 : Record 320;
      DtldCVLedgEntryBuf2@1015 : Record 383;
      OriginalAmountAddCurr@1016 : Decimal;
      PmtDiscRounding@1017 : Decimal;
      PmtDiscRoundingAddCurr@1018 : Decimal;
      PmtDiscFactorLCY@1019 : Decimal;
      PmtDiscFactorAddCurr@1020 : Decimal;
      VATBase@1021 : Decimal;
      VATBaseAddCurr@1022 : Decimal;
      VATAmount@1023 : Decimal;
      VATAmountAddCurr@1024 : Decimal;
      TotalVATAmount@1025 : Decimal;
      LastConnectionNo@1026 : Integer;
      VATEntryModifier@1027 : Integer;
    BEGIN
      IF OldCVLedgEntryBuf."Original Amt. (LCY)" = 0 THEN
        EXIT;

      IF (AddCurrencyCode = '') OR (AddCurrencyCode = OldCVLedgEntryBuf."Currency Code") THEN
        OriginalAmountAddCurr := OldCVLedgEntryBuf.Amount
      ELSE
        OriginalAmountAddCurr := CalcLCYToAddCurr(OldCVLedgEntryBuf."Original Amt. (LCY)");

      PmtDiscRounding := PmtDiscLCY2;
      PmtDiscFactorLCY := PmtDiscLCY2 / OldCVLedgEntryBuf."Original Amt. (LCY)";
      IF OriginalAmountAddCurr <> 0 THEN
        PmtDiscFactorAddCurr := PmtDiscAddCurr2 / OriginalAmountAddCurr
      ELSE
        PmtDiscFactorAddCurr := 0;
      VATEntry2.RESET;
      VATEntry2.SETCURRENTKEY("Transaction No.");
      VATEntry2.SETRANGE("Transaction No.",OldCVLedgEntryBuf."Transaction No.");
      IF OldCVLedgEntryBuf."Transaction No." = NextTransactionNo THEN
        VATEntry2.SETRANGE("Entry No.",0,FirstNewVATEntryNo - 1);
      IF VATEntry2.FINDSET THEN BEGIN
        TotalVATAmount := 0;
        LastConnectionNo := 0;
        REPEAT
          VATPostingSetup.GET(VATEntry2."VAT Bus. Posting Group",VATEntry2."VAT Prod. Posting Group");
          IF VATEntry2."VAT Calculation Type" =
             VATEntry2."VAT Calculation Type"::"Sales Tax"
          THEN BEGIN
            TaxJurisdiction.GET(VATEntry2."Tax Jurisdiction Code");
            VATPostingSetup."Adjust for Payment Discount" :=
              TaxJurisdiction."Adjust for Payment Discount";
          END;
          IF VATPostingSetup."Adjust for Payment Discount" THEN BEGIN
            IF LastConnectionNo <> VATEntry2."Sales Tax Connection No." THEN BEGIN
              IF LastConnectionNo <> 0 THEN BEGIN
                DtldCVLedgEntryBuf := DtldCVLedgEntryBuf2;
                DtldCVLedgEntryBuf."VAT Amount (LCY)" := -TotalVATAmount;
                DtldCVLedgEntryBuf.InsertDtldCVLedgEntry(DtldCVLedgEntryBuf,NewCVLedgEntryBuf,FALSE);
                InsertSummarizedVAT(GenJnlLine);
              END;

              CalcPmtDiscVATBases(VATEntry2,VATBase,VATBaseAddCurr);

              PmtDiscRounding := PmtDiscRounding + VATBase * PmtDiscFactorLCY;
              VATBase := ROUND(PmtDiscRounding - PmtDiscLCY2);
              PmtDiscLCY2 := PmtDiscLCY2 + VATBase;

              PmtDiscRoundingAddCurr := PmtDiscRoundingAddCurr + VATBaseAddCurr * PmtDiscFactorAddCurr;
              VATBaseAddCurr := ROUND(CalcLCYToAddCurr(VATBase),AddCurrency."Amount Rounding Precision");
              PmtDiscAddCurr2 := PmtDiscAddCurr2 + VATBaseAddCurr;

              DtldCVLedgEntryBuf2.INIT;
              DtldCVLedgEntryBuf2."Posting Date" := GenJnlLine."Posting Date";
              DtldCVLedgEntryBuf2."Document Date" := GenJnlLine."Document Date";
              DtldCVLedgEntryBuf2."Document Type" := GenJnlLine."Document Type";
              DtldCVLedgEntryBuf2."Document No." := GenJnlLine."Document No.";
              DtldCVLedgEntryBuf2.Amount := 0;
              DtldCVLedgEntryBuf2."Amount (LCY)" := -VATBase;
              DtldCVLedgEntryBuf2."Entry Type" := EntryType;
              CASE EntryType OF
                DtldCVLedgEntryBuf."Entry Type"::"Payment Discount Tolerance (VAT Excl.)":
                  VATEntryModifier := 1000000;
                DtldCVLedgEntryBuf."Entry Type"::"Payment Tolerance (VAT Excl.)":
                  VATEntryModifier := 2000000;
                DtldCVLedgEntryBuf."Entry Type"::"Payment Discount (VAT Excl.)":
                  VATEntryModifier := 3000000;
              END;
              DtldCVLedgEntryBuf2.CopyFromCVLedgEntryBuf(NewCVLedgEntryBuf);
              // The total payment discount in currency is posted on the entry made in
              // the function CalcPmtDisc.
              DtldCVLedgEntryBuf2."User ID" := USERID;
              DtldCVLedgEntryBuf2."Additional-Currency Amount" := -VATBaseAddCurr;
              DtldCVLedgEntryBuf2.CopyPostingGroupsFromVATEntry(VATEntry2);
              TotalVATAmount := 0;
              LastConnectionNo := VATEntry2."Sales Tax Connection No.";
            END;

            CalcPmtDiscVATAmounts(
              VATEntry2,VATBase,VATBaseAddCurr,VATAmount,VATAmountAddCurr,
              PmtDiscRounding,PmtDiscFactorLCY,PmtDiscLCY2,PmtDiscAddCurr2);

            TotalVATAmount := TotalVATAmount + VATAmount;

            IF (PmtDiscAddCurr2 <> 0) AND (PmtDiscLCY2 = 0) THEN BEGIN
              VATAmountAddCurr := VATAmountAddCurr - PmtDiscAddCurr2;
              PmtDiscAddCurr2 := 0;
            END;

            // Post VAT
            // VAT for VAT entry
            IF VATEntry2.Type <> 0 THEN
              InsertPmtDiscVATForVATEntry(
                GenJnlLine,TempVATEntry,VATEntry2,VATEntryModifier,
                VATAmount,VATAmountAddCurr,VATBase,VATBaseAddCurr,
                PmtDiscFactorLCY,PmtDiscFactorAddCurr);

            // VAT for G/L entry/entries
            InsertPmtDiscVATForGLEntry(
              GenJnlLine,DtldCVLedgEntryBuf,NewCVLedgEntryBuf,VATEntry2,
              VATPostingSetup,TaxJurisdiction,EntryType,VATAmount,VATAmountAddCurr);
          END;
        UNTIL VATEntry2.NEXT = 0;

        IF LastConnectionNo <> 0 THEN BEGIN
          DtldCVLedgEntryBuf := DtldCVLedgEntryBuf2;
          DtldCVLedgEntryBuf."VAT Amount (LCY)" := -TotalVATAmount;
          DtldCVLedgEntryBuf.InsertDtldCVLedgEntry(DtldCVLedgEntryBuf,NewCVLedgEntryBuf,TRUE);
          InsertSummarizedVAT(GenJnlLine);
        END;
      END;
    END;

    LOCAL PROCEDURE CalcPmtDiscTolerance@60(VAR NewCVLedgEntryBuf@1008 : Record 382;VAR OldCVLedgEntryBuf@1007 : Record 382;VAR OldCVLedgEntryBuf2@1006 : Record 382;VAR DtldCVLedgEntryBuf@1005 : Record 383;GenJnlLine@1004 : Record 81;NextTransactionNo@1001 : Integer;FirstNewVATEntryNo@1000 : Integer);
    VAR
      PmtDiscTol@1011 : Decimal;
      PmtDiscTolLCY@1010 : Decimal;
      PmtDiscTolAddCurr@1009 : Decimal;
    BEGIN
      IF NOT OldCVLedgEntryBuf2."Accepted Pmt. Disc. Tolerance" THEN
        EXIT;

      PmtDiscTol := -OldCVLedgEntryBuf2."Remaining Pmt. Disc. Possible";
      PmtDiscTolLCY :=
        ROUND(
          (NewCVLedgEntryBuf."Original Amount" + PmtDiscTol) / NewCVLedgEntryBuf."Original Currency Factor") -
        NewCVLedgEntryBuf."Original Amt. (LCY)";

      OldCVLedgEntryBuf."Pmt. Disc. Given (LCY)" := -PmtDiscTolLCY;

      IF NewCVLedgEntryBuf."Currency Code" = AddCurrencyCode THEN
        PmtDiscTolAddCurr := PmtDiscTol
      ELSE
        PmtDiscTolAddCurr := CalcLCYToAddCurr(PmtDiscTolLCY);

      IF NOT GLSetup."Pmt. Disc. Excl. VAT" AND GLSetup."Adjust for Payment Disc." AND (PmtDiscTolLCY <> 0) THEN
        CalcPmtDiscIfAdjVAT(
          NewCVLedgEntryBuf,OldCVLedgEntryBuf2,DtldCVLedgEntryBuf,GenJnlLine,PmtDiscTolLCY,PmtDiscTolAddCurr,
          NextTransactionNo,FirstNewVATEntryNo,DtldCVLedgEntryBuf."Entry Type"::"Payment Discount Tolerance (VAT Excl.)");

      DtldCVLedgEntryBuf.InitDtldCVLedgEntryBuf(
        GenJnlLine,NewCVLedgEntryBuf,DtldCVLedgEntryBuf,
        DtldCVLedgEntryBuf."Entry Type"::"Payment Discount Tolerance",PmtDiscTol,PmtDiscTolLCY,PmtDiscTolAddCurr,0,0,0);
    END;

    LOCAL PROCEDURE CalcPmtDiscVATBases@118(VATEntry2@1001 : Record 254;VAR VATBase@1002 : Decimal;VAR VATBaseAddCurr@1003 : Decimal);
    VAR
      VATEntry@1000 : Record 254;
    BEGIN
      CASE VATEntry2."VAT Calculation Type" OF
        VATEntry2."VAT Calculation Type"::"Normal VAT",
        VATEntry2."VAT Calculation Type"::"Reverse Charge VAT",
        VATEntry2."VAT Calculation Type"::"Full VAT":
          BEGIN
            VATBase :=
              VATEntry2.Base + VATEntry2."Unrealized Base";
            VATBaseAddCurr :=
              VATEntry2."Additional-Currency Base" +
              VATEntry2."Add.-Currency Unrealized Base";
          END;
        VATEntry2."VAT Calculation Type"::"Sales Tax":
          BEGIN
            VATEntry.RESET;
            VATEntry.SETCURRENTKEY("Transaction No.");
            VATEntry.SETRANGE("Transaction No.",VATEntry2."Transaction No.");
            VATEntry.SETRANGE("Sales Tax Connection No.",VATEntry2."Sales Tax Connection No.");
            VATEntry := VATEntry2;
            REPEAT
              IF VATEntry.Base < 0 THEN
                VATEntry.SETFILTER(Base,'>%1',VATEntry.Base)
              ELSE
                VATEntry.SETFILTER(Base,'<%1',VATEntry.Base);
            UNTIL NOT VATEntry.FINDLAST;
            VATEntry.RESET;
            VATBase :=
              VATEntry.Base + VATEntry."Unrealized Base";
            VATBaseAddCurr :=
              VATEntry."Additional-Currency Base" +
              VATEntry."Add.-Currency Unrealized Base";
          END;
      END;
    END;

    LOCAL PROCEDURE CalcPmtDiscVATAmounts@129(VATEntry2@1000 : Record 254;VATBase@1001 : Decimal;VATBaseAddCurr@1007 : Decimal;VAR VATAmount@1002 : Decimal;VAR VATAmountAddCurr@1003 : Decimal;VAR PmtDiscRounding@1004 : Decimal;PmtDiscFactorLCY@1005 : Decimal;VAR PmtDiscLCY2@1006 : Decimal;VAR PmtDiscAddCurr2@1008 : Decimal);
    BEGIN
      CASE VATEntry2."VAT Calculation Type" OF
        VATEntry2."VAT Calculation Type"::"Normal VAT",
        VATEntry2."VAT Calculation Type"::"Full VAT":
          IF (VATEntry2.Amount + VATEntry2."Unrealized Amount" <> 0) OR
             (VATEntry2."Additional-Currency Amount" + VATEntry2."Add.-Currency Unrealized Amt." <> 0)
          THEN BEGIN
            IF (VATBase = 0) AND
               (VATEntry2."VAT Calculation Type" <> VATEntry2."VAT Calculation Type"::"Full VAT")
            THEN
              VATAmount := 0
            ELSE BEGIN
              PmtDiscRounding :=
                PmtDiscRounding +
                (VATEntry2.Amount + VATEntry2."Unrealized Amount") * PmtDiscFactorLCY;
              VATAmount := ROUND(PmtDiscRounding - PmtDiscLCY2);
              PmtDiscLCY2 := PmtDiscLCY2 + VATAmount;
            END;
            IF (VATBaseAddCurr = 0) AND
               (VATEntry2."VAT Calculation Type" <> VATEntry2."VAT Calculation Type"::"Full VAT")
            THEN
              VATAmountAddCurr := 0
            ELSE BEGIN
              VATAmountAddCurr := ROUND(CalcLCYToAddCurr(VATAmount),AddCurrency."Amount Rounding Precision");
              PmtDiscAddCurr2 := PmtDiscAddCurr2 + VATAmountAddCurr;
            END;
          END ELSE BEGIN
            VATAmount := 0;
            VATAmountAddCurr := 0;
          END;
        VATEntry2."VAT Calculation Type"::"Reverse Charge VAT":
          BEGIN
            VATAmount :=
              ROUND((VATEntry2.Amount + VATEntry2."Unrealized Amount") * PmtDiscFactorLCY);
            VATAmountAddCurr := ROUND(CalcLCYToAddCurr(VATAmount),AddCurrency."Amount Rounding Precision");
          END;
        VATEntry2."VAT Calculation Type"::"Sales Tax":
          IF (VATEntry2.Type = VATEntry2.Type::Purchase) AND VATEntry2."Use Tax" THEN BEGIN
            VATAmount :=
              ROUND((VATEntry2.Amount + VATEntry2."Unrealized Amount") * PmtDiscFactorLCY);
            VATAmountAddCurr := ROUND(CalcLCYToAddCurr(VATAmount),AddCurrency."Amount Rounding Precision");
          END ELSE
            IF (VATEntry2.Amount + VATEntry2."Unrealized Amount" <> 0) OR
               (VATEntry2."Additional-Currency Amount" + VATEntry2."Add.-Currency Unrealized Amt." <> 0)
            THEN BEGIN
              IF VATBase = 0 THEN
                VATAmount := 0
              ELSE BEGIN
                PmtDiscRounding :=
                  PmtDiscRounding +
                  (VATEntry2.Amount + VATEntry2."Unrealized Amount") * PmtDiscFactorLCY;
                VATAmount := ROUND(PmtDiscRounding - PmtDiscLCY2);
                PmtDiscLCY2 := PmtDiscLCY2 + VATAmount;
              END;

              IF VATBaseAddCurr = 0 THEN
                VATAmountAddCurr := 0
              ELSE BEGIN
                VATAmountAddCurr := ROUND(CalcLCYToAddCurr(VATAmount),AddCurrency."Amount Rounding Precision");
                PmtDiscAddCurr2 := PmtDiscAddCurr2 + VATAmountAddCurr;
              END;
            END ELSE BEGIN
              VATAmount := 0;
              VATAmountAddCurr := 0;
            END;
      END;
    END;

    LOCAL PROCEDURE InsertPmtDiscVATForVATEntry@43(GenJnlLine@1000 : Record 81;VAR TempVATEntry@1001 : TEMPORARY Record 254;VATEntry2@1004 : Record 254;VATEntryModifier@1002 : Integer;VATAmount@1005 : Decimal;VATAmountAddCurr@1006 : Decimal;VATBase@1007 : Decimal;VATBaseAddCurr@1008 : Decimal;PmtDiscFactorLCY@1009 : Decimal;PmtDiscFactorAddCurr@1010 : Decimal);
    VAR
      TempVATEntryNo@1003 : Integer;
    BEGIN
      TempVATEntry.RESET;
      TempVATEntry.SETRANGE("Entry No.",VATEntryModifier,VATEntryModifier + 999999);
      IF TempVATEntry.FINDLAST THEN
        TempVATEntryNo := TempVATEntry."Entry No." + 1
      ELSE
        TempVATEntryNo := VATEntryModifier + 1;
      TempVATEntry := VATEntry2;
      TempVATEntry."Entry No." := TempVATEntryNo;
      TempVATEntry."Posting Date" := GenJnlLine."Posting Date";
      TempVATEntry."Document Date" := GenJnlLine."Document Date";
      TempVATEntry."Document No." := GenJnlLine."Document No.";
      TempVATEntry."External Document No." := GenJnlLine."External Document No.";
      TempVATEntry."Document Type" := GenJnlLine."Document Type";
      TempVATEntry."Source Code" := GenJnlLine."Source Code";
      TempVATEntry."Reason Code" := GenJnlLine."Reason Code";
      TempVATEntry."Transaction No." := NextTransactionNo;
      TempVATEntry."Sales Tax Connection No." := NextConnectionNo;
      TempVATEntry."Unrealized Amount" := 0;
      TempVATEntry."Unrealized Base" := 0;
      TempVATEntry."Remaining Unrealized Amount" := 0;
      TempVATEntry."Remaining Unrealized Base" := 0;
      TempVATEntry."User ID" := USERID;
      TempVATEntry."Closed by Entry No." := 0;
      TempVATEntry.Closed := FALSE;
      TempVATEntry."Internal Ref. No." := '';
      TempVATEntry.Amount := VATAmount;
      TempVATEntry."Additional-Currency Amount" := VATAmountAddCurr;
      TempVATEntry."VAT Difference" := 0;
      TempVATEntry."Add.-Curr. VAT Difference" := 0;
      TempVATEntry."Add.-Currency Unrealized Amt." := 0;
      TempVATEntry."Add.-Currency Unrealized Base" := 0;
      IF VATEntry2."Tax on Tax" THEN BEGIN
        TempVATEntry.Base :=
          ROUND((VATEntry2.Base + VATEntry2."Unrealized Base") * PmtDiscFactorLCY);
        TempVATEntry."Additional-Currency Base" :=
          ROUND(
            (VATEntry2."Additional-Currency Base" +
             VATEntry2."Add.-Currency Unrealized Base") * PmtDiscFactorAddCurr,
            AddCurrency."Amount Rounding Precision");
      END ELSE BEGIN
        TempVATEntry.Base := VATBase;
        TempVATEntry."Additional-Currency Base" := VATBaseAddCurr;
      END;

      IF AddCurrencyCode = '' THEN BEGIN
        TempVATEntry."Additional-Currency Base" := 0;
        TempVATEntry."Additional-Currency Amount" := 0;
        TempVATEntry."Add.-Currency Unrealized Amt." := 0;
        TempVATEntry."Add.-Currency Unrealized Base" := 0;
      END;
      TempVATEntry.INSERT;
    END;

    LOCAL PROCEDURE InsertPmtDiscVATForGLEntry@94(GenJnlLine@1000 : Record 81;VAR DtldCVLedgEntryBuf@1001 : Record 383;VAR NewCVLedgEntryBuf@1003 : Record 382;VATEntry2@1004 : Record 254;VAR VATPostingSetup@1005 : Record 325;VAR TaxJurisdiction@1008 : Record 320;EntryType@1002 : Integer;VATAmount@1006 : Decimal;VATAmountAddCurr@1007 : Decimal);
    BEGIN
      DtldCVLedgEntryBuf.INIT;
      DtldCVLedgEntryBuf.CopyFromCVLedgEntryBuf(NewCVLedgEntryBuf);
      CASE EntryType OF
        DtldCVLedgEntryBuf."Entry Type"::"Payment Discount (VAT Excl.)":
          DtldCVLedgEntryBuf."Entry Type" :=
            DtldCVLedgEntryBuf."Entry Type"::"Payment Discount (VAT Adjustment)";
        DtldCVLedgEntryBuf."Entry Type"::"Payment Discount Tolerance (VAT Excl.)":
          DtldCVLedgEntryBuf."Entry Type" :=
            DtldCVLedgEntryBuf."Entry Type"::"Payment Discount Tolerance (VAT Adjustment)";
        DtldCVLedgEntryBuf."Entry Type"::"Payment Tolerance (VAT Excl.)":
          DtldCVLedgEntryBuf."Entry Type" :=
            DtldCVLedgEntryBuf."Entry Type"::"Payment Tolerance (VAT Adjustment)";
      END;
      DtldCVLedgEntryBuf."Posting Date" := GenJnlLine."Posting Date";
      DtldCVLedgEntryBuf."Document Date" := GenJnlLine."Document Date";
      DtldCVLedgEntryBuf."Document Type" := GenJnlLine."Document Type";
      DtldCVLedgEntryBuf."Document No." := GenJnlLine."Document No.";
      DtldCVLedgEntryBuf.Amount := 0;
      DtldCVLedgEntryBuf."VAT Bus. Posting Group" := VATEntry2."VAT Bus. Posting Group";
      DtldCVLedgEntryBuf."VAT Prod. Posting Group" := VATEntry2."VAT Prod. Posting Group";
      DtldCVLedgEntryBuf."Tax Jurisdiction Code" := VATEntry2."Tax Jurisdiction Code";
      // The total payment discount in currency is posted on the entry made in
      // the function CalcPmtDisc.
      DtldCVLedgEntryBuf."User ID" := USERID;
      DtldCVLedgEntryBuf."Use Additional-Currency Amount" := TRUE;

      CASE VATEntry2.Type OF
        VATEntry2.Type::Purchase:
          CASE VATEntry2."VAT Calculation Type" OF
            VATEntry2."VAT Calculation Type"::"Normal VAT",
            VATEntry2."VAT Calculation Type"::"Full VAT":
              BEGIN
                InitGLEntryVAT(GenJnlLine,VATPostingSetup.GetPurchAccount(FALSE),'',
                  VATAmount,VATAmountAddCurr,FALSE);
                DtldCVLedgEntryBuf."Amount (LCY)" := -VATAmount;
                DtldCVLedgEntryBuf."Additional-Currency Amount" := -VATAmountAddCurr;
                DtldCVLedgEntryBuf.InsertDtldCVLedgEntry(DtldCVLedgEntryBuf,NewCVLedgEntryBuf,TRUE);
              END;
            VATEntry2."VAT Calculation Type"::"Reverse Charge VAT":
              BEGIN
                InitGLEntryVAT(GenJnlLine,VATPostingSetup.GetPurchAccount(FALSE),'',
                  VATAmount,VATAmountAddCurr,FALSE);
                InitGLEntryVAT(GenJnlLine,VATPostingSetup.GetRevChargeAccount(FALSE),'',
                  -VATAmount,-VATAmountAddCurr,FALSE);
              END;
            VATEntry2."VAT Calculation Type"::"Sales Tax":
              IF VATEntry2."Use Tax" THEN BEGIN
                InitGLEntryVAT(GenJnlLine,TaxJurisdiction.GetPurchAccount(FALSE),'',
                  VATAmount,VATAmountAddCurr,FALSE);
                InitGLEntryVAT(GenJnlLine,TaxJurisdiction.GetRevChargeAccount(FALSE),'',
                  -VATAmount,-VATAmountAddCurr,FALSE);
              END ELSE BEGIN
                InitGLEntryVAT(GenJnlLine,TaxJurisdiction.GetPurchAccount(FALSE),'',
                  VATAmount,VATAmountAddCurr,FALSE);
                DtldCVLedgEntryBuf."Amount (LCY)" := -VATAmount;
                DtldCVLedgEntryBuf."Additional-Currency Amount" := -VATAmountAddCurr;
                DtldCVLedgEntryBuf.InsertDtldCVLedgEntry(DtldCVLedgEntryBuf,NewCVLedgEntryBuf,TRUE);
              END;
          END;
        VATEntry2.Type::Sale:
          CASE VATEntry2."VAT Calculation Type" OF
            VATEntry2."VAT Calculation Type"::"Normal VAT",
            VATEntry2."VAT Calculation Type"::"Full VAT":
              BEGIN
                InitGLEntryVAT(
                  GenJnlLine,VATPostingSetup.GetSalesAccount(FALSE),'',
                  VATAmount,VATAmountAddCurr,FALSE);
                DtldCVLedgEntryBuf."Amount (LCY)" := -VATAmount;
                DtldCVLedgEntryBuf."Additional-Currency Amount" := -VATAmountAddCurr;
                DtldCVLedgEntryBuf.InsertDtldCVLedgEntry(DtldCVLedgEntryBuf,NewCVLedgEntryBuf,TRUE);
              END;
            VATEntry2."VAT Calculation Type"::"Reverse Charge VAT":
              ;
            VATEntry2."VAT Calculation Type"::"Sales Tax":
              BEGIN
                InitGLEntryVAT(
                  GenJnlLine,TaxJurisdiction.GetSalesAccount(FALSE),'',
                  VATAmount,VATAmountAddCurr,FALSE);
                DtldCVLedgEntryBuf."Amount (LCY)" := -VATAmount;
                DtldCVLedgEntryBuf."Additional-Currency Amount" := -VATAmountAddCurr;
                DtldCVLedgEntryBuf.InsertDtldCVLedgEntry(DtldCVLedgEntryBuf,NewCVLedgEntryBuf,TRUE);
              END;
          END;
      END;
    END;

    LOCAL PROCEDURE CalcCurrencyApplnRounding@51(VAR NewCVLedgEntryBuf@1000 : Record 382;VAR OldCVLedgEntryBuf@1001 : Record 382;VAR DtldCVLedgEntryBuf@1002 : Record 383;GenJnlLine@1003 : Record 81;ApplnRoundingPrecision@1005 : Decimal);
    VAR
      ApplnRounding@1006 : Decimal;
      ApplnRoundingLCY@1007 : Decimal;
    BEGIN
      IF ((NewCVLedgEntryBuf."Document Type" <> NewCVLedgEntryBuf."Document Type"::Payment) AND
          (NewCVLedgEntryBuf."Document Type" <> NewCVLedgEntryBuf."Document Type"::Refund)) OR
         (NewCVLedgEntryBuf."Currency Code" = OldCVLedgEntryBuf."Currency Code")
      THEN
        EXIT;

      ApplnRounding := -(NewCVLedgEntryBuf."Remaining Amount" + OldCVLedgEntryBuf."Remaining Amount");
      ApplnRoundingLCY := ROUND(ApplnRounding / NewCVLedgEntryBuf."Adjusted Currency Factor");

      IF (ApplnRounding = 0) OR (ABS(ApplnRounding) > ApplnRoundingPrecision) THEN
        EXIT;

      DtldCVLedgEntryBuf.InitDtldCVLedgEntryBuf(
        GenJnlLine,NewCVLedgEntryBuf,DtldCVLedgEntryBuf,
        DtldCVLedgEntryBuf."Entry Type"::"Appln. Rounding",ApplnRounding,ApplnRoundingLCY,ApplnRounding,0,0,0);
    END;

    LOCAL PROCEDURE FindAmtForAppln@6(VAR NewCVLedgEntryBuf@1000 : Record 382;VAR OldCVLedgEntryBuf@1001 : Record 382;VAR OldCVLedgEntryBuf2@1002 : Record 382;VAR AppliedAmount@1003 : Decimal;VAR AppliedAmountLCY@1004 : Decimal;VAR OldAppliedAmount@1005 : Decimal;ApplnRoundingPrecision@1007 : Decimal);
    BEGIN
      IF OldCVLedgEntryBuf2.GETFILTER(Positive) <> '' THEN BEGIN
        IF OldCVLedgEntryBuf2."Amount to Apply" <> 0 THEN BEGIN
          IF (PaymentToleranceMgt.CheckCalcPmtDisc(NewCVLedgEntryBuf,OldCVLedgEntryBuf2,ApplnRoundingPrecision,FALSE,FALSE) AND
              (ABS(OldCVLedgEntryBuf2."Amount to Apply") >=
               ABS(OldCVLedgEntryBuf2."Remaining Amount" - OldCVLedgEntryBuf2."Remaining Pmt. Disc. Possible")))
          THEN
            AppliedAmount := -OldCVLedgEntryBuf2."Remaining Amount"
          ELSE
            AppliedAmount := -OldCVLedgEntryBuf2."Amount to Apply"
        END ELSE
          AppliedAmount := -OldCVLedgEntryBuf2."Remaining Amount";
      END ELSE BEGIN
        IF OldCVLedgEntryBuf2."Amount to Apply" <> 0 THEN
          IF (PaymentToleranceMgt.CheckCalcPmtDisc(NewCVLedgEntryBuf,OldCVLedgEntryBuf2,ApplnRoundingPrecision,FALSE,FALSE) AND
              (ABS(OldCVLedgEntryBuf2."Amount to Apply") >=
               ABS(OldCVLedgEntryBuf2."Remaining Amount" - OldCVLedgEntryBuf2."Remaining Pmt. Disc. Possible")) AND
              (ABS(NewCVLedgEntryBuf."Remaining Amount") >=
               ABS(
                 ABSMin(
                   OldCVLedgEntryBuf2."Remaining Amount" - OldCVLedgEntryBuf2."Remaining Pmt. Disc. Possible",
                   OldCVLedgEntryBuf2."Amount to Apply")))) OR
             OldCVLedgEntryBuf."Accepted Pmt. Disc. Tolerance"
          THEN BEGIN
            AppliedAmount := -OldCVLedgEntryBuf2."Remaining Amount";
            OldCVLedgEntryBuf."Accepted Pmt. Disc. Tolerance" := FALSE;
          END ELSE
            AppliedAmount := ABSMin(NewCVLedgEntryBuf."Remaining Amount",-OldCVLedgEntryBuf2."Amount to Apply")
        ELSE
          AppliedAmount := ABSMin(NewCVLedgEntryBuf."Remaining Amount",-OldCVLedgEntryBuf2."Remaining Amount");
      END;

      IF (ABS(OldCVLedgEntryBuf2."Remaining Amount" - OldCVLedgEntryBuf2."Amount to Apply") < ApplnRoundingPrecision) AND
         (ApplnRoundingPrecision <> 0) AND
         (OldCVLedgEntryBuf2."Amount to Apply" <> 0)
      THEN
        AppliedAmount := AppliedAmount - (OldCVLedgEntryBuf2."Remaining Amount" - OldCVLedgEntryBuf2."Amount to Apply");

      IF NewCVLedgEntryBuf."Currency Code" = OldCVLedgEntryBuf2."Currency Code" THEN BEGIN
        AppliedAmountLCY := ROUND(AppliedAmount / OldCVLedgEntryBuf."Original Currency Factor");
        OldAppliedAmount := AppliedAmount;
      END ELSE BEGIN
        // Management of posting in multiple currencies
        IF AppliedAmount = -OldCVLedgEntryBuf2."Remaining Amount" THEN
          OldAppliedAmount := -OldCVLedgEntryBuf."Remaining Amount"
        ELSE
          OldAppliedAmount :=
            CurrExchRate.ExchangeAmount(
              AppliedAmount,NewCVLedgEntryBuf."Currency Code",
              OldCVLedgEntryBuf2."Currency Code",NewCVLedgEntryBuf."Posting Date");

        IF NewCVLedgEntryBuf."Currency Code" <> '' THEN
          // Post the realized gain or loss on the NewCVLedgEntryBuf
          AppliedAmountLCY := ROUND(OldAppliedAmount / OldCVLedgEntryBuf."Original Currency Factor")
        ELSE
          // Post the realized gain or loss on the OldCVLedgEntryBuf
          AppliedAmountLCY := ROUND(AppliedAmount / NewCVLedgEntryBuf."Original Currency Factor");
      END;
    END;

    LOCAL PROCEDURE CalcCurrencyUnrealizedGainLoss@48(VAR CVLedgEntryBuf@1000 : Record 382;VAR TempDtldCVLedgEntryBuf@1002 : TEMPORARY Record 383;GenJnlLine@1003 : Record 81;AppliedAmount@1004 : Decimal;RemainingAmountBeforeAppln@1007 : Decimal);
    VAR
      DtldCustLedgEntry@1008 : Record 379;
      DtldVendLedgEntry@1009 : Record 380;
      UnRealizedGainLossLCY@1001 : Decimal;
      DtldEmplLedgEntry@1005 : Record 5223;
    BEGIN
      IF (CVLedgEntryBuf."Currency Code" = '') OR (RemainingAmountBeforeAppln = 0) THEN
        EXIT;

      // Calculate Unrealized GainLoss
      IF GenJnlLine."Account Type" = GenJnlLine."Account Type"::Customer THEN
        UnRealizedGainLossLCY :=
          ROUND(
            DtldCustLedgEntry.GetUnrealizedGainLossAmount(CVLedgEntryBuf."Entry No.") *
            ABS(AppliedAmount / RemainingAmountBeforeAppln))
      ELSE IF GenJnlLine."Account Type" = GenJnlLine."Account Type"::Vendor THEN
        UnRealizedGainLossLCY :=
          ROUND(
            DtldVendLedgEntry.GetUnrealizedGainLossAmount(CVLedgEntryBuf."Entry No.") *
            ABS(AppliedAmount / RemainingAmountBeforeAppln))
      //<<LPC-CA JOPS 102419
      ELSE IF GenJnlLine."Account Type" = GenJnlLine."Account Type"::Employee THEN
         UnRealizedGainLossLCY :=
          ROUND(
            DtldEmplLedgEntry.GetUnrealizedGainLossAmount(CVLedgEntryBuf."Entry No.") *
            ABS(AppliedAmount / RemainingAmountBeforeAppln));
      //>>LPC-CA JOPS 102419

      IF UnRealizedGainLossLCY <> 0 THEN
        IF UnRealizedGainLossLCY < 0 THEN
          TempDtldCVLedgEntryBuf.InitDtldCVLedgEntryBuf(
            GenJnlLine,CVLedgEntryBuf,TempDtldCVLedgEntryBuf,
            TempDtldCVLedgEntryBuf."Entry Type"::"Unrealized Loss",0,-UnRealizedGainLossLCY,0,0,0,0)
        ELSE
          TempDtldCVLedgEntryBuf.InitDtldCVLedgEntryBuf(
            GenJnlLine,CVLedgEntryBuf,TempDtldCVLedgEntryBuf,
            TempDtldCVLedgEntryBuf."Entry Type"::"Unrealized Gain",0,-UnRealizedGainLossLCY,0,0,0,0);
    END;

    LOCAL PROCEDURE CalcCurrencyRealizedGainLoss@62(VAR CVLedgEntryBuf@1005 : Record 382;VAR TempDtldCVLedgEntryBuf@1003 : TEMPORARY Record 383;GenJnlLine@1002 : Record 81;AppliedAmount@1001 : Decimal;AppliedAmountLCY@1000 : Decimal);
    VAR
      RealizedGainLossLCY@1006 : Decimal;
    BEGIN
      IF CVLedgEntryBuf."Currency Code" = '' THEN
        EXIT;

      // Calculate Realized GainLoss
      RealizedGainLossLCY :=
        AppliedAmountLCY - ROUND(AppliedAmount / CVLedgEntryBuf."Original Currency Factor");
      IF RealizedGainLossLCY <> 0 THEN
        IF RealizedGainLossLCY < 0 THEN
          TempDtldCVLedgEntryBuf.InitDtldCVLedgEntryBuf(
            GenJnlLine,CVLedgEntryBuf,TempDtldCVLedgEntryBuf,
            TempDtldCVLedgEntryBuf."Entry Type"::"Realized Loss",0,RealizedGainLossLCY,0,0,0,0)
        ELSE
          TempDtldCVLedgEntryBuf.InitDtldCVLedgEntryBuf(
            GenJnlLine,CVLedgEntryBuf,TempDtldCVLedgEntryBuf,
            TempDtldCVLedgEntryBuf."Entry Type"::"Realized Gain",0,RealizedGainLossLCY,0,0,0,0);
    END;

    LOCAL PROCEDURE CalcApplication@55(VAR NewCVLedgEntryBuf@1000 : Record 382;VAR OldCVLedgEntryBuf@1001 : Record 382;VAR DtldCVLedgEntryBuf@1002 : Record 383;GenJnlLine@1003 : Record 81;AppliedAmount@1004 : Decimal;AppliedAmountLCY@1005 : Decimal;OldAppliedAmount@1006 : Decimal;PrevNewCVLedgEntryBuf@1008 : Record 382;PrevOldCVLedgEntryBuf@1007 : Record 382;VAR AllApplied@1009 : Boolean);
    BEGIN
      IF AppliedAmount = 0 THEN
        EXIT;

      DtldCVLedgEntryBuf.InitDtldCVLedgEntryBuf(
        GenJnlLine,OldCVLedgEntryBuf,DtldCVLedgEntryBuf,
        DtldCVLedgEntryBuf."Entry Type"::Application,OldAppliedAmount,AppliedAmountLCY,0,
        NewCVLedgEntryBuf."Entry No.",PrevOldCVLedgEntryBuf."Remaining Pmt. Disc. Possible",
        PrevOldCVLedgEntryBuf."Max. Payment Tolerance");

      OldCVLedgEntryBuf.Open := OldCVLedgEntryBuf."Remaining Amount" <> 0;
      IF NOT OldCVLedgEntryBuf.Open THEN
        OldCVLedgEntryBuf.SetClosedFields(
          NewCVLedgEntryBuf."Entry No.",GenJnlLine."Posting Date",
          -OldAppliedAmount,-AppliedAmountLCY,NewCVLedgEntryBuf."Currency Code",-AppliedAmount)
      ELSE
        AllApplied := FALSE;

      DtldCVLedgEntryBuf.InitDtldCVLedgEntryBuf(
        GenJnlLine,NewCVLedgEntryBuf,DtldCVLedgEntryBuf,
        DtldCVLedgEntryBuf."Entry Type"::Application,-AppliedAmount,-AppliedAmountLCY,0,
        NewCVLedgEntryBuf."Entry No.",PrevNewCVLedgEntryBuf."Remaining Pmt. Disc. Possible",
        PrevNewCVLedgEntryBuf."Max. Payment Tolerance");

      NewCVLedgEntryBuf.Open := NewCVLedgEntryBuf."Remaining Amount" <> 0;
      IF NOT NewCVLedgEntryBuf.Open AND NOT AllApplied THEN
        NewCVLedgEntryBuf.SetClosedFields(
          OldCVLedgEntryBuf."Entry No.",GenJnlLine."Posting Date",
          AppliedAmount,AppliedAmountLCY,OldCVLedgEntryBuf."Currency Code",OldAppliedAmount);
    END;

    LOCAL PROCEDURE CalcAmtLCYAdjustment@52(VAR CVLedgEntryBuf@1000 : Record 382;VAR DtldCVLedgEntryBuf@1002 : Record 383;GenJnlLine@1003 : Record 81);
    VAR
      AdjustedAmountLCY@1005 : Decimal;
    BEGIN
      IF CVLedgEntryBuf."Currency Code" = '' THEN
        EXIT;

      AdjustedAmountLCY :=
        ROUND(CVLedgEntryBuf."Remaining Amount" / CVLedgEntryBuf."Adjusted Currency Factor");

      IF AdjustedAmountLCY <> CVLedgEntryBuf."Remaining Amt. (LCY)" THEN BEGIN
        DtldCVLedgEntryBuf.InitFromGenJnlLine(GenJnlLine);
        DtldCVLedgEntryBuf.CopyFromCVLedgEntryBuf(CVLedgEntryBuf);
        DtldCVLedgEntryBuf."Entry Type" :=
          DtldCVLedgEntryBuf."Entry Type"::"Correction of Remaining Amount";
        DtldCVLedgEntryBuf."Amount (LCY)" := AdjustedAmountLCY - CVLedgEntryBuf."Remaining Amt. (LCY)";
        DtldCVLedgEntryBuf.InsertDtldCVLedgEntry(DtldCVLedgEntryBuf,CVLedgEntryBuf,FALSE);
      END;
    END;

    LOCAL PROCEDURE InitBankAccLedgEntry@59(GenJnlLine@1000 : Record 81;VAR BankAccLedgEntry@1001 : Record 271);
    BEGIN
      BankAccLedgEntry.INIT;
      BankAccLedgEntry.CopyFromGenJnlLine(GenJnlLine);
      BankAccLedgEntry."Entry No." := NextEntryNo;
      BankAccLedgEntry."Transaction No." := NextTransactionNo;
    END;

    LOCAL PROCEDURE InitCheckLedgEntry@65(BankAccLedgEntry@1000 : Record 271;VAR CheckLedgEntry@1001 : Record 272);
    BEGIN
      CheckLedgEntry.INIT;
      CheckLedgEntry.CopyFromBankAccLedgEntry(BankAccLedgEntry);
      CheckLedgEntry."Entry No." := NextCheckEntryNo;
    END;

    LOCAL PROCEDURE InitCustLedgEntry@57(GenJnlLine@1000 : Record 81;VAR CustLedgEntry@1001 : Record 21);
    BEGIN
      CustLedgEntry.INIT;
      CustLedgEntry.CopyFromGenJnlLine(GenJnlLine);
      CustLedgEntry."Entry No." := NextEntryNo;
      CustLedgEntry."Transaction No." := NextTransactionNo;
    END;

    LOCAL PROCEDURE InitVendLedgEntry@58(GenJnlLine@1001 : Record 81;VAR VendLedgEntry@1000 : Record 25);
    BEGIN
      VendLedgEntry.INIT;
      VendLedgEntry.CopyFromGenJnlLine(GenJnlLine);
      VendLedgEntry."Entry No." := NextEntryNo;
      VendLedgEntry."Transaction No." := NextTransactionNo;
    END;

    LOCAL PROCEDURE InitEmployeeLedgerEntry@134(GenJnlLine@1001 : Record 81;VAR EmployeeLedgerEntry@1000 : Record 5222);
    BEGIN
      EmployeeLedgerEntry.INIT;
      EmployeeLedgerEntry.CopyFromGenJnlLine(GenJnlLine);
      EmployeeLedgerEntry."Entry No." := NextEntryNo;
      EmployeeLedgerEntry."Transaction No." := NextTransactionNo;
    END;

    LOCAL PROCEDURE InsertDtldCustLedgEntry@102(GenJnlLine@1003 : Record 81;DtldCVLedgEntryBuf@1002 : Record 383;VAR DtldCustLedgEntry@1001 : Record 379;Offset@1000 : Integer);
    BEGIN
      WITH DtldCustLedgEntry DO BEGIN
        INIT;
        TRANSFERFIELDS(DtldCVLedgEntryBuf);
        "Entry No." := Offset + DtldCVLedgEntryBuf."Entry No.";
        "Journal Batch Name" := GenJnlLine."Journal Batch Name";
        "Reason Code" := GenJnlLine."Reason Code";
        "Source Code" := GenJnlLine."Source Code";
        "Transaction No." := NextTransactionNo;
        "Document Date" := GenJnlLine."Document Date";
        UpdateDebitCredit(GenJnlLine.Correction);
        INSERT(TRUE);
      END;
    END;

    LOCAL PROCEDURE InsertDtldVendLedgEntry@103(GenJnlLine@1000 : Record 81;DtldCVLedgEntryBuf@1001 : Record 383;VAR DtldVendLedgEntry@1004 : Record 380;Offset@1002 : Integer);
    BEGIN
      WITH DtldVendLedgEntry DO BEGIN
        INIT;
        TRANSFERFIELDS(DtldCVLedgEntryBuf);
        "Entry No." := Offset + DtldCVLedgEntryBuf."Entry No.";
        "Journal Batch Name" := GenJnlLine."Journal Batch Name";
        "Reason Code" := GenJnlLine."Reason Code";
        "Source Code" := GenJnlLine."Source Code";
        "Transaction No." := NextTransactionNo;
        "Document Date" := GenJnlLine."Document Date";
        UpdateDebitCredit(GenJnlLine.Correction);
        INSERT(TRUE);
      END;
    END;

    LOCAL PROCEDURE InsertDtldEmplLedgEntry@143(GenJnlLine@1000 : Record 81;DtldCVLedgEntryBuf@1001 : Record 383;VAR DtldEmplLedgEntry@1004 : Record 5223;Offset@1002 : Integer);
    BEGIN
      WITH DtldEmplLedgEntry DO BEGIN
        INIT;
        TRANSFERFIELDS(DtldCVLedgEntryBuf);
        "Entry No." := Offset + DtldCVLedgEntryBuf."Entry No.";
        "Journal Batch Name" := GenJnlLine."Journal Batch Name";
        "Reason Code" := GenJnlLine."Reason Code";
        "Source Code" := GenJnlLine."Source Code";
        "Transaction No." := NextTransactionNo;
        UpdateDebitCredit(GenJnlLine.Correction);
        INSERT(TRUE);
      END;
    END;

    LOCAL PROCEDURE ApplyCustLedgEntry@1(VAR NewCVLedgEntryBuf@1000 : Record 382;VAR DtldCVLedgEntryBuf@1001 : Record 383;GenJnlLine@1002 : Record 81;Cust@1003 : Record 18);
    VAR
      OldCustLedgEntry@1005 : Record 21;
      OldCVLedgEntryBuf@1006 : Record 382;
      NewCustLedgEntry@1008 : Record 21;
      NewCVLedgEntryBuf2@1019 : Record 382;
      TempOldCustLedgEntry@1021 : TEMPORARY Record 21;
      WHTPostingSetup@1240002 : Record 50003;
      SavedTempOldCustLedgEntry@1240000 : TEMPORARY Record 21;
      Completed@1009 : Boolean;
      AppliedAmount@1010 : Decimal;
      OldAppliedAmount@1240001 : Decimal;
      NewRemainingAmtBeforeAppln@1014 : Decimal;
      ApplyingDate@1017 : Date;
      PmtTolAmtToBeApplied@1020 : Decimal;
      AllApplied@1024 : Boolean;
      SourceCodeSetup@1500000 : Record 242;
      ApplyingCustLedgEntry@1500001 : Record 21;
      WHTEntry1@1500002 : Record 50004;
      WHTEntryInserted@1500004 : Boolean;
      PostedWHTEntry@1500006 : Record 50004;
      LastWHtEntry@1500007 : Record 50004;
      RemAmtWHT@1240003 : Decimal;
    BEGIN
      IF NewCVLedgEntryBuf."Amount to Apply" = 0 THEN
        EXIT;

      WHTEntryInserted := FALSE;
      AllApplied := TRUE;
      IF (GenJnlLine."Applies-to Doc. No." = '') AND (GenJnlLine."Applies-to ID" = '') AND
         NOT
         ((Cust."Application Method" = Cust."Application Method"::"Apply to Oldest") AND
          GenJnlLine."Allow Application")
      THEN
        EXIT;

      PmtTolAmtToBeApplied := 0;
      NewRemainingAmtBeforeAppln := NewCVLedgEntryBuf."Remaining Amount";
      NewCVLedgEntryBuf2 := NewCVLedgEntryBuf;

      ApplyingDate := GenJnlLine."Posting Date";

      IF NOT PrepareTempCustLedgEntry(GenJnlLine,NewCVLedgEntryBuf,TempOldCustLedgEntry,Cust,ApplyingDate) THEN
        EXIT;

      GenJnlLine."Posting Date" := ApplyingDate;
      // Apply the new entry (Payment) to the old entries (Invoices) one at a time
      REPEAT
        TempOldCustLedgEntry.CALCFIELDS(
          Amount,"Amount (LCY)","Remaining Amount","Remaining Amt. (LCY)",
          "Original Amount","Original Amt. (LCY)");
        TempOldCustLedgEntry.COPYFILTER(Positive,OldCVLedgEntryBuf.Positive);
        OldCVLedgEntryBuf.CopyFromCustLedgEntry(TempOldCustLedgEntry);

        PostApplyCust(
          GenJnlLine,DtldCVLedgEntryBuf,OldCVLedgEntryBuf,NewCVLedgEntryBuf,NewCVLedgEntryBuf2,
          Cust,AllApplied,AppliedAmount,OldAppliedAmount,PmtTolAmtToBeApplied,TempOldCustLedgEntry);

        IF NOT OldCVLedgEntryBuf.Open THEN BEGIN
          UpdateCalcInterest(OldCVLedgEntryBuf);
          UpdateCalcInterest2(OldCVLedgEntryBuf,NewCVLedgEntryBuf);
        END;

        IF TempOldCustLedgEntry."Document Type" = TempOldCustLedgEntry."Document Type"::Payment THEN BEGIN
          ApplyingCustLedgEntry.SETRANGE("Document Type",NewCVLedgEntryBuf."Document Type");
          ApplyingCustLedgEntry.SETRANGE("Document No.",NewCVLedgEntryBuf."Document No.");
          IF ApplyingCustLedgEntry.FINDFIRST THEN;
          WHTEntry1.SETRANGE("Document No.",ApplyingCustLedgEntry."Document No.");
          WHTEntry1.SETRANGE("Posting Date",ApplyingCustLedgEntry."Posting Date");
          IF WHTEntry1.FINDFIRST THEN;
        END;

        IF GLSetup."Enable WHT" AND (IsPaymentOrRefund(GenJnlLine)) THEN
          IF (GenJnlLine."Applies-to Doc. No." = '') AND (GenJnlLine."Applies-to ID" = '') THEN BEGIN
            GenJnlLine1.RESET;
            GenJnlLine1.COPY(GenJnlLine);
            GenJnlLine1.VALIDATE(Amount,AppliedAmount);
            GenJnlLine1."Posting Date" := 0D;
            GenJnlLine1."Applies-to Doc. Type" := OldCustLedgEntry."Document Type";
            GenJnlLine1."Applies-to Doc. No." := OldCustLedgEntry."Document No.";
            NextWHTEntryNo := WHTManagement.ProcessPayment(
                GenJnlLine1,NextTransactionNo,
                OldCustLedgEntry."Entry No.",1,TRUE);
          END;
        IF GLSetup."Enable Tax Invoices" THEN
          IF IsPaymentOrRefund(GenJnlLine) THEN
            IF (GenJnlLine."Applies-to Doc. No." = '') AND (GenJnlLine."Applies-to ID" = '') THEN BEGIN
              GenJnlLine1.RESET;
              GenJnlLine1.COPY(GenJnlLine);
              GenJnlLine1."Posting Date" := 0D;
              GenJnlLine1.VALIDATE(Amount,AppliedAmount);
              GenJnlLine1."Applies-to Doc. Type" := OldCustLedgEntry."Document Type";
              GenJnlLine1."Applies-to Doc. No." := OldCustLedgEntry."Document No.";

              //IF OldCustLedgEntry."Document Type" = OldCustLedgEntry."Document Type"::"Credit Memo" THEN
                //TaxManagement.TaxInvoiceSalesCrMemo(GenJnlLine1,FALSE,TRUE)
              //ELSE
                //TaxManagement.TaxInvoiceSales(GenJnlLine1,TRUE,TRUE);
            END;

        RemAmtWHT := TempOldCustLedgEntry."Remaining Amount";

        TempOldCustLedgEntry.CopyFromCVLedgEntryBuffer(OldCVLedgEntryBuf);
        SavedTempOldCustLedgEntry := TempOldCustLedgEntry;
        TempOldCustLedgEntry."Remaining Amount" := TempOldCustLedgEntry.Amount;
        TempOldCustLedgEntry."Remaining Amt. (LCY)" := TempOldCustLedgEntry."Amount (LCY)";
        OldCustLedgEntry := TempOldCustLedgEntry;
        OldCustLedgEntry."Rem. Amt for WHT" := -OldAppliedAmount;
        OldCustLedgEntry."Rem. Amt" := RemAmtWHT;
        OldCustLedgEntry."Amount to Apply" := 0;
        OldCustLedgEntry.MODIFY;
        GenJnlLine."WHT Payment" := OldCustLedgEntry."WHT Payment";
        TempOldCustLedgEntry := SavedTempOldCustLedgEntry;

        GenJnlLine1.RESET;
        IF GLSetup."Enable WHT" THEN
          SourceCodeSetup.GET;
        IF GenJnlLine."Source Code" = SourceCodeSetup."Sales Entry Application" THEN
          IF (GenJnlLine."Applies-to Doc. No." <> '') OR (GenJnlLine."Applies-to ID" <> '') THEN BEGIN
            GenJnlLine1.COPY(GenJnlLine);
            GenJnlLine1.VALIDATE(Amount,AppliedAmount);
            GenJnlLine1."WHT Entry No." := NextEntryNo;
            IF NOT GenJnlLine."Skip WHT" THEN BEGIN
              KeepWHTEntryNo := NextWHTEntryNo;
              CASE GenJnlLine1."Document Type" OF
                GenJnlLine1."Document Type"::Payment:
                  BEGIN
                    NextNo := WHTManagement.ApplyCustInvoiceWHTPosted(ApplyingCustLedgEntry,GenJnlLine1,NextTransactionNo,0);
                    IF NextWHTEntryNo <> -1 THEN
                      HadWHTEntryNo := TRUE
                    ELSE
                      NextWHTEntryNo := KeepWHTEntryNo;
                    WHTEntry.SETRANGE("Original Document No.",GenJnlLine."Document No.");
                    IF WHTEntry.FIND('-') THEN
                      WHTPostingSetup.GET(WHTEntry."WHT Bus. Posting Group",WHTEntry."WHT Prod. Posting Group");
                    IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Payment THEN BEGIN
                      REPEAT
                        GenJnlLine1.COPY(GenJnlLine);
                        GenJnlLine1.Amount := WHTEntry.Amount;
                        GenJnlLine1."Amount (LCY)" := WHTEntry."Amount (LCY)";
                        IF NOT WHTEntryInserted THEN
                          InsertWHTPostingBufferPosted(WHTEntry,GenJnlLine1,TRUE,0);
                      UNTIL WHTEntry.NEXT = 0;
                      NextWHTEntryNo := WHTEntry."Entry No." + 1;
                    END;
                  END;
              END;
            END;
          END;

        PostedWHTEntry.RESET;
        GenJnlLine1.RESET;
        PostedWHTEntry.SETRANGE("Document Type",OldCustLedgEntry."Document Type");
        PostedWHTEntry.SETRANGE("Document No.",OldCustLedgEntry."Document No.");
        PostedWHTEntry.SETRANGE("Posting Date",OldCustLedgEntry."Posting Date");
        IF NOT PostedWHTEntry.FINDFIRST THEN BEGIN
          IF GLSetup."Enable WHT" THEN
            SourceCodeSetup.GET;
          IF GenJnlLine."Source Code" = SourceCodeSetup."Sales Entry Application" THEN
            IF (GenJnlLine."Applies-to Doc. No." <> '') OR (GenJnlLine."Applies-to ID" <> '') THEN BEGIN
              GenJnlLine1.COPY(GenJnlLine);
              GenJnlLine1.VALIDATE(Amount,AppliedAmount);
              GenJnlLine1."WHT Entry No." := NextEntryNo;
              IF NOT GenJnlLine."Skip WHT" THEN BEGIN
                KeepWHTEntryNo := NextWHTEntryNo;
                CASE GenJnlLine1."Document Type" OF
                  GenJnlLine1."Document Type"::Invoice:
                    BEGIN
                      NextNo := WHTManagement.ApplyCustInvoiceWHTPosted(ApplyingCustLedgEntry,GenJnlLine1,NextTransactionNo,
                          OldCustLedgEntry."Transaction No.");
                      IF NextWHTEntryNo <> -1 THEN
                        HadWHTEntryNo := TRUE
                      ELSE
                        NextWHTEntryNo := KeepWHTEntryNo;
                      GenJnlLine1.COPY(GenJnlLine);
                      LastWHtEntry.SETRANGE("Original Document No.",GenJnlLine1."Document No.");
                      LastWHtEntry.SETRANGE("Entry No.",NextNo);
                      IF LastWHtEntry.FINDLAST THEN BEGIN
                        GenJnlLine1.Amount := LastWHtEntry.Amount;
                        GenJnlLine1."Amount (LCY)" := LastWHtEntry."Amount (LCY)";
                        InsertWHTPostingBufferPosted(LastWHtEntry,GenJnlLine1,FALSE,0);
                      END;
                      NextWHTEntryNo := LastWHtEntry."Entry No." + 1;
                    END;
                END;
              END;
            END;
        END;

        IF GLSetup."Unrealized VAT" OR
           (GLSetup."Prepayment Unrealized VAT" AND TempOldCustLedgEntry.Prepayment)
        THEN
          IF (TempOldCustLedgEntry."Document Type" IN
              [TempOldCustLedgEntry."Document Type"::Invoice,
               TempOldCustLedgEntry."Document Type"::"Credit Memo",
               TempOldCustLedgEntry."Document Type"::"Finance Charge Memo",
               TempOldCustLedgEntry."Document Type"::Payment,
               TempOldCustLedgEntry."Document Type"::Reminder])
          THEN BEGIN
            TempOldCustLedgEntry.RecalculateAmounts(
              NewCVLedgEntryBuf."Currency Code",TempOldCustLedgEntry."Currency Code",NewCVLedgEntryBuf."Posting Date");
            IF TempOldCustLedgEntry."Document Type" = TempOldCustLedgEntry."Document Type"::Payment THEN BEGIN
              ApplyingCustLedgEntry.SETRANGE("Document Type",NewCVLedgEntryBuf."Document Type");
              ApplyingCustLedgEntry.SETRANGE("Document No.",NewCVLedgEntryBuf."Document No.");
              IF ApplyingCustLedgEntry.FINDFIRST THEN;
            END;
            IF AppliedAmount <> 0 THEN
              IF TempOldCustLedgEntry."Document Type" = TempOldCustLedgEntry."Document Type"::Payment THEN BEGIN
                ApplyingCustLedgEntry.CopyFromCVLedgEntryBuffer(NewCVLedgEntryBuf);
                CustUnrealizedVAT(
                  GenJnlLine,
                  ApplyingCustLedgEntry,
                  CurrExchRate.ExchangeAmount(
                    AppliedAmount,NewCVLedgEntryBuf."Currency Code",
                    TempOldCustLedgEntry."Currency Code",NewCVLedgEntryBuf."Posting Date"))
              END ELSE
                CustUnrealizedVAT(
                  GenJnlLine,
                  TempOldCustLedgEntry,
                  CurrExchRate.ExchangeAmount(
                    AppliedAmount,NewCVLedgEntryBuf."Currency Code",
                    TempOldCustLedgEntry."Currency Code",NewCVLedgEntryBuf."Posting Date"))
            ELSE
              IF GLSetup."Manual Sales WHT Calc." AND
                 (GenJnlLine."Document Type" = GenJnlLine."Document Type"::Refund) AND
                 GenJnlLine."WHT Payment"
              THEN
                CustUnrealizedVAT(
                  GenJnlLine,
                  TempOldCustLedgEntry,
                  CurrExchRate.ExchangeAmount(
                    AppliedAmount,NewCVLedgEntryBuf."Currency Code",
                    TempOldCustLedgEntry."Currency Code",NewCVLedgEntryBuf."Posting Date"));
          END;

        TempOldCustLedgEntry.DELETE;

        // Find the next old entry for application of the new entry
        IF GenJnlLine."Applies-to Doc. No." <> '' THEN
          Completed := TRUE
        ELSE
          IF TempOldCustLedgEntry.GETFILTER(Positive) <> '' THEN
            IF TempOldCustLedgEntry.NEXT = 1 THEN
              Completed := FALSE
            ELSE BEGIN
              TempOldCustLedgEntry.SETRANGE(Positive);
              TempOldCustLedgEntry.FIND('-');
              TempOldCustLedgEntry.CALCFIELDS("Remaining Amount");
              Completed := TempOldCustLedgEntry."Remaining Amount" * NewCVLedgEntryBuf."Remaining Amount" >= 0;
            END
          ELSE
            IF NewCVLedgEntryBuf.Open THEN
              Completed := TempOldCustLedgEntry.NEXT = 0
            ELSE
              Completed := TRUE;
      UNTIL Completed;

      DtldCVLedgEntryBuf.SETCURRENTKEY("CV Ledger Entry No.","Entry Type");
      DtldCVLedgEntryBuf.SETRANGE("CV Ledger Entry No.",NewCVLedgEntryBuf."Entry No.");
      DtldCVLedgEntryBuf.SETRANGE(
        "Entry Type",
        DtldCVLedgEntryBuf."Entry Type"::Application);
      DtldCVLedgEntryBuf.CALCSUMS("Amount (LCY)",Amount);

      CalcCurrencyUnrealizedGainLoss(
        NewCVLedgEntryBuf,DtldCVLedgEntryBuf,GenJnlLine,DtldCVLedgEntryBuf.Amount,NewRemainingAmtBeforeAppln);

      CalcAmtLCYAdjustment(NewCVLedgEntryBuf,DtldCVLedgEntryBuf,GenJnlLine);

      NewCVLedgEntryBuf."Applies-to ID" := '';
      NewCVLedgEntryBuf."Amount to Apply" := 0;

      IF NOT NewCVLedgEntryBuf.Open THEN
        UpdateCalcInterest(NewCVLedgEntryBuf);

      IF GLSetup."Unrealized VAT" OR
         (GLSetup."Prepayment Unrealized VAT" AND NewCVLedgEntryBuf.Prepayment)
      THEN
        IF IsNotPayment(NewCVLedgEntryBuf."Document Type") AND
           (NewRemainingAmtBeforeAppln - NewCVLedgEntryBuf."Remaining Amount" <> 0) AND
           (OldCVLedgEntryBuf."Document Type" <> OldCVLedgEntryBuf."Document Type"::Payment)
        THEN BEGIN
          NewCustLedgEntry.CopyFromCVLedgEntryBuffer(NewCVLedgEntryBuf);
          CheckUnrealizedCust := TRUE;
          UnrealizedCustLedgEntry := NewCustLedgEntry;
          UnrealizedCustLedgEntry.CALCFIELDS("Amount (LCY)","Original Amt. (LCY)");
          UnrealizedRemainingAmountCust := NewCustLedgEntry."Remaining Amount" - NewRemainingAmtBeforeAppln;
        END;
    END;

    [External]
    PROCEDURE CustPostApplyCustLedgEntry@74(VAR GenJnlLinePostApply@1000 : Record 81;VAR CustLedgEntryPostApply@1001 : Record 21);
    VAR
      Cust@1002 : Record 18;
      CustPostingGr@1007 : Record 92;
      CustLedgEntry@1006 : Record 21;
      DtldCustLedgEntry@1003 : Record 379;
      TempDtldCVLedgEntryBuf@1004 : TEMPORARY Record 383;
      CVLedgEntryBuf@1005 : Record 382;
      GenJnlLine@1008 : Record 81;
      SourceCodeSetup@1240000 : Record 242;
      DtldLedgEntryInserted@1009 : Boolean;
    BEGIN
      GenJnlLine := GenJnlLinePostApply;
      CustLedgEntry.TRANSFERFIELDS(CustLedgEntryPostApply);
      WITH GenJnlLine DO BEGIN
        "Source Currency Code" := CustLedgEntryPostApply."Currency Code";
        "Applies-to ID" := CustLedgEntryPostApply."Applies-to ID";

        GenJnlCheckLine.RunCheck(GenJnlLine);

        IF NextEntryNo = 0 THEN
          StartPosting(GenJnlLine)
        ELSE
          ContinuePosting(GenJnlLine);

        Cust.GET(CustLedgEntry."Customer No.");
        Cust.CheckBlockedCustOnJnls(Cust,"Document Type",TRUE);

        IF "Posting Group" = '' THEN BEGIN
          Cust.TESTFIELD("Customer Posting Group");
          "Posting Group" := Cust."Customer Posting Group";
        END;
        //LPC-CM
        IF "Cylinder Posting" THEN
          "Posting Group" := Cust."Customer Posting Group 2";
        //LPC-CM
        CustPostingGr.GET("Posting Group");
        CustPostingGr.GetReceivablesAccount;

        DtldCustLedgEntry.LOCKTABLE;
        CustLedgEntry.LOCKTABLE;

        //Post the application
        CustLedgEntry.CALCFIELDS(
          Amount,"Amount (LCY)","Remaining Amount","Remaining Amt. (LCY)",
          "Original Amount","Original Amt. (LCY)");
        CVLedgEntryBuf.CopyFromCustLedgEntry(CustLedgEntry);
        ApplyCustLedgEntry(CVLedgEntryBuf,TempDtldCVLedgEntryBuf,GenJnlLine,Cust);
        CustLedgEntry.CopyFromCVLedgEntryBuffer(CVLedgEntryBuf);
        CustLedgEntry."Applies-to ID" := '';
        CustLedgEntry.MODIFY;

        SourceCodeSetup.GET;
        IF "Source Code" = SourceCodeSetup."Sales Entry Application" THEN BEGIN
          CustLedgEntry.RESET;
          CustLedgEntry.SETRANGE("Applies-to ID","Applies-to ID");
          IF "Account Type" = "Account Type"::Customer THEN BEGIN
            CustLedgEntry.SETRANGE("Customer No.","Account No.");
            IF CustLedgEntry.FINDFIRST THEN
              CustLedgEntry.MODIFYALL("Applies-to ID",'');
          END;
        END;

        // Post the Dtld customer entry
        DtldLedgEntryInserted := PostDtldCustLedgEntries(GenJnlLine,TempDtldCVLedgEntryBuf,CustPostingGr,FALSE);

        CheckPostUnrealizedVAT(GenJnlLine,TRUE);

        IF DtldLedgEntryInserted THEN
          IF IsTempGLEntryBufEmpty THEN
            DtldCustLedgEntry.SetZeroTransNo(NextTransactionNo);

        FinishPosting;
      END;
    END;

    LOCAL PROCEDURE PrepareTempCustLedgEntry@111(GenJnlLine@1000 : Record 81;VAR NewCVLedgEntryBuf@1015 : Record 382;VAR TempOldCustLedgEntry@1010 : TEMPORARY Record 21;Cust@1016 : Record 18;VAR ApplyingDate@1001 : Date) : Boolean;
    VAR
      OldCustLedgEntry@1014 : Record 21;
      SalesSetup@1009 : Record 311;
      GenJnlApply@1008 : Codeunit 225;
      RemainingAmount@1002 : Decimal;
    BEGIN
      IF (GenJnlLine."Adjustment Applies-to" <> '') AND (GenJnlLine."Bal. Account No." = '') THEN BEGIN
        // Management of posting in multiple currencies
        OldCustLedgEntry.RESET;
        OldCustLedgEntry.SETCURRENTKEY("Document No.","Document Type","Customer No.");
        IF GenJnlLine."Applies-to Doc. Type" <> GenJnlLine."Applies-to Doc. Type"::" " THEN
          OldCustLedgEntry.SETRANGE("Document Type",GenJnlLine."Applies-to Doc. Type");
        OldCustLedgEntry.SETRANGE("Customer No.",NewCVLedgEntryBuf."CV No.");
        OldCustLedgEntry.SETRANGE("Document No.",GenJnlLine."Adjustment Applies-to");
        OldCustLedgEntry.FIND('-');
      END;

      IF GenJnlLine."Applies-to Doc. No." <> '' THEN BEGIN
        // Find the entry to be applied to
        OldCustLedgEntry.RESET;
        OldCustLedgEntry.SETCURRENTKEY("Document No.");
        OldCustLedgEntry.SETRANGE("Document No.",GenJnlLine."Applies-to Doc. No.");
        OldCustLedgEntry.SETRANGE("Document Type",GenJnlLine."Applies-to Doc. Type");
        OldCustLedgEntry.SETRANGE("Customer No.",NewCVLedgEntryBuf."CV No.");
        OldCustLedgEntry.SETRANGE(Open,TRUE);

        OldCustLedgEntry.FINDFIRST;
        OldCustLedgEntry.TESTFIELD(Positive,NOT NewCVLedgEntryBuf.Positive);
        IF OldCustLedgEntry."Posting Date" > ApplyingDate THEN
          ApplyingDate := OldCustLedgEntry."Posting Date";
        GenJnlApply.CheckAgainstApplnCurrency(
          NewCVLedgEntryBuf."Currency Code",OldCustLedgEntry."Currency Code",GenJnlLine."Account Type"::Customer,TRUE);
        TempOldCustLedgEntry := OldCustLedgEntry;
        TempOldCustLedgEntry.INSERT;
      END ELSE BEGIN
        // Find the first old entry (Invoice) which the new entry (Payment) should apply to
        OldCustLedgEntry.RESET;
        OldCustLedgEntry.SETCURRENTKEY("Customer No.","Applies-to ID",Open,Positive,"Due Date");
        TempOldCustLedgEntry.SETCURRENTKEY("Customer No.","Applies-to ID",Open,Positive,"Due Date");
        OldCustLedgEntry.SETRANGE("Customer No.",NewCVLedgEntryBuf."CV No.");
        OldCustLedgEntry.SETRANGE("Applies-to ID",GenJnlLine."Applies-to ID");
        OldCustLedgEntry.SETRANGE(Open,TRUE);
        OldCustLedgEntry.SETFILTER("Entry No.",'<>%1',NewCVLedgEntryBuf."Entry No.");
        IF NOT (Cust."Application Method" = Cust."Application Method"::"Apply to Oldest") THEN
          OldCustLedgEntry.SETFILTER("Amount to Apply",'<>%1',0);

        IF Cust."Application Method" = Cust."Application Method"::"Apply to Oldest" THEN
          OldCustLedgEntry.SETFILTER("Posting Date",'..%1',GenJnlLine."Posting Date");

        // Check Cust Ledger Entry and add to Temp.
        SalesSetup.GET;
        IF SalesSetup."Appln. between Currencies" = SalesSetup."Appln. between Currencies"::None THEN
          OldCustLedgEntry.SETRANGE("Currency Code",NewCVLedgEntryBuf."Currency Code");
        IF OldCustLedgEntry.FINDSET(FALSE,FALSE) THEN
          REPEAT
            IF GenJnlApply.CheckAgainstApplnCurrency(
                 NewCVLedgEntryBuf."Currency Code",OldCustLedgEntry."Currency Code",GenJnlLine."Account Type"::Customer,FALSE)
            THEN BEGIN
              IF (OldCustLedgEntry."Posting Date" > ApplyingDate) AND (OldCustLedgEntry."Applies-to ID" <> '') THEN
                ApplyingDate := OldCustLedgEntry."Posting Date";
              TempOldCustLedgEntry := OldCustLedgEntry;
              TempOldCustLedgEntry.INSERT;
            END;
          UNTIL OldCustLedgEntry.NEXT = 0;

        TempOldCustLedgEntry.SETRANGE(Positive,NewCVLedgEntryBuf."Remaining Amount" > 0);

        IF TempOldCustLedgEntry.FIND('-') THEN BEGIN
          RemainingAmount := NewCVLedgEntryBuf."Remaining Amount";
          TempOldCustLedgEntry.SETRANGE(Positive);
          TempOldCustLedgEntry.FIND('-');
          REPEAT
            TempOldCustLedgEntry.CALCFIELDS("Remaining Amount");
            TempOldCustLedgEntry.RecalculateAmounts(
              TempOldCustLedgEntry."Currency Code",NewCVLedgEntryBuf."Currency Code",NewCVLedgEntryBuf."Posting Date");
            IF PaymentToleranceMgt.CheckCalcPmtDiscCVCust(NewCVLedgEntryBuf,TempOldCustLedgEntry,0,FALSE,FALSE) THEN
              TempOldCustLedgEntry."Remaining Amount" -= TempOldCustLedgEntry."Remaining Pmt. Disc. Possible";
            RemainingAmount += TempOldCustLedgEntry."Remaining Amount";
          UNTIL TempOldCustLedgEntry.NEXT = 0;
          TempOldCustLedgEntry.SETRANGE(Positive,RemainingAmount < 0);
        END ELSE
          TempOldCustLedgEntry.SETRANGE(Positive);

        EXIT(TempOldCustLedgEntry.FIND('-'));
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE PostDtldCustLedgEntries@46(GenJnlLine@1000 : Record 81;VAR DtldCVLedgEntryBuf@1001 : Record 383;CustPostingGr@1002 : Record 92;LedgEntryInserted@1012 : Boolean) DtldLedgEntryInserted : Boolean;
    VAR
      TempInvPostBuf@1011 : TEMPORARY Record 49;
      DtldCustLedgEntry@1005 : Record 379;
      AdjAmount@1003 : ARRAY [4] OF Decimal;
      DtldCustLedgEntryNoOffset@1006 : Integer;
      SaveEntryNo@1014 : Integer;
    BEGIN
      IF GenJnlLine."Account Type" <> GenJnlLine."Account Type"::Customer THEN
        EXIT;

      IF DtldCustLedgEntry.FINDLAST THEN
        DtldCustLedgEntryNoOffset := DtldCustLedgEntry."Entry No."
      ELSE
        DtldCustLedgEntryNoOffset := 0;

      DtldCVLedgEntryBuf.RESET;
      IF DtldCVLedgEntryBuf.FINDSET THEN BEGIN
        IF LedgEntryInserted THEN BEGIN
          SaveEntryNo := NextEntryNo;
          NextEntryNo := NextEntryNo + 1;
        END;
        REPEAT
          InsertDtldCustLedgEntry(GenJnlLine,DtldCVLedgEntryBuf,DtldCustLedgEntry,DtldCustLedgEntryNoOffset);

          UpdateTotalAmounts(
            TempInvPostBuf,GenJnlLine."Dimension Set ID",
            DtldCVLedgEntryBuf."Amount (LCY)",DtldCVLedgEntryBuf."Additional-Currency Amount");

          // Post automatic entries.
          IF ((DtldCVLedgEntryBuf."Amount (LCY)" <> 0) OR
              (DtldCVLedgEntryBuf."VAT Amount (LCY)" <> 0)) OR
             ((AddCurrencyCode <> '') AND (DtldCVLedgEntryBuf."Additional-Currency Amount" <> 0))
          THEN
            PostDtldCustLedgEntry(GenJnlLine,DtldCVLedgEntryBuf,CustPostingGr,AdjAmount);
        UNTIL DtldCVLedgEntryBuf.NEXT = 0;
      END;

      CreateGLEntriesForTotalAmounts(
        GenJnlLine,TempInvPostBuf,AdjAmount,SaveEntryNo,CustPostingGr.GetReceivablesAccount,LedgEntryInserted);

      DtldLedgEntryInserted := NOT DtldCVLedgEntryBuf.ISEMPTY;
      DtldCVLedgEntryBuf.DELETEALL;
    END;

    LOCAL PROCEDURE PostDtldCustLedgEntry@82(GenJnlLine@1005 : Record 81;DtldCVLedgEntryBuf@1003 : Record 383;CustPostingGr@1002 : Record 92;VAR AdjAmount@1001 : ARRAY [4] OF Decimal);
    VAR
      AccNo@1006 : Code[20];
    BEGIN
      AccNo := GetDtldCustLedgEntryAccNo(GenJnlLine,DtldCVLedgEntryBuf,CustPostingGr,0,FALSE);
      PostDtldCVLedgEntry(GenJnlLine,DtldCVLedgEntryBuf,AccNo,AdjAmount,FALSE);
    END;

    LOCAL PROCEDURE PostDtldCustLedgEntryUnapply@114(GenJnlLine@1007 : Record 81;DtldCVLedgEntryBuf@1001 : Record 383;CustPostingGr@1000 : Record 92;OriginalTransactionNo@1006 : Integer);
    VAR
      AdjAmount@1004 : ARRAY [4] OF Decimal;
      AccNo@1002 : Code[20];
    BEGIN
      IF (DtldCVLedgEntryBuf."Amount (LCY)" = 0) AND
         (DtldCVLedgEntryBuf."VAT Amount (LCY)" = 0) AND
         ((AddCurrencyCode = '') OR (DtldCVLedgEntryBuf."Additional-Currency Amount" = 0))
      THEN
        EXIT;

      AccNo := GetDtldCustLedgEntryAccNo(GenJnlLine,DtldCVLedgEntryBuf,CustPostingGr,OriginalTransactionNo,TRUE);
      DtldCVLedgEntryBuf."Gen. Posting Type" := DtldCVLedgEntryBuf."Gen. Posting Type"::Sale;
      PostDtldCVLedgEntry(GenJnlLine,DtldCVLedgEntryBuf,AccNo,AdjAmount,TRUE);
    END;

    LOCAL PROCEDURE GetDtldCustLedgEntryAccNo@147(GenJnlLine@1007 : Record 81;DtldCVLedgEntryBuf@1001 : Record 383;CustPostingGr@1000 : Record 92;OriginalTransactionNo@1006 : Integer;Unapply@1012 : Boolean) : Code[20];
    VAR
      GenPostingSetup@1005 : Record 252;
      Currency@1009 : Record 4;
      AmountCondition@1002 : Boolean;
    BEGIN
      WITH DtldCVLedgEntryBuf DO BEGIN
        AmountCondition := IsDebitAmount(DtldCVLedgEntryBuf,Unapply);
        CASE "Entry Type" OF
          "Entry Type"::"Initial Entry":
            ;
          "Entry Type"::Application:
            ;
          "Entry Type"::"Unrealized Loss",
          "Entry Type"::"Unrealized Gain",
          "Entry Type"::"Realized Loss",
          "Entry Type"::"Realized Gain":
            BEGIN
              GetCurrency(Currency,"Currency Code");
              CheckNonAddCurrCodeOccurred(Currency.Code);
              EXIT(Currency.GetGainLossAccount(DtldCVLedgEntryBuf));
            END;
          "Entry Type"::"Payment Discount":
            EXIT(CustPostingGr.GetPmtDiscountAccount(AmountCondition));
          "Entry Type"::"Payment Discount (VAT Excl.)":
            BEGIN
              TESTFIELD("Gen. Prod. Posting Group");
              GenPostingSetup.GET("Gen. Bus. Posting Group","Gen. Prod. Posting Group");
              EXIT(GenPostingSetup.GetSalesPmtDiscountAccount(AmountCondition));
            END;
          "Entry Type"::"Appln. Rounding":
            EXIT(CustPostingGr.GetApplRoundingAccount(AmountCondition));
          "Entry Type"::"Correction of Remaining Amount":
            EXIT(CustPostingGr.GetRoundingAccount(AmountCondition));
          "Entry Type"::"Payment Discount Tolerance":
            CASE GLSetup."Pmt. Disc. Tolerance Posting" OF
              GLSetup."Pmt. Disc. Tolerance Posting"::"Payment Tolerance Accounts":
                EXIT(CustPostingGr.GetPmtToleranceAccount(AmountCondition));
              GLSetup."Pmt. Disc. Tolerance Posting"::"Payment Discount Accounts":
                EXIT(CustPostingGr.GetPmtDiscountAccount(AmountCondition));
            END;
          "Entry Type"::"Payment Tolerance":
            CASE GLSetup."Payment Tolerance Posting" OF
              GLSetup."Payment Tolerance Posting"::"Payment Tolerance Accounts":
                EXIT(CustPostingGr.GetPmtToleranceAccount(AmountCondition));
              GLSetup."Payment Tolerance Posting"::"Payment Discount Accounts":
                EXIT(CustPostingGr.GetPmtDiscountAccount(AmountCondition));
            END;
          "Entry Type"::"Payment Tolerance (VAT Excl.)":
            BEGIN
              TESTFIELD("Gen. Prod. Posting Group");
              GenPostingSetup.GET("Gen. Bus. Posting Group","Gen. Prod. Posting Group");
              CASE GLSetup."Payment Tolerance Posting" OF
                GLSetup."Payment Tolerance Posting"::"Payment Tolerance Accounts":
                  EXIT(GenPostingSetup.GetSalesPmtToleranceAccount(AmountCondition));
                GLSetup."Payment Tolerance Posting"::"Payment Discount Accounts":
                  EXIT(GenPostingSetup.GetSalesPmtDiscountAccount(AmountCondition));
              END;
            END;
          "Entry Type"::"Payment Discount Tolerance (VAT Excl.)":
            BEGIN
              GenPostingSetup.GET("Gen. Bus. Posting Group","Gen. Prod. Posting Group");
              CASE GLSetup."Pmt. Disc. Tolerance Posting" OF
                GLSetup."Pmt. Disc. Tolerance Posting"::"Payment Tolerance Accounts":
                  EXIT(GenPostingSetup.GetSalesPmtToleranceAccount(AmountCondition));
                GLSetup."Pmt. Disc. Tolerance Posting"::"Payment Discount Accounts":
                  EXIT(GenPostingSetup.GetSalesPmtDiscountAccount(AmountCondition));
              END;
            END;
          "Entry Type"::"Payment Discount (VAT Adjustment)",
          "Entry Type"::"Payment Tolerance (VAT Adjustment)",
          "Entry Type"::"Payment Discount Tolerance (VAT Adjustment)":
            IF Unapply THEN
              PostDtldCustVATAdjustment(GenJnlLine,DtldCVLedgEntryBuf,OriginalTransactionNo);
          ELSE
            FIELDERROR("Entry Type");
        END;
      END;
    END;

    LOCAL PROCEDURE CustUnrealizedVAT@16(GenJnlLine@1015 : Record 81;VAR CustLedgEntry2@1000 : Record 21;SettledAmount@1001 : Decimal);
    VAR
      VATEntry2@1002 : Record 254;
      TaxJurisdiction@1014 : Record 320;
      VATPostingSetup@1017 : Record 325;
      VATPart@1003 : Decimal;
      VATAmount@1004 : Decimal;
      VATBase@1005 : Decimal;
      VATAmountAddCurr@1006 : Decimal;
      VATBaseAddCurr@1007 : Decimal;
      PaidAmount@1008 : Decimal;
      TotalUnrealVATAmountLast@1012 : Decimal;
      TotalUnrealVATAmountFirst@1013 : Decimal;
      SalesVATAccount@1009 : Code[20];
      SalesVATUnrealAccount@1010 : Code[20];
      LastConnectionNo@1011 : Integer;
      PrevVAT@1500003 : Decimal;
      RemUnrealVAT@1500004 : Decimal;
      LastVATEntryTranNo@1500005 : Integer;
      WHTAmount@1240000 : Decimal;
      GLEntryNo@1016 : Integer;
    BEGIN
      PaidAmount := CustLedgEntry2."Amount (LCY)" - CustLedgEntry2."Remaining Amt. (LCY)";
      VATEntry2.RESET;
      VATEntry2.SETCURRENTKEY("Transaction No.");
      VATEntry2.SETRANGE("Transaction No.",CustLedgEntry2."Transaction No.");
      IF VATEntry2.FINDSET THEN
        REPEAT
          VATPostingSetup.GET(VATEntry2."VAT Bus. Posting Group",VATEntry2."VAT Prod. Posting Group");
          IF VATPostingSetup."Unrealized VAT Type" IN
             [VATPostingSetup."Unrealized VAT Type"::Last,VATPostingSetup."Unrealized VAT Type"::"Last (Fully Paid)"]
          THEN
            TotalUnrealVATAmountLast := TotalUnrealVATAmountLast - VATEntry2."Remaining Unrealized Amount";
          IF VATPostingSetup."Unrealized VAT Type" IN
             [VATPostingSetup."Unrealized VAT Type"::First,VATPostingSetup."Unrealized VAT Type"::"First (Fully Paid)"]
          THEN
            TotalUnrealVATAmountFirst := TotalUnrealVATAmountFirst - VATEntry2."Remaining Unrealized Amount";
        UNTIL VATEntry2.NEXT = 0;

      WHTAmount := CollectWHTAmount(CustLedgEntry2."Document No.");

      IF (SettledAmount > 0) AND (WHTAmount <> 0) THEN
        SettledAmount := -SettledAmount;

      IF WHTAmount <> 0 THEN
        IF GLSetup."Manual Sales WHT Calc." THEN BEGIN
          IF ((GenJnlLine."Posting Date" < CustLedgEntry2."Pmt. Discount Date") AND
              ((ABS(SettledAmount) + ABS(WHTAmount)) >=
               (ABS(CustLedgEntry2."Rem. Amt") - ABS(CustLedgEntry2."Original Pmt. Disc. Possible"))))
          THEN
            SettledAmount := SettledAmount - CustLedgEntry2."Remaining Pmt. Disc. Possible";
        END;

      IF VATEntry2.FINDSET THEN BEGIN
        LastConnectionNo := 0;
        REPEAT
          VATPostingSetup.GET(VATEntry2."VAT Bus. Posting Group",VATEntry2."VAT Prod. Posting Group");
          IF LastConnectionNo <> VATEntry2."Sales Tax Connection No." THEN BEGIN
            InsertSummarizedVAT(GenJnlLine);
            LastConnectionNo := VATEntry2."Sales Tax Connection No.";
          END;

          VATPart :=
            VATEntry2.GetCustUnrealizedVATPart(
              ROUND(SettledAmount / CustLedgEntry2.GetAdjustedCurrencyFactor),
              PaidAmount,
              CustLedgEntry2."Amount (LCY)",
              TotalUnrealVATAmountFirst,
              TotalUnrealVATAmountLast,
              CustLedgEntry2,
              GenJnlLine,
              WHTAmount);

          IF VATPart > 0 THEN BEGIN
            CASE VATEntry2."VAT Calculation Type" OF
              VATEntry2."VAT Calculation Type"::"Normal VAT",
              VATEntry2."VAT Calculation Type"::"Reverse Charge VAT",
              VATEntry2."VAT Calculation Type"::"Full VAT":
                BEGIN
                  SalesVATAccount := VATPostingSetup.GetSalesAccount(FALSE);
                  SalesVATUnrealAccount := VATPostingSetup.GetSalesAccount(TRUE);
                END;
              VATEntry2."VAT Calculation Type"::"Sales Tax":
                BEGIN
                  TaxJurisdiction.GET(VATEntry2."Tax Jurisdiction Code");
                  SalesVATAccount := TaxJurisdiction.GetSalesAccount(FALSE);
                  SalesVATUnrealAccount := TaxJurisdiction.GetSalesAccount(TRUE);
                END;
            END;

            IF VATPart = 1 THEN BEGIN
              VATAmount := VATEntry2."Remaining Unrealized Amount";
              VATBase := VATEntry2."Remaining Unrealized Base";
              VATAmountAddCurr := VATEntry2."Add.-Curr. Rem. Unreal. Amount";
              VATBaseAddCurr := VATEntry2."Add.-Curr. Rem. Unreal. Base";
            END ELSE BEGIN
              VATAmount := ROUND(VATEntry2."Remaining Unrealized Amount" * VATPart,GLSetup."Amount Rounding Precision");
              VATBase := ROUND(VATEntry2."Remaining Unrealized Base" * VATPart,GLSetup."Amount Rounding Precision");
              VATAmountAddCurr :=
                ROUND(
                  VATEntry2."Add.-Curr. Rem. Unreal. Amount" * VATPart,
                  AddCurrency."Amount Rounding Precision");
              VATBaseAddCurr :=
                ROUND(
                  VATEntry2."Add.-Curr. Rem. Unreal. Base" * VATPart,
                  AddCurrency."Amount Rounding Precision");
            END;

            IF LastVATEntryTranNo = VATEntry2."Transaction No." THEN BEGIN
              PrevVAT := VATEntry2."Remaining Unrealized Amount";
              IF PrevVAT = 0 THEN
                EXIT;
              IF VATAmount < PrevVAT THEN BEGIN
                RemUnrealVAT := VATAmount;
                VATAmount := PrevVAT;
                VATBase := VATEntry2."Remaining Unrealized Base";
                VATAmountAddCurr := VATAmountAddCurr * (VATAmount / RemUnrealVAT);
                VATBaseAddCurr := VATBaseAddCurr * (VATAmount / RemUnrealVAT);
              END;
              LastVATEntryTranNo := VATEntry2."Transaction No.";
            END;

            InitGLEntryVAT(
              GenJnlLine,SalesVATUnrealAccount,SalesVATAccount,-VATAmount,-VATAmountAddCurr,FALSE);
            GLEntryNo :=
              InitGLEntryVATCopy(GenJnlLine,SalesVATAccount,SalesVATUnrealAccount,VATAmount,VATAmountAddCurr,VATEntry2);

            PostUnrealVATEntry(GenJnlLine,VATEntry2,VATAmount,VATBase,VATAmountAddCurr,VATBaseAddCurr,GLEntryNo);
          END;
        UNTIL VATEntry2.NEXT = 0;

        InsertSummarizedVAT(GenJnlLine);
      END;
    END;

    LOCAL PROCEDURE ApplyVendLedgEntry@4(VAR NewCVLedgEntryBuf@1000 : Record 382;VAR DtldCVLedgEntryBuf@1001 : Record 383;GenJnlLine@1002 : Record 81;Vend@1015 : Record 23);
    VAR
      OldVendLedgEntry@1005 : Record 25;
      OldCVLedgEntryBuf@1006 : Record 382;
      NewVendLedgEntry@1008 : Record 25;
      NewCVLedgEntryBuf2@1019 : Record 382;
      TempOldVendLedgEntry@1003 : TEMPORARY Record 25;
      WHTPostingSetup@1240001 : Record 50003;
      Completed@1009 : Boolean;
      AppliedAmount@1010 : Decimal;
      OldAppliedAmount@1240000 : Decimal;
      NewRemainingAmtBeforeAppln@1014 : Decimal;
      ApplyingDate@1017 : Date;
      PmtTolAmtToBeApplied@1020 : Decimal;
      AllApplied@1024 : Boolean;
      SourceCodeSetup@1500000 : Record 242;
      RemAmtWHT@1240002 : Decimal;
    BEGIN
      IF NewCVLedgEntryBuf."Amount to Apply" = 0 THEN
        EXIT;

      AllApplied := TRUE;
      IF (GenJnlLine."Applies-to Doc. No." = '') AND (GenJnlLine."Applies-to ID" = '') AND
         NOT
         ((Vend."Application Method" = Vend."Application Method"::"Apply to Oldest") AND
          GenJnlLine."Allow Application")
      THEN
        EXIT;

      PmtTolAmtToBeApplied := 0;
      NewRemainingAmtBeforeAppln := NewCVLedgEntryBuf."Remaining Amount";
      NewCVLedgEntryBuf2 := NewCVLedgEntryBuf;

      ApplyingDate := GenJnlLine."Posting Date";

      IF NOT PrepareTempVendLedgEntry(GenJnlLine,NewCVLedgEntryBuf,TempOldVendLedgEntry,Vend,ApplyingDate) THEN
        EXIT;

      GenJnlLine."Posting Date" := ApplyingDate;
      // Apply the new entry (Payment) to the old entries (Invoices) one at a time
      REPEAT
        TempOldVendLedgEntry.CALCFIELDS(
          Amount,"Amount (LCY)","Remaining Amount","Remaining Amt. (LCY)",
          "Original Amount","Original Amt. (LCY)");
        OldCVLedgEntryBuf.CopyFromVendLedgEntry(TempOldVendLedgEntry);
        TempOldVendLedgEntry.COPYFILTER(Positive,OldCVLedgEntryBuf.Positive);

        PostApplyVend(
          GenJnlLine,DtldCVLedgEntryBuf,OldCVLedgEntryBuf,NewCVLedgEntryBuf,NewCVLedgEntryBuf2,
          Vend,AllApplied,AppliedAmount,OldAppliedAmount,PmtTolAmtToBeApplied);

        // Update the Old Entry
        IF GLSetup."Enable WHT" AND
           (GenJnlLine."Document Type" IN [GenJnlLine."Document Type"::Payment,GenJnlLine."Document Type"::Refund])
        THEN
          IF (GenJnlLine."Applies-to Doc. No." = '') AND (GenJnlLine."Applies-to ID" = '') THEN BEGIN
            GenJnlLine1.RESET;
            GenJnlLine1.COPY(GenJnlLine);
            GenJnlLine1.VALIDATE(Amount,AppliedAmount - GenJnlLine1."Interest Amount");
            GenJnlLine1."Applies-to Doc. Type" := OldVendLedgEntry."Document Type";
            GenJnlLine1."Applies-to Doc. No." := OldVendLedgEntry."Document No.";
            NextWHTEntryNo := WHTManagement.ProcessPayment(GenJnlLine1,NextTransactionNo,
                OldVendLedgEntry."Entry No.",0,TRUE);
          END;
        IF GLSetup."Enable Tax Invoices" THEN
          IF GenJnlLine."Document Type" IN [GenJnlLine."Document Type"::Payment,GenJnlLine."Document Type"::Refund] THEN
            IF (GenJnlLine."Applies-to Doc. No." = '') AND (GenJnlLine."Applies-to ID" = '') THEN BEGIN
              GenJnlLine1.RESET;
              GenJnlLine1.COPY(GenJnlLine);
              GenJnlLine1."WHT Payment" := FALSE;
              GenJnlLine1.VALIDATE(Amount,AppliedAmount - GenJnlLine."Interest Amount");
              GenJnlLine1."Applies-to Doc. Type" := OldVendLedgEntry."Document Type";
              GenJnlLine1."Applies-to Doc. No." := OldVendLedgEntry."Document No.";
              //IF OldVendLedgEntry."Document Type" = OldVendLedgEntry."Document Type"::"Credit Memo" THEN
                //TaxManagement.TaxInvoicePurchaseCrMemo(GenJnlLine1,FALSE,TRUE)
              //ELSE
                //TaxManagement.TaxInvoicePurchase(GenJnlLine1,TRUE);
            END;

        RemAmtWHT := TempOldVendLedgEntry."Remaining Amount";
        TempOldVendLedgEntry.CopyFromCVLedgEntryBuffer(OldCVLedgEntryBuf);
        OldVendLedgEntry := TempOldVendLedgEntry;
        OldVendLedgEntry."Rem. Amt for WHT" := -OldAppliedAmount;
        OldVendLedgEntry."Rem. Amt" := RemAmtWHT;
        OldVendLedgEntry."Amount to Apply" := 0;
        OldVendLedgEntry."EFT Register No." := 0;
        OldVendLedgEntry."EFT Amount Transferred" := 0;
        OldVendLedgEntry."EFT Bank Account No." := '';
        OldVendLedgEntry.MODIFY;

        GenJnlLine1.RESET;
        SourceCodeSetup.GET;
        IF GLSetup."Enable WHT" THEN
          IF GenJnlLine."Source Code" = SourceCodeSetup."Purchase Entry Application" THEN
            IF (GenJnlLine."Applies-to Doc. No." <> '') OR (GenJnlLine."Applies-to ID" <> '') THEN BEGIN
              GenJnlLine1.COPY(GenJnlLine);
              GenJnlLine1.VALIDATE(Amount,AppliedAmount);
              GenJnlLine1."WHT Entry No." := NextEntryNo;
              IF NOT GenJnlLine."Skip WHT" THEN BEGIN
                KeepWHTEntryNo := NextWHTEntryNo;
                CASE GenJnlLine1."Document Type" OF
                  GenJnlLine1."Document Type"::Payment:
                    BEGIN
                      NextNo := WHTManagement.ApplyVendInvoiceWHTPosted(VendLedgEntry,GenJnlLine1,NextTransactionNo);
                      IF NextWHTEntryNo <> -1 THEN
                        HadWHTEntryNo := TRUE
                      ELSE
                        NextWHTEntryNo := KeepWHTEntryNo;
                      WHTEntry.SETRANGE("Original Document No.",GenJnlLine."Document No.");
                      IF WHTEntry.FIND('-') THEN
                        IF WHTPostingSetup.GET(
                             WHTEntry."WHT Bus. Posting Group",
                             WHTEntry."WHT Prod. Posting Group")
                        THEN
                          IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Payment THEN BEGIN
                            REPEAT
                              GenJnlLine1.COPY(GenJnlLine);
                              GenJnlLine1.Amount := WHTEntry.Amount;
                              GenJnlLine1."Amount (LCY)" := WHTEntry."Amount (LCY)";
                              InsertWHTPostingBufferPosted(WHTEntry,GenJnlLine1,TRUE,1);
                            UNTIL WHTEntry.NEXT = 0;
                            NextWHTEntryNo := WHTEntry."Entry No." + 1;
                          END;
                    END;
                END;
              END;
            END;

        IF GLSetup."Unrealized VAT" OR
           (GLSetup."Prepayment Unrealized VAT" AND TempOldVendLedgEntry.Prepayment)
        THEN
          IF IsNotPayment(TempOldVendLedgEntry."Document Type") THEN BEGIN
            TempOldVendLedgEntry.RecalculateAmounts(
              NewCVLedgEntryBuf."Currency Code",TempOldVendLedgEntry."Currency Code",NewCVLedgEntryBuf."Posting Date");
            VendUnrealizedVAT(
              GenJnlLine,
              TempOldVendLedgEntry,
              CurrExchRate.ExchangeAmount(
                AppliedAmount,NewCVLedgEntryBuf."Currency Code",
                TempOldVendLedgEntry."Currency Code",NewCVLedgEntryBuf."Posting Date"));
          END;

        TempOldVendLedgEntry.DELETE;

        // Find the next old entry to apply to the new entry
        IF GenJnlLine."Applies-to Doc. No." <> '' THEN
          Completed := TRUE
        ELSE
          IF TempOldVendLedgEntry.GETFILTER(Positive) <> '' THEN
            IF TempOldVendLedgEntry.NEXT = 1 THEN
              Completed := FALSE
            ELSE BEGIN
              TempOldVendLedgEntry.SETRANGE(Positive);
              TempOldVendLedgEntry.FIND('-');
              TempOldVendLedgEntry.CALCFIELDS("Remaining Amount");
              Completed := TempOldVendLedgEntry."Remaining Amount" * NewCVLedgEntryBuf."Remaining Amount" >= 0;
            END
          ELSE
            IF NewCVLedgEntryBuf.Open THEN
              Completed := TempOldVendLedgEntry.NEXT = 0
            ELSE
              Completed := TRUE;
      UNTIL Completed;

      DtldCVLedgEntryBuf.SETCURRENTKEY("CV Ledger Entry No.","Entry Type");
      DtldCVLedgEntryBuf.SETRANGE("CV Ledger Entry No.",NewCVLedgEntryBuf."Entry No.");
      DtldCVLedgEntryBuf.SETRANGE(
        "Entry Type",
        DtldCVLedgEntryBuf."Entry Type"::Application);
      DtldCVLedgEntryBuf.CALCSUMS("Amount (LCY)",Amount);

      CalcCurrencyUnrealizedGainLoss(
        NewCVLedgEntryBuf,DtldCVLedgEntryBuf,GenJnlLine,DtldCVLedgEntryBuf.Amount,NewRemainingAmtBeforeAppln);

      CalcAmtLCYAdjustment(NewCVLedgEntryBuf,DtldCVLedgEntryBuf,GenJnlLine);

      NewCVLedgEntryBuf."Applies-to ID" := '';
      NewCVLedgEntryBuf."Amount to Apply" := 0;

      IF GLSetup."Unrealized VAT" OR
         (GLSetup."Prepayment Unrealized VAT" AND NewCVLedgEntryBuf.Prepayment)
      THEN
        IF IsNotPayment(NewCVLedgEntryBuf."Document Type") AND
           (NewRemainingAmtBeforeAppln - NewCVLedgEntryBuf."Remaining Amount" <> 0)
        THEN BEGIN
          NewVendLedgEntry.CopyFromCVLedgEntryBuffer(NewCVLedgEntryBuf);
          CheckUnrealizedVend := TRUE;
          UnrealizedVendLedgEntry := NewVendLedgEntry;
          UnrealizedVendLedgEntry.CALCFIELDS("Amount (LCY)","Original Amt. (LCY)");
          UnrealizedRemainingAmountVend := -(NewRemainingAmtBeforeAppln - NewVendLedgEntry."Remaining Amount");
        END;
    END;

    LOCAL PROCEDURE ApplyEmplLedgEntry@141(VAR NewCVLedgEntryBuf@1000 : Record 382;VAR DtldCVLedgEntryBuf@1001 : Record 383;GenJnlLine@1002 : Record 81;Employee@1015 : Record 5200);
    VAR
      OldEmplLedgEntry@1005 : Record 5222;
      OldCVLedgEntryBuf@1006 : Record 382;
      NewCVLedgEntryBuf2@1019 : Record 382;
      TempOldEmplLedgEntry@1003 : TEMPORARY Record 5222;
      Completed@1009 : Boolean;
      AppliedAmount@1010 : Decimal;
      ApplyingDate@1017 : Date;
      PmtTolAmtToBeApplied@1004 : Decimal;
      AllApplied@1024 : Boolean;
      NewRemainingAmtBeforeAppln@1007 : Decimal;
    BEGIN
      IF NewCVLedgEntryBuf."Amount to Apply" = 0 THEN
        EXIT;

      AllApplied := TRUE;
      IF (GenJnlLine."Applies-to Doc. No." = '') AND (GenJnlLine."Applies-to ID" = '') AND
         NOT
         ((Employee."Application Method" = Employee."Application Method"::"Apply to Oldest") AND
          GenJnlLine."Allow Application")
      THEN
        EXIT;

      PmtTolAmtToBeApplied := 0;
      NewRemainingAmtBeforeAppln := NewCVLedgEntryBuf."Remaining Amount"; //<< LPC-CA JOPS 102019
      NewCVLedgEntryBuf2 := NewCVLedgEntryBuf;

      ApplyingDate := GenJnlLine."Posting Date";

      IF NOT PrepareTempEmplLedgEntry(GenJnlLine,NewCVLedgEntryBuf,TempOldEmplLedgEntry,Employee,ApplyingDate) THEN
        EXIT;

      GenJnlLine."Posting Date" := ApplyingDate;

      // Apply the new entry (Payment) to the old entries one at a time
      REPEAT
        TempOldEmplLedgEntry.CALCFIELDS(
          Amount,"Amount (LCY)","Remaining Amount","Remaining Amt. (LCY)",
          "Original Amount","Original Amt. (LCY)");
        OldCVLedgEntryBuf.CopyFromEmplLedgEntry(TempOldEmplLedgEntry);
        TempOldEmplLedgEntry.COPYFILTER(Positive,OldCVLedgEntryBuf.Positive);

        PostApply(
          GenJnlLine,DtldCVLedgEntryBuf,OldCVLedgEntryBuf,NewCVLedgEntryBuf,NewCVLedgEntryBuf2,
          TRUE,AllApplied,AppliedAmount,PmtTolAmtToBeApplied);

        // Update the Old Entry
        TempOldEmplLedgEntry.CopyFromCVLedgEntryBuffer(OldCVLedgEntryBuf);
        OldEmplLedgEntry := TempOldEmplLedgEntry;
        OldEmplLedgEntry."Applies-to ID" := '';
        OldEmplLedgEntry."Amount to Apply" := 0;
        OldEmplLedgEntry.MODIFY;

        TempOldEmplLedgEntry.DELETE;

        // Find the next old entry to apply to the new entry
        IF GenJnlLine."Applies-to Doc. No." <> '' THEN
          Completed := TRUE
        ELSE
          IF TempOldEmplLedgEntry.GETFILTER(Positive) <> '' THEN
            IF TempOldEmplLedgEntry.NEXT = 1 THEN
              Completed := FALSE
            ELSE BEGIN
              TempOldEmplLedgEntry.SETRANGE(Positive);
              TempOldEmplLedgEntry.FIND('-');
              TempOldEmplLedgEntry.CALCFIELDS("Remaining Amount");
              Completed := TempOldEmplLedgEntry."Remaining Amount" * NewCVLedgEntryBuf."Remaining Amount" >= 0;
            END
          ELSE
            IF NewCVLedgEntryBuf.Open THEN
              Completed := TempOldEmplLedgEntry.NEXT = 0
            ELSE
              Completed := TRUE;
      UNTIL Completed;

      DtldCVLedgEntryBuf.SETCURRENTKEY("CV Ledger Entry No.","Entry Type");
      DtldCVLedgEntryBuf.SETRANGE("CV Ledger Entry No.",NewCVLedgEntryBuf."Entry No.");
      DtldCVLedgEntryBuf.SETRANGE(
        "Entry Type",
        DtldCVLedgEntryBuf."Entry Type"::Application);
      DtldCVLedgEntryBuf.CALCSUMS("Amount (LCY)",Amount);
      //<<LPC-CA JOPS102019
      CalcCurrencyUnrealizedGainLoss(
        NewCVLedgEntryBuf,DtldCVLedgEntryBuf,GenJnlLine,DtldCVLedgEntryBuf.Amount,NewRemainingAmtBeforeAppln);

      CalcAmtLCYAdjustment(NewCVLedgEntryBuf,DtldCVLedgEntryBuf,GenJnlLine);
      //>>LPC-CA JOPS102019
      NewCVLedgEntryBuf."Applies-to ID" := '';
      NewCVLedgEntryBuf."Amount to Apply" := 0;
    END;

    [Internal]
    PROCEDURE VendPostApplyVendLedgEntry@66(VAR GenJnlLinePostApply@1000 : Record 81;VAR VendLedgEntryPostApply@1001 : Record 25);
    VAR
      Vend@1002 : Record 23;
      VendPostingGr@1007 : Record 93;
      VendLedgEntry@1006 : Record 25;
      DtldVendLedgEntry@1003 : Record 380;
      TempDtldCVLedgEntryBuf@1004 : TEMPORARY Record 383;
      CVLedgEntryBuf@1005 : Record 382;
      GenJnlLine@1008 : Record 81;
      SourceCodeSetup@1250000 : Record 242;
      DtldLedgEntryInserted@1009 : Boolean;
    BEGIN
      GenJnlLine := GenJnlLinePostApply;
      VendLedgEntry.TRANSFERFIELDS(VendLedgEntryPostApply);
      WITH GenJnlLine DO BEGIN
        "Source Currency Code" := VendLedgEntryPostApply."Currency Code";
        "Applies-to ID" := VendLedgEntryPostApply."Applies-to ID";

        GenJnlCheckLine.RunCheck(GenJnlLine);

        IF NextEntryNo = 0 THEN
          StartPosting(GenJnlLine)
        ELSE
          ContinuePosting(GenJnlLine);

        Vend.GET(VendLedgEntry."Vendor No.");
        Vend.CheckBlockedVendOnJnls(Vend,"Document Type",TRUE);

        IF "Posting Group" = '' THEN BEGIN
          Vend.TESTFIELD("Vendor Posting Group");
          "Posting Group" := Vend."Vendor Posting Group";
        END;
        VendPostingGr.GET("Posting Group");
        VendPostingGr.GetPayablesAccount;

        DtldVendLedgEntry.LOCKTABLE;
        VendLedgEntry.LOCKTABLE;

        // Post the application
        VendLedgEntry.CALCFIELDS(
          Amount,"Amount (LCY)","Remaining Amount","Remaining Amt. (LCY)",
          "Original Amount","Original Amt. (LCY)");
        CVLedgEntryBuf.CopyFromVendLedgEntry(VendLedgEntry);
        ApplyVendLedgEntry(CVLedgEntryBuf,TempDtldCVLedgEntryBuf,GenJnlLine,Vend);
        VendLedgEntry.CopyFromCVLedgEntryBuffer(CVLedgEntryBuf);
        VendLedgEntry."Applies-to ID" := '';
        VendLedgEntry."EFT Register No." := 0;
        VendLedgEntry."EFT Amount Transferred" := 0;
        VendLedgEntry."EFT Bank Account No." := '';
        VendLedgEntry.MODIFY(TRUE);

        SourceCodeSetup.GET;
        IF "Source Code" = SourceCodeSetup."Purchase Entry Application" THEN BEGIN
          VendLedgEntry.RESET;
          VendLedgEntry.SETRANGE("Applies-to ID","Applies-to ID");
          VendLedgEntry.SETRANGE("Vendor No.","Account No.");
          VendLedgEntry.MODIFYALL("Applies-to ID",'');
        END;

        // Post Dtld vendor entry
        DtldLedgEntryInserted := PostDtldVendLedgEntries(GenJnlLine,TempDtldCVLedgEntryBuf,VendPostingGr,FALSE);

        CheckPostUnrealizedVAT(GenJnlLine,TRUE);

        IF DtldLedgEntryInserted THEN
          IF IsTempGLEntryBufEmpty THEN
            DtldVendLedgEntry.SetZeroTransNo(NextTransactionNo);

        FinishPosting;
      END;
    END;

    [Internal]
    PROCEDURE EmplPostApplyEmplLedgEntry@138(VAR GenJnlLinePostApply@1000 : Record 81;VAR EmplLedgEntryPostApply@1001 : Record 5222);
    VAR
      Empl@1002 : Record 5200;
      EmplPostingGr@1007 : Record 5221;
      EmplLedgEntry@1006 : Record 5222;
      DtldEmplLedgEntry@1003 : Record 5223;
      TempDtldCVLedgEntryBuf@1004 : TEMPORARY Record 383;
      CVLedgEntryBuf@1005 : Record 382;
      GenJnlLine@1008 : Record 81;
      DtldLedgEntryInserted@1009 : Boolean;
    BEGIN
      GenJnlLine := GenJnlLinePostApply;
      EmplLedgEntry.TRANSFERFIELDS(EmplLedgEntryPostApply);
      WITH GenJnlLine DO BEGIN
        "Source Currency Code" := EmplLedgEntryPostApply."Currency Code";
        "Applies-to ID" := EmplLedgEntryPostApply."Applies-to ID";

        GenJnlCheckLine.RunCheck(GenJnlLine);

        IF NextEntryNo = 0 THEN
          StartPosting(GenJnlLine)
        ELSE
          ContinuePosting(GenJnlLine);

        Empl.GET(EmplLedgEntry."Employee No.");

        IF "Posting Group" = '' THEN BEGIN
          Empl.TESTFIELD("Employee Posting Group");
          "Posting Group" := Empl."Employee Posting Group";
        END;
        EmplPostingGr.GET("Posting Group");
        EmplPostingGr.GetPayablesAccount;

        DtldEmplLedgEntry.LOCKTABLE;
        EmplLedgEntry.LOCKTABLE;

        // Post the application
        EmplLedgEntry.CALCFIELDS(
          Amount,"Amount (LCY)","Remaining Amount","Remaining Amt. (LCY)",
          "Original Amount","Original Amt. (LCY)");
        CVLedgEntryBuf.CopyFromEmplLedgEntry(EmplLedgEntry);
        ApplyEmplLedgEntry(
          CVLedgEntryBuf,TempDtldCVLedgEntryBuf,GenJnlLine,Empl);
        EmplLedgEntry.CopyFromCVLedgEntryBuffer(CVLedgEntryBuf);
        EmplLedgEntry.MODIFY(TRUE);

        // Post Dtld vendor entry
        DtldLedgEntryInserted := PostDtldEmplLedgEntries(GenJnlLine,TempDtldCVLedgEntryBuf,EmplPostingGr,FALSE);

        CheckPostUnrealizedVAT(GenJnlLine,TRUE);

        IF DtldLedgEntryInserted THEN
          IF IsTempGLEntryBufEmpty THEN
            DtldEmplLedgEntry.SetZeroTransNo(NextTransactionNo);

        FinishPosting;
      END;
    END;

    LOCAL PROCEDURE PrepareTempVendLedgEntry@119(GenJnlLine@1004 : Record 81;VAR NewCVLedgEntryBuf@1003 : Record 382;VAR TempOldVendLedgEntry@1002 : TEMPORARY Record 25;Vend@1001 : Record 23;VAR ApplyingDate@1000 : Date) : Boolean;
    VAR
      OldVendLedgEntry@1018 : Record 25;
      PurchSetup@1013 : Record 312;
      GenJnlApply@1012 : Codeunit 225;
      RemainingAmount@1009 : Decimal;
    BEGIN
      IF GenJnlLine."Applies-to Doc. No." <> '' THEN BEGIN
        // Find the entry to be applied to
        OldVendLedgEntry.RESET;
        OldVendLedgEntry.SETCURRENTKEY("Document No.");
        OldVendLedgEntry.SETRANGE("Document No.",GenJnlLine."Applies-to Doc. No.");
        OldVendLedgEntry.SETRANGE("Document Type",GenJnlLine."Applies-to Doc. Type");
        OldVendLedgEntry.SETRANGE("Vendor No.",NewCVLedgEntryBuf."CV No.");
        OldVendLedgEntry.SETRANGE(Open,TRUE);
        OldVendLedgEntry.FINDFIRST;
        OldVendLedgEntry.TESTFIELD(Positive,NOT NewCVLedgEntryBuf.Positive);
        IF OldVendLedgEntry."Posting Date" > ApplyingDate THEN
          ApplyingDate := OldVendLedgEntry."Posting Date";
        GenJnlApply.CheckAgainstApplnCurrency(
          NewCVLedgEntryBuf."Currency Code",OldVendLedgEntry."Currency Code",GenJnlLine."Account Type"::Vendor,TRUE);
        TempOldVendLedgEntry := OldVendLedgEntry;
        TempOldVendLedgEntry.INSERT;
      END ELSE BEGIN
        // Find the first old entry (Invoice) which the new entry (Payment) should apply to
        OldVendLedgEntry.RESET;
        OldVendLedgEntry.SETCURRENTKEY("Vendor No.","Applies-to ID",Open,Positive,"Due Date");
        TempOldVendLedgEntry.SETCURRENTKEY("Vendor No.","Applies-to ID",Open,Positive,"Due Date");
        OldVendLedgEntry.SETRANGE("Vendor No.",NewCVLedgEntryBuf."CV No.");
        OldVendLedgEntry.SETRANGE("Applies-to ID",GenJnlLine."Applies-to ID");
        OldVendLedgEntry.SETRANGE(Open,TRUE);
        OldVendLedgEntry.SETFILTER("Entry No.",'<>%1',NewCVLedgEntryBuf."Entry No.");
        IF NOT (Vend."Application Method" = Vend."Application Method"::"Apply to Oldest") THEN
          OldVendLedgEntry.SETFILTER("Amount to Apply",'<>%1',0);

        IF Vend."Application Method" = Vend."Application Method"::"Apply to Oldest" THEN
          OldVendLedgEntry.SETFILTER("Posting Date",'..%1',GenJnlLine."Posting Date");

        // Check and Move Ledger Entries to Temp
        PurchSetup.GET;
        IF PurchSetup."Appln. between Currencies" = PurchSetup."Appln. between Currencies"::None THEN
          OldVendLedgEntry.SETRANGE("Currency Code",NewCVLedgEntryBuf."Currency Code");
        IF OldVendLedgEntry.FINDSET(FALSE,FALSE) THEN
          REPEAT
            IF GenJnlApply.CheckAgainstApplnCurrency(
                 NewCVLedgEntryBuf."Currency Code",OldVendLedgEntry."Currency Code",GenJnlLine."Account Type"::Vendor,FALSE)
            THEN BEGIN
              IF (OldVendLedgEntry."Posting Date" > ApplyingDate) AND (OldVendLedgEntry."Applies-to ID" <> '') THEN
                ApplyingDate := OldVendLedgEntry."Posting Date";
              TempOldVendLedgEntry := OldVendLedgEntry;
              TempOldVendLedgEntry.INSERT;
            END;
          UNTIL OldVendLedgEntry.NEXT = 0;

        TempOldVendLedgEntry.SETRANGE(Positive,NewCVLedgEntryBuf."Remaining Amount" > 0);

        IF TempOldVendLedgEntry.FIND('-') THEN BEGIN
          RemainingAmount := NewCVLedgEntryBuf."Remaining Amount";
          TempOldVendLedgEntry.SETRANGE(Positive);
          TempOldVendLedgEntry.FIND('-');
          REPEAT
            TempOldVendLedgEntry.CALCFIELDS("Remaining Amount");
            TempOldVendLedgEntry.RecalculateAmounts(
              TempOldVendLedgEntry."Currency Code",NewCVLedgEntryBuf."Currency Code",NewCVLedgEntryBuf."Posting Date");
            IF PaymentToleranceMgt.CheckCalcPmtDiscCVVend(NewCVLedgEntryBuf,TempOldVendLedgEntry,0,FALSE,FALSE) THEN
              TempOldVendLedgEntry."Remaining Amount" -= TempOldVendLedgEntry."Remaining Pmt. Disc. Possible";
            RemainingAmount += TempOldVendLedgEntry."Remaining Amount";
          UNTIL TempOldVendLedgEntry.NEXT = 0;
          TempOldVendLedgEntry.SETRANGE(Positive,RemainingAmount < 0);
        END ELSE
          TempOldVendLedgEntry.SETRANGE(Positive);
        EXIT(TempOldVendLedgEntry.FIND('-'));
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE PrepareTempEmplLedgEntry@145(GenJnlLine@1004 : Record 81;VAR NewCVLedgEntryBuf@1003 : Record 382;VAR TempOldEmplLedgEntry@1002 : TEMPORARY Record 5222;Employee@1001 : Record 5200;VAR ApplyingDate@1000 : Date) : Boolean;
    VAR
      OldEmplLedgEntry@1018 : Record 5222;
      RemainingAmount@1009 : Decimal;
      GenJnlApply@1005 : Codeunit 225;
      SalesSetup@1006 : Record 311;
    BEGIN
      //<<LPC-CA
      // IF (GenJnlLine."Adjustment Applies-to" <> '') AND (GenJnlLine."Bal. Account No." = '') THEN BEGIN
      //  // Management of posting in multiple currencies
      //  OldEmplLedgEntry.RESET;
      //  OldEmplLedgEntry.SETCURRENTKEY("Document No.","Document Type","Employee No.");
      //  IF GenJnlLine."Applies-to Doc. Type" <> GenJnlLine."Applies-to Doc. Type"::" " THEN
      //    OldEmplLedgEntry.SETRANGE("Document Type",GenJnlLine."Applies-to Doc. Type");
      //  OldEmplLedgEntry.SETRANGE("Employee No.",NewCVLedgEntryBuf."CV No.");
      //  OldEmplLedgEntry.SETRANGE("Document No.",GenJnlLine."Adjustment Applies-to");
      //  OldEmplLedgEntry.FIND('-');
      // END;
      //>> LPC-CA

      IF GenJnlLine."Applies-to Doc. No." <> '' THEN BEGIN
        // Find the entry to be applied to
        OldEmplLedgEntry.RESET;
        OldEmplLedgEntry.SETCURRENTKEY("Document No.");
        OldEmplLedgEntry.SETRANGE("Document Type",GenJnlLine."Applies-to Doc. Type");
        OldEmplLedgEntry.SETRANGE("Document No.",GenJnlLine."Applies-to Doc. No.");
        OldEmplLedgEntry.SETRANGE("Employee No.",NewCVLedgEntryBuf."CV No.");
        OldEmplLedgEntry.SETRANGE(Open,TRUE);
        OldEmplLedgEntry.FINDFIRST;
        OldEmplLedgEntry.TESTFIELD(Positive,NOT NewCVLedgEntryBuf.Positive);
        IF OldEmplLedgEntry."Posting Date" > ApplyingDate THEN
          ApplyingDate := OldEmplLedgEntry."Posting Date";
        //<<LPC-CA  JOPS 102019
        GenJnlApply.CheckAgainstApplnCurrency(
          NewCVLedgEntryBuf."Currency Code",OldEmplLedgEntry."Currency Code",GenJnlLine."Account Type"::Employee,TRUE);
        //>>LPC-CA  JOPS 102019
        TempOldEmplLedgEntry := OldEmplLedgEntry;
        TempOldEmplLedgEntry.INSERT;
      END ELSE BEGIN
        // Find the first old entry which the new entry (Payment) should apply to
        OldEmplLedgEntry.RESET;
        OldEmplLedgEntry.SETCURRENTKEY("Employee No.","Applies-to ID",Open,Positive);
        TempOldEmplLedgEntry.SETCURRENTKEY("Employee No.","Applies-to ID",Open,Positive);
        OldEmplLedgEntry.SETRANGE("Employee No.",NewCVLedgEntryBuf."CV No.");
        OldEmplLedgEntry.SETRANGE("Applies-to ID",GenJnlLine."Applies-to ID");
        OldEmplLedgEntry.SETRANGE(Open,TRUE);
        OldEmplLedgEntry.SETFILTER("Entry No.",'<>%1',NewCVLedgEntryBuf."Entry No.");
        IF NOT (Employee."Application Method" = Employee."Application Method"::"Apply to Oldest") THEN
          OldEmplLedgEntry.SETFILTER("Amount to Apply",'<>%1',0);

        IF Employee."Application Method" = Employee."Application Method"::"Apply to Oldest" THEN
          OldEmplLedgEntry.SETFILTER("Posting Date",'..%1',GenJnlLine."Posting Date");

        // Check Cust Ledger Entry and add to Temp.
        //<<LPC-CA JOPS 102019
        SalesSetup.GET;
        IF SalesSetup."Appln. between Currencies" = SalesSetup."Appln. between Currencies"::None THEN
        //>>LPC-CA JOPS 102019
          OldEmplLedgEntry.SETRANGE("Currency Code",NewCVLedgEntryBuf."Currency Code");
        IF OldEmplLedgEntry.FINDSET(FALSE,FALSE) THEN
          REPEAT
            //<<LPC-CA JOPS 102019
            IF GenJnlApply.CheckAgainstApplnCurrency(
                 NewCVLedgEntryBuf."Currency Code",OldEmplLedgEntry."Currency Code",GenJnlLine."Account Type"::Employee,FALSE)
            THEN BEGIN
            //>>LPC-CA JOPS 102019

            IF (OldEmplLedgEntry."Posting Date" > ApplyingDate) AND (OldEmplLedgEntry."Applies-to ID" <> '') THEN
              ApplyingDate := OldEmplLedgEntry."Posting Date";
            TempOldEmplLedgEntry := OldEmplLedgEntry;
            TempOldEmplLedgEntry.INSERT;
            END; //<<LPC-CA JOPS 102019
          UNTIL OldEmplLedgEntry.NEXT = 0;

        TempOldEmplLedgEntry.SETRANGE(Positive,NewCVLedgEntryBuf."Remaining Amount" > 0);

        IF TempOldEmplLedgEntry.FIND('-') THEN BEGIN
          RemainingAmount := NewCVLedgEntryBuf."Remaining Amount";
          TempOldEmplLedgEntry.SETRANGE(Positive);
          TempOldEmplLedgEntry.FIND('-');
          REPEAT
            TempOldEmplLedgEntry.CALCFIELDS("Remaining Amount");
            //<<LPC-CA JOPS 102019
            TempOldEmplLedgEntry.RecalculateAmounts(
              TempOldEmplLedgEntry."Currency Code",NewCVLedgEntryBuf."Currency Code",NewCVLedgEntryBuf."Posting Date");
      //      IF PaymentToleranceMgt.CheckCalcPmtDiscCVCust(NewCVLedgEntryBuf,TempOldCustLedgEntry,0,FALSE,FALSE) THEN
      //        TempOldEmplLedgEntry."Remaining Amount" -= TempOldEmplLedgEntry."Remaining Pmt. Disc. Possible";
            //>>LPC-CA JOPS 102019
            RemainingAmount += TempOldEmplLedgEntry."Remaining Amount";
          UNTIL TempOldEmplLedgEntry.NEXT = 0;
          TempOldEmplLedgEntry.SETRANGE(Positive,RemainingAmount < 0);
        END ELSE
          TempOldEmplLedgEntry.SETRANGE(Positive);
        EXIT(TempOldEmplLedgEntry.FIND('-'));
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE PostDtldVendLedgEntries@32(GenJnlLine@1000 : Record 81;VAR DtldCVLedgEntryBuf@1001 : Record 383;VendPostingGr@1002 : Record 93;LedgEntryInserted@1011 : Boolean) DtldLedgEntryInserted : Boolean;
    VAR
      TempInvPostBuf@1007 : TEMPORARY Record 49;
      DtldVendLedgEntry@1004 : Record 380;
      AdjAmount@1012 : ARRAY [4] OF Decimal;
      DtldVendLedgEntryNoOffset@1005 : Integer;
      SaveEntryNo@1013 : Integer;
    BEGIN
      IF GenJnlLine."Account Type" <> GenJnlLine."Account Type"::Vendor THEN
        EXIT;

      IF DtldVendLedgEntry.FINDLAST THEN
        DtldVendLedgEntryNoOffset := DtldVendLedgEntry."Entry No."
      ELSE
        DtldVendLedgEntryNoOffset := 0;

      DtldCVLedgEntryBuf.RESET;
      IF DtldCVLedgEntryBuf.FINDSET THEN BEGIN
        IF LedgEntryInserted THEN BEGIN
          SaveEntryNo := NextEntryNo;
          NextEntryNo := NextEntryNo + 1;
        END;
        REPEAT
          InsertDtldVendLedgEntry(GenJnlLine,DtldCVLedgEntryBuf,DtldVendLedgEntry,DtldVendLedgEntryNoOffset);

          UpdateTotalAmounts(
            TempInvPostBuf,GenJnlLine."Dimension Set ID",
            DtldCVLedgEntryBuf."Amount (LCY)",DtldCVLedgEntryBuf."Additional-Currency Amount");

          // Post automatic entries.
          IF ((DtldCVLedgEntryBuf."Amount (LCY)" <> 0) OR
              (DtldCVLedgEntryBuf."VAT Amount (LCY)" <> 0)) OR
             ((AddCurrencyCode <> '') AND (DtldCVLedgEntryBuf."Additional-Currency Amount" <> 0))
          THEN
            PostDtldVendLedgEntry(GenJnlLine,DtldCVLedgEntryBuf,VendPostingGr,AdjAmount);
        UNTIL DtldCVLedgEntryBuf.NEXT = 0;
      END;

      CreateVendGLEntriesForTotalAmounts(
        GenJnlLine,TempInvPostBuf,VendPostingGr,AdjAmount,SaveEntryNo,LedgEntryInserted);

      DtldLedgEntryInserted := NOT DtldCVLedgEntryBuf.ISEMPTY;
      DtldCVLedgEntryBuf.DELETEALL;
    END;

    LOCAL PROCEDURE PostDtldVendLedgEntry@81(GenJnlLine@1000 : Record 81;DtldCVLedgEntryBuf@1002 : Record 383;VendPostingGr@1006 : Record 93;VAR AdjAmount@1003 : ARRAY [4] OF Decimal);
    VAR
      AccNo@1005 : Code[20];
    BEGIN
      AccNo := GetDtldVendLedgEntryAccNo(GenJnlLine,DtldCVLedgEntryBuf,VendPostingGr,0,FALSE);
      PostDtldCVLedgEntry(GenJnlLine,DtldCVLedgEntryBuf,AccNo,AdjAmount,FALSE);
    END;

    LOCAL PROCEDURE PostDtldVendLedgEntryUnapply@69(GenJnlLine@1007 : Record 81;DtldCVLedgEntryBuf@1001 : Record 383;VendPostingGr@1000 : Record 93;OriginalTransactionNo@1006 : Integer);
    VAR
      AccNo@1002 : Code[20];
      AdjAmount@1003 : ARRAY [4] OF Decimal;
    BEGIN
      IF (DtldCVLedgEntryBuf."Amount (LCY)" = 0) AND
         (DtldCVLedgEntryBuf."VAT Amount (LCY)" = 0) AND
         ((AddCurrencyCode = '') OR (DtldCVLedgEntryBuf."Additional-Currency Amount" = 0))
      THEN
        EXIT;

      AccNo := GetDtldVendLedgEntryAccNo(GenJnlLine,DtldCVLedgEntryBuf,VendPostingGr,OriginalTransactionNo,TRUE);
      DtldCVLedgEntryBuf."Gen. Posting Type" := DtldCVLedgEntryBuf."Gen. Posting Type"::Purchase;
      PostDtldCVLedgEntry(GenJnlLine,DtldCVLedgEntryBuf,AccNo,AdjAmount,TRUE);
    END;

    LOCAL PROCEDURE GetDtldVendLedgEntryAccNo@56(GenJnlLine@1000 : Record 81;DtldCVLedgEntryBuf@1002 : Record 383;VendPostingGr@1006 : Record 93;OriginalTransactionNo@1003 : Integer;Unapply@1001 : Boolean) : Code[20];
    VAR
      Currency@1008 : Record 4;
      GenPostingSetup@1007 : Record 252;
      AmountCondition@1004 : Boolean;
    BEGIN
      WITH DtldCVLedgEntryBuf DO BEGIN
        AmountCondition := IsDebitAmount(DtldCVLedgEntryBuf,Unapply);
        CASE "Entry Type" OF
          "Entry Type"::"Initial Entry":
            ;
          "Entry Type"::Application:
            ;
          "Entry Type"::"Unrealized Loss",
          "Entry Type"::"Unrealized Gain",
          "Entry Type"::"Realized Loss",
          "Entry Type"::"Realized Gain":
            BEGIN
              GetCurrency(Currency,"Currency Code");
              CheckNonAddCurrCodeOccurred(Currency.Code);
              EXIT(Currency.GetGainLossAccount(DtldCVLedgEntryBuf));
            END;
          "Entry Type"::"Payment Discount":
            EXIT(VendPostingGr.GetPmtDiscountAccount(AmountCondition));
          "Entry Type"::"Payment Discount (VAT Excl.)":
            BEGIN
              GenPostingSetup.GET("Gen. Bus. Posting Group","Gen. Prod. Posting Group");
              EXIT(GenPostingSetup.GetPurchPmtDiscountAccount(AmountCondition));
            END;
          "Entry Type"::"Appln. Rounding":
            EXIT(VendPostingGr.GetApplRoundingAccount(AmountCondition));
          "Entry Type"::"Correction of Remaining Amount":
            EXIT(VendPostingGr.GetRoundingAccount(AmountCondition));
          "Entry Type"::"Payment Discount Tolerance":
            CASE GLSetup."Pmt. Disc. Tolerance Posting" OF
              GLSetup."Pmt. Disc. Tolerance Posting"::"Payment Tolerance Accounts":
                EXIT(VendPostingGr.GetPmtToleranceAccount(AmountCondition));
              GLSetup."Pmt. Disc. Tolerance Posting"::"Payment Discount Accounts":
                EXIT(VendPostingGr.GetPmtDiscountAccount(AmountCondition));
            END;
          "Entry Type"::"Payment Tolerance":
            CASE GLSetup."Payment Tolerance Posting" OF
              GLSetup."Payment Tolerance Posting"::"Payment Tolerance Accounts":
                EXIT(VendPostingGr.GetPmtToleranceAccount(AmountCondition));
              GLSetup."Payment Tolerance Posting"::"Payment Discount Accounts":
                EXIT(VendPostingGr.GetPmtDiscountAccount(AmountCondition));
            END;
          "Entry Type"::"Payment Tolerance (VAT Excl.)":
            BEGIN
              GenPostingSetup.GET("Gen. Bus. Posting Group","Gen. Prod. Posting Group");
              CASE GLSetup."Payment Tolerance Posting" OF
                GLSetup."Payment Tolerance Posting"::"Payment Tolerance Accounts":
                  EXIT(GenPostingSetup.GetPurchPmtToleranceAccount(AmountCondition));
                GLSetup."Payment Tolerance Posting"::"Payment Discount Accounts":
                  EXIT(GenPostingSetup.GetPurchPmtDiscountAccount(AmountCondition));
              END;
            END;
          "Entry Type"::"Payment Discount Tolerance (VAT Excl.)":
            BEGIN
              GenPostingSetup.GET("Gen. Bus. Posting Group","Gen. Prod. Posting Group");
              CASE GLSetup."Pmt. Disc. Tolerance Posting" OF
                GLSetup."Pmt. Disc. Tolerance Posting"::"Payment Tolerance Accounts":
                  EXIT(GenPostingSetup.GetPurchPmtToleranceAccount(AmountCondition));
                GLSetup."Pmt. Disc. Tolerance Posting"::"Payment Discount Accounts":
                  EXIT(GenPostingSetup.GetPurchPmtDiscountAccount(AmountCondition));
              END;
            END;
          "Entry Type"::"Payment Discount (VAT Adjustment)",
          "Entry Type"::"Payment Tolerance (VAT Adjustment)",
          "Entry Type"::"Payment Discount Tolerance (VAT Adjustment)":
            IF Unapply THEN
              PostDtldVendVATAdjustment(GenJnlLine,DtldCVLedgEntryBuf,OriginalTransactionNo);
          ELSE
            FIELDERROR("Entry Type");
        END;
      END;
    END;

    LOCAL PROCEDURE PostDtldEmplLedgEntries@148(GenJnlLine@1000 : Record 81;VAR DtldCVLedgEntryBuf@1001 : Record 383;EmplPostingGr@1002 : Record 5221;LedgEntryInserted@1011 : Boolean) DtldLedgEntryInserted : Boolean;
    VAR
      TempInvPostBuf@1007 : TEMPORARY Record 49;
      DtldEmplLedgEntry@1004 : Record 5223;
      DummyAdjAmount@1012 : ARRAY [4] OF Decimal;
      DtldEmplLedgEntryNoOffset@1005 : Integer;
      SaveEntryNo@1013 : Integer;
    BEGIN
      IF GenJnlLine."Account Type" <> GenJnlLine."Account Type"::Employee THEN
        EXIT;

      IF DtldEmplLedgEntry.FINDLAST THEN
        DtldEmplLedgEntryNoOffset := DtldEmplLedgEntry."Entry No."
      ELSE
        DtldEmplLedgEntryNoOffset := 0;

      DtldCVLedgEntryBuf.RESET;
      IF DtldCVLedgEntryBuf.FINDSET THEN BEGIN
        IF LedgEntryInserted THEN BEGIN
          SaveEntryNo := NextEntryNo;
          NextEntryNo := NextEntryNo + 1;
        END;
        REPEAT
          InsertDtldEmplLedgEntry(GenJnlLine,DtldCVLedgEntryBuf,DtldEmplLedgEntry,DtldEmplLedgEntryNoOffset);

          UpdateTotalAmounts(
            TempInvPostBuf,GenJnlLine."Dimension Set ID",
            DtldCVLedgEntryBuf."Amount (LCY)",DtldCVLedgEntryBuf."Additional-Currency Amount");
        UNTIL DtldCVLedgEntryBuf.NEXT = 0;
      END;

      CreateGLEntriesForTotalAmounts(
        GenJnlLine,TempInvPostBuf,DummyAdjAmount,SaveEntryNo,EmplPostingGr.GetPayablesAccount,LedgEntryInserted);

      DtldLedgEntryInserted := NOT DtldCVLedgEntryBuf.ISEMPTY;
      DtldCVLedgEntryBuf.DELETEALL;
    END;

    LOCAL PROCEDURE PostDtldCVLedgEntry@15(GenJnlLine@1000 : Record 81;DtldCVLedgEntryBuf@1001 : Record 383;AccNo@1002 : Code[20];VAR AdjAmount@1004 : ARRAY [4] OF Decimal;Unapply@1005 : Boolean);
    BEGIN
      WITH DtldCVLedgEntryBuf DO
        CASE "Entry Type" OF
          "Entry Type"::"Initial Entry":
            ;
          "Entry Type"::Application:
            ;
          "Entry Type"::"Unrealized Loss",
          "Entry Type"::"Unrealized Gain",
          "Entry Type"::"Realized Loss",
          "Entry Type"::"Realized Gain":
            BEGIN
              CreateGLEntryGainLoss(GenJnlLine,AccNo,-"Amount (LCY)","Currency Code" = AddCurrencyCode);
              IF NOT Unapply THEN
                CollectAdjustment(AdjAmount,-"Amount (LCY)",0);
            END;
          "Entry Type"::"Payment Discount",
          "Entry Type"::"Payment Tolerance",
          "Entry Type"::"Payment Discount Tolerance":
            BEGIN
              CreateGLEntry(GenJnlLine,AccNo,-"Amount (LCY)",-"Additional-Currency Amount",FALSE);
              IF NOT Unapply THEN
                CollectAdjustment(AdjAmount,-"Amount (LCY)",-"Additional-Currency Amount");
            END;
          "Entry Type"::"Payment Discount (VAT Excl.)",
          "Entry Type"::"Payment Tolerance (VAT Excl.)",
          "Entry Type"::"Payment Discount Tolerance (VAT Excl.)":
            BEGIN
              IF NOT Unapply THEN
                CreateGLEntryVATCollectAdj(
                  GenJnlLine,AccNo,-"Amount (LCY)",-"Additional-Currency Amount",-"VAT Amount (LCY)",DtldCVLedgEntryBuf,
                  AdjAmount)
              ELSE
                CreateGLEntryVAT(
                  GenJnlLine,AccNo,-"Amount (LCY)",-"Additional-Currency Amount",-"VAT Amount (LCY)",DtldCVLedgEntryBuf);
            END;
          "Entry Type"::"Appln. Rounding":
            IF "Amount (LCY)" <> 0 THEN BEGIN
              CreateGLEntry(GenJnlLine,AccNo,-"Amount (LCY)",-"Additional-Currency Amount",TRUE);
              IF NOT Unapply THEN
                CollectAdjustment(AdjAmount,-"Amount (LCY)",-"Additional-Currency Amount");
            END;
          "Entry Type"::"Correction of Remaining Amount":
            IF "Amount (LCY)" <> 0 THEN BEGIN
              CreateGLEntry(GenJnlLine,AccNo,-"Amount (LCY)",0,FALSE);
              IF NOT Unapply THEN
                CollectAdjustment(AdjAmount,-"Amount (LCY)",0);
            END;
          "Entry Type"::"Payment Discount (VAT Adjustment)",
          "Entry Type"::"Payment Tolerance (VAT Adjustment)",
          "Entry Type"::"Payment Discount Tolerance (VAT Adjustment)":
            ;
          ELSE
            FIELDERROR("Entry Type");
        END;
    END;

    LOCAL PROCEDURE PostDtldCustVATAdjustment@75(GenJnlLine@1003 : Record 81;DtldCVLedgEntryBuf@1002 : Record 383;OriginalTransactionNo@1000 : Integer);
    VAR
      VATPostingSetup@1005 : Record 325;
      TaxJurisdiction@1004 : Record 320;
    BEGIN
      WITH DtldCVLedgEntryBuf DO BEGIN
        FindVATEntry(VATEntry,OriginalTransactionNo);

        CASE VATPostingSetup."VAT Calculation Type" OF
          VATPostingSetup."VAT Calculation Type"::"Normal VAT",
          VATPostingSetup."VAT Calculation Type"::"Full VAT":
            BEGIN
              VATPostingSetup.GET("VAT Bus. Posting Group","VAT Prod. Posting Group");
              VATPostingSetup.TESTFIELD("VAT Calculation Type",VATEntry."VAT Calculation Type");
              CreateGLEntry(
                GenJnlLine,VATPostingSetup.GetSalesAccount(FALSE),-"Amount (LCY)",-"Additional-Currency Amount",FALSE);
            END;
          VATPostingSetup."VAT Calculation Type"::"Reverse Charge VAT":
            ;
          VATPostingSetup."VAT Calculation Type"::"Sales Tax":
            BEGIN
              TESTFIELD("Tax Jurisdiction Code");
              TaxJurisdiction.GET("Tax Jurisdiction Code");
              CreateGLEntry(
                GenJnlLine,TaxJurisdiction.GetPurchAccount(FALSE),-"Amount (LCY)",-"Additional-Currency Amount",FALSE);
            END;
        END;
      END;
    END;

    LOCAL PROCEDURE PostDtldVendVATAdjustment@73(GenJnlLine@1004 : Record 81;DtldCVLedgEntryBuf@1002 : Record 383;OriginalTransactionNo@1000 : Integer);
    VAR
      VATPostingSetup@1003 : Record 325;
      TaxJurisdiction@1005 : Record 320;
    BEGIN
      WITH DtldCVLedgEntryBuf DO BEGIN
        FindVATEntry(VATEntry,OriginalTransactionNo);

        CASE VATPostingSetup."VAT Calculation Type" OF
          VATPostingSetup."VAT Calculation Type"::"Normal VAT",
          VATPostingSetup."VAT Calculation Type"::"Full VAT":
            BEGIN
              VATPostingSetup.GET("VAT Bus. Posting Group","VAT Prod. Posting Group");
              VATPostingSetup.TESTFIELD("VAT Calculation Type",VATEntry."VAT Calculation Type");
              CreateGLEntry(
                GenJnlLine,VATPostingSetup.GetPurchAccount(FALSE),-"Amount (LCY)",-"Additional-Currency Amount",FALSE);
            END;
          VATPostingSetup."VAT Calculation Type"::"Reverse Charge VAT":
            BEGIN
              VATPostingSetup.GET("VAT Bus. Posting Group","VAT Prod. Posting Group");
              VATPostingSetup.TESTFIELD("VAT Calculation Type",VATEntry."VAT Calculation Type");
              CreateGLEntry(
                GenJnlLine,VATPostingSetup.GetPurchAccount(FALSE),-"Amount (LCY)",-"Additional-Currency Amount",FALSE);
              CreateGLEntry(
                GenJnlLine,VATPostingSetup.GetRevChargeAccount(FALSE),"Amount (LCY)","Additional-Currency Amount",FALSE);
            END;
          VATPostingSetup."VAT Calculation Type"::"Sales Tax":
            BEGIN
              TaxJurisdiction.GET("Tax Jurisdiction Code");
              IF "Use Tax" THEN BEGIN
                CreateGLEntry(
                  GenJnlLine,TaxJurisdiction.GetPurchAccount(FALSE),-"Amount (LCY)",-"Additional-Currency Amount",FALSE);
                CreateGLEntry(
                  GenJnlLine,TaxJurisdiction.GetRevChargeAccount(FALSE),"Amount (LCY)","Additional-Currency Amount",FALSE);
              END ELSE
                CreateGLEntry(
                  GenJnlLine,TaxJurisdiction.GetPurchAccount(FALSE),-"Amount (LCY)",-"Additional-Currency Amount",FALSE);
            END;
        END;
      END;
    END;

    LOCAL PROCEDURE VendUnrealizedVAT@18(GenJnlLine@1017 : Record 81;VAR VendLedgEntry2@1000 : Record 25;SettledAmount@1001 : Decimal);
    VAR
      VATEntry2@1002 : Record 254;
      TaxJurisdiction@1016 : Record 320;
      VATPostingSetup@1019 : Record 325;
      PurchCrMemoHeader@1240001 : Record 124;
      VATPart@1003 : Decimal;
      VATAmount@1004 : Decimal;
      VATBase@1005 : Decimal;
      VATAmountAddCurr@1006 : Decimal;
      VATBaseAddCurr@1007 : Decimal;
      PaidAmount@1008 : Decimal;
      TotalUnrealVATAmountFirst@1014 : Decimal;
      TotalUnrealVATAmountLast@1015 : Decimal;
      PurchVATAccount@1009 : Code[20];
      PurchVATUnrealAccount@1010 : Code[20];
      PurchReverseAccount@1011 : Code[20];
      PurchReverseUnrealAccount@1012 : Code[20];
      LastConnectionNo@1013 : Integer;
      WHTAmount@1240000 : Decimal;
      GLEntryNo@1018 : Integer;
    BEGIN
      VATEntry2.RESET;
      VATEntry2.SETCURRENTKEY("Transaction No.");
      VATEntry2.SETRANGE("Transaction No.",VendLedgEntry2."Transaction No.");
      VendLedgEntry2.CALCFIELDS("Amount (LCY)","Original Amt. (LCY)");
      PaidAmount := -VendLedgEntry2."Amount (LCY)" + VendLedgEntry2."Remaining Amt. (LCY)";
      IF VATEntry2.FINDSET THEN
        REPEAT
          VATPostingSetup.GET(VATEntry2."VAT Bus. Posting Group",VATEntry2."VAT Prod. Posting Group");
          IF VATPostingSetup."Unrealized VAT Type" IN
             [VATPostingSetup."Unrealized VAT Type"::Last,VATPostingSetup."Unrealized VAT Type"::"Last (Fully Paid)"]
          THEN
            TotalUnrealVATAmountLast := TotalUnrealVATAmountLast - VATEntry2."Remaining Unrealized Amount";
          IF VATPostingSetup."Unrealized VAT Type" IN
             [VATPostingSetup."Unrealized VAT Type"::First,VATPostingSetup."Unrealized VAT Type"::"First (Fully Paid)"]
          THEN
            TotalUnrealVATAmountFirst := TotalUnrealVATAmountFirst - VATEntry2."Remaining Unrealized Amount";
        UNTIL VATEntry2.NEXT = 0;
      IF VATEntry2.FINDSET THEN BEGIN
        LastConnectionNo := 0;
        REPEAT
          VATPostingSetup.GET(VATEntry2."VAT Bus. Posting Group",VATEntry2."VAT Prod. Posting Group");
          IF LastConnectionNo <> VATEntry2."Sales Tax Connection No." THEN BEGIN
            InsertSummarizedVAT(GenJnlLine);
            LastConnectionNo := VATEntry2."Sales Tax Connection No.";
          END;
          IF GenJnlLine."Document Type" = GenJnlLine."Document Type"::Refund THEN
            IF GLSetup."Manual Sales WHT Calc." THEN BEGIN
              IF PurchCrMemoHeader.GET(VendLedgEntry2."Document No.") THEN
                WHTAmount := -CollectWHTAmount(PurchCrMemoHeader."No.");
          END;

          VATPart :=
            VATEntry2.GetUnrealizedVATPart(
              ROUND(SettledAmount / VendLedgEntry2.GetAdjustedCurrencyFactor),
              PaidAmount,
              VendLedgEntry2."Amount (LCY)",
              TotalUnrealVATAmountFirst,
              TotalUnrealVATAmountLast);

          IF VATPart > 0 THEN BEGIN
            CASE VATEntry2."VAT Calculation Type" OF
              VATEntry2."VAT Calculation Type"::"Normal VAT",
              VATEntry2."VAT Calculation Type"::"Full VAT":
                BEGIN
                  PurchVATAccount := VATPostingSetup.GetPurchAccount(FALSE);
                  PurchVATUnrealAccount := VATPostingSetup.GetPurchAccount(TRUE);
                END;
              VATEntry2."VAT Calculation Type"::"Reverse Charge VAT":
                BEGIN
                  PurchVATAccount := VATPostingSetup.GetPurchAccount(FALSE);
                  PurchVATUnrealAccount := VATPostingSetup.GetPurchAccount(TRUE);
                  PurchReverseAccount := VATPostingSetup.GetRevChargeAccount(FALSE);
                  PurchReverseUnrealAccount := VATPostingSetup.GetRevChargeAccount(TRUE);
                END;
              VATEntry2."VAT Calculation Type"::"Sales Tax":
                IF (VATEntry2.Type = VATEntry2.Type::Purchase) AND VATEntry2."Use Tax" THEN BEGIN
                  TaxJurisdiction.GET(VATEntry2."Tax Jurisdiction Code");
                  PurchVATAccount := TaxJurisdiction.GetPurchAccount(FALSE);
                  PurchVATUnrealAccount := TaxJurisdiction.GetPurchAccount(TRUE);
                  PurchReverseAccount := TaxJurisdiction.GetRevChargeAccount(FALSE);
                  PurchReverseUnrealAccount := TaxJurisdiction.GetRevChargeAccount(TRUE);
                END ELSE BEGIN
                  TaxJurisdiction.GET(VATEntry2."Tax Jurisdiction Code");
                  PurchVATAccount := TaxJurisdiction.GetPurchAccount(FALSE);
                  PurchVATUnrealAccount := TaxJurisdiction.GetPurchAccount(TRUE);
                END;
            END;

            IF VATPart = 1 THEN BEGIN
              VATAmount := VATEntry2."Remaining Unrealized Amount";
              VATBase := VATEntry2."Remaining Unrealized Base";
              VATAmountAddCurr := VATEntry2."Add.-Curr. Rem. Unreal. Amount";
              VATBaseAddCurr := VATEntry2."Add.-Curr. Rem. Unreal. Base";
            END ELSE BEGIN
              VATAmount := ROUND(VATEntry2."Remaining Unrealized Amount" * VATPart,GLSetup."Amount Rounding Precision");
              VATBase := ROUND(VATEntry2."Remaining Unrealized Base" * VATPart,GLSetup."Amount Rounding Precision");
              VATAmountAddCurr :=
                ROUND(
                  VATEntry2."Add.-Curr. Rem. Unreal. Amount" * VATPart,
                  AddCurrency."Amount Rounding Precision");
              VATBaseAddCurr :=
                ROUND(
                  VATEntry2."Add.-Curr. Rem. Unreal. Base" * VATPart,
                  AddCurrency."Amount Rounding Precision");
            END;

            InitGLEntryVAT(
              GenJnlLine,PurchVATUnrealAccount,PurchVATAccount,-VATAmount,-VATAmountAddCurr,FALSE);
            GLEntryNo :=
              InitGLEntryVATCopy(GenJnlLine,PurchVATAccount,PurchVATUnrealAccount,VATAmount,VATAmountAddCurr,VATEntry2);

            IF (VATEntry2."VAT Calculation Type" =
                VATEntry2."VAT Calculation Type"::"Reverse Charge VAT") OR
               ((VATEntry2."VAT Calculation Type" =
                 VATEntry2."VAT Calculation Type"::"Sales Tax") AND
                (VATEntry2.Type = VATEntry2.Type::Purchase) AND VATEntry2."Use Tax")
            THEN BEGIN
              InitGLEntryVAT(
                GenJnlLine,PurchReverseUnrealAccount,PurchReverseAccount,VATAmount,VATAmountAddCurr,FALSE);
              GLEntryNo :=
                InitGLEntryVATCopy(GenJnlLine,PurchReverseAccount,PurchReverseUnrealAccount,-VATAmount,-VATAmountAddCurr,VATEntry2);
            END;

            PostUnrealVATEntry(GenJnlLine,VATEntry2,VATAmount,VATBase,VATAmountAddCurr,VATBaseAddCurr,GLEntryNo);
          END;
        UNTIL VATEntry2.NEXT = 0;

        InsertSummarizedVAT(GenJnlLine);
      END;
    END;

    LOCAL PROCEDURE PostUnrealVATEntry@5(GenJnlLine@1002 : Record 81;VAR VATEntry2@1000 : Record 254;VATAmount@1003 : Decimal;VATBase@1004 : Decimal;VATAmountAddCurr@1006 : Decimal;VATBaseAddCurr@1005 : Decimal;GLEntryNo@1001 : Integer);
    BEGIN
      VATEntry.LOCKTABLE;
      VATEntry := VATEntry2;
      VATEntry."Entry No." := NextVATEntryNo;
      VATEntry."Posting Date" := GenJnlLine."Posting Date";
      VATEntry."Document No." := GenJnlLine."Document No.";
      VATEntry."External Document No." := GenJnlLine."External Document No.";
      VATEntry."Document Type" := GenJnlLine."Document Type";
      VATEntry.Amount := VATAmount;
      VATEntry.Base := VATBase;
      VATEntry."Additional-Currency Amount" := VATAmountAddCurr;
      VATEntry."Additional-Currency Base" := VATBaseAddCurr;
      VATEntry.SetUnrealAmountsToZero;
      VATEntry."User ID" := USERID;
      VATEntry."Source Code" := GenJnlLine."Source Code";
      VATEntry."Reason Code" := GenJnlLine."Reason Code";
      VATEntry."Closed by Entry No." := 0;
      VATEntry.Closed := FALSE;
      VATEntry."Transaction No." := NextTransactionNo;
      VATEntry."Sales Tax Connection No." := NextConnectionNo;
      VATEntry."Unrealized VAT Entry No." := VATEntry2."Entry No.";
      VATEntry.INSERT(TRUE);
      GLEntryVATEntryLink.InsertLink(GLEntryNo + 1,NextVATEntryNo);
      NextVATEntryNo := NextVATEntryNo + 1;

      VATEntry2."Remaining Unrealized Amount" :=
        VATEntry2."Remaining Unrealized Amount" - VATEntry.Amount;
      VATEntry2."Remaining Unrealized Base" :=
        VATEntry2."Remaining Unrealized Base" - VATEntry.Base;
      VATEntry2."Add.-Curr. Rem. Unreal. Amount" :=
        VATEntry2."Add.-Curr. Rem. Unreal. Amount" - VATEntry."Additional-Currency Amount";
      VATEntry2."Add.-Curr. Rem. Unreal. Base" :=
        VATEntry2."Add.-Curr. Rem. Unreal. Base" - VATEntry."Additional-Currency Base";
      VATEntry2.MODIFY;
    END;

    LOCAL PROCEDURE PostApplyCust@1500105(GenJnlLine@1007 : Record 81;VAR DtldCVLedgEntryBuf@1008 : Record 383;VAR OldCVLedgEntryBuf@1000 : Record 382;VAR NewCVLedgEntryBuf@1005 : Record 382;VAR NewCVLedgEntryBuf2@1013 : Record 382;Cust@1006 : Record 18;AllApplied@1009 : Boolean;VAR AppliedAmount@1016 : Decimal;VAR OldAppliedAmount@1250000 : Decimal;VAR PmtTolAmtToBeApplied@1010 : Decimal;CustLedgEntry2@1250001 : Record 21);
    VAR
      OldCVLedgEntryBuf2@1003 : Record 382;
      OldCVLedgEntryBuf3@1002 : Record 382;
      OldRemainingAmtBeforeAppln@1001 : Decimal;
      ApplnRoundingPrecision@1004 : Decimal;
      AppliedAmountLCY@1012 : Decimal;
    BEGIN
      OldRemainingAmtBeforeAppln := OldCVLedgEntryBuf."Remaining Amount";
      OldCVLedgEntryBuf3 := OldCVLedgEntryBuf;

      // Management of posting in multiple currencies
      OldCVLedgEntryBuf2 := OldCVLedgEntryBuf;
      OldCVLedgEntryBuf.COPYFILTER(Positive,OldCVLedgEntryBuf2.Positive);
      ApplnRoundingPrecision := GetApplnRoundPrecision(NewCVLedgEntryBuf,OldCVLedgEntryBuf);

      OldCVLedgEntryBuf2.RecalculateAmounts(
        OldCVLedgEntryBuf2."Currency Code",NewCVLedgEntryBuf."Currency Code",NewCVLedgEntryBuf."Posting Date");

      IF NOT Cust."Block Payment Tolerance" THEN
        CalcPmtTolerance(
          NewCVLedgEntryBuf,OldCVLedgEntryBuf,OldCVLedgEntryBuf2,DtldCVLedgEntryBuf,GenJnlLine,
          PmtTolAmtToBeApplied,NextTransactionNo,FirstNewVATEntryNo);

      IF CustLedgEntry2."Document Type" = CustLedgEntry2."Document Type"::Payment THEN BEGIN
        IF CheckRem THEN
          DtldCVLedgEntryBuf.InitDtldCVLedgEntryBuf(
            GenJnlLine,NewCVLedgEntryBuf,DtldCVLedgEntryBuf,
            DtldCVLedgEntryBuf."Entry Type"::"Payment Discount",
            -NewCVLedgEntryBuf."Remaining Pmt. Disc. Possible",
            -NewCVLedgEntryBuf."Remaining Pmt. Disc. Possible",
            -NewCVLedgEntryBuf."Remaining Pmt. Disc. Possible",0,0,0);
        CalcPmtDisc(
          OldCVLedgEntryBuf,NewCVLedgEntryBuf,NewCVLedgEntryBuf,DtldCVLedgEntryBuf,GenJnlLine,
          PmtTolAmtToBeApplied,ApplnRoundingPrecision,NextTransactionNo,FirstNewVATEntryNo);
        CheckRem := TRUE;
      END ELSE
        CalcPmtDisc(
          NewCVLedgEntryBuf,OldCVLedgEntryBuf,OldCVLedgEntryBuf2,DtldCVLedgEntryBuf,GenJnlLine,
          PmtTolAmtToBeApplied,ApplnRoundingPrecision,NextTransactionNo,FirstNewVATEntryNo);

      IF NOT Cust."Block Payment Tolerance" THEN
        CalcPmtDiscTolerance(
          NewCVLedgEntryBuf,OldCVLedgEntryBuf,OldCVLedgEntryBuf2,DtldCVLedgEntryBuf,GenJnlLine,
          NextTransactionNo,FirstNewVATEntryNo);

      CalcCurrencyApplnRounding(
        NewCVLedgEntryBuf,OldCVLedgEntryBuf2,DtldCVLedgEntryBuf,
        GenJnlLine,ApplnRoundingPrecision);

      FindAmtForAppln(
        NewCVLedgEntryBuf,OldCVLedgEntryBuf,OldCVLedgEntryBuf2,
        AppliedAmount,AppliedAmountLCY,OldAppliedAmount,ApplnRoundingPrecision);

      CalcCurrencyUnrealizedGainLoss(
        OldCVLedgEntryBuf,DtldCVLedgEntryBuf,GenJnlLine,-OldAppliedAmount,OldRemainingAmtBeforeAppln);

      CalcCurrencyRealizedGainLoss(
        NewCVLedgEntryBuf,DtldCVLedgEntryBuf,GenJnlLine,AppliedAmount,AppliedAmountLCY);

      CalcCurrencyRealizedGainLoss(
        OldCVLedgEntryBuf,DtldCVLedgEntryBuf,GenJnlLine,-OldAppliedAmount,-AppliedAmountLCY);

      CalcApplication(
        NewCVLedgEntryBuf,OldCVLedgEntryBuf,DtldCVLedgEntryBuf,
        GenJnlLine,AppliedAmount,AppliedAmountLCY,OldAppliedAmount,
        NewCVLedgEntryBuf2,OldCVLedgEntryBuf3,AllApplied);

      PaymentToleranceMgt.CalcRemainingPmtDisc(NewCVLedgEntryBuf,OldCVLedgEntryBuf,OldCVLedgEntryBuf2,GLSetup);

      CalcAmtLCYAdjustment(OldCVLedgEntryBuf,DtldCVLedgEntryBuf,GenJnlLine);
    END;

    LOCAL PROCEDURE PostApplyVend@1500106(GenJnlLine@1007 : Record 81;VAR DtldCVLedgEntryBuf@1008 : Record 383;VAR OldCVLedgEntryBuf@1000 : Record 382;VAR NewCVLedgEntryBuf@1005 : Record 382;VAR NewCVLedgEntryBuf2@1013 : Record 382;Vend@1006 : Record 23;AllApplied@1009 : Boolean;VAR AppliedAmount@1016 : Decimal;VAR OldAppliedAmount@1240000 : Decimal;VAR PmtTolAmtToBeApplied@1010 : Decimal);
    VAR
      OldCVLedgEntryBuf2@1003 : Record 382;
      OldCVLedgEntryBuf3@1002 : Record 382;
      OldRemainingAmtBeforeAppln@1001 : Decimal;
      ApplnRoundingPrecision@1004 : Decimal;
      AppliedAmountLCY@1012 : Decimal;
    BEGIN
      OldRemainingAmtBeforeAppln := OldCVLedgEntryBuf."Remaining Amount";
      OldCVLedgEntryBuf3 := OldCVLedgEntryBuf;

      // Management of posting in multiple currencies
      OldCVLedgEntryBuf2 := OldCVLedgEntryBuf;
      OldCVLedgEntryBuf.COPYFILTER(Positive,OldCVLedgEntryBuf2.Positive);
      ApplnRoundingPrecision := GetApplnRoundPrecision(NewCVLedgEntryBuf,OldCVLedgEntryBuf);

      OldCVLedgEntryBuf2.RecalculateAmounts(
        OldCVLedgEntryBuf2."Currency Code",NewCVLedgEntryBuf."Currency Code",NewCVLedgEntryBuf."Posting Date");

      IF NOT Vend."Block Payment Tolerance" THEN
        CalcPmtTolerance(
          NewCVLedgEntryBuf,OldCVLedgEntryBuf,OldCVLedgEntryBuf2,DtldCVLedgEntryBuf,GenJnlLine,
          PmtTolAmtToBeApplied,NextTransactionNo,FirstNewVATEntryNo);

      CalcPmtDisc(
        NewCVLedgEntryBuf,OldCVLedgEntryBuf,OldCVLedgEntryBuf2,DtldCVLedgEntryBuf,GenJnlLine,
        PmtTolAmtToBeApplied,ApplnRoundingPrecision,NextTransactionNo,FirstNewVATEntryNo);

      IF NOT Vend."Block Payment Tolerance" THEN
        CalcPmtDiscTolerance(
          NewCVLedgEntryBuf,OldCVLedgEntryBuf,OldCVLedgEntryBuf2,DtldCVLedgEntryBuf,GenJnlLine,
          NextTransactionNo,FirstNewVATEntryNo);

      CalcCurrencyApplnRounding(
        NewCVLedgEntryBuf,OldCVLedgEntryBuf2,DtldCVLedgEntryBuf,
        GenJnlLine,ApplnRoundingPrecision);

      FindAmtForAppln(
        NewCVLedgEntryBuf,OldCVLedgEntryBuf,OldCVLedgEntryBuf2,
        AppliedAmount,AppliedAmountLCY,OldAppliedAmount,ApplnRoundingPrecision);

      CalcCurrencyUnrealizedGainLoss(
        OldCVLedgEntryBuf,DtldCVLedgEntryBuf,GenJnlLine,-OldAppliedAmount,OldRemainingAmtBeforeAppln);

      CalcCurrencyRealizedGainLoss(
        NewCVLedgEntryBuf,DtldCVLedgEntryBuf,GenJnlLine,AppliedAmount,AppliedAmountLCY);

      CalcCurrencyRealizedGainLoss(
        OldCVLedgEntryBuf,DtldCVLedgEntryBuf,GenJnlLine,-OldAppliedAmount,-AppliedAmountLCY);

      CalcApplication(
        NewCVLedgEntryBuf,OldCVLedgEntryBuf,DtldCVLedgEntryBuf,
        GenJnlLine,AppliedAmount,AppliedAmountLCY,OldAppliedAmount,
        NewCVLedgEntryBuf2,OldCVLedgEntryBuf3,AllApplied);

      PaymentToleranceMgt.CalcRemainingPmtDisc(NewCVLedgEntryBuf,OldCVLedgEntryBuf,OldCVLedgEntryBuf2,GLSetup);

      CalcAmtLCYAdjustment(OldCVLedgEntryBuf,DtldCVLedgEntryBuf,GenJnlLine);
    END;

    LOCAL PROCEDURE PostApply@105(GenJnlLine@1007 : Record 81;VAR DtldCVLedgEntryBuf@1008 : Record 383;VAR OldCVLedgEntryBuf@1000 : Record 382;VAR NewCVLedgEntryBuf@1005 : Record 382;VAR NewCVLedgEntryBuf2@1013 : Record 382;BlockPaymentTolerance@1006 : Boolean;AllApplied@1009 : Boolean;VAR AppliedAmount@1016 : Decimal;VAR PmtTolAmtToBeApplied@1010 : Decimal);
    VAR
      OldCVLedgEntryBuf2@1003 : Record 382;
      OldCVLedgEntryBuf3@1002 : Record 382;
      OldRemainingAmtBeforeAppln@1001 : Decimal;
      ApplnRoundingPrecision@1004 : Decimal;
      AppliedAmountLCY@1012 : Decimal;
      OldAppliedAmount@1011 : Decimal;
    BEGIN
      OldRemainingAmtBeforeAppln := OldCVLedgEntryBuf."Remaining Amount";
      OldCVLedgEntryBuf3 := OldCVLedgEntryBuf;

      // Management of posting in multiple currencies
      OldCVLedgEntryBuf2 := OldCVLedgEntryBuf;
      OldCVLedgEntryBuf.COPYFILTER(Positive,OldCVLedgEntryBuf2.Positive);
      ApplnRoundingPrecision := GetApplnRoundPrecision(NewCVLedgEntryBuf,OldCVLedgEntryBuf);

      OldCVLedgEntryBuf2.RecalculateAmounts(
        OldCVLedgEntryBuf2."Currency Code",NewCVLedgEntryBuf."Currency Code",NewCVLedgEntryBuf."Posting Date");

      IF NOT BlockPaymentTolerance THEN
        CalcPmtTolerance(
          NewCVLedgEntryBuf,OldCVLedgEntryBuf,OldCVLedgEntryBuf2,DtldCVLedgEntryBuf,GenJnlLine,
          PmtTolAmtToBeApplied,NextTransactionNo,FirstNewVATEntryNo);

      CalcPmtDisc(
        NewCVLedgEntryBuf,OldCVLedgEntryBuf,OldCVLedgEntryBuf2,DtldCVLedgEntryBuf,GenJnlLine,
        PmtTolAmtToBeApplied,ApplnRoundingPrecision,NextTransactionNo,FirstNewVATEntryNo);

      IF NOT BlockPaymentTolerance THEN
        CalcPmtDiscTolerance(
          NewCVLedgEntryBuf,OldCVLedgEntryBuf,OldCVLedgEntryBuf2,DtldCVLedgEntryBuf,GenJnlLine,
          NextTransactionNo,FirstNewVATEntryNo);

      CalcCurrencyApplnRounding(
        NewCVLedgEntryBuf,OldCVLedgEntryBuf2,DtldCVLedgEntryBuf,
        GenJnlLine,ApplnRoundingPrecision);

      FindAmtForAppln(
        NewCVLedgEntryBuf,OldCVLedgEntryBuf,OldCVLedgEntryBuf2,
        AppliedAmount,AppliedAmountLCY,OldAppliedAmount,ApplnRoundingPrecision);

      CalcCurrencyUnrealizedGainLoss(
        OldCVLedgEntryBuf,DtldCVLedgEntryBuf,GenJnlLine,-OldAppliedAmount,OldRemainingAmtBeforeAppln);

      CalcCurrencyRealizedGainLoss(
        NewCVLedgEntryBuf,DtldCVLedgEntryBuf,GenJnlLine,AppliedAmount,AppliedAmountLCY);

      CalcCurrencyRealizedGainLoss(
        OldCVLedgEntryBuf,DtldCVLedgEntryBuf,GenJnlLine,-OldAppliedAmount,-AppliedAmountLCY);

      CalcApplication(
        NewCVLedgEntryBuf,OldCVLedgEntryBuf,DtldCVLedgEntryBuf,
        GenJnlLine,AppliedAmount,AppliedAmountLCY,OldAppliedAmount,
        NewCVLedgEntryBuf2,OldCVLedgEntryBuf3,AllApplied);

      PaymentToleranceMgt.CalcRemainingPmtDisc(NewCVLedgEntryBuf,OldCVLedgEntryBuf,OldCVLedgEntryBuf2,GLSetup);

      CalcAmtLCYAdjustment(OldCVLedgEntryBuf,DtldCVLedgEntryBuf,GenJnlLine);
    END;

    [External]
    PROCEDURE UnapplyCustLedgEntry@109(GenJnlLine2@1004 : Record 81;DtldCustLedgEntry@1003 : Record 379);
    VAR
      Cust@1006 : Record 18;
      CustPostingGr@1019 : Record 92;
      GenJnlLine@1021 : Record 81;
      DtldCustLedgEntry2@1013 : Record 379;
      NewDtldCustLedgEntry@1012 : Record 379;
      CustLedgEntry@1011 : Record 21;
      DtldCVLedgEntryBuf@1010 : Record 383;
      VATEntry@1009 : Record 254;
      TempVATEntry2@1023 : TEMPORARY Record 254;
      CurrencyLCY@1024 : Record 4;
      TempInvPostBuf@1002 : TEMPORARY Record 49;
      WHTEntry@1250000 : Record 50004;
      AdjAmount@1031 : ARRAY [4] OF Decimal;
      NextDtldLedgEntryNo@1001 : Integer;
      UnapplyVATEntries@1000 : Boolean;
      PmtDiscTolExists@1007 : Boolean;
    BEGIN
      GenJnlLine.TRANSFERFIELDS(GenJnlLine2);
      IF GenJnlLine."Document Date" = 0D THEN
        GenJnlLine."Document Date" := GenJnlLine."Posting Date";

      IF NextEntryNo = 0 THEN
        StartPosting(GenJnlLine)
      ELSE
        ContinuePosting(GenJnlLine);

      ReadGLSetup(GLSetup);

      Cust.GET(DtldCustLedgEntry."Customer No.");
      Cust.CheckBlockedCustOnJnls(Cust,GenJnlLine2."Document Type"::Payment,TRUE);
      CustPostingGr.GET(GenJnlLine."Posting Group");
      CustPostingGr.GetReceivablesAccount;

      VATEntry.LOCKTABLE;
      DtldCustLedgEntry.LOCKTABLE;
      CustLedgEntry.LOCKTABLE;

      DtldCustLedgEntry.TESTFIELD("Entry Type",DtldCustLedgEntry."Entry Type"::Application);

      DtldCustLedgEntry2.RESET;
      DtldCustLedgEntry2.FINDLAST;
      NextDtldLedgEntryNo := DtldCustLedgEntry2."Entry No." + 1;
      IF DtldCustLedgEntry."Transaction No." = 0 THEN BEGIN
        DtldCustLedgEntry2.SETCURRENTKEY("Application No.","Customer No.","Entry Type");
        DtldCustLedgEntry2.SETRANGE("Application No.",DtldCustLedgEntry."Application No.");
      END ELSE BEGIN
        DtldCustLedgEntry2.SETCURRENTKEY("Transaction No.","Customer No.","Entry Type");
        DtldCustLedgEntry2.SETRANGE("Transaction No.",DtldCustLedgEntry."Transaction No.");
      END;
      DtldCustLedgEntry2.SETRANGE("Customer No.",DtldCustLedgEntry."Customer No.");
      DtldCustLedgEntry2.SETFILTER("Entry Type",'>%1',DtldCustLedgEntry."Entry Type"::"Initial Entry");
      IF DtldCustLedgEntry."Transaction No." <> 0 THEN BEGIN
        UnapplyVATEntries := FALSE;
        DtldCustLedgEntry2.FINDSET;
        REPEAT
          DtldCustLedgEntry2.TESTFIELD(Unapplied,FALSE);
          IF IsVATAdjustment(DtldCustLedgEntry2."Entry Type") THEN
            UnapplyVATEntries := TRUE;
          IF NOT GLSetup."Pmt. Disc. Excl. VAT" AND GLSetup."Adjust for Payment Disc." THEN
            IF IsVATExcluded(DtldCustLedgEntry2."Entry Type") THEN
              UnapplyVATEntries := TRUE;
          IF DtldCustLedgEntry2."Entry Type" = DtldCustLedgEntry2."Entry Type"::"Payment Discount Tolerance (VAT Excl.)" THEN
            PmtDiscTolExists := TRUE;
        UNTIL DtldCustLedgEntry2.NEXT = 0;

        PostUnapply(
          GenJnlLine,VATEntry,VATEntry.Type::Sale,
          DtldCustLedgEntry."Customer No.",DtldCustLedgEntry."Transaction No.",UnapplyVATEntries,TempVATEntry);

        IF PmtDiscTolExists THEN
          ProcessTempVATEntryCust(DtldCustLedgEntry2,TempVATEntry)
        ELSE BEGIN
          DtldCustLedgEntry2.SETRANGE("Entry Type",DtldCustLedgEntry2."Entry Type"::"Payment Tolerance (VAT Excl.)");
          ProcessTempVATEntryCust(DtldCustLedgEntry2,TempVATEntry);
          DtldCustLedgEntry2.SETRANGE("Entry Type",DtldCustLedgEntry2."Entry Type"::"Payment Discount (VAT Excl.)");
          ProcessTempVATEntryCust(DtldCustLedgEntry2,TempVATEntry);
          DtldCustLedgEntry2.SETFILTER("Entry Type",'>%1',DtldCustLedgEntry."Entry Type"::"Initial Entry");
        END;
      END;

      // Look one more time
      DtldCustLedgEntry2.FINDSET;
      TempInvPostBuf.DELETEALL;
      REPEAT
        DtldCustLedgEntry2.TESTFIELD(Unapplied,FALSE);
        InsertDtldCustLedgEntryUnapply(GenJnlLine,NewDtldCustLedgEntry,DtldCustLedgEntry2,NextDtldLedgEntryNo);

        DtldCVLedgEntryBuf.INIT;
        DtldCVLedgEntryBuf.TRANSFERFIELDS(NewDtldCustLedgEntry);
        SetAddCurrForUnapplication(DtldCVLedgEntryBuf);
        CurrencyLCY.InitRoundingPrecision;

        IF (DtldCustLedgEntry2."Transaction No." <> 0) AND IsVATExcluded(DtldCustLedgEntry2."Entry Type") THEN BEGIN
          UnapplyExcludedVAT(
            TempVATEntry2,DtldCustLedgEntry2."Transaction No.",DtldCustLedgEntry2."VAT Bus. Posting Group",
            DtldCustLedgEntry2."VAT Prod. Posting Group",DtldCustLedgEntry2."Gen. Prod. Posting Group");
          DtldCVLedgEntryBuf."VAT Amount (LCY)" :=
            CalcVATAmountFromVATEntry(DtldCVLedgEntryBuf."Amount (LCY)",TempVATEntry2,CurrencyLCY);
        END;
        UpdateTotalAmounts(
          TempInvPostBuf,GenJnlLine."Dimension Set ID",DtldCVLedgEntryBuf."Amount (LCY)",
          DtldCVLedgEntryBuf."Additional-Currency Amount");

        IF NOT (DtldCVLedgEntryBuf."Entry Type" IN [
                                                    DtldCVLedgEntryBuf."Entry Type"::"Initial Entry",
                                                    DtldCVLedgEntryBuf."Entry Type"::Application])
        THEN
          CollectAdjustment(AdjAmount,
            -DtldCVLedgEntryBuf."Amount (LCY)",-DtldCVLedgEntryBuf."Additional-Currency Amount");

        PostDtldCustLedgEntryUnapply(
          GenJnlLine,DtldCVLedgEntryBuf,CustPostingGr,DtldCustLedgEntry2."Transaction No.");

        DtldCustLedgEntry2.Unapplied := TRUE;
        DtldCustLedgEntry2."Unapplied by Entry No." := NewDtldCustLedgEntry."Entry No.";
        DtldCustLedgEntry2.MODIFY;

        UpdateCustLedgEntry(DtldCustLedgEntry2);
      UNTIL DtldCustLedgEntry2.NEXT = 0;

      CreateGLEntriesForTotalAmountsUnapply(GenJnlLine,TempInvPostBuf,CustPostingGr.GetReceivablesAccount);

      UnapplyWHTEntry(
        GenJnlLine,WHTEntry."Transaction Type"::Sale,DtldCustLedgEntry."Customer No.",DtldCustLedgEntry."Transaction No.",FALSE);

      IF IsTempGLEntryBufEmpty THEN
        DtldCustLedgEntry.SetZeroTransNo(NextTransactionNo);
      CheckPostUnrealizedVAT(GenJnlLine,TRUE);
      FinishPosting;
    END;

    [External]
    PROCEDURE UnapplyVendLedgEntry@108(GenJnlLine2@1003 : Record 81;DtldVendLedgEntry@1002 : Record 380;VoidCheck@1250001 : Boolean);
    VAR
      Vend@1005 : Record 23;
      VendPostingGr@1019 : Record 93;
      GenJnlLine@1021 : Record 81;
      DtldVendLedgEntry2@1012 : Record 380;
      NewDtldVendLedgEntry@1011 : Record 380;
      VendLedgEntry@1010 : Record 25;
      DtldCVLedgEntryBuf@1009 : Record 383;
      VATEntry@1008 : Record 254;
      TempVATEntry2@1023 : TEMPORARY Record 254;
      CurrencyLCY@1024 : Record 4;
      TempInvPostBuf@1001 : TEMPORARY Record 49;
      WHTEntry@1250000 : Record 50004;
      AdjAmount@1031 : ARRAY [4] OF Decimal;
      NextDtldLedgEntryNo@1000 : Integer;
      UnapplyVATEntries@1013 : Boolean;
      PmtDiscTolExists@1006 : Boolean;
    BEGIN
      GenJnlLine.TRANSFERFIELDS(GenJnlLine2);
      IF GenJnlLine."Document Date" = 0D THEN
        GenJnlLine."Document Date" := GenJnlLine."Posting Date";

      IF NextEntryNo = 0 THEN
        StartPosting(GenJnlLine)
      ELSE
        ContinuePosting(GenJnlLine);

      ReadGLSetup(GLSetup);

      Vend.GET(DtldVendLedgEntry."Vendor No.");
      Vend.CheckBlockedVendOnJnls(Vend,GenJnlLine2."Document Type"::Payment,TRUE);
      VendPostingGr.GET(GenJnlLine."Posting Group");
      VendPostingGr.GetPayablesAccount;

      VATEntry.LOCKTABLE;
      DtldVendLedgEntry.LOCKTABLE;
      VendLedgEntry.LOCKTABLE;

      DtldVendLedgEntry.TESTFIELD("Entry Type",DtldVendLedgEntry."Entry Type"::Application);

      DtldVendLedgEntry2.RESET;
      DtldVendLedgEntry2.FINDLAST;
      NextDtldLedgEntryNo := DtldVendLedgEntry2."Entry No." + 1;
      IF DtldVendLedgEntry."Transaction No." = 0 THEN BEGIN
        DtldVendLedgEntry2.SETCURRENTKEY("Application No.","Vendor No.","Entry Type");
        DtldVendLedgEntry2.SETRANGE("Application No.",DtldVendLedgEntry."Application No.");
      END ELSE BEGIN
        DtldVendLedgEntry2.SETCURRENTKEY("Transaction No.","Vendor No.","Entry Type");
        DtldVendLedgEntry2.SETRANGE("Transaction No.",DtldVendLedgEntry."Transaction No.");
      END;
      DtldVendLedgEntry2.SETRANGE("Vendor No.",DtldVendLedgEntry."Vendor No.");
      DtldVendLedgEntry2.SETFILTER("Entry Type",'>%1',DtldVendLedgEntry."Entry Type"::"Initial Entry");
      IF DtldVendLedgEntry."Transaction No." <> 0 THEN BEGIN
        UnapplyVATEntries := FALSE;
        DtldVendLedgEntry2.FINDSET;
        REPEAT
          DtldVendLedgEntry2.TESTFIELD(Unapplied,FALSE);
          IF IsVATAdjustment(DtldVendLedgEntry2."Entry Type") THEN
            UnapplyVATEntries := TRUE;
          IF NOT GLSetup."Pmt. Disc. Excl. VAT" AND GLSetup."Adjust for Payment Disc." THEN
            IF IsVATExcluded(DtldVendLedgEntry2."Entry Type") THEN
              UnapplyVATEntries := TRUE;
          IF DtldVendLedgEntry2."Entry Type" = DtldVendLedgEntry2."Entry Type"::"Payment Discount Tolerance (VAT Excl.)" THEN
            PmtDiscTolExists := TRUE;
        UNTIL DtldVendLedgEntry2.NEXT = 0;

        PostUnapply(
          GenJnlLine,VATEntry,VATEntry.Type::Purchase,
          DtldVendLedgEntry."Vendor No.",DtldVendLedgEntry."Transaction No.",UnapplyVATEntries,TempVATEntry);

        IF PmtDiscTolExists THEN
          ProcessTempVATEntryVend(DtldVendLedgEntry2,TempVATEntry)
        ELSE BEGIN
          DtldVendLedgEntry2.SETRANGE("Entry Type",DtldVendLedgEntry2."Entry Type"::"Payment Tolerance (VAT Excl.)");
          ProcessTempVATEntryVend(DtldVendLedgEntry2,TempVATEntry);
          DtldVendLedgEntry2.SETRANGE("Entry Type",DtldVendLedgEntry2."Entry Type"::"Payment Discount (VAT Excl.)");
          ProcessTempVATEntryVend(DtldVendLedgEntry2,TempVATEntry);
          DtldVendLedgEntry2.SETFILTER("Entry Type",'>%1',DtldVendLedgEntry2."Entry Type"::"Initial Entry");
        END;
      END;

      // Look one more time
      DtldVendLedgEntry2.FINDSET;
      TempInvPostBuf.DELETEALL;
      REPEAT
        DtldVendLedgEntry2.TESTFIELD(Unapplied,FALSE);
        InsertDtldVendLedgEntryUnapply(GenJnlLine,NewDtldVendLedgEntry,DtldVendLedgEntry2,NextDtldLedgEntryNo);

        DtldCVLedgEntryBuf.INIT;
        DtldCVLedgEntryBuf.TRANSFERFIELDS(NewDtldVendLedgEntry);
        SetAddCurrForUnapplication(DtldCVLedgEntryBuf);
        CurrencyLCY.InitRoundingPrecision;

        IF (DtldVendLedgEntry2."Transaction No." <> 0) AND IsVATExcluded(DtldVendLedgEntry2."Entry Type") THEN BEGIN
          UnapplyExcludedVAT(
            TempVATEntry2,DtldVendLedgEntry2."Transaction No.",DtldVendLedgEntry2."VAT Bus. Posting Group",
            DtldVendLedgEntry2."VAT Prod. Posting Group",DtldVendLedgEntry2."Gen. Prod. Posting Group");
          DtldCVLedgEntryBuf."VAT Amount (LCY)" :=
            CalcVATAmountFromVATEntry(DtldCVLedgEntryBuf."Amount (LCY)",TempVATEntry2,CurrencyLCY);
        END;
        UpdateTotalAmounts(
          TempInvPostBuf,GenJnlLine."Dimension Set ID",DtldCVLedgEntryBuf."Amount (LCY)",
          DtldCVLedgEntryBuf."Additional-Currency Amount");

        IF NOT (DtldCVLedgEntryBuf."Entry Type" IN [
                                                    DtldCVLedgEntryBuf."Entry Type"::"Initial Entry",
                                                    DtldCVLedgEntryBuf."Entry Type"::Application])
        THEN
          CollectAdjustment(AdjAmount,
            -DtldCVLedgEntryBuf."Amount (LCY)",-DtldCVLedgEntryBuf."Additional-Currency Amount");

        PostDtldVendLedgEntryUnapply(
          GenJnlLine,DtldCVLedgEntryBuf,VendPostingGr,DtldVendLedgEntry2."Transaction No.");

        DtldVendLedgEntry2.Unapplied := TRUE;
        DtldVendLedgEntry2."Unapplied by Entry No." := NewDtldVendLedgEntry."Entry No.";
        DtldVendLedgEntry2.MODIFY;

        UpdateVendLedgEntry(DtldVendLedgEntry2);
      UNTIL DtldVendLedgEntry2.NEXT = 0;

      CreateGLEntriesForTotalAmountsUnapply(GenJnlLine,TempInvPostBuf,VendPostingGr.GetPayablesAccount);

      UnapplyWHTEntry(
        GenJnlLine,WHTEntry."Transaction Type"::Purchase,DtldVendLedgEntry."Vendor No.",DtldVendLedgEntry."Transaction No.",VoidCheck);

      IF IsTempGLEntryBufEmpty THEN
        DtldVendLedgEntry.SetZeroTransNo(NextTransactionNo);
      CheckPostUnrealizedVAT(GenJnlLine,TRUE);
      FinishPosting;
    END;

    [Internal]
    PROCEDURE UnapplyEmplLedgEntry@89(GenJnlLine2@1003 : Record 81;DtldEmplLedgEntry@1002 : Record 5223);
    VAR
      Employee@1005 : Record 5200;
      EmployeePostingGroup@1019 : Record 5221;
      GenJnlLine@1021 : Record 81;
      DtldEmplLedgEntry2@1012 : Record 5223;
      NewDtldEmplLedgEntry@1011 : Record 5223;
      EmplLedgEntry@1010 : Record 5222;
      DtldCVLedgEntryBuf@1009 : Record 383;
      CurrencyLCY@1024 : Record 4;
      TempInvPostBuf@1001 : TEMPORARY Record 49;
      NextDtldLedgEntryNo@1000 : Integer;
    BEGIN
      GenJnlLine.TRANSFERFIELDS(GenJnlLine2);
      IF GenJnlLine."Document Date" = 0D THEN
        GenJnlLine."Document Date" := GenJnlLine."Posting Date";

      IF NextEntryNo = 0 THEN
        StartPosting(GenJnlLine)
      ELSE
        ContinuePosting(GenJnlLine);

      ReadGLSetup(GLSetup);

      Employee.GET(DtldEmplLedgEntry."Employee No.");
      Employee.CheckBlockedEmployeeOnJnls(TRUE);
      EmployeePostingGroup.GET(GenJnlLine."Posting Group");
      EmployeePostingGroup.GetPayablesAccount;

      DtldEmplLedgEntry.LOCKTABLE;
      EmplLedgEntry.LOCKTABLE;

      DtldEmplLedgEntry.TESTFIELD("Entry Type",DtldEmplLedgEntry."Entry Type"::Application);

      DtldEmplLedgEntry2.RESET;
      DtldEmplLedgEntry2.FINDLAST;
      NextDtldLedgEntryNo := DtldEmplLedgEntry2."Entry No." + 1;
      IF DtldEmplLedgEntry."Transaction No." = 0 THEN BEGIN
        DtldEmplLedgEntry2.SETCURRENTKEY("Application No.","Employee No.","Entry Type");
        DtldEmplLedgEntry2.SETRANGE("Application No.",DtldEmplLedgEntry."Application No.");
      END ELSE BEGIN
        DtldEmplLedgEntry2.SETCURRENTKEY("Transaction No.","Employee No.","Entry Type");
        DtldEmplLedgEntry2.SETRANGE("Transaction No.",DtldEmplLedgEntry."Transaction No.");
      END;
      DtldEmplLedgEntry2.SETRANGE("Employee No.",DtldEmplLedgEntry."Employee No.");
      DtldEmplLedgEntry2.SETFILTER("Entry Type",'>%1',DtldEmplLedgEntry."Entry Type"::"Initial Entry");

      // Look one more time
      DtldEmplLedgEntry2.FINDSET;
      TempInvPostBuf.DELETEALL;
      REPEAT
        DtldEmplLedgEntry2.TESTFIELD(Unapplied,FALSE);
        InsertDtldEmplLedgEntryUnapply(GenJnlLine,NewDtldEmplLedgEntry,DtldEmplLedgEntry2,NextDtldLedgEntryNo);

        DtldCVLedgEntryBuf.INIT;
        DtldCVLedgEntryBuf.TRANSFERFIELDS(NewDtldEmplLedgEntry);
        SetAddCurrForUnapplication(DtldCVLedgEntryBuf);
        CurrencyLCY.InitRoundingPrecision;

        UpdateTotalAmounts(
          TempInvPostBuf,GenJnlLine."Dimension Set ID",DtldCVLedgEntryBuf."Amount (LCY)",
          DtldCVLedgEntryBuf."Additional-Currency Amount");

        DtldEmplLedgEntry2.Unapplied := TRUE;
        DtldEmplLedgEntry2."Unapplied by Entry No." := NewDtldEmplLedgEntry."Entry No.";
        DtldEmplLedgEntry2.MODIFY;

        UpdateEmplLedgEntry(DtldEmplLedgEntry2);
      UNTIL DtldEmplLedgEntry2.NEXT = 0;

      CreateGLEntriesForTotalAmountsUnapply(GenJnlLine,TempInvPostBuf,EmployeePostingGroup.GetPayablesAccount);

      IF IsTempGLEntryBufEmpty THEN
        DtldEmplLedgEntry.SetZeroTransNo(NextTransactionNo);
      FinishPosting;
    END;

    LOCAL PROCEDURE UnapplyExcludedVAT@85(VAR TempVATEntry@1000 : TEMPORARY Record 254;TransactionNo@1004 : Integer;VATBusPostingGroup@1001 : Code[20];VATProdPostingGroup@1002 : Code[20];GenProdPostingGroup@1003 : Code[20]);
    BEGIN
      TempVATEntry.SETRANGE("VAT Bus. Posting Group",VATBusPostingGroup);
      TempVATEntry.SETRANGE("VAT Prod. Posting Group",VATProdPostingGroup);
      TempVATEntry.SETRANGE("Gen. Prod. Posting Group",GenProdPostingGroup);
      IF NOT TempVATEntry.FINDFIRST THEN BEGIN
        TempVATEntry.RESET;
        IF TempVATEntry.FINDLAST THEN
          TempVATEntry."Entry No." := TempVATEntry."Entry No." + 1
        ELSE
          TempVATEntry."Entry No." := 1;
        TempVATEntry.INIT;
        TempVATEntry."VAT Bus. Posting Group" := VATBusPostingGroup;
        TempVATEntry."VAT Prod. Posting Group" := VATProdPostingGroup;
        TempVATEntry."Gen. Prod. Posting Group" := GenProdPostingGroup;
        VATEntry.SETCURRENTKEY("Transaction No.");
        VATEntry.SETRANGE("Transaction No.",TransactionNo);
        VATEntry.SETRANGE("VAT Bus. Posting Group",VATBusPostingGroup);
        VATEntry.SETRANGE("VAT Prod. Posting Group",VATProdPostingGroup);
        VATEntry.SETRANGE("Gen. Prod. Posting Group",GenProdPostingGroup);
        IF VATEntry.FINDSET THEN
          REPEAT
            IF VATEntry."Unrealized VAT Entry No." = 0 THEN BEGIN
              TempVATEntry.Base := TempVATEntry.Base + VATEntry.Base;
              TempVATEntry.Amount := TempVATEntry.Amount + VATEntry.Amount;
            END;
          UNTIL VATEntry.NEXT = 0;
        CLEAR(VATEntry);
        TempVATEntry.INSERT;
      END;
    END;

    LOCAL PROCEDURE PostUnrealVATByUnapply@106(GenJnlLine@1002 : Record 81;VATPostingSetup@1008 : Record 325;VATEntry@1005 : Record 254;NewVATEntry@1004 : Record 254) : Integer;
    VAR
      VATEntry2@1003 : Record 254;
      AmountAddCurr@1007 : Decimal;
      GLEntryNoFromVAT@1000 : Integer;
    BEGIN
      AmountAddCurr := CalcAddCurrForUnapplication(VATEntry."Posting Date",VATEntry.Amount);
      CreateGLEntry(
        GenJnlLine,GetPostingAccountNo(VATPostingSetup,VATEntry,TRUE),VATEntry.Amount,AmountAddCurr,FALSE);
      GLEntryNoFromVAT :=
        CreateGLEntryFromVATEntry(
          GenJnlLine,GetPostingAccountNo(VATPostingSetup,VATEntry,FALSE),-VATEntry.Amount,-AmountAddCurr,VATEntry);

      WITH VATEntry2 DO BEGIN
        GET(VATEntry."Unrealized VAT Entry No.");
        "Remaining Unrealized Amount" := "Remaining Unrealized Amount" - NewVATEntry.Amount;
        "Remaining Unrealized Base" := "Remaining Unrealized Base" - NewVATEntry.Base;
        "Add.-Curr. Rem. Unreal. Amount" :=
          "Add.-Curr. Rem. Unreal. Amount" - NewVATEntry."Additional-Currency Amount";
        "Add.-Curr. Rem. Unreal. Base" :=
          "Add.-Curr. Rem. Unreal. Base" - NewVATEntry."Additional-Currency Base";
        MODIFY;
      END;

      EXIT(GLEntryNoFromVAT);
    END;

    LOCAL PROCEDURE PostPmtDiscountVATByUnapply@104(GenJnlLine@1003 : Record 81;ReverseChargeVATAccNo@1002 : Code[20];VATAccNo@1001 : Code[20];VATEntry@1000 : Record 254);
    VAR
      AmountAddCurr@1005 : Decimal;
    BEGIN
      AmountAddCurr := CalcAddCurrForUnapplication(VATEntry."Posting Date",VATEntry.Amount);
      CreateGLEntry(GenJnlLine,ReverseChargeVATAccNo,VATEntry.Amount,AmountAddCurr,FALSE);
      CreateGLEntry(GenJnlLine,VATAccNo,-VATEntry.Amount,-AmountAddCurr,FALSE);
    END;

    LOCAL PROCEDURE PostUnapply@101(GenJnlLine@1007 : Record 81;VAR VATEntry@1002 : Record 254;VATEntryType@1004 : Option;BilltoPaytoNo@1001 : Code[20];TransactionNo@1003 : Integer;UnapplyVATEntries@1006 : Boolean;VAR TempVATEntry@1013 : TEMPORARY Record 254);
    VAR
      VATPostingSetup@1000 : Record 325;
      VATEntry2@1009 : Record 254;
      GLEntryVATEntryLink@1011 : Record 253;
      AccNo@1010 : Code[20];
      TempVATEntryNo@1005 : Integer;
      GLEntryNoFromVAT@1008 : Integer;
    BEGIN
      TempVATEntryNo := 1;
      VATEntry.SETCURRENTKEY(Type,"Bill-to/Pay-to No.","Transaction No.");
      VATEntry.SETRANGE(Type,VATEntryType);
      VATEntry.SETRANGE("Bill-to/Pay-to No.",BilltoPaytoNo);
      VATEntry.SETRANGE("Transaction No.",TransactionNo);
      IF VATEntry.FINDSET THEN
        REPEAT
          VATPostingSetup.GET(VATEntry."VAT Bus. Posting Group",VATEntry."VAT Prod. Posting Group");
          IF UnapplyVATEntries OR (VATEntry."Unrealized VAT Entry No." <> 0) THEN BEGIN
            InsertTempVATEntry(GenJnlLine,VATEntry,TempVATEntryNo,TempVATEntry);
            IF VATEntry."Unrealized VAT Entry No." <> 0 THEN BEGIN
              VATPostingSetup.GET(VATEntry."VAT Bus. Posting Group",VATEntry."VAT Prod. Posting Group");
              IF VATPostingSetup."VAT Calculation Type" IN
                 [VATPostingSetup."VAT Calculation Type"::"Normal VAT",
                  VATPostingSetup."VAT Calculation Type"::"Full VAT"]
              THEN
                GLEntryNoFromVAT := PostUnrealVATByUnapply(GenJnlLine,VATPostingSetup,VATEntry,TempVATEntry)
              ELSE
                IF VATPostingSetup."VAT Calculation Type" = VATPostingSetup."VAT Calculation Type"::"Reverse Charge VAT" THEN BEGIN
                  GLEntryNoFromVAT := PostUnrealVATByUnapply(GenJnlLine,VATPostingSetup,VATEntry,TempVATEntry);
                  CreateGLEntry(
                    GenJnlLine,VATPostingSetup.GetRevChargeAccount(TRUE),
                    -VATEntry.Amount,CalcAddCurrForUnapplication(VATEntry."Posting Date",-VATEntry.Amount),FALSE);
                  CreateGLEntry(
                    GenJnlLine,VATPostingSetup.GetRevChargeAccount(FALSE),
                    VATEntry.Amount,CalcAddCurrForUnapplication(VATEntry."Posting Date",VATEntry.Amount),FALSE);
                END ELSE
                  GLEntryNoFromVAT := PostUnrealVATByUnapply(GenJnlLine,VATPostingSetup,VATEntry,TempVATEntry);
              VATEntry2 := TempVATEntry;
              VATEntry2."Entry No." := NextVATEntryNo;
              VATEntry2.INSERT;
              IF GLEntryNoFromVAT <> 0 THEN
                GLEntryVATEntryLink.InsertLink(GLEntryNoFromVAT,VATEntry2."Entry No.");
              GLEntryNoFromVAT := 0;
              TempVATEntry.DELETE;
              IncrNextVATEntryNo;
            END;

            IF VATPostingSetup."Adjust for Payment Discount" AND NOT IsNotPayment(VATEntry."Document Type") AND
               (VATPostingSetup."VAT Calculation Type" =
                VATPostingSetup."VAT Calculation Type"::"Reverse Charge VAT") AND
               (VATEntry."Unrealized VAT Entry No." = 0) AND UnapplyVATEntries AND (VATEntry.Amount <> 0)
            THEN BEGIN
              CASE VATEntryType OF
                VATEntry.Type::Sale:
                  AccNo := VATPostingSetup.GetSalesAccount(FALSE);
                VATEntry.Type::Purchase:
                  AccNo := VATPostingSetup.GetPurchAccount(FALSE);
              END;
              PostPmtDiscountVATByUnapply(GenJnlLine,VATPostingSetup.GetRevChargeAccount(FALSE),AccNo,VATEntry);
            END;
          END;
        UNTIL VATEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE CalcAddCurrForUnapplication@100(Date@1001 : Date;Amt@1002 : Decimal) : Decimal;
    VAR
      AddCurrency@1000 : Record 4;
      CurrExchRate@1003 : Record 330;
    BEGIN
      IF AddCurrencyCode = '' THEN
        EXIT;

      AddCurrency.GET(AddCurrencyCode);
      AddCurrency.TESTFIELD("Amount Rounding Precision");

      EXIT(
        ROUND(
          CurrExchRate.ExchangeAmtLCYToFCY(
            Date,AddCurrencyCode,Amt,CurrExchRate.ExchangeRate(Date,AddCurrencyCode)),
          AddCurrency."Amount Rounding Precision"));
    END;

    LOCAL PROCEDURE CalcVATAmountFromVATEntry@99(AmountLCY@1000 : Decimal;VAR VATEntry@1001 : Record 254;CurrencyLCY@1003 : Record 4) VATAmountLCY : Decimal;
    BEGIN
      WITH VATEntry DO
        IF (AmountLCY = Base) OR (Base = 0) THEN BEGIN
          VATAmountLCY := Amount;
          DELETE;
        END ELSE BEGIN
          VATAmountLCY :=
            ROUND(
              Amount * AmountLCY / Base,
              CurrencyLCY."Amount Rounding Precision",
              CurrencyLCY.VATRoundingDirection);
          Base := Base - AmountLCY;
          Amount := Amount - VATAmountLCY;
          MODIFY;
        END;
    END;

    LOCAL PROCEDURE InsertDtldCustLedgEntryUnapply@91(GenJnlLine@1002 : Record 81;VAR NewDtldCustLedgEntry@1000 : Record 379;OldDtldCustLedgEntry@1001 : Record 379;VAR NextDtldLedgEntryNo@1003 : Integer);
    BEGIN
      NewDtldCustLedgEntry := OldDtldCustLedgEntry;
      WITH NewDtldCustLedgEntry DO BEGIN
        "Entry No." := NextDtldLedgEntryNo;
        "Posting Date" := GenJnlLine."Posting Date";
        "Transaction No." := NextTransactionNo;
        "Application No." := 0;
        Amount := -OldDtldCustLedgEntry.Amount;
        "Amount (LCY)" := -OldDtldCustLedgEntry."Amount (LCY)";
        "Debit Amount" := -OldDtldCustLedgEntry."Debit Amount";
        "Credit Amount" := -OldDtldCustLedgEntry."Credit Amount";
        "Debit Amount (LCY)" := -OldDtldCustLedgEntry."Debit Amount (LCY)";
        "Credit Amount (LCY)" := -OldDtldCustLedgEntry."Credit Amount (LCY)";
        Unapplied := TRUE;
        "Unapplied by Entry No." := OldDtldCustLedgEntry."Entry No.";
        "Document No." := GenJnlLine."Document No.";
        "Source Code" := GenJnlLine."Source Code";
        "User ID" := USERID;
        INSERT(TRUE);
      END;
      NextDtldLedgEntryNo := NextDtldLedgEntryNo + 1;
    END;

    LOCAL PROCEDURE InsertDtldVendLedgEntryUnapply@90(GenJnlLine@1003 : Record 81;VAR NewDtldVendLedgEntry@1002 : Record 380;OldDtldVendLedgEntry@1001 : Record 380;VAR NextDtldLedgEntryNo@1000 : Integer);
    BEGIN
      NewDtldVendLedgEntry := OldDtldVendLedgEntry;
      WITH NewDtldVendLedgEntry DO BEGIN
        "Entry No." := NextDtldLedgEntryNo;
        "Posting Date" := GenJnlLine."Posting Date";
        "Transaction No." := NextTransactionNo;
        "Application No." := 0;
        Amount := -OldDtldVendLedgEntry.Amount;
        "Amount (LCY)" := -OldDtldVendLedgEntry."Amount (LCY)";
        "Debit Amount" := -OldDtldVendLedgEntry."Debit Amount";
        "Credit Amount" := -OldDtldVendLedgEntry."Credit Amount";
        "Debit Amount (LCY)" := -OldDtldVendLedgEntry."Debit Amount (LCY)";
        "Credit Amount (LCY)" := -OldDtldVendLedgEntry."Credit Amount (LCY)";
        Unapplied := TRUE;
        "Unapplied by Entry No." := OldDtldVendLedgEntry."Entry No.";
        "Document No." := GenJnlLine."Document No.";
        "Source Code" := GenJnlLine."Source Code";
        "User ID" := USERID;
        INSERT(TRUE);
      END;
      NextDtldLedgEntryNo := NextDtldLedgEntryNo + 1;
    END;

    LOCAL PROCEDURE InsertDtldEmplLedgEntryUnapply@140(GenJnlLine@1003 : Record 81;VAR NewDtldEmplLedgEntry@1002 : Record 5223;OldDtldEmplLedgEntry@1001 : Record 5223;VAR NextDtldLedgEntryNo@1000 : Integer);
    BEGIN
      NewDtldEmplLedgEntry := OldDtldEmplLedgEntry;
      WITH NewDtldEmplLedgEntry DO BEGIN
        "Entry No." := NextDtldLedgEntryNo;
        "Posting Date" := GenJnlLine."Posting Date";
        "Transaction No." := NextTransactionNo;
        "Application No." := 0;
        Amount := -OldDtldEmplLedgEntry.Amount;
        "Amount (LCY)" := -OldDtldEmplLedgEntry."Amount (LCY)";
        "Debit Amount" := -OldDtldEmplLedgEntry."Debit Amount";
        "Credit Amount" := -OldDtldEmplLedgEntry."Credit Amount";
        "Debit Amount (LCY)" := -OldDtldEmplLedgEntry."Debit Amount (LCY)";
        "Credit Amount (LCY)" := -OldDtldEmplLedgEntry."Credit Amount (LCY)";
        Unapplied := TRUE;
        "Unapplied by Entry No." := OldDtldEmplLedgEntry."Entry No.";
        "Document No." := GenJnlLine."Document No.";
        "Source Code" := GenJnlLine."Source Code";
        "User ID" := USERID;
        INSERT(TRUE);
      END;
      NextDtldLedgEntryNo := NextDtldLedgEntryNo + 1;
    END;

    LOCAL PROCEDURE InsertTempVATEntry@88(GenJnlLine@1002 : Record 81;VATEntry@1000 : Record 254;VAR TempVATEntryNo@1001 : Integer;VAR TempVATEntry@1003 : TEMPORARY Record 254);
    BEGIN
      TempVATEntry := VATEntry;
      WITH TempVATEntry DO BEGIN
        "Entry No." := TempVATEntryNo;
        TempVATEntryNo := TempVATEntryNo + 1;
        "Closed by Entry No." := 0;
        Closed := FALSE;
        CopyAmountsFromVATEntry(VATEntry,TRUE);
        "Posting Date" := GenJnlLine."Posting Date";
        "Document No." := GenJnlLine."Document No.";
        "User ID" := USERID;
        "Transaction No." := NextTransactionNo;
        INSERT;
      END;
    END;

    LOCAL PROCEDURE ProcessTempVATEntry@87(DtldCVLedgEntryBuf@1000 : Record 383;VAR TempVATEntry@1004 : TEMPORARY Record 254);
    VAR
      VATEntrySaved@1005 : Record 254;
      VATBaseSum@1003 : ARRAY [3] OF Decimal;
      DeductedVATBase@1006 : Decimal;
      EntryNoBegin@1002 : ARRAY [3] OF Integer;
      i@1001 : Integer;
      SummarizedVAT@1007 : Boolean;
    BEGIN
      IF NOT (DtldCVLedgEntryBuf."Entry Type" IN
              [DtldCVLedgEntryBuf."Entry Type"::"Payment Discount (VAT Excl.)",
               DtldCVLedgEntryBuf."Entry Type"::"Payment Tolerance (VAT Excl.)",
               DtldCVLedgEntryBuf."Entry Type"::"Payment Discount Tolerance (VAT Excl.)"])
      THEN
        EXIT;

      DeductedVATBase := 0;
      TempVATEntry.RESET;
      TempVATEntry.SETRANGE("Entry No.",0,999999);
      TempVATEntry.SETRANGE("Gen. Bus. Posting Group",DtldCVLedgEntryBuf."Gen. Bus. Posting Group");
      TempVATEntry.SETRANGE("Gen. Prod. Posting Group",DtldCVLedgEntryBuf."Gen. Prod. Posting Group");
      TempVATEntry.SETRANGE("VAT Bus. Posting Group",DtldCVLedgEntryBuf."VAT Bus. Posting Group");
      TempVATEntry.SETRANGE("VAT Prod. Posting Group",DtldCVLedgEntryBuf."VAT Prod. Posting Group");
      IF TempVATEntry.FINDSET THEN
        REPEAT
          CASE TRUE OF
            SummarizedVAT AND (VATBaseSum[3] + TempVATEntry.Base = DtldCVLedgEntryBuf."Amount (LCY)" - DeductedVATBase):
              i := 4;
            SummarizedVAT AND (VATBaseSum[2] + TempVATEntry.Base = DtldCVLedgEntryBuf."Amount (LCY)" - DeductedVATBase):
              i := 3;
            SummarizedVAT AND (VATBaseSum[1] + TempVATEntry.Base = DtldCVLedgEntryBuf."Amount (LCY)" - DeductedVATBase):
              i := 2;
            TempVATEntry.Base = DtldCVLedgEntryBuf."Amount (LCY)" - DeductedVATBase:
              i := 1;
            ELSE
              i := 0;
          END;
          IF i > 0 THEN BEGIN
            TempVATEntry.RESET;
            IF i > 1 THEN BEGIN
              IF EntryNoBegin[i - 1] < TempVATEntry."Entry No." THEN
                TempVATEntry.SETRANGE("Entry No.",EntryNoBegin[i - 1],TempVATEntry."Entry No.")
              ELSE
                TempVATEntry.SETRANGE("Entry No.",TempVATEntry."Entry No.",EntryNoBegin[i - 1]);
            END ELSE
              TempVATEntry.SETRANGE("Entry No.",TempVATEntry."Entry No.");
            TempVATEntry.FINDSET;
            REPEAT
              VATEntrySaved := TempVATEntry;
              CASE DtldCVLedgEntryBuf."Entry Type" OF
                DtldCVLedgEntryBuf."Entry Type"::"Payment Discount (VAT Excl.)":
                  TempVATEntry.RENAME(TempVATEntry."Entry No." + 3000000);
                DtldCVLedgEntryBuf."Entry Type"::"Payment Tolerance (VAT Excl.)":
                  TempVATEntry.RENAME(TempVATEntry."Entry No." + 2000000);
                DtldCVLedgEntryBuf."Entry Type"::"Payment Discount Tolerance (VAT Excl.)":
                  TempVATEntry.RENAME(TempVATEntry."Entry No." + 1000000);
              END;
              TempVATEntry := VATEntrySaved;
              DeductedVATBase += TempVATEntry.Base;
            UNTIL TempVATEntry.NEXT = 0;
            FOR i := 1 TO 3 DO BEGIN
              VATBaseSum[i] := 0;
              EntryNoBegin[i] := 0;
              SummarizedVAT := FALSE;
            END;
            TempVATEntry.SETRANGE("Entry No.",0,999999);
          END ELSE BEGIN
            VATBaseSum[3] += TempVATEntry.Base;
            VATBaseSum[2] := VATBaseSum[1] + TempVATEntry.Base;
            VATBaseSum[1] := TempVATEntry.Base;
            IF EntryNoBegin[3] > 0 THEN
              EntryNoBegin[3] := TempVATEntry."Entry No.";
            EntryNoBegin[2] := EntryNoBegin[1];
            EntryNoBegin[1] := TempVATEntry."Entry No.";
            SummarizedVAT := TRUE;
          END;
        UNTIL TempVATEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE ProcessTempVATEntryCust@327(VAR DetailedCustLedgEntry@1001 : Record 379;VAR TempVATEntry@1000 : TEMPORARY Record 254);
    VAR
      DetailedCVLedgEntryBuffer@1004 : Record 383;
    BEGIN
      IF NOT DetailedCustLedgEntry.FINDSET THEN
        EXIT;
      REPEAT
        DetailedCVLedgEntryBuffer.INIT;
        DetailedCVLedgEntryBuffer.TRANSFERFIELDS(DetailedCustLedgEntry);
        ProcessTempVATEntry(DetailedCVLedgEntryBuffer,TempVATEntry);
      UNTIL DetailedCustLedgEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE ProcessTempVATEntryVend@296(VAR DetailedVendorLedgEntry@1001 : Record 380;VAR TempVATEntry@1000 : TEMPORARY Record 254);
    VAR
      DetailedCVLedgEntryBuffer@1004 : Record 383;
    BEGIN
      IF NOT DetailedVendorLedgEntry.FINDSET THEN
        EXIT;
      REPEAT
        DetailedCVLedgEntryBuffer.INIT;
        DetailedCVLedgEntryBuffer.TRANSFERFIELDS(DetailedVendorLedgEntry);
        ProcessTempVATEntry(DetailedCVLedgEntryBuffer,TempVATEntry);
      UNTIL DetailedVendorLedgEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE UpdateCustLedgEntry@80(DtldCustLedgEntry@1000 : Record 379);
    VAR
      CustLedgEntry@1001 : Record 21;
    BEGIN
      IF DtldCustLedgEntry."Entry Type" <> DtldCustLedgEntry."Entry Type"::Application THEN
        EXIT;

      CustLedgEntry.GET(DtldCustLedgEntry."Cust. Ledger Entry No.");
      CustLedgEntry."Remaining Pmt. Disc. Possible" := DtldCustLedgEntry."Remaining Pmt. Disc. Possible";
      CustLedgEntry."Max. Payment Tolerance" := DtldCustLedgEntry."Max. Payment Tolerance";
      CustLedgEntry."Accepted Payment Tolerance" := 0;
      IF NOT CustLedgEntry.Open THEN BEGIN
        CustLedgEntry.Open := TRUE;
        CustLedgEntry."Closed by Entry No." := 0;
        CustLedgEntry."Closed at Date" := 0D;
        CustLedgEntry."Closed by Amount" := 0;
        CustLedgEntry."Closed by Amount (LCY)" := 0;
        CustLedgEntry."Closed by Currency Code" := '';
        CustLedgEntry."Closed by Currency Amount" := 0;
        CustLedgEntry."Pmt. Disc. Given (LCY)" := 0;
        CustLedgEntry."Pmt. Tolerance (LCY)" := 0;
        CustLedgEntry."Calculate Interest" := FALSE;
      END;
      CustLedgEntry.MODIFY;
    END;

    LOCAL PROCEDURE UpdateVendLedgEntry@76(DtldVendLedgEntry@1000 : Record 380);
    VAR
      VendLedgEntry@1001 : Record 25;
    BEGIN
      IF DtldVendLedgEntry."Entry Type" <> DtldVendLedgEntry."Entry Type"::Application THEN
        EXIT;

      VendLedgEntry.GET(DtldVendLedgEntry."Vendor Ledger Entry No.");
      VendLedgEntry."Remaining Pmt. Disc. Possible" := DtldVendLedgEntry."Remaining Pmt. Disc. Possible";
      VendLedgEntry."Max. Payment Tolerance" := DtldVendLedgEntry."Max. Payment Tolerance";
      VendLedgEntry."Accepted Payment Tolerance" := 0;
      IF NOT VendLedgEntry.Open THEN BEGIN
        VendLedgEntry.Open := TRUE;
        VendLedgEntry."Closed by Entry No." := 0;
        VendLedgEntry."Closed at Date" := 0D;
        VendLedgEntry."Closed by Amount" := 0;
        VendLedgEntry."Closed by Amount (LCY)" := 0;
        VendLedgEntry."Closed by Currency Code" := '';
        VendLedgEntry."Closed by Currency Amount" := 0;
        VendLedgEntry."Pmt. Disc. Rcd.(LCY)" := 0;
        VendLedgEntry."Pmt. Tolerance (LCY)" := 0;
      END;
      VendLedgEntry."EFT Register No." := 0;
      VendLedgEntry."EFT Amount Transferred" := 0;
      VendLedgEntry."EFT Bank Account No." := '';
      VendLedgEntry.MODIFY;
    END;

    LOCAL PROCEDURE UpdateEmplLedgEntry@151(DtldEmplLedgEntry@1000 : Record 5223);
    VAR
      EmplLedgEntry@1001 : Record 5222;
    BEGIN
      IF DtldEmplLedgEntry."Entry Type" <> DtldEmplLedgEntry."Entry Type"::Application THEN
        EXIT;

      EmplLedgEntry.GET(DtldEmplLedgEntry."Employee Ledger Entry No.");
      IF NOT EmplLedgEntry.Open THEN BEGIN
        EmplLedgEntry.Open := TRUE;
        EmplLedgEntry."Closed by Entry No." := 0;
        EmplLedgEntry."Closed at Date" := 0D;
        EmplLedgEntry."Closed by Amount" := 0;
        EmplLedgEntry."Closed by Amount (LCY)" := 0;
      END;
      EmplLedgEntry.MODIFY;
    END;

    LOCAL PROCEDURE UpdateCalcInterest@28(VAR CVLedgEntryBuf@1000 : Record 382);
    VAR
      CustLedgEntry@1001 : Record 21;
      CVLedgEntryBuf2@1002 : Record 382;
    BEGIN
      WITH CVLedgEntryBuf DO BEGIN
        IF CustLedgEntry.GET("Closed by Entry No.") THEN BEGIN
          CVLedgEntryBuf2.TRANSFERFIELDS(CustLedgEntry);
          UpdateCalcInterest2(CVLedgEntryBuf,CVLedgEntryBuf2);
        END;
        CustLedgEntry.SETCURRENTKEY("Closed by Entry No.");
        CustLedgEntry.SETRANGE("Closed by Entry No.","Entry No.");
        IF CustLedgEntry.FINDSET THEN
          REPEAT
            CVLedgEntryBuf2.TRANSFERFIELDS(CustLedgEntry);
            UpdateCalcInterest2(CVLedgEntryBuf,CVLedgEntryBuf2);
          UNTIL CustLedgEntry.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE UpdateCalcInterest2@27(VAR CVLedgEntryBuf@1000 : Record 382;VAR CVLedgEntryBuf2@1001 : Record 382);
    BEGIN
      WITH CVLedgEntryBuf DO
        IF "Due Date" < CVLedgEntryBuf2."Document Date" THEN
          "Calculate Interest" := TRUE;
    END;

    LOCAL PROCEDURE GLCalcAddCurrency@35(Amount@1003 : Decimal;AddCurrAmount@1000 : Decimal;OldAddCurrAmount@1004 : Decimal;UseAddCurrAmount@1001 : Boolean;GenJnlLine@1002 : Record 81;VAR UseVendExchRateCheck@1500000 : Boolean) : Decimal;
    VAR
      PurchSetup@1450000 : Record 312;
    BEGIN
      IF (AddCurrencyCode <> '') AND
         (GenJnlLine."Additional-Currency Posting" = GenJnlLine."Additional-Currency Posting"::None)
      THEN BEGIN
        IF (GenJnlLine."Source Currency Code" = AddCurrencyCode) AND UseAddCurrAmount THEN
          EXIT(AddCurrAmount);

        PurchSetup.GET;
        IF PurchSetup.GET AND PurchSetup."Enable Vendor GST Amount (ACY)" AND UseVendExchRateCheck THEN
          EXIT(AddCurrAmount);

        EXIT(ExchangeAmtLCYToFCY2(Amount));
      END;
      UseVendExchRate := FALSE;
      EXIT(OldAddCurrAmount);
    END;

    LOCAL PROCEDURE HandleAddCurrResidualGLEntry@38(GenJnlLine@1003 : Record 81;Amount@1000 : Decimal;AmountAddCurr@1001 : Decimal);
    VAR
      GLAcc@1002 : Record 15;
      GLEntry@1004 : Record 17;
    BEGIN
      IF AddCurrencyCode = '' THEN
        EXIT;

      TotalAddCurrAmount := TotalAddCurrAmount + AmountAddCurr;
      TotalAmount := TotalAmount + Amount;

      IF (GenJnlLine."Additional-Currency Posting" = GenJnlLine."Additional-Currency Posting"::None) AND
         (TotalAmount = 0) AND (TotalAddCurrAmount <> 0) AND
         CheckNonAddCurrCodeOccurred(GenJnlLine."Source Currency Code")
      THEN BEGIN
        GLEntry.INIT;
        GLEntry.CopyFromGenJnlLine(GenJnlLine);
        GLEntry."External Document No." := '';
        GLEntry.Description :=
          COPYSTR(
            STRSUBSTNO(
              ResidualRoundingErr,
              GLEntry.FIELDCAPTION("Additional-Currency Amount")),
            1,MAXSTRLEN(GLEntry.Description));
        GLEntry."Source Type" := 0;
        GLEntry."Source No." := '';
        GLEntry."Job No." := '';
        GLEntry.Quantity := 0;
        GLEntry."Entry No." := NextEntryNo;
        GLEntry."Transaction No." := NextTransactionNo;
        IF TotalAddCurrAmount < 0 THEN
          GLEntry."G/L Account No." := AddCurrency."Residual Losses Account"
        ELSE
          GLEntry."G/L Account No." := AddCurrency."Residual Gains Account";
        GLEntry.Amount := 0;
        GLEntry."System-Created Entry" := TRUE;
        GLEntry."Additional-Currency Amount" := -TotalAddCurrAmount;
        GLAcc.GET(GLEntry."G/L Account No.");
        GLAcc.TESTFIELD(Blocked,FALSE);
        GLAcc.TESTFIELD("Account Type",GLAcc."Account Type"::Posting);
        InsertGLEntry(GenJnlLine,GLEntry,FALSE);

        CheckGLAccDimError(GenJnlLine,GLEntry."G/L Account No.");

        TotalAddCurrAmount := 0;
      END;
    END;

    LOCAL PROCEDURE CalcLCYToAddCurr@42(AmountLCY@1000 : Decimal) : Decimal;
    BEGIN
      IF AddCurrencyCode = '' THEN
        EXIT;

      EXIT(ExchangeAmtLCYToFCY2(AmountLCY));
    END;

    LOCAL PROCEDURE GetCurrencyExchRate@39(GenJnlLine@1001 : Record 81);
    VAR
      NewCurrencyDate@1000 : Date;
    BEGIN
      IF AddCurrencyCode = '' THEN
        EXIT;

      AddCurrency.GET(AddCurrencyCode);
      AddCurrency.TESTFIELD("Amount Rounding Precision");
      AddCurrency.TESTFIELD("Residual Gains Account");
      AddCurrency.TESTFIELD("Residual Losses Account");

      NewCurrencyDate := GenJnlLine."Posting Date";
      IF GenJnlLine."Reversing Entry" THEN
        NewCurrencyDate := NewCurrencyDate - 1;
      IF (NewCurrencyDate <> CurrencyDate) OR
         UseCurrFactorOnly
      THEN BEGIN
        UseCurrFactorOnly := FALSE;
        CurrencyDate := NewCurrencyDate;
        CurrencyFactor :=
          CurrExchRate.ExchangeRate(CurrencyDate,AddCurrencyCode);
      END;
      IF (GenJnlLine."FA Add.-Currency Factor" <> 0) AND
         (GenJnlLine."FA Add.-Currency Factor" <> CurrencyFactor)
      THEN BEGIN
        UseCurrFactorOnly := TRUE;
        CurrencyDate := 0D;
        CurrencyFactor := GenJnlLine."FA Add.-Currency Factor";
      END;
    END;

    LOCAL PROCEDURE ExchangeAmtLCYToFCY2@40(Amount@1000 : Decimal) : Decimal;
    BEGIN
      IF UseCurrFactorOnly THEN
        EXIT(
          ROUND(
            CurrExchRate.ExchangeAmtLCYToFCYOnlyFactor(Amount,CurrencyFactor),
            AddCurrency."Amount Rounding Precision"));
      EXIT(
        ROUND(
          CurrExchRate.ExchangeAmtLCYToFCY(
            CurrencyDate,AddCurrencyCode,Amount,CurrencyFactor),
          AddCurrency."Amount Rounding Precision"));
    END;

    LOCAL PROCEDURE CheckNonAddCurrCodeOccurred@54(CurrencyCode@1000 : Code[10]) : Boolean;
    BEGIN
      NonAddCurrCodeOccured :=
        NonAddCurrCodeOccured OR (AddCurrencyCode <> CurrencyCode);
      EXIT(NonAddCurrCodeOccured);
    END;

    LOCAL PROCEDURE TotalVATAmountOnJnlLines@1130(GenJnlLine@1000 : Record 81) TotalVATAmount : Decimal;
    VAR
      GenJnlLine2@1001 : Record 81;
    BEGIN
      WITH GenJnlLine2 DO BEGIN
        SETRANGE("Source Code",GenJnlLine."Source Code");
        SETRANGE("Document No.",GenJnlLine."Document No.");
        SETRANGE("Posting Date",GenJnlLine."Posting Date");
        IF FINDSET THEN
          REPEAT
            TotalVATAmount += "VAT Amount (LCY)" - "Bal. VAT Amount (LCY)";
          UNTIL NEXT = 0;
      END;
      EXIT(TotalVATAmount);
    END;

    [External]
    PROCEDURE SetGLRegReverse@8(VAR ReverseGLReg@1000 : Record 45);
    BEGIN
      GLReg.Reversed := TRUE;
      ReverseGLReg := GLReg;
    END;

    LOCAL PROCEDURE InsertVATEntriesFromTemp@83(VAR DtldCVLedgEntryBuf@1000 : Record 383;GLEntry@1003 : Record 17);
    VAR
      Complete@1001 : Boolean;
      LinkedAmount@1002 : Decimal;
      FirstEntryNo@1006 : Integer;
      LastEntryNo@1004 : Integer;
    BEGIN
      TempVATEntry.RESET;
      TempVATEntry.SETRANGE("Gen. Bus. Posting Group",GLEntry."Gen. Bus. Posting Group");
      TempVATEntry.SETRANGE("Gen. Prod. Posting Group",GLEntry."Gen. Prod. Posting Group");
      TempVATEntry.SETRANGE("VAT Bus. Posting Group",GLEntry."VAT Bus. Posting Group");
      TempVATEntry.SETRANGE("VAT Prod. Posting Group",GLEntry."VAT Prod. Posting Group");
      CASE DtldCVLedgEntryBuf."Entry Type" OF
        DtldCVLedgEntryBuf."Entry Type"::"Payment Discount Tolerance (VAT Excl.)":
          BEGIN
            FirstEntryNo := 1000000;
            LastEntryNo := 1999999;
          END;
        DtldCVLedgEntryBuf."Entry Type"::"Payment Tolerance (VAT Excl.)":
          BEGIN
            FirstEntryNo := 2000000;
            LastEntryNo := 2999999;
          END;
        DtldCVLedgEntryBuf."Entry Type"::"Payment Discount (VAT Excl.)":
          BEGIN
            FirstEntryNo := 3000000;
            LastEntryNo := 3999999;
          END;
      END;
      TempVATEntry.SETRANGE("Entry No.",FirstEntryNo,LastEntryNo);
      IF TempVATEntry.FINDSET THEN
        REPEAT
          VATEntry := TempVATEntry;
          VATEntry."Entry No." := NextVATEntryNo;
          VATEntry.INSERT(TRUE);
          NextVATEntryNo := NextVATEntryNo + 1;
          IF VATEntry."Unrealized VAT Entry No." = 0 THEN
            GLEntryVATEntryLink.InsertLink(GLEntry."Entry No.",VATEntry."Entry No.");
          LinkedAmount += VATEntry.Amount + VATEntry.Base;
          Complete := LinkedAmount = -(DtldCVLedgEntryBuf."Amount (LCY)" + DtldCVLedgEntryBuf."VAT Amount (LCY)");
          LastEntryNo := TempVATEntry."Entry No.";
        UNTIL Complete OR (TempVATEntry.NEXT = 0);

      TempVATEntry.SETRANGE("Entry No.",FirstEntryNo,LastEntryNo);
      TempVATEntry.DELETEALL;
    END;

    LOCAL PROCEDURE ABSMin@84(Decimal1@1000 : Decimal;Decimal2@1001 : Decimal) : Decimal;
    BEGIN
      IF ABS(Decimal1) < ABS(Decimal2) THEN
        EXIT(Decimal1);
      EXIT(Decimal2);
    END;

    LOCAL PROCEDURE GetApplnRoundPrecision@92(NewCVLedgEntryBuf@1002 : Record 382;OldCVLedgEntryBuf@1003 : Record 382) : Decimal;
    VAR
      ApplnCurrency@1000 : Record 4;
      CurrencyCode@1005 : Code[10];
    BEGIN
      IF NewCVLedgEntryBuf."Currency Code" <> '' THEN
        CurrencyCode := NewCVLedgEntryBuf."Currency Code"
      ELSE
        CurrencyCode := OldCVLedgEntryBuf."Currency Code";
      IF CurrencyCode = '' THEN
        EXIT(0);
      ApplnCurrency.GET(CurrencyCode);
      IF ApplnCurrency."Appln. Rounding Precision" <> 0 THEN
        EXIT(ApplnCurrency."Appln. Rounding Precision");
      EXIT(GLSetup."Appln. Rounding Precision");
    END;

    LOCAL PROCEDURE GetGLSetup@19();
    BEGIN
      IF GLSetupRead THEN
        EXIT;

      GLSetup.GET;
      GLSetupRead := TRUE;

      AddCurrencyCode := GLSetup."Additional Reporting Currency";
    END;

    LOCAL PROCEDURE ReadGLSetup@17(VAR NewGLSetup@1000 : Record 98);
    BEGIN
      NewGLSetup := GLSetup;
    END;

    LOCAL PROCEDURE CheckSalesExtDocNo@115(GenJnlLine@1001 : Record 81);
    VAR
      SalesSetup@1000 : Record 311;
    BEGIN
      SalesSetup.GET;
      IF NOT SalesSetup."Ext. Doc. No. Mandatory" THEN
        EXIT;

      IF GenJnlLine."Document Type" IN
         [GenJnlLine."Document Type"::Invoice,
          GenJnlLine."Document Type"::"Credit Memo",
          GenJnlLine."Document Type"::Payment,
          GenJnlLine."Document Type"::Refund,
          GenJnlLine."Document Type"::" "]
      THEN
        GenJnlLine.TESTFIELD("External Document No.");
    END;

    LOCAL PROCEDURE CheckPurchExtDocNo@107(GenJnlLine@1003 : Record 81);
    VAR
      PurchSetup@1002 : Record 312;
      OldVendLedgEntry@1001 : Record 25;
      VendorMgt@1000 : Codeunit 1312;
    BEGIN
      PurchSetup.GET;
      IF NOT (PurchSetup."Ext. Doc. No. Mandatory" OR (GenJnlLine."External Document No." <> '')) THEN
        EXIT;

      GenJnlLine.TESTFIELD("External Document No.");
      OldVendLedgEntry.RESET;
      VendorMgt.SetFilterForExternalDocNo(
        OldVendLedgEntry,GenJnlLine."Document Type",GenJnlLine."External Document No.",
        GenJnlLine."Account No.",GenJnlLine."Document Date");
      IF NOT OldVendLedgEntry.ISEMPTY THEN
        ERROR(
          PurchaseAlreadyExistsErr,
          GenJnlLine."Document Type",GenJnlLine."External Document No.");
    END;

    LOCAL PROCEDURE CheckDimValueForDisposal@93(GenJnlLine@1001 : Record 81;AccountNo@1002 : Code[20]);
    VAR
      DimMgt@1000 : Codeunit 408;
      TableID@1025 : ARRAY [10] OF Integer;
      AccNo@1026 : ARRAY [10] OF Code[20];
    BEGIN
      IF ((GenJnlLine.Amount = 0) OR (GenJnlLine."Amount (LCY)" = 0)) AND
         (GenJnlLine."FA Posting Type" = GenJnlLine."FA Posting Type"::Disposal)
      THEN BEGIN
        TableID[1] := DimMgt.TypeToTableID1(GenJnlLine."Account Type"::"G/L Account");
        AccNo[1] := AccountNo;
        IF NOT DimMgt.CheckDimValuePosting(TableID,AccNo,GenJnlLine."Dimension Set ID") THEN
          ERROR(DimMgt.GetDimValuePostingErr);
      END;
    END;

    [External]
    PROCEDURE SetOverDimErr@79();
    BEGIN
      OverrideDimErr := TRUE;
    END;

    LOCAL PROCEDURE CheckGLAccDimError@97(GenJnlLine@1005 : Record 81;GLAccNo@1004 : Code[20]);
    VAR
      DimMgt@1002 : Codeunit 408;
      TableID@1001 : ARRAY [10] OF Integer;
      AccNo@1000 : ARRAY [10] OF Code[20];
    BEGIN
      IF (GenJnlLine.Amount = 0) AND (GenJnlLine."Amount (LCY)" = 0) THEN
        EXIT;

      TableID[1] := DATABASE::"G/L Account";
      AccNo[1] := GLAccNo;
      IF DimMgt.CheckDimValuePosting(TableID,AccNo,GenJnlLine."Dimension Set ID") THEN
        EXIT;

      IF GenJnlLine."Line No." <> 0 THEN
        ERROR(
          DimensionUsedErr,
          GenJnlLine.TABLECAPTION,GenJnlLine."Journal Template Name",
          GenJnlLine."Journal Batch Name",GenJnlLine."Line No.",
          DimMgt.GetDimValuePostingErr);

      ERROR(DimMgt.GetDimValuePostingErr);
    END;

    LOCAL PROCEDURE CalculateCurrentBalance@95(AccountNo@1000 : Code[20];BalAccountNo@1001 : Code[20];InclVATAmount@1002 : Boolean;AmountLCY@1004 : Decimal;VATAmount@1005 : Decimal);
    BEGIN
      IF (AccountNo <> '') AND (BalAccountNo <> '') THEN
        EXIT;

      IF AccountNo = BalAccountNo THEN
        EXIT;

      IF NOT InclVATAmount THEN
        VATAmount := 0;

      IF BalAccountNo <> '' THEN
        CurrentBalance -= AmountLCY + VATAmount
      ELSE
        CurrentBalance += AmountLCY + VATAmount;
    END;

    LOCAL PROCEDURE GetCurrency@191(VAR Currency@1000 : Record 4;CurrencyCode@1001 : Code[10]);
    BEGIN
      IF Currency.Code <> CurrencyCode THEN BEGIN
        IF CurrencyCode = '' THEN
          CLEAR(Currency)
        ELSE
          Currency.GET(CurrencyCode);
      END;
    END;

    LOCAL PROCEDURE CollectAdjustment@181(VAR AdjAmount@1003 : ARRAY [4] OF Decimal;Amount@1004 : Decimal;AmountAddCurr@1005 : Decimal);
    VAR
      Offset@1001 : Integer;
    BEGIN
      Offset := GetAdjAmountOffset(Amount,AmountAddCurr);
      AdjAmount[Offset] += Amount;
      AdjAmount[Offset + 1] += AmountAddCurr;
    END;

    LOCAL PROCEDURE HandleDtldAdjustment@182(GenJnlLine@1008 : Record 81;VAR GLEntry@1002 : Record 17;AdjAmount@1010 : ARRAY [4] OF Decimal;TotalAmountLCY@1004 : Decimal;TotalAmountAddCurr@1005 : Decimal;GLAccNo@1007 : Code[20]);
    BEGIN
      IF NOT PostDtldAdjustment(
           GenJnlLine,GLEntry,AdjAmount,
           TotalAmountLCY,TotalAmountAddCurr,GLAccNo,
           GetAdjAmountOffset(TotalAmountLCY,TotalAmountAddCurr))
      THEN
        InitGLEntry(GenJnlLine,GLEntry,GLAccNo,TotalAmountLCY,TotalAmountAddCurr,TRUE,TRUE);
    END;

    LOCAL PROCEDURE PostDtldAdjustment@96(GenJnlLine@1006 : Record 81;VAR GLEntry@1005 : Record 17;AdjAmount@1004 : ARRAY [4] OF Decimal;TotalAmountLCY@1002 : Decimal;TotalAmountAddCurr@1001 : Decimal;GLAcc@1000 : Code[20];ArrayIndex@1007 : Integer) : Boolean;
    BEGIN
      IF (GenJnlLine."Bal. Account No." <> '') AND
         ((AdjAmount[ArrayIndex] <> 0) OR (AdjAmount[ArrayIndex + 1] <> 0)) AND
         ((TotalAmountLCY + AdjAmount[ArrayIndex] <> 0) OR (TotalAmountAddCurr + AdjAmount[ArrayIndex + 1] <> 0))
      THEN BEGIN
        CreateGLEntryBalAcc(
          GenJnlLine,GLAcc,-AdjAmount[ArrayIndex],-AdjAmount[ArrayIndex + 1],
          GenJnlLine."Bal. Account Type",GenJnlLine."Bal. Account No.");
        InitGLEntry(GenJnlLine,GLEntry,
          GLAcc,TotalAmountLCY + AdjAmount[ArrayIndex],
          TotalAmountAddCurr + AdjAmount[ArrayIndex + 1],TRUE,TRUE);
        AdjAmount[ArrayIndex] := 0;
        AdjAmount[ArrayIndex + 1] := 0;
        EXIT(TRUE);
      END;

      EXIT(FALSE);
    END;

    LOCAL PROCEDURE GetAdjAmountOffset@121(Amount@1000 : Decimal;AmountACY@1001 : Decimal) : Integer;
    BEGIN
      IF (Amount > 0) OR (Amount = 0) AND (AmountACY > 0) THEN
        EXIT(1);
      EXIT(3);
    END;

    [External]
    PROCEDURE GetNextEntryNo@53() : Integer;
    BEGIN
      EXIT(NextEntryNo);
    END;

    [External]
    PROCEDURE GetNextTransactionNo@67() : Integer;
    BEGIN
      EXIT(NextTransactionNo);
    END;

    [External]
    PROCEDURE GetNextVATEntryNo@68() : Integer;
    BEGIN
      EXIT(NextVATEntryNo);
    END;

    [External]
    PROCEDURE IncrNextVATEntryNo@70();
    BEGIN
      NextVATEntryNo := NextVATEntryNo + 1;
    END;

    LOCAL PROCEDURE IsNotPayment@77(DocumentType@1000 : ' ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund') : Boolean;
    BEGIN
      EXIT(DocumentType IN [DocumentType::Invoice,
                            DocumentType::"Credit Memo",
                            DocumentType::"Finance Charge Memo",
                            DocumentType::Reminder]);
    END;

    LOCAL PROCEDURE IsTempGLEntryBufEmpty@44() : Boolean;
    BEGIN
      EXIT(TempGLEntryBuf.ISEMPTY);
    END;

    LOCAL PROCEDURE IsVATAdjustment@20(EntryType@1000 : Option) : Boolean;
    VAR
      DtldCVLedgEntryBuf@1001 : Record 383;
    BEGIN
      EXIT(EntryType IN [DtldCVLedgEntryBuf."Entry Type"::"Payment Discount (VAT Adjustment)",
                         DtldCVLedgEntryBuf."Entry Type"::"Payment Tolerance (VAT Adjustment)",
                         DtldCVLedgEntryBuf."Entry Type"::"Payment Discount Tolerance (VAT Adjustment)"]);
    END;

    LOCAL PROCEDURE IsVATExcluded@7(EntryType@1000 : Option) : Boolean;
    VAR
      DtldCVLedgEntryBuf@1001 : Record 383;
    BEGIN
      EXIT(EntryType IN [DtldCVLedgEntryBuf."Entry Type"::"Payment Discount (VAT Excl.)",
                         DtldCVLedgEntryBuf."Entry Type"::"Payment Tolerance (VAT Excl.)",
                         DtldCVLedgEntryBuf."Entry Type"::"Payment Discount Tolerance (VAT Excl.)"]);
    END;

    LOCAL PROCEDURE UpdateGLEntryNo@120(VAR GLEntryNo@1002 : Integer;VAR SavedEntryNo@1000 : Integer);
    BEGIN
      IF SavedEntryNo <> 0 THEN BEGIN
        GLEntryNo := SavedEntryNo;
        NextEntryNo := NextEntryNo - 1;
        SavedEntryNo := 0;
      END;
    END;

    LOCAL PROCEDURE UpdateTotalAmounts@132(VAR TempInvPostBuf@1003 : TEMPORARY Record 49;DimSetID@1000 : Integer;AmountToCollect@1001 : Decimal;AmountACYToCollect@1002 : Decimal);
    BEGIN
      WITH TempInvPostBuf DO BEGIN
        SETRANGE("Dimension Set ID",DimSetID);
        IF FINDFIRST THEN BEGIN
          Amount += AmountToCollect;
          "Amount (ACY)" += AmountACYToCollect;
          MODIFY;
        END ELSE BEGIN
          INIT;
          "Dimension Set ID" := DimSetID;
          Amount := AmountToCollect;
          "Amount (ACY)" := AmountACYToCollect;
          INSERT;
        END;
      END;
    END;

    LOCAL PROCEDURE CreateGLEntriesForTotalAmountsUnapply@135(GenJnlLine@1000 : Record 81;VAR TempInvPostBuf@1002 : TEMPORARY Record 49;Account@1001 : Code[20]);
    VAR
      DimMgt@1003 : Codeunit 408;
    BEGIN
      WITH TempInvPostBuf DO BEGIN
        SETRANGE("Dimension Set ID");
        IF FINDSET THEN
          REPEAT
            IF (Amount <> 0) OR
               ("Amount (ACY)" <> 0) AND (GLSetup."Additional Reporting Currency" <> '')
            THEN BEGIN
              DimMgt.UpdateGenJnlLineDim(GenJnlLine,"Dimension Set ID");
              CreateGLEntry(GenJnlLine,Account,Amount,"Amount (ACY)",TRUE);
            END;
          UNTIL NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE CreateGLEntriesForTotalAmounts@36(GenJnlLine@1004 : Record 81;VAR InvPostBuf@1001 : Record 49;AdjAmountBuf@1006 : ARRAY [4] OF Decimal;SavedEntryNo@1009 : Integer;GLAccNo@1007 : Code[20];LedgEntryInserted@1003 : Boolean);
    VAR
      DimMgt@1002 : Codeunit 408;
      GLEntryInserted@1000 : Boolean;
    BEGIN
      GLEntryInserted := FALSE;

      WITH InvPostBuf DO BEGIN
        RESET;
        IF FINDSET THEN
          REPEAT
            IF (Amount <> 0) OR ("Amount (ACY)" <> 0) AND (AddCurrencyCode <> '') THEN BEGIN
              DimMgt.UpdateGenJnlLineDim(GenJnlLine,"Dimension Set ID");
              CreateGLEntryForTotalAmounts(GenJnlLine,Amount,"Amount (ACY)",AdjAmountBuf,SavedEntryNo,GLAccNo);
              GLEntryInserted := TRUE;
            END;
          UNTIL NEXT = 0;
      END;

      IF NOT GLEntryInserted AND LedgEntryInserted THEN
        CreateGLEntryForTotalAmounts(GenJnlLine,0,0,AdjAmountBuf,SavedEntryNo,GLAccNo);
    END;

    LOCAL PROCEDURE CreateGLEntryForTotalAmounts@122(GenJnlLine@1004 : Record 81;Amount@1000 : Decimal;AmountACY@1001 : Decimal;AdjAmountBuf@1006 : ARRAY [4] OF Decimal;VAR SavedEntryNo@1009 : Integer;GLAccNo@1007 : Code[20]);
    VAR
      GLEntry@1005 : Record 17;
    BEGIN
      HandleDtldAdjustment(GenJnlLine,GLEntry,AdjAmountBuf,Amount,AmountACY,GLAccNo);
      GLEntry."Bal. Account Type" := GenJnlLine."Bal. Account Type";
      GLEntry."Bal. Account No." := GenJnlLine."Bal. Account No.";
      UpdateGLEntryNo(GLEntry."Entry No.",SavedEntryNo);
      InsertGLEntry(GenJnlLine,GLEntry,TRUE);
    END;

    PROCEDURE InsertWHTPostingBufferPosted@1500000(VAR WHTEntryGL@1500003 : Record 50004;VAR GenJnlLine@1500002 : Record 81;Apply@1500001 : Boolean;Source@1500000 : 'Sales,Purchase');
    VAR
      BankAcc@1240005 : Record 270;
      BankAccLedgEntry@1240004 : Record 271;
      CheckLedgEntry@1240003 : Record 272;
      CheckLedgEntry2@1240002 : Record 272;
      BankAccPostingGr@1240001 : Record 277;
      PurchSetup@1500007 : Record 98;
      GenJnlLine3@1500005 : Record 81;
      Vendor@1500009 : Record 23;
      WHTPostingSetup@1240000 : Record 50003;
      GLEntry@1250000 : Record 17;
    BEGIN
      IF WHTEntryGL."Amount (LCY)" <> 0 THEN BEGIN
        WHTPostingSetup.GET(WHTEntryGL."WHT Bus. Posting Group",WHTEntryGL."WHT Prod. Posting Group");
        PurchSetup.GET;
        GenJnlLine3.RESET;
        GenJnlLine3 := GenJnlLine;
        GenJnlLine3.SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
        GenJnlLine3.SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
        GenJnlLine3."Line No." := 10000;

        GenJnlLine3.INIT;
        GenJnlLine3.VALIDATE("Posting Date",GenJnlLine."Posting Date");
        GenJnlLine3."Document Type" := GenJnlLine."Document Type";
        GenJnlLine3."Account Type" := GenJnlLine3."Account Type"::"G/L Account";
        GenJnlLine3.VALIDATE("Currency Code",WHTEntryGL."Currency Code");
        IF Apply THEN
          GenJnlLine3.VALIDATE(Amount,WHTEntryGL.Amount)
        ELSE
          GenJnlLine3.VALIDATE(Amount,-WHTEntryGL.Amount);

        IF Source = Source::Purchase THEN BEGIN
          IF GenJnlLine."Document Type" = GenJnlLine."Document Type"::Refund THEN BEGIN
            GenJnlLine3.VALIDATE("Account No.",WHTPostingSetup."Purch. WHT Adj. Account No.");
          END ELSE
            GenJnlLine3.VALIDATE("Account No.",WHTPostingSetup."Payable WHT Account Code");

          CASE WHTPostingSetup."Bal. Payable Account Type" OF
            WHTPostingSetup."Bal. Payable Account Type"::"Bank Account":
              GenJnlLine3."Bal. Account Type" := GenJnlLine3."Account Type"::"Bank Account";
            WHTPostingSetup."Bal. Payable Account Type"::"G/L Account":
              GenJnlLine3."Bal. Account Type" := GenJnlLine3."Account Type"::"G/L Account";
          END;
          GenJnlLine3.VALIDATE("Bal. Account No.",WHTPostingSetup."Bal. Payable Account No.");
        END;

        IF Source = Source::Sales THEN BEGIN
          IF GenJnlLine."Document Type" = GenJnlLine."Document Type"::Refund THEN BEGIN
            GenJnlLine3.VALIDATE("Account No.",WHTPostingSetup."Sales WHT Adj. Account No.");
          END ELSE BEGIN
            WHTPostingSetup.TESTFIELD("Prepaid WHT Account Code");
            GenJnlLine3.VALIDATE("Account No.",WHTPostingSetup."Prepaid WHT Account Code");
          END;

          CASE WHTPostingSetup."Bal. Prepaid Account Type" OF
            WHTPostingSetup."Bal. Prepaid Account Type"::"Bank Account":
              GenJnlLine3."Bal. Account Type" := GenJnlLine3."Account Type"::"Bank Account";
            WHTPostingSetup."Bal. Prepaid Account Type"::"G/L Account":
              GenJnlLine3."Bal. Account Type" := GenJnlLine3."Account Type"::"G/L Account";
          END;
          GenJnlLine3.VALIDATE("Bal. Account No.",WHTPostingSetup."Bal. Prepaid Account No.");
        END;
        GenJnlLine3.VALIDATE("Currency Code",WHTEntryGL."Currency Code");
        IF Apply THEN BEGIN
          GenJnlLine3.VALIDATE(Amount,WHTEntryGL.Amount);
          GenJnlLine3."Amount (LCY)" := WHTEntryGL."Amount (LCY)";
        END ELSE BEGIN
          GenJnlLine3.VALIDATE(Amount,-WHTEntryGL.Amount);
          GenJnlLine3."Amount (LCY)" := -WHTEntryGL."Amount (LCY)";
        END;
        GenJnlLine3.TESTFIELD("Bal. Account No.");
        GenJnlLine3."Source Code" := GenJnlLine."Source Code";
        GenJnlLine3."Reason Code" := GenJnlLine."Reason Code";
        GenJnlLine3."Shortcut Dimension 1 Code" := GenJnlLine."Shortcut Dimension 1 Code";
        GenJnlLine3."Shortcut Dimension 2 Code" := GenJnlLine."Shortcut Dimension 2 Code";
        GenJnlLine3."Allow Zero-Amount Posting" := TRUE;
        GenJnlLine3."WHT Business Posting Group" := WHTEntryGL."WHT Bus. Posting Group";
        GenJnlLine3."WHT Product Posting Group" := WHTEntryGL."WHT Prod. Posting Group";
        GenJnlLine3."Document Type" := GenJnlLine."Document Type";
        GenJnlLine3."Document No." := GenJnlLine."Document No.";
        GenJnlLine3."External Document No." := GenJnlLine."External Document No.";
        IF GenJnlLine."Document Type" = GenJnlLine."Document Type"::Refund THEN
          GenJnlLine3."Gen. Posting Type" := GenJnlLine3."Gen. Posting Type"::" ";

        IF NextEntryNo = 0 THEN
          NextEntryNo := GenJnlLine."WHT Entry No.";
        IF Apply THEN BEGIN
          IF Source = Source::Purchase THEN BEGIN
            IF GenJnlLine."Document Type" = GenJnlLine."Document Type"::Refund THEN
              InitGLEntry(
                GenJnlLine,GLEntry,WHTPostingSetup."Purch. WHT Adj. Account No.",-WHTEntryGL."Amount (LCY)",0,FALSE,TRUE)
            ELSE
              InitGLEntry(
                GenJnlLine,GLEntry,WHTPostingSetup."Payable WHT Account Code",-WHTEntryGL."Amount (LCY)",0,FALSE,TRUE);
          END;

          IF Source = Source::Sales THEN
            IF GenJnlLine."Document Type" = GenJnlLine."Document Type"::Refund THEN
              InitGLEntry(
                GenJnlLine,GLEntry,WHTPostingSetup."Sales WHT Adj. Account No.",-WHTEntryGL."Amount (LCY)",0,FALSE,TRUE)
            ELSE BEGIN
              WHTPostingSetup.TESTFIELD("Prepaid WHT Account Code");
              InitGLEntry(
                GenJnlLine,GLEntry,WHTPostingSetup."Prepaid WHT Account Code",-WHTEntryGL."Amount (LCY)",0,FALSE,TRUE);
            END;
          GLEntry."Posting Date" := GenJnlLine3."Posting Date";
          GLEntry."Additional-Currency Amount" := -WHTEntryGL.Amount;
        END ELSE BEGIN
          IF Source = Source::Purchase THEN BEGIN
            IF GenJnlLine."Document Type" = GenJnlLine."Document Type"::Refund THEN
              InitGLEntry(
                GenJnlLine,GLEntry,WHTPostingSetup."Purch. WHT Adj. Account No.",WHTEntryGL."Amount (LCY)",0,FALSE,TRUE)
            ELSE
              InitGLEntry(
                GenJnlLine,GLEntry,WHTPostingSetup."Payable WHT Account Code",WHTEntryGL."Amount (LCY)",0,FALSE,TRUE);
          END;
          IF Source = Source::Sales THEN
            IF GenJnlLine."Document Type" = GenJnlLine."Document Type"::Refund THEN
              InitGLEntry(
                GenJnlLine,GLEntry,WHTPostingSetup."Sales WHT Adj. Account No.",WHTEntryGL."Amount (LCY)",0,FALSE,TRUE)
            ELSE BEGIN
              WHTPostingSetup.TESTFIELD("Prepaid WHT Account Code");
              InitGLEntry(
                GenJnlLine,GLEntry,WHTPostingSetup."Prepaid WHT Account Code",WHTEntryGL."Amount (LCY)",0,FALSE,TRUE);
            END;
          GLEntry."Posting Date" := GenJnlLine3."Posting Date";
          GLEntry."Additional-Currency Amount" := WHTEntryGL.Amount;
        END;

        GLEntry."Gen. Posting Type" := GenJnlLine."Gen. Posting Type";
        GLEntry."Gen. Bus. Posting Group" := GenJnlLine."Gen. Bus. Posting Group";
        GLEntry."Gen. Prod. Posting Group" := GenJnlLine."Gen. Prod. Posting Group";
        GLEntry."VAT Bus. Posting Group" := GenJnlLine."VAT Bus. Posting Group";
        GLEntry."VAT Prod. Posting Group" := GenJnlLine."VAT Prod. Posting Group";
        InsertGLEntry(GenJnlLine,GLEntry,TRUE);

        CASE GenJnlLine3."Bal. Account Type" OF
          GenJnlLine3."Bal. Account Type"::"Bank Account":
            WITH GenJnlLine3 DO BEGIN
              BankAccLedgEntry.LOCKTABLE;
              IF BankAcc."No." <> "Bal. Account No." THEN
                BankAcc.GET("Bal. Account No.");
              BankAcc.TESTFIELD(Blocked,FALSE);
              IF "Currency Code" = '' THEN
                BankAcc.TESTFIELD("Currency Code",'')
              ELSE
                IF BankAcc."Currency Code" <> '' THEN
                  TESTFIELD("Currency Code",BankAcc."Currency Code");

              BankAcc.TESTFIELD("Bank Acc. Posting Group");
              BankAccPostingGr.GET(BankAcc."Bank Acc. Posting Group");

              BankAccLedgEntry.INIT;
              BankAccLedgEntry."Bank Account No." := "Bal. Account No.";
              BankAccLedgEntry."Posting Date" := "Posting Date";
              BankAccLedgEntry."Document Date" := "Document Date";
              BankAccLedgEntry."Document Type" := "Document Type";
              BankAccLedgEntry."Document No." := "Document No.";
              BankAccLedgEntry."External Document No." := "External Document No.";
              BankAccLedgEntry.Description := Description;
              BankAccLedgEntry."Bank Acc. Posting Group" := BankAcc."Bank Acc. Posting Group";
              BankAccLedgEntry."Global Dimension 1 Code" := "Shortcut Dimension 1 Code";
              BankAccLedgEntry."Global Dimension 2 Code" := "Shortcut Dimension 2 Code";
              BankAccLedgEntry."Dimension Set ID" := "Dimension Set ID";
              BankAccLedgEntry."Our Contact Code" := "Salespers./Purch. Code";
              BankAccLedgEntry."Source Code" := "Source Code";
              BankAccLedgEntry."Journal Batch Name" := "Journal Batch Name";
              BankAccLedgEntry."Reason Code" := "Reason Code";
              BankAccLedgEntry."Entry No." := NextEntryNo;
              BankAccLedgEntry."Transaction No." := NextTransactionNo;
              BankAccLedgEntry."Currency Code" := BankAcc."Currency Code";
              IF BankAcc."Currency Code" <> '' THEN
                BankAccLedgEntry.Amount := Amount
              ELSE
                BankAccLedgEntry.Amount := "Amount (LCY)";
              BankAccLedgEntry."Amount (LCY)" := "Amount (LCY)";
              BankAccLedgEntry."User ID" := USERID;
              IF BankAccLedgEntry.Amount <> 0 THEN BEGIN
                BankAccLedgEntry.Open := TRUE;
                BankAccLedgEntry."Remaining Amount" := BankAccLedgEntry.Amount;
              END;
              BankAccLedgEntry.Positive := BankAccLedgEntry.Amount > 0;
              BankAccLedgEntry."Bal. Account Type" := "Bal. Account Type";
              BankAccLedgEntry."Bal. Account No." := "Bal. Account No.";
              IF (Amount > 0) AND (NOT Correction) OR
                 ("Amount (LCY)" > 0) AND (NOT Correction) OR
                 (Amount < 0) AND Correction OR
                 ("Amount (LCY)" < 0) AND Correction
              THEN BEGIN
                BankAccLedgEntry."Debit Amount" := BankAccLedgEntry.Amount;
                BankAccLedgEntry."Credit Amount" := 0;
                BankAccLedgEntry."Debit Amount (LCY)" := BankAccLedgEntry."Amount (LCY)";
                BankAccLedgEntry."Credit Amount (LCY)" := 0;
              END ELSE BEGIN
                BankAccLedgEntry."Debit Amount" := 0;
                BankAccLedgEntry."Credit Amount" := -BankAccLedgEntry.Amount;
                BankAccLedgEntry."Debit Amount (LCY)" := 0;
                BankAccLedgEntry."Credit Amount (LCY)" := -BankAccLedgEntry."Amount (LCY)";
              END;
              BankAccLedgEntry.INSERT;

              IF ((Amount <= 0) AND ("Bank Payment Type" = "Bank Payment Type"::"Computer Check") AND "Check Printed") OR
                 ((Amount < 0) AND ("Bank Payment Type" = "Bank Payment Type"::"Manual Check"))
              THEN BEGIN
                IF BankAcc."Currency Code" <> "Currency Code" THEN
                  ERROR(BankPaymentTypeMustNotBeFilledErr);
                CASE "Bank Payment Type" OF
                  "Bank Payment Type"::"Computer Check":
                    BEGIN
                      TESTFIELD("Check Printed",TRUE);
                      CheckLedgEntry.LOCKTABLE;
                      CheckLedgEntry.RESET;
                      CheckLedgEntry.SETCURRENTKEY("Bank Account No.","Entry Status","Check No.");
                      CheckLedgEntry.SETRANGE("Bank Account No.","Account No.");
                      CheckLedgEntry.SETRANGE("Entry Status",CheckLedgEntry."Entry Status"::Printed);
                      CheckLedgEntry.SETRANGE("Check No.","Document No.");
                      IF CheckLedgEntry.FIND('-') THEN
                        REPEAT
                          CheckLedgEntry2 := CheckLedgEntry;
                          CheckLedgEntry2."Entry Status" := CheckLedgEntry2."Entry Status"::Posted;
                          CheckLedgEntry2."Bank Account Ledger Entry No." := BankAccLedgEntry."Entry No.";
                          CheckLedgEntry2.MODIFY;
                        UNTIL CheckLedgEntry.NEXT = 0;
                    END;
                  "Bank Payment Type"::"Manual Check":
                    BEGIN
                      IF "Document No." = '' THEN
                        ERROR(DocNoMustBeEnteredErr,"Bank Payment Type");
                      CheckLedgEntry.RESET;
                      IF NextCheckEntryNo = 0 THEN BEGIN
                        CheckLedgEntry.LOCKTABLE;
                        IF CheckLedgEntry.FIND('+') THEN
                          NextCheckEntryNo := CheckLedgEntry."Entry No." + 1
                        ELSE
                          NextCheckEntryNo := 1;
                      END;

                      CheckLedgEntry.SETCURRENTKEY("Bank Account No.","Entry Status","Check No.");
                      CheckLedgEntry.SETRANGE("Bank Account No.","Account No.");
                      CheckLedgEntry.SETFILTER(
                        "Entry Status",'%1|%2|%3',
                        CheckLedgEntry."Entry Status"::Printed,
                        CheckLedgEntry."Entry Status"::Posted,
                        CheckLedgEntry."Entry Status"::"Financially Voided");
                      CheckLedgEntry.SETRANGE("Check No.","Document No.");
                      IF CheckLedgEntry.FIND('-') THEN
                        ERROR(CheckAlreadyExistsErr,"Document No.");

                      CheckLedgEntry.INIT;
                      CheckLedgEntry."Entry No." := NextCheckEntryNo;
                      CheckLedgEntry."Bank Account No." := BankAccLedgEntry."Bank Account No.";
                      CheckLedgEntry."Bank Account Ledger Entry No." := BankAccLedgEntry."Entry No.";
                      CheckLedgEntry."Posting Date" := BankAccLedgEntry."Posting Date";
                      CheckLedgEntry."Document Type" := BankAccLedgEntry."Document Type";
                      CheckLedgEntry."Document No." := BankAccLedgEntry."Document No.";
                      CheckLedgEntry."External Document No." := BankAccLedgEntry."External Document No.";
                      CheckLedgEntry.Description := BankAccLedgEntry.Description;
                      CheckLedgEntry."Bank Payment Type" := "Bank Payment Type";
                      CheckLedgEntry."Bal. Account Type" := BankAccLedgEntry."Bal. Account Type";
                      CheckLedgEntry."Bal. Account No." := BankAccLedgEntry."Bal. Account No.";
                      CheckLedgEntry."Entry Status" := CheckLedgEntry."Entry Status"::Posted;
                      CheckLedgEntry.Open := TRUE;
                      CheckLedgEntry."User ID" := USERID;
                      CheckLedgEntry."Check Date" := BankAccLedgEntry."Posting Date";
                      CheckLedgEntry."Check No." := BankAccLedgEntry."Document No.";

                      GenJnlLine1.RESET;
                      GenJnlLine1.COPY(GenJnlLine);
                      IF GLSetup."Enable WHT" THEN
                        IF NOT GenJnlLine."Skip WHT" THEN
                          CheckLedgEntry."WHT Amount" := -WHTManagement.WHTAmountJournal(GenJnlLine1,FALSE);
                      CheckLedgEntry."Interest Amount" := "Interest Amount";
                      IF BankAcc."Currency Code" <> '' THEN
                        CheckLedgEntry.Amount := -Amount - CheckLedgEntry."WHT Amount"
                      ELSE
                        CheckLedgEntry.Amount := -"Amount (LCY)" - CheckLedgEntry."WHT Amount";
                      CheckLedgEntry.INSERT;
                      NextCheckEntryNo := NextCheckEntryNo + 1;
                    END;
                END;
              END;

              BankAccPostingGr.TESTFIELD("G/L Bank Account No.");
              InitGLEntry(GenJnlLine,GLEntry,
                BankAccPostingGr."G/L Bank Account No.","Amount (LCY)","Source Currency Amount",TRUE,TRUE);
            END;
          GenJnlLine3."Bal. Account Type"::"G/L Account":
            InitGLEntry(GenJnlLine,GLEntry,
              GenJnlLine3."Bal. Account No.",GenJnlLine3."Amount (LCY)",GenJnlLine3."Source Currency Amount",TRUE,TRUE);
        END;
        GLEntry."Posting Date" := GenJnlLine3."Posting Date";
        GLEntry."Bal. Account Type" := GenJnlLine3."Bal. Account Type";
        GLEntry."Bal. Account No." := GenJnlLine3."Bal. Account No.";
        InsertGLEntry(GenJnlLine,GLEntry,TRUE);
      END;
    END;

    PROCEDURE IncreaseWHTEntryNo@1500003();
    BEGIN
      NextWHTEntryNo := NextWHTEntryNo + 1;
    END;

    PROCEDURE GetTransactionNo@1500004(VAR TransactionNo@1500000 : Integer);
    BEGIN
      TransactionNo := NextTransactionNo;
    END;

    PROCEDURE GetVATEntryNo@1500007() : Integer;
    BEGIN
      IF GLSetup."GST Report" THEN
        EXIT(VATEntry."Entry No.");
    END;

    PROCEDURE InsertWHTPaymentGL@1500036(GenJnlLine@1250000 : Record 81;VAR GenJnlLineWHT@1500000 : Record 81;VAR EntryNo@1500001 : Integer);
    VAR
      BankAcc@1240006 : Record 270;
      BankAccLedgEntry@1240005 : Record 271;
      CheckLedgEntry@1240004 : Record 272;
      CheckLedgEntry2@1240003 : Record 272;
      BankAccPostingGr@1240002 : Record 277;
      WHTPostingSetup@1500002 : Record 50003;
      WHTAmountLCY@1240000 : Decimal;
      WHTAmount@1240001 : Decimal;
    BEGIN
      WITH GenJnlLineWHT DO BEGIN
        WHTPostingSetup.GET("WHT Business Posting Group","WHT Product Posting Group");
        IF "Bal. Account Type" = "Bal. Account Type"::"Bank Account" THEN BEGIN
          "Line No." += 10000;
          BankAcc.GET("Bal. Account No.");
          BankAccPostingGr.GET(BankAcc."Bank Acc. Posting Group");
        END;
        WHTPostingSetup.TESTFIELD("Prepaid WHT Account Code");
        CreateGLEntry(
          GenJnlLineWHT,WHTPostingSetup."Prepaid WHT Account Code",-GenJnlLine.Amount,-GenJnlLine.Amount,TRUE);
        CreateGLEntry(
          GenJnlLineWHT,BankAccPostingGr."G/L Bank Account No.",GenJnlLine.Amount,GenJnlLine.Amount,TRUE);

        BankAccLedgEntry.LOCKTABLE;
        IF BankAcc."No." <> "Bal. Account No." THEN
          BankAcc.GET("Bal. Account No.");
        BankAcc.TESTFIELD(Blocked,FALSE);
        IF "Currency Code" = '' THEN
          BankAcc.TESTFIELD("Currency Code",'')
        ELSE
          IF BankAcc."Currency Code" <> '' THEN
            TESTFIELD("Currency Code",BankAcc."Currency Code");

        BankAcc.TESTFIELD("Bank Acc. Posting Group");
        BankAccPostingGr.GET(BankAcc."Bank Acc. Posting Group");

        BankAccLedgEntry.INIT;
        BankAccLedgEntry."Bank Account No." := "Bal. Account No.";
        BankAccLedgEntry."Posting Date" := "Posting Date";
        BankAccLedgEntry."Document Date" := "Document Date";
        BankAccLedgEntry."Document Type" := "Document Type";
        BankAccLedgEntry."Document No." := "Document No.";
        BankAccLedgEntry."External Document No." := "External Document No.";
        BankAccLedgEntry.Description := Description;
        BankAccLedgEntry."Bank Acc. Posting Group" := BankAcc."Bank Acc. Posting Group";
        BankAccLedgEntry."Global Dimension 1 Code" := "Shortcut Dimension 1 Code";
        BankAccLedgEntry."Global Dimension 2 Code" := "Shortcut Dimension 2 Code";
        BankAccLedgEntry."Our Contact Code" := "Salespers./Purch. Code";
        BankAccLedgEntry."Source Code" := "Source Code";
        BankAccLedgEntry."Journal Batch Name" := "Journal Batch Name";
        BankAccLedgEntry."Reason Code" := "Reason Code";
        BankAccLedgEntry."Entry No." := NextEntryNo;
        BankAccLedgEntry."Transaction No." := NextTransactionNo;
        BankAccLedgEntry."Currency Code" := BankAcc."Currency Code";

        CalcBankAccWHT2(GenJnlLine,WHTPostingSetup,WHTAmountLCY,WHTAmount);

        IF BankAcc."Currency Code" <> '' THEN
          BankAccLedgEntry.Amount := Amount
        ELSE
          BankAccLedgEntry.Amount := "Amount (LCY)" + WHTAmountLCY;
        BankAccLedgEntry."Amount (LCY)" := "Amount (LCY)" + WHTAmountLCY;
        BankAccLedgEntry."User ID" := USERID;
        IF BankAccLedgEntry.Amount <> 0 THEN BEGIN
          BankAccLedgEntry.Open := TRUE;
          BankAccLedgEntry."Remaining Amount" := BankAccLedgEntry.Amount;
        END;
        BankAccLedgEntry.Positive := BankAccLedgEntry.Amount > 0;
        BankAccLedgEntry."Bal. Account Type" := "Bal. Account Type";
        BankAccLedgEntry."Bal. Account No." := "Bal. Account No.";
        IF (Amount > 0) AND (NOT Correction) OR
           ("Amount (LCY)" > 0) AND (NOT Correction) OR
           (Amount < 0) AND Correction OR
           ("Amount (LCY)" < 0) AND Correction
        THEN BEGIN
          BankAccLedgEntry."Debit Amount" := BankAccLedgEntry.Amount;
          BankAccLedgEntry."Credit Amount" := 0;
          BankAccLedgEntry."Debit Amount (LCY)" := BankAccLedgEntry."Amount (LCY)";
          BankAccLedgEntry."Credit Amount (LCY)" := 0;
        END ELSE BEGIN
          BankAccLedgEntry."Debit Amount" := 0;
          BankAccLedgEntry."Credit Amount" := -BankAccLedgEntry.Amount;
          BankAccLedgEntry."Debit Amount (LCY)" := 0;
          BankAccLedgEntry."Credit Amount (LCY)" := -BankAccLedgEntry."Amount (LCY)";
        END;
        BankAccLedgEntry.INSERT;
      END;
    END;

    LOCAL PROCEDURE IsAboveWHTMinInvoiceAmount@1240011(GenJnlLine@1240000 : Record 81) : Boolean;
    VAR
      WHTPostingSetup@1240001 : Record 50003;
    BEGIN
      WITH GenJnlLine DO BEGIN
        IF NOT ("Document Type" IN ["Document Type"::Invoice,"Document Type"::"Credit Memo"]) THEN
          EXIT(TRUE);

        IF WHTPostingSetup.GET("WHT Business Posting Group","WHT Product Posting Group") THEN
          EXIT(ABS(Amount) >= WHTPostingSetup."WHT Minimum Invoice Amount");
        EXIT(FALSE);
      END;
    END;

    PROCEDURE SetReversalDocument@1500041(ReversalDoc@1500040 : Boolean);
    BEGIN
      IsReversal := ReversalDoc;
    END;

    LOCAL PROCEDURE CalcDtldCVLedgEntryAmount@1500038(GenJnlLine@1240000 : Record 81;WHTPercent@1240001 : Decimal) : Decimal;
    BEGIN
      WITH GenJnlLine DO BEGIN
        IF "WHT Absorb Base" <> 0 THEN
          EXIT(ExchangeAmtLCYToFCY2(-ROUND("WHT Absorb Base" * WHTPercent / 100)));
        EXIT(ExchangeAmtLCYToFCY2(ROUND(Amount * WHTPercent / 100)));
      END;
    END;

    PROCEDURE CalcGLAccWHT@1240001(GenJnlLine@1240000 : Record 81;VAR WHTPostingSetup@1240001 : Record 50003;VAR WHTAmountLCY@1240002 : Decimal;VAR WHTAmount@1240003 : Decimal);
    BEGIN
      WITH GenJnlLine DO BEGIN
        WHTAmountLCY := 0;
        IF GLSetup."Enable WHT" THEN
          IF NOT "Skip WHT" THEN
            IF ("Applies-to ID" = '') AND ("Applies-to Doc. No." = '') THEN BEGIN
              IF ("Document Type" = "Document Type"::Payment) OR
                 ("Document Type" = "Document Type"::Refund)
              THEN
                IF WHTPostingSetup.GET(
                     "WHT Business Posting Group",
                     "WHT Product Posting Group")
                THEN
                  IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest THEN
                    IF IsCustAcc(GenJnlLine) THEN BEGIN
                      IF "WHT Absorb Base" <> 0 THEN
                        WHTAmountLCY :=
                          -ABS(ROUND("WHT Absorb Base" * WHTPostingSetup."WHT %" / 100))
                      ELSE
                        WHTAmountLCY :=
                          -ABS(ROUND(Amount * WHTPostingSetup."WHT %" / 100));
                      IF "Document Type" = "Document Type"::Refund THEN
                        WHTAmountLCY := ABS(WHTAmountLCY);
                    END ELSE
                      IF IsVendAcc(GenJnlLine) THEN BEGIN
                        IF "WHT Absorb Base" <> 0 THEN
                          WHTAmountLCY :=
                            ABS(ROUND("WHT Absorb Base" * WHTPostingSetup."WHT %" / 100))
                        ELSE
                          WHTAmountLCY :=
                            ABS(ROUND(Amount * WHTPostingSetup."WHT %" / 100));

                        IF "Document Type" = "Document Type"::Refund THEN
                          WHTAmountLCY := -ABS(WHTAmountLCY);
                      END;
            END ELSE
              IF ("Applies-to ID" <> '') OR ("Applies-to Doc. No." <> '') THEN BEGIN
                GenJnlLine1.RESET;
                GenJnlLine1.COPY(GenJnlLine);
                IF "Applies-to Doc. No." <> '' THEN
                  GenJnlLine1.SETRANGE("Applies-to Doc. No.","Applies-to Doc. No.")
                ELSE
                  GenJnlLine1.SETRANGE("Applies-to ID","Applies-to ID");

                GenJnlLine1.SETRANGE("Account Type","Account Type"::Vendor);
                IF ("Account Type" = "Account Type"::Vendor) OR
                   ("Bal. Account Type" = "Bal. Account Type"::Vendor) OR
                   GenJnlLine1.FINDFIRST
                THEN BEGIN
                  CurrFactor :=
                    CurrExchRate.ExchangeRate(
                      "Document Date","Currency Code");
                  IF GenJnlLine1."Interest Amount" <> 0 THEN
                    GenJnlLine1.VALIDATE(Amount,GenJnlLine1.Amount - GenJnlLine1."Interest Amount");

                  IF ("Document Type" = "Document Type"::Payment) OR
                     ("Document Type" = "Document Type"::Refund)
                  THEN
                    IF WHTPostingSetup.GET(
                         "WHT Business Posting Group",
                         "WHT Product Posting Group")
                    THEN BEGIN
                      IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest THEN BEGIN
                        IF GenJnlLine1.FINDFIRST THEN
                          WHTManagement.CheckApplicationGenPurchWHT(GenJnlLine1);
                        WHTAmountLCY :=
                          CurrExchRate.ExchangeAmtFCYToLCY(
                            "Document Date",
                            "Currency Code",
                            ABS(
                              WHTManagement.CalcVendExtraWHTForEarliest(GenJnlLine1)),CurrFactor);
                      END;

                      IF (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Payment) AND
                         (NOT GLSetup."Manual Sales WHT Calc.")
                      THEN
                        WHTAmountLCY :=
                          CurrExchRate.ExchangeAmtFCYToLCY(
                            "Document Date",
                            "Currency Code",
                            ABS(
                              WHTManagement.WHTAmountJournal(GenJnlLine1,TRUE)),CurrFactor);
                    END;
                  IF "Document Type" = "Document Type"::Refund THEN
                    WHTAmountLCY := -ABS(WHTAmountLCY);
                END;

                IF IsCustAcc(GenJnlLine) THEN BEGIN
                  CurrFactor :=
                    CurrExchRate.ExchangeRate(
                      GenJnlLine1."Document Date","Currency Code");
                  IF "Bal. Account Type" = "Bal. Account Type"::Customer THEN
                    GenJnlLine1.VALIDATE(Amount,-GenJnlLine1.Amount);

                  IF IsPaymentOrRefund(GenJnlLine) THEN
                    IF WHTPostingSetup.GET("WHT Business Posting Group","WHT Product Posting Group") THEN BEGIN
                      IF (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest) THEN BEGIN
                        IF GenJnlLine1.FINDFIRST THEN
                          WHTManagement.CheckApplicationGenSalesWHT(GenJnlLine1);
                        WHTAmountLCY :=
                          -CurrExchRate.ExchangeAmtFCYToLCY(
                            "Document Date",
                            "Currency Code",
                            ABS(
                              WHTManagement.CalcCustExtraWHTForEarliest(GenJnlLine)),CurrFactor);
                      END;

                      IF (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Payment) AND
                         (NOT GLSetup."Manual Sales WHT Calc.")
                      THEN
                        WHTAmountLCY :=
                          -CurrExchRate.ExchangeAmtFCYToLCY(
                            GenJnlLine1."Document Date",
                            GenJnlLine1."Currency Code",
                            ABS(
                              WHTManagement.ApplyCustCalcWHT(GenJnlLine1)),CurrFactor);
                    END;

                  IF "Document Type" = "Document Type"::Refund THEN
                    WHTAmountLCY := -ABS(WHTAmountLCY);
                END;
                WHTAmountLCY := ROUND(WHTAmountLCY);
              END;

        IF WHTPostingSetup.GET("WHT Business Posting Group",
             "WHT Product Posting Group")
        THEN
          IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest THEN BEGIN
            IF ABS(Amount) < WHTPostingSetup."WHT Minimum Invoice Amount" THEN
              WHTAmountLCY := 0;
          END;
      END;
    END;

    PROCEDURE PostGLAccWHT@1240005(GenJnlLine@1240000 : Record 81;WHTPostingSetup@1240002 : Record 50003;WHTAmountLCY@1240001 : Decimal);
    BEGIN
      IF WHTAmountLCY = 0 THEN
        EXIT;

      WITH GenJnlLine DO BEGIN
        IF "Document Type" = "Document Type"::Invoice THEN
          EXIT;

        CASE TRUE OF
          IsVendAcc(GenJnlLine):
            IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest THEN
              CreateGLEntry(
                GenJnlLine,WHTPostingSetup."Payable WHT Account Code",-WHTAmountLCY,-WHTAmountLCY,TRUE);
          IsCustAcc(GenJnlLine):
            IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest THEN
              CreateGLEntry(
                GenJnlLine,WHTPostingSetup."Prepaid WHT Account Code",-WHTAmountLCY,-WHTAmountLCY,TRUE);
        END;
      END;
    END;

    PROCEDURE CalcCustWHT@1240007(GenJnlLine@1240000 : Record 81;VAR WHTPostingSetup@1240001 : Record 50003;VAR WHTAmountLCY@1240002 : Decimal;VAR WHTAmount@1240003 : Decimal);
    VAR
      SourceCodeSetup@1250000 : Record 242;
    BEGIN
      WITH GenJnlLine DO BEGIN
        WHTAmountLCY := 0;
        SourceCodeSetup.GET;
        IF ProcessSourceCode("Source Code",SourceCodeSetup) THEN BEGIN
          GenJnlLine1.RESET;
          GenJnlLine1.COPY(GenJnlLine);
          IF GLSetup."Enable WHT" THEN
            IF NOT GenJnlLine1."Skip WHT" THEN
              IF (GenJnlLine1."Applies-to Doc. No." = '') AND
                 (GenJnlLine1."Applies-to ID" = '')
              THEN BEGIN
                IF (((GenJnlLine1."Document Type" = GenJnlLine1."Document Type"::Invoice) OR
                     (GenJnlLine1."Document Type" = GenJnlLine1."Document Type"::"Credit Memo")) AND
                    ((GenJnlLine1."Account Type" = GenJnlLine1."Account Type"::"G/L Account") OR
                     (GenJnlLine1."Bal. Account Type" = GenJnlLine1."Bal. Account Type"::"G/L Account")))
                THEN
                  IF WHTPostingSetup.GET(GenJnlLine1."WHT Business Posting Group",GenJnlLine1."WHT Product Posting Group") THEN
                    IF (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Invoice) OR
                       (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest)
                    THEN BEGIN
                      IF GenJnlLine1."WHT Absorb Base" <> 0 THEN
                        WHTAmountLCY := -ROUND(GenJnlLine1."WHT Absorb Base" * WHTPostingSetup."WHT %" / 100)
                      ELSE
                        WHTAmountLCY := ROUND(GenJnlLine1.Amount * WHTPostingSetup."WHT %" / 100);
                      WHTAmount := WHTAmountLCY;
                    END;
              END ELSE
                IF (((GenJnlLine1."Document Type" = GenJnlLine1."Document Type"::Invoice) OR
                     (GenJnlLine1."Document Type" = GenJnlLine1."Document Type"::"Credit Memo")) AND
                    ((GenJnlLine1."Account Type" = GenJnlLine1."Account Type"::"G/L Account") OR
                     (GenJnlLine1."Bal. Account Type" = GenJnlLine1."Bal. Account Type"::"G/L Account")))
                THEN
                  IF WHTPostingSetup.GET(GenJnlLine1."WHT Business Posting Group",GenJnlLine1."WHT Product Posting Group") THEN BEGIN
                    IF (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest) THEN BEGIN
                      GenJnlLine1.RESET;
                      GenJnlLine1.COPY(GenJnlLine);
                      IF GenJnlLine1.FINDFIRST THEN
                        WHTManagement.CheckApplicationGenSalesWHT(GenJnlLine1);
                      WHTAmountLCY := ROUND(WHTManagement.CalcCustExtraWHTForEarliest(GenJnlLine1));
                      WHTAmount := WHTAmountLCY;
                    END;

                    IF (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Invoice) THEN BEGIN
                      GenJnlLine1.RESET;
                      GenJnlLine1.COPY(GenJnlLine);
                      IF GenJnlLine1.FINDFIRST THEN
                        WHTManagement.CheckApplicationGenSalesWHT(GenJnlLine1);
                      IF GenJnlLine1."WHT Absorb Base" <> 0 THEN
                        WHTAmountLCY := -ROUND(GenJnlLine1."WHT Absorb Base" * WHTPostingSetup."WHT %" / 100)
                      ELSE
                        WHTAmountLCY := ROUND(GenJnlLine1.Amount * WHTPostingSetup."WHT %" / 100);
                      WHTAmount := WHTAmountLCY;
                    END;
                  END;

          IF GenJnlLine1."Document Type" = GenJnlLine1."Document Type"::Invoice THEN BEGIN
            IF "Currency Code" <> '' THEN
              WHTAmountLCY :=
                ABS(ROUND(
                    CurrExchRate.ExchangeAmtFCYToLCY(
                      "Posting Date","Currency Code",WHTAmountLCY,
                      CurrExchRate.ExchangeRate("Posting Date","Currency Code"))))
            ELSE
              WHTAmountLCY := ABS(WHTAmountLCY);
            WHTAmount := ABS(WHTAmount);
          END ELSE BEGIN
            IF "Currency Code" <> '' THEN
              WHTAmountLCY :=
                -ABS(ROUND(
                    CurrExchRate.ExchangeAmtFCYToLCY(
                      "Posting Date","Currency Code",WHTAmountLCY,
                      CurrExchRate.ExchangeRate("Posting Date","Currency Code"))))
            ELSE
              WHTAmountLCY := -ABS(WHTAmountLCY);
            WHTAmount := -ABS(WHTAmount);
          END;
        END;
      END;
    END;

    PROCEDURE PostCustWHT@1240012(GenJnlLine@1240000 : Record 81;CustLedgEntry@1240001 : Record 21;WHTPostingSetup@1240003 : Record 50003;WHTAmountLCY@1240002 : Decimal);
    VAR
      TempGenJnlTemp@1240004 : Record 80;
      SourceCodeSetup@1250000 : Record 242;
    BEGIN
      WITH GenJnlLine DO BEGIN
        IF WHTAmountLCY <> 0 THEN
          IF ((GenJnlLine1."Document Type" = GenJnlLine1."Document Type"::Invoice) OR
              (GenJnlLine1."Document Type" = GenJnlLine1."Document Type"::"Credit Memo"))
          THEN
            IF (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Invoice) OR
               (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest)
            THEN BEGIN
              WHTPostingSetup.TESTFIELD("Prepaid WHT Account Code");
              CreateGLEntry(
                GenJnlLine,WHTPostingSetup."Prepaid WHT Account Code",WHTAmountLCY,WHTAmountLCY,TRUE);
            END;

        SourceCodeSetup.GET;
        IF "Source Code" <> SourceCodeSetup."Financially Voided Check" THEN BEGIN
          GenJnlLine1.RESET;
          GenJnlLine1.COPY(GenJnlLine);
          IF GLSetup."Enable WHT" THEN
            IF IsAboveWHTMinInvoiceAmount(GenJnlLine) THEN BEGIN
              IF NOT "Skip WHT" THEN BEGIN
                IF ("Applies-to Doc. No." <> '') OR ("Applies-to ID" <> '') THEN BEGIN
                  KeepWHTEntryNo := NextWHTEntryNo;
                  CASE "Document Type" OF
                    "Document Type"::Payment:
                      IF GLSetup."Manual Sales WHT Calc." THEN BEGIN
                        IF "WHT Payment" THEN
                          NextWHTEntryNo := WHTManagement.ApplyManualCustInvoiceWHT(CustLedgEntry,GenJnlLine1);
                      END ELSE
                        BEGIN
                          IF WHTPostingSetup.GET(
                               "WHT Business Posting Group",
                               "WHT Product Posting Group")
                          THEN BEGIN
                            IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Payment THEN BEGIN
                            NextWHTEntryNo := WHTManagement.ApplyCustInvoiceWHT(CustLedgEntry,GenJnlLine1);
                            IF NextWHTEntryNo <> -1 THEN
                              HadWHTEntryNo := TRUE
                            ELSE
                              NextWHTEntryNo := KeepWHTEntryNo;
                          END;

                          IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest THEN BEGIN
                            NextWHTEntryNo := WHTManagement.InsertCustJournalWHT(GenJnlLine);
                            IF WHTEntry.GET(NextWHTEntryNo - 1) THEN BEGIN
                              WHTEntry."Transaction No." := NextTransactionNo;
                              WHTEntry.MODIFY;
                            END;
                          END;
                        END;
                      END;
                    "Document Type"::Invoice:
                      BEGIN
                        IF ProcessSourceCode("Source Code",SourceCodeSetup) THEN
                          IF WHTPostingSetup.GET("WHT Business Posting Group","WHT Product Posting Group") THEN
                            IF (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Invoice) OR
                               (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest)
                            THEN BEGIN
                              GenJnlLine2.RESET;
                              GenJnlLine2.COPY(GenJnlLine);
                              GenJnlLine2.Amount := ABS(GenJnlLine2.Amount);
                              GenJnlLine2."WHT Absorb Base" := ABS(GenJnlLine2."WHT Absorb Base");
                              NextWHTEntryNo := WHTManagement.InsertCustJournalWHT(GenJnlLine2);
                              IF WHTEntry.GET(NextWHTEntryNo - 1) THEN BEGIN
                                WHTEntry."Transaction No." := NextTransactionNo;
                                WHTEntry.MODIFY;
                              END;
                            END;
                        IF SourceCodeSetup.Sales = "Source Code" THEN
                          UpdateWHTEntryTransaction("Document No.");
                      END;
                    "Document Type"::"Credit Memo":
                      BEGIN
                        IF ProcessSourceCode("Source Code",SourceCodeSetup) THEN
                          IF WHTPostingSetup.GET("WHT Business Posting Group","WHT Product Posting Group") THEN
                            IF (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Invoice) OR
                               (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest)
                            THEN BEGIN
                              GenJnlLine2.RESET;
                              GenJnlLine2.COPY(GenJnlLine);
                              GenJnlLine2.Amount := ABS(GenJnlLine2.Amount);
                              GenJnlLine2."WHT Absorb Base" := ABS(GenJnlLine2."WHT Absorb Base");
                              NextWHTEntryNo := WHTManagement.InsertCustJournalWHT(GenJnlLine2);
                              IF WHTEntry.GET(NextWHTEntryNo - 1) THEN BEGIN
                                WHTEntry."Transaction No." := NextTransactionNo;
                                WHTEntry.MODIFY;
                              END;
                            END;

                        IF SourceCodeSetup.Sales = "Source Code" THEN
                          UpdateWHTEntryTransaction("Document No.");
                      END;
                    "Document Type"::Refund:
                      IF GLSetup."Manual Sales WHT Calc." THEN BEGIN
                        IF "WHT Payment" THEN
                          NextWHTEntryNo := WHTManagement.ApplyManualCustInvoiceWHT(CustLedgEntry,GenJnlLine1);
                      END ELSE
                        IF WHTPostingSetup.GET("WHT Business Posting Group","WHT Product Posting Group") THEN BEGIN
                          IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Payment THEN BEGIN
                            NextWHTEntryNo := WHTManagement.ApplyCustInvoiceWHT(CustLedgEntry,GenJnlLine1);
                            IF NextWHTEntryNo <> -1 THEN
                              HadWHTEntryNo := TRUE
                            ELSE
                              NextWHTEntryNo := KeepWHTEntryNo;
                          END;

                          IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest THEN BEGIN
                            NextWHTEntryNo := WHTManagement.InsertCustJournalWHT(GenJnlLine);
                            IF WHTEntry.GET(NextWHTEntryNo - 1) THEN BEGIN
                              WHTEntry."Transaction No." := NextTransactionNo;
                              WHTEntry.MODIFY;
                            END;
                          END;
                        END;
                  END;
                  TempGenJnlTemp.SETRANGE(Type,TempGenJnlTemp.Type::Sales);
                  IF TempGenJnlTemp.FINDFIRST THEN
                    IF "Journal Template Name" = TempGenJnlTemp.Name THEN BEGIN
                      WHTEntry.RESET;
                      WHTEntry.SETRANGE("Document Type",WHTEntry."Document Type"::Payment);
                      WHTEntry.SETRANGE("Document No.","Document No.");
                      WHTEntry.SETRANGE("Bill-to/Pay-to No.","Account No.");
                      WHTEntry.SETFILTER("Remaining Unrealized Amount",'<>0');
                      IF WHTEntry.FIND('-') THEN
                        REPEAT
                          WHTPostingSetup.GET(WHTEntry."WHT Bus. Posting Group",WHTEntry."WHT Prod. Posting Group");
                          WHTPostingSetup.TESTFIELD("Prepaid WHT Account Code");
                          CreateGLEntry(
                            GenJnlLine,WHTPostingSetup."Prepaid WHT Account Code",-WHTEntry."Amount (LCY)","Source Currency Amount",TRUE);
                        UNTIL WHTEntry.NEXT = 0;
                    END;
                END ELSE
                  KeepWHTEntryNo := NextWHTEntryNo;
                CASE "Document Type" OF
                  "Document Type"::Invoice:
                    BEGIN
                      IF ProcessSourceCode("Source Code",SourceCodeSetup) THEN BEGIN
                        GenJnlLine2.RESET;
                        GenJnlLine2.COPY(GenJnlLine);
                        GenJnlLine2.Amount := -ABS(GenJnlLine2.Amount);
                        GenJnlLine2."WHT Absorb Base" := -ABS(GenJnlLine2."WHT Absorb Base");
                        NextWHTEntryNo := WHTManagement.InsertCustJournalWHT(GenJnlLine2);
                      END;
                      UpdateWHTEntryTransaction("Document No.");
                    END;
                  "Document Type"::"Credit Memo":
                    BEGIN
                      IF ProcessSourceCode("Source Code",SourceCodeSetup) THEN BEGIN
                        GenJnlLine2.RESET;
                        GenJnlLine2.COPY(GenJnlLine);
                        GenJnlLine2.Amount := ABS(GenJnlLine2.Amount);
                        GenJnlLine2."WHT Absorb Base" := ABS(GenJnlLine2."WHT Absorb Base");
                        NextWHTEntryNo := WHTManagement.InsertCustJournalWHT(GenJnlLine2);
                      END;
                      UpdateWHTEntryTransaction("Document No.");
                    END;
                  "Document Type"::Payment:
                    IF WHTPostingSetup.GET("WHT Business Posting Group","WHT Product Posting Group") THEN
                      IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest THEN BEGIN
                        GenJnlLine2.RESET;
                        GenJnlLine2.COPY(GenJnlLine);
                        GenJnlLine2.Amount := -ABS(GenJnlLine2.Amount);
                        GenJnlLine2."WHT Absorb Base" := -ABS(GenJnlLine2."WHT Absorb Base");
                        NextWHTEntryNo := WHTManagement.InsertCustJournalWHT(GenJnlLine2);
                        IF WHTEntry.GET(NextWHTEntryNo - 1) THEN BEGIN
                          WHTEntry."Transaction No." := NextTransactionNo;
                          WHTEntry.MODIFY;
                        END;
                      END ELSE
                        IF GLSetup."Manual Sales WHT Calc." THEN
                          IF "WHT Payment" THEN BEGIN
                            GenJnlLine2.RESET;
                            GenJnlLine2.COPY(GenJnlLine);
                            GenJnlLine2.Amount := -ABS(GenJnlLine2.Amount);
                            GenJnlLine2."WHT Absorb Base" := -ABS(GenJnlLine2."WHT Absorb Base");
                            NextWHTEntryNo := WHTManagement.InsertCustJournalWHT(GenJnlLine2);
                            InsertWHTPaymentGL(GenJnlLine2,GenJnlLine,CustLedgEntry."Entry No.");
                            IF WHTEntry.GET(NextWHTEntryNo - 1) THEN BEGIN
                              WHTEntry."Transaction No." := NextTransactionNo;
                              WHTEntry.MODIFY;
                            END;
                          END;
                  "Document Type"::Refund:
                    IF WHTPostingSetup.GET("WHT Business Posting Group","WHT Product Posting Group") THEN
                      IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest THEN BEGIN
                        GenJnlLine2.RESET;
                        GenJnlLine2.COPY(GenJnlLine);
                        GenJnlLine2.Amount := -ABS(GenJnlLine2.Amount);
                        GenJnlLine2."WHT Absorb Base" := -ABS(GenJnlLine2."WHT Absorb Base");
                        NextWHTEntryNo := WHTManagement.InsertCustJournalWHT(GenJnlLine2);
                        IF WHTEntry.GET(NextWHTEntryNo - 1) THEN BEGIN
                          WHTEntry."Transaction No." := NextTransactionNo;
                          WHTEntry.MODIFY;
                        END;
                      END;
                END;
              END
            END;
          IF NextWHTEntryNo = 0 THEN
            NextWHTEntryNo := KeepWHTEntryNo;

          IF "Applies-to ID" <> '' THEN BEGIN
            CustLedgEntry.RESET;
            CustLedgEntry.SETCURRENTKEY("Customer No.","Applies-to ID",Open,Positive,"Due Date");
            IF "Account Type" = "Account Type"::Customer THEN
              CustLedgEntry.SETRANGE("Customer No.","Account No.");
            CustLedgEntry.SETRANGE("Applies-to ID","Applies-to ID");
            CustLedgEntry.SETRANGE("Amount to Apply",0);
            IF CustLedgEntry.FINDFIRST THEN
              CustLedgEntry.MODIFYALL("Applies-to ID",'');
          END;
        END;
      END;
    END;

    PROCEDURE CalcVendWHT@1240014(GenJnlLine@1240000 : Record 81;VAR WHTPostingSetup@1240001 : Record 50003;VAR WHTAmountLCY@1240002 : Decimal;VAR WHTAmount@1240003 : Decimal);
    VAR
      SourceCodeSetup@1250000 : Record 242;
    BEGIN
      WITH GenJnlLine DO BEGIN
        WHTAmountLCY := 0;
        SourceCodeSetup.GET;
        IF ProcessSourceCode("Source Code",SourceCodeSetup) THEN BEGIN
          GenJnlLine1.RESET;
          GenJnlLine1.COPY(GenJnlLine);
          IF GLSetup."Enable WHT" THEN
            IF NOT GenJnlLine1."Skip WHT" THEN
              IF (GenJnlLine1."Applies-to Doc. No." = '') AND
                 (GenJnlLine1."Applies-to ID" = '')
              THEN BEGIN
                IF (((GenJnlLine1."Document Type" = GenJnlLine1."Document Type"::Invoice) OR
                     (GenJnlLine1."Document Type" = GenJnlLine1."Document Type"::"Credit Memo")) AND
                    ((GenJnlLine1."Account Type" = GenJnlLine1."Account Type"::"G/L Account") OR
                     (GenJnlLine1."Bal. Account Type" = GenJnlLine1."Bal. Account Type"::"G/L Account")))
                THEN
                  IF WHTPostingSetup.GET(GenJnlLine1."WHT Business Posting Group",GenJnlLine1."WHT Product Posting Group") THEN
                    IF (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Invoice) OR
                       (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest)
                    THEN BEGIN
                      IF GenJnlLine1."WHT Absorb Base" <> 0 THEN
                        WHTAmountLCY :=
                          -ROUND(
                            CurrExchRate.ExchangeAmtFCYToLCY(
                              "Posting Date","Currency Code",
                              ROUND(GenJnlLine1."WHT Absorb Base" * WHTPostingSetup."WHT %" / 100),"Currency Factor"))
                      ELSE
                        WHTAmountLCY :=
                          ROUND(
                            CurrExchRate.ExchangeAmtFCYToLCY(
                              "Posting Date","Currency Code",
                              ROUND(GenJnlLine1.Amount * WHTPostingSetup."WHT %" / 100),"Currency Factor"));
                      WHTAmount := CalcDtldCVLedgEntryAmount(GenJnlLine,WHTPostingSetup."WHT %");
                    END;
              END ELSE
                IF (((GenJnlLine1."Document Type" = GenJnlLine1."Document Type"::Invoice) OR
                     (GenJnlLine1."Document Type" = GenJnlLine1."Document Type"::"Credit Memo")) AND
                    ((GenJnlLine1."Account Type" = GenJnlLine1."Account Type"::"G/L Account") OR
                     (GenJnlLine1."Bal. Account Type" = GenJnlLine1."Bal. Account Type"::"G/L Account")))
                THEN
                  IF WHTPostingSetup.GET(GenJnlLine1."WHT Business Posting Group",GenJnlLine1."WHT Product Posting Group") THEN BEGIN
                    IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest THEN BEGIN
                      GenJnlLine1.RESET;
                      GenJnlLine1.COPY(GenJnlLine);
                      IF GenJnlLine1.FINDFIRST THEN
                        WHTManagement.CheckApplicationGenPurchWHT(GenJnlLine1);
                      WHTAmountLCY := ROUND(WHTManagement.CalcVendExtraWHTForEarliest(GenJnlLine1));
                      WHTAmount := WHTAmountLCY;
                    END;

                    IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Invoice THEN BEGIN
                      GenJnlLine1.RESET;
                      GenJnlLine1.COPY(GenJnlLine);
                      IF GenJnlLine1.FINDFIRST THEN
                        WHTManagement.CheckApplicationGenPurchWHT(GenJnlLine1);
                      IF GenJnlLine1."WHT Absorb Base" <> 0 THEN
                        WHTAmountLCY :=
                          -ROUND(
                            CurrExchRate.ExchangeAmtFCYToLCY(
                              "Posting Date","Currency Code",
                              ROUND(GenJnlLine1."WHT Absorb Base" * WHTPostingSetup."WHT %" / 100),"Currency Factor"))
                      ELSE
                        WHTAmountLCY :=
                          ROUND(
                            CurrExchRate.ExchangeAmtFCYToLCY(
                              "Posting Date","Currency Code",
                              ROUND(GenJnlLine1.Amount * WHTPostingSetup."WHT %" / 100),"Currency Factor"));
                      WHTAmount := CalcDtldCVLedgEntryAmount(GenJnlLine,WHTPostingSetup."WHT %");
                    END;
                  END;
          IF GenJnlLine1."Document Type" = GenJnlLine1."Document Type"::Invoice THEN BEGIN
            WHTAmountLCY := -ABS(WHTAmountLCY);
            WHTAmount := -ABS(WHTAmount);
          END ELSE BEGIN
            WHTAmountLCY := ABS(WHTAmountLCY);
            WHTAmount := ABS(WHTAmount);
          END;
        END;

        IF WHTPostingSetup.GET("WHT Business Posting Group","WHT Product Posting Group") THEN
          IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest THEN
            IF Amount < WHTPostingSetup."WHT Minimum Invoice Amount" THEN
              WHTAmountLCY := 0;
      END;
    END;

    PROCEDURE PostVendWHT@1240015(GenJnlLine@1240000 : Record 81;VendLedgEntry@1240005 : Record 25;WHTPostingSetup@1240001 : Record 50003;WHTAmountLCY@1240002 : Decimal;WHTAmount@1240003 : Decimal);
    VAR
      TempGenJnlTemp@1240004 : Record 80;
      SourceCodeSetup@1250000 : Record 242;
    BEGIN
      WITH GenJnlLine DO BEGIN
        IF WHTAmountLCY <> 0 THEN
          IF (GenJnlLine1."Document Type" = GenJnlLine1."Document Type"::Invoice) OR
             (GenJnlLine1."Document Type" = GenJnlLine1."Document Type"::"Credit Memo")
          THEN
            IF (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Invoice) OR
               (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest)
            THEN
              CreateGLEntry(
                GenJnlLine,WHTPostingSetup."Payable WHT Account Code",WHTAmountLCY,WHTAmountLCY,TRUE);

        SourceCodeSetup.GET;
        IF "Source Code" <> SourceCodeSetup."Financially Voided Check" THEN BEGIN
          GenJnlLine1.RESET;
          IF GLSetup."Enable WHT" THEN
            IF IsAboveWHTMinInvoiceAmount(GenJnlLine) THEN BEGIN
              IF ("Applies-to Doc. No." <> '') OR ("Applies-to ID" <> '') THEN BEGIN
                GenJnlLine1.COPY(GenJnlLine);
                IF GenJnlLine1."Interest Amount" <> 0 THEN
                  GenJnlLine1.VALIDATE(Amount,GenJnlLine1.Amount - GenJnlLine1."Interest Amount");
                IF NOT "Skip WHT" THEN BEGIN
                  KeepWHTEntryNo := NextWHTEntryNo;
                  CASE "Document Type" OF
                    "Document Type"::"Credit Memo":
                      BEGIN
                        IF ProcessSourceCode("Source Code",SourceCodeSetup) THEN
                          IF WHTPostingSetup.GET("WHT Business Posting Group","WHT Product Posting Group") THEN
                            IF (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Invoice) OR
                               (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest)
                            THEN BEGIN
                              GenJnlLine2.RESET;
                              GenJnlLine2.COPY(GenJnlLine);
                              GenJnlLine2.Amount := -ABS(GenJnlLine2.Amount);
                              GenJnlLine2."WHT Absorb Base" := -ABS(GenJnlLine2."WHT Absorb Base");
                              NextWHTEntryNo := WHTManagement.InsertVendJournalWHT(GenJnlLine2);
                              IF WHTEntry.GET(NextWHTEntryNo - 1) THEN BEGIN
                                WHTEntry."Transaction No." := NextTransactionNo;
                                WHTEntry.MODIFY;
                              END;
                            END;
                        IF SourceCodeSetup.Purchases = "Source Code" THEN
                          UpdateWHTEntryTransaction("Document No.");
                      END;
                    "Document Type"::Payment:
                      IF WHTPostingSetup.GET("WHT Business Posting Group","WHT Product Posting Group") THEN BEGIN
                        IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Payment THEN BEGIN
                          NextWHTEntryNo := WHTManagement.ApplyVendInvoiceWHT(VendLedgEntry,GenJnlLine1);
                          IF NextWHTEntryNo <> -1 THEN
                            HadWHTEntryNo := TRUE
                          ELSE
                            NextWHTEntryNo := KeepWHTEntryNo;
                        END;
                        IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest THEN BEGIN
                          NextWHTEntryNo := WHTManagement.InsertVendJournalWHT(GenJnlLine);
                          IF WHTEntry.GET(NextWHTEntryNo - 1) THEN BEGIN
                            WHTEntry."Transaction No." := NextTransactionNo;
                            WHTEntry.MODIFY;
                          END;
                        END;
                      END;
                    "Document Type"::Refund:
                      IF GLSetup."Manual Sales WHT Calc." THEN BEGIN
                        IF "WHT Payment" THEN
                          NextWHTEntryNo := WHTManagement.ProcessManualReceipt(
                              GenJnlLine1,VendLedgEntry."Transaction No.",VendLedgEntry."Entry No.",0);
                      END ELSE
                        IF WHTPostingSetup.GET("WHT Business Posting Group","WHT Product Posting Group") THEN BEGIN
                          IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Payment THEN BEGIN
                            NextWHTEntryNo := WHTManagement.ApplyVendInvoiceWHT(VendLedgEntry,GenJnlLine1);
                            IF NextWHTEntryNo <> -1 THEN
                              HadWHTEntryNo := TRUE
                            ELSE
                              NextWHTEntryNo := KeepWHTEntryNo;
                          END;
                          IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest THEN BEGIN
                            NextWHTEntryNo := WHTManagement.InsertVendJournalWHT(GenJnlLine);
                            IF WHTEntry.GET(NextWHTEntryNo - 1) THEN BEGIN
                              WHTEntry."Transaction No." := NextTransactionNo;
                              WHTEntry.MODIFY;
                            END;
                          END;
                        END;
                    "Document Type"::Invoice:
                      BEGIN
                        IF ProcessSourceCode("Source Code",SourceCodeSetup) THEN
                          IF WHTPostingSetup.GET("WHT Business Posting Group","WHT Product Posting Group") THEN
                            IF (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Invoice) OR
                               (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest)
                            THEN BEGIN
                              GenJnlLine2.RESET;
                              GenJnlLine2.COPY(GenJnlLine);
                              GenJnlLine2.Amount := -ABS(GenJnlLine2.Amount);
                              GenJnlLine2."WHT Absorb Base" := -ABS(GenJnlLine2."WHT Absorb Base");
                              NextWHTEntryNo := WHTManagement.InsertVendJournalWHT(GenJnlLine2);
                              IF WHTEntry.GET(NextWHTEntryNo - 1) THEN BEGIN
                                WHTEntry."Transaction No." := NextTransactionNo;
                                WHTEntry.MODIFY;
                              END;
                            END;
                        IF SourceCodeSetup.Purchases = "Source Code" THEN
                          UpdateWHTEntryTransaction("Document No.");
                      END;
                  END;
                END;

                TempGenJnlTemp.SETRANGE(Type,TempGenJnlTemp.Type::Purchases);
                IF TempGenJnlTemp.FINDFIRST THEN
                  IF "Journal Template Name" = TempGenJnlTemp.Name THEN BEGIN
                    WHTEntry.RESET;
                    WHTEntry.SETRANGE("Document Type",WHTEntry."Document Type"::Payment);
                    WHTEntry.SETRANGE("Document No.","Document No.");
                    WHTEntry.SETRANGE("Bill-to/Pay-to No.","Account No.");
                    IF WHTEntry.FIND('-') THEN
                      REPEAT
                        WHTPostingSetup.GET(WHTEntry."WHT Bus. Posting Group",WHTEntry."WHT Prod. Posting Group");
                        WHTPostingSetup.TESTFIELD("Payable WHT Account Code");
                        CreateGLEntry(
                          GenJnlLine,WHTPostingSetup."Payable WHT Account Code",-WHTEntry."Amount (LCY)","Source Currency Amount",TRUE);
                      UNTIL WHTEntry.NEXT = 0;
                  END;
              END ELSE BEGIN
                KeepWHTEntryNo := NextWHTEntryNo;
                CASE "Document Type" OF
                  "Document Type"::Invoice,
                  "Document Type"::"Credit Memo":
                    BEGIN
                      IF ProcessSourceCode("Source Code",SourceCodeSetup) THEN BEGIN
                        GenJnlLine2.RESET;
                        GenJnlLine2.COPY(GenJnlLine);
                        GenJnlLine2."WHT Absorb Base" := -ABS(GenJnlLine2."WHT Absorb Base");
                        NextWHTEntryNo := WHTManagement.InsertVendJournalWHT(GenJnlLine2);
                      END;
                      UpdateWHTEntryTransaction("Document No.");
                    END;
                  "Document Type"::Payment,
                  "Document Type"::Refund:
                    IF WHTPostingSetup.GET("WHT Business Posting Group","WHT Product Posting Group") THEN
                      IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest THEN BEGIN
                        NextWHTEntryNo := WHTManagement.InsertVendJournalWHT(GenJnlLine);
                        IF WHTEntry.GET(NextWHTEntryNo - 1) THEN BEGIN
                          WHTEntry."Transaction No." := NextTransactionNo;
                          WHTEntry.MODIFY;
                        END;
                      END;
                  "Document Type"::" ":
                    NextWHTEntryNo := KeepWHTEntryNo;
                END;
              END;
              IF NextWHTEntryNo = 0 THEN
                NextWHTEntryNo := KeepWHTEntryNo;

            END;

          IF "Applies-to ID" <> '' THEN BEGIN
            VendLedgEntry.RESET;
            VendLedgEntry.SETCURRENTKEY("Vendor No.","Applies-to ID",Open,Positive,"Due Date");
            VendLedgEntry.SETRANGE("Vendor No.","Account No.");
            VendLedgEntry.SETRANGE("Applies-to ID","Applies-to ID");
            VendLedgEntry.SETRANGE("Amount to Apply",0);
            VendLedgEntry.MODIFYALL("Applies-to ID",'');
          END;
        END;
      END;
    END;

    PROCEDURE CalcBankAccWHT@1240016(GenJnlLine@1240003 : Record 81;VAR WHTPostingSetup@1240002 : Record 50003;VAR WHTAmountLCY@1240001 : Decimal;VAR WHTAmount@1240000 : Decimal);
    VAR
      CheckLedgEntry@1240004 : Record 272;
    BEGIN
      WITH GenJnlLine DO BEGIN
        WHTAmountLCY := 0;
        IF NOT GLSetup."Enable WHT" OR "Skip WHT" THEN
          EXIT;

        IF ("Applies-to ID" = '') AND ("Applies-to Doc. No." = '') THEN BEGIN
          IF ("Document Type" = "Document Type"::Payment) OR ("Document Type" = "Document Type"::Refund) THEN
            IF WHTPostingSetup.GET("WHT Business Posting Group","WHT Product Posting Group") THEN BEGIN
              IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest THEN
                IF IsCustAcc(GenJnlLine) THEN BEGIN
                  IF "WHT Absorb Base" <> 0 THEN
                    WHTAmountLCY := -ABS(ROUND("WHT Absorb Base" * WHTPostingSetup."WHT %" / 100))
                  ELSE
                    WHTAmountLCY := -ABS(ROUND(Amount * WHTPostingSetup."WHT %" / 100));
                  IF "Document Type" = "Document Type"::Refund THEN
                    WHTAmountLCY := ABS(WHTAmountLCY);
                END ELSE
                  IF IsVendAcc(GenJnlLine) THEN BEGIN
                    IF "WHT Absorb Base" <> 0 THEN
                      WHTAmountLCY := ABS(ROUND("WHT Absorb Base" * WHTPostingSetup."WHT %" / 100))
                    ELSE
                      WHTAmountLCY := ABS(ROUND(Amount * WHTPostingSetup."WHT %" / 100));
                    IF "Document Type" = "Document Type"::Refund THEN
                      WHTAmountLCY := -ABS(WHTAmountLCY);
                  END;
              IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Payment THEN
                ERROR(Text016,WHTPostingSetup."Realized WHT Type" );
            END;
          IF ("Currency Code" <> '') AND (CurrFactor <> 0) THEN
            IF (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Payment) AND
               (NOT GLSetup."Manual Sales WHT Calc.")
            THEN
              WHTAmountLCY :=
                CurrExchRate.ExchangeAmtFCYToLCY(
                  "Document Date","Currency Code",
                  ABS(WHTManagement.WHTAmountJournal(GenJnlLine,TRUE)),CurrFactor);
        END ELSE
          IF ("Applies-to ID" <> '') OR ("Applies-to Doc. No." <> '') THEN BEGIN
            GenJnlLine1.RESET;
            GenJnlLine1.COPY(GenJnlLine);
            IF "Applies-to Doc. No." <> '' THEN
              GenJnlLine1.SETRANGE("Applies-to Doc. No.","Applies-to Doc. No.")
            ELSE
              GenJnlLine1.SETRANGE("Applies-to ID","Applies-to ID");

            GenJnlLine1.SETRANGE("Account Type","Account Type"::Vendor);
            IF ("Account Type" = "Account Type"::Vendor) OR
               ("Bal. Account Type" = "Bal. Account Type"::Vendor) OR
               GenJnlLine1.FINDFIRST
            THEN BEGIN
              CurrFactor :=
                CurrExchRate.ExchangeRate(
                  "Document Date","Currency Code");
              IF GenJnlLine1."Interest Amount" <> 0 THEN
                GenJnlLine1.VALIDATE(Amount,GenJnlLine1.Amount - GenJnlLine1."Interest Amount");

              IF (GenJnlLine1."Document Type" = GenJnlLine1."Document Type"::Payment) OR
                 (GenJnlLine1."Document Type" = GenJnlLine1."Document Type"::Refund)
              THEN
                IF WHTPostingSetup.GET(
                     GenJnlLine1."WHT Business Posting Group",
                     GenJnlLine1."WHT Product Posting Group")
                THEN BEGIN
                  IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest THEN BEGIN
                    IF GenJnlLine1.FINDFIRST THEN
                      WHTManagement.CheckApplicationGenPurchWHT(GenJnlLine1);
                    WHTAmountLCY :=
                      CurrExchRate.ExchangeAmtFCYToLCY(
                        "Document Date","Currency Code",
                        ABS(WHTManagement.CalcVendExtraWHTForEarliest(GenJnlLine1)),CurrFactor);
                  END;

                  IF (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Payment) AND
                     (NOT GLSetup."Manual Sales WHT Calc.")
                  THEN BEGIN
                    WHTAmount := ABS(WHTManagement.WHTAmountJournal(GenJnlLine1,TRUE));
                    WHTAmountLCY :=
                      CurrExchRate.ExchangeAmtFCYToLCY(
                        GenJnlLine1."Document Date",
                        GenJnlLine1."Currency Code",
                        WHTAmount,CurrFactor);
                  END;
                END;

              IF "Document Type" = "Document Type"::Refund THEN
                WHTAmountLCY := -ABS(WHTAmountLCY);
            END;

            IF IsCustAcc(GenJnlLine) THEN BEGIN
              CurrFactor :=
                CurrExchRate.ExchangeRate(
                  GenJnlLine1."Document Date","Currency Code");
              IF "Bal. Account Type" = "Bal. Account Type"::Customer THEN
                GenJnlLine1.VALIDATE(Amount,-GenJnlLine1.Amount);

              IF IsPaymentOrRefund(GenJnlLine1) THEN
                IF WHTPostingSetup.GET(GenJnlLine1."WHT Business Posting Group",GenJnlLine1."WHT Product Posting Group") THEN BEGIN
                  IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest THEN BEGIN
                    IF GenJnlLine1.FINDFIRST THEN
                      WHTManagement.CheckApplicationGenSalesWHT(GenJnlLine1);
                    WHTAmountLCY :=
                      -CurrExchRate.ExchangeAmtFCYToLCY(
                        GenJnlLine1."Document Date",GenJnlLine1."Currency Code",
                        ABS(WHTManagement.CalcCustExtraWHTForEarliest(GenJnlLine1)),CurrFactor);
                  END;

                  IF (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Payment) AND
                     (NOT GLSetup."Manual Sales WHT Calc.")
                  THEN BEGIN
                    WHTAmount := -ABS(WHTManagement.ApplyCustCalcWHT(GenJnlLine1));
                    WHTAmountLCY :=
                      -CurrExchRate.ExchangeAmtFCYToLCY(
                        GenJnlLine1."Document Date",
                        GenJnlLine1."Currency Code",
                        ABS(WHTAmount),CurrFactor);
                  END;
                END;

              IF "Document Type" = "Document Type"::Refund THEN
                WHTAmountLCY := ABS(WHTAmountLCY);
            END;
            WHTAmountLCY := ROUND(WHTAmountLCY);

            IF GLSetup."Round Amount for WHT Calc" THEN
              WHTAmountLCY := ROUND(WHTAmountLCY,1,'<');
          END ELSE
            IF "Bank Payment Type" =
               "Bank Payment Type"::"Computer Check"
            THEN BEGIN
              TESTFIELD("Check Printed",TRUE);
              CheckLedgEntry.LOCKTABLE;
              CheckLedgEntry.RESET;
              CheckLedgEntry.SETCURRENTKEY("Bank Account No.","Entry Status","Check No.");
              CheckLedgEntry.SETRANGE("Bank Account No.","Account No.");
              CheckLedgEntry.SETRANGE("Entry Status",CheckLedgEntry."Entry Status"::Printed);
              CheckLedgEntry.SETRANGE("Check No.","Document No.");
              IF CheckLedgEntry.FIND('-') THEN
                WHTAmountLCY := ABS(CheckLedgEntry."WHT Amount")
            END;

        IF WHTPostingSetup.GET("WHT Business Posting Group","WHT Product Posting Group") THEN
          IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest THEN
            IF ABS(Amount) < WHTPostingSetup."WHT Minimum Invoice Amount" THEN
              WHTAmountLCY := 0;
      END;
    END;

    PROCEDURE CalcBankAccWHT2@1240017(GenJnlLine@1240003 : Record 81;VAR WHTPostingSetup@1240002 : Record 50003;VAR WHTAmountLCY@1240001 : Decimal;VAR WHTAmount@1240000 : Decimal);
    VAR
      CheckLedgEntry@1240004 : Record 272;
    BEGIN
      WHTAmountLCY := 0;
      WITH GenJnlLine DO BEGIN
        IF NOT GLSetup."Enable WHT" OR "Skip WHT" THEN
          EXIT;

        IF ("Applies-to ID" = '') AND ("Applies-to Doc. No." = '') THEN BEGIN
          IF IsPaymentOrRefund(GenJnlLine) THEN
            IF WHTPostingSetup.GET("WHT Business Posting Group","WHT Product Posting Group") THEN
              IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest THEN
                IF IsCustAcc(GenJnlLine) THEN BEGIN
                  IF "WHT Absorb Base" <> 0 THEN
                    WHTAmountLCY := -ABS(ROUND("WHT Absorb Base" * WHTPostingSetup."WHT %" / 100))
                  ELSE
                    WHTAmountLCY := -ABS(ROUND(Amount * WHTPostingSetup."WHT %" / 100));
                  IF "Document Type" = "Document Type"::Refund THEN
                    WHTAmountLCY := ABS(WHTAmountLCY);
                END ELSE
                  IF IsVendAcc(GenJnlLine) THEN BEGIN
                    IF "WHT Absorb Base" <> 0 THEN
                      WHTAmountLCY :=
                        ABS(ROUND("WHT Absorb Base" * WHTPostingSetup."WHT %" / 100))
                    ELSE
                      WHTAmountLCY :=
                        ABS(ROUND(Amount * WHTPostingSetup."WHT %" / 100));
                    IF "Document Type" = "Document Type"::Refund THEN
                      WHTAmountLCY := -ABS(WHTAmountLCY);
                  END;
          IF (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Payment) AND
             (NOT GLSetup."Manual Sales WHT Calc.")
          THEN
            WHTAmountLCY :=
              CurrExchRate.ExchangeAmtFCYToLCY(
                GenJnlLine1."Document Date",
                GenJnlLine1."Currency Code",
                ABS(
                  WHTManagement.WHTAmountJournal(GenJnlLine1,TRUE)),CurrFactor);
        END ELSE
          IF ("Applies-to ID" <> '') OR ("Applies-to Doc. No." <> '') THEN BEGIN
            GenJnlLine1.RESET;
            GenJnlLine1.COPY(GenJnlLine);
            IF "Applies-to Doc. No." <> '' THEN
              GenJnlLine1.SETRANGE("Applies-to Doc. No.","Applies-to Doc. No.")
            ELSE
              GenJnlLine1.SETRANGE("Applies-to ID","Applies-to ID");

            GenJnlLine1.SETRANGE("Account Type","Account Type"::Vendor);
            IF ("Account Type" = "Account Type"::Vendor) OR ("Bal. Account Type" = "Bal. Account Type"::Vendor) OR
               GenJnlLine1.FINDFIRST
            THEN BEGIN
              CurrFactor :=
                CurrExchRate.ExchangeRate(
                  "Document Date","Currency Code");
              IF GenJnlLine1."Interest Amount" <> 0 THEN
                GenJnlLine1.VALIDATE(Amount,GenJnlLine1.Amount - GenJnlLine1."Interest Amount");

              IF IsPaymentOrRefund(GenJnlLine1) THEN
                IF WHTPostingSetup.GET(GenJnlLine1."WHT Business Posting Group",GenJnlLine1."WHT Product Posting Group") THEN BEGIN
                  IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest THEN BEGIN
                    IF GenJnlLine1.FINDFIRST THEN
                      WHTManagement.CheckApplicationGenPurchWHT(GenJnlLine1);
                    WHTAmountLCY :=
                      CurrExchRate.ExchangeAmtFCYToLCY(
                        "Document Date","Currency Code",
                        ABS(
                          WHTManagement.CalcVendExtraWHTForEarliest(GenJnlLine1)),CurrFactor);
                  END;

                  IF (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Payment) AND
                     (NOT GLSetup."Manual Sales WHT Calc.")
                  THEN
                    WHTAmountLCY :=
                      CurrExchRate.ExchangeAmtFCYToLCY(
                        GenJnlLine1."Document Date",
                        GenJnlLine1."Currency Code",
                        ABS(
                          WHTManagement.WHTAmountJournal(GenJnlLine1,TRUE)),CurrFactor);
                END;

              IF "Document Type" = "Document Type"::Refund THEN
                WHTAmountLCY := -ABS(WHTAmountLCY);
            END;

            IF IsCustAcc(GenJnlLine) THEN BEGIN
              CurrFactor :=
                CurrExchRate.ExchangeRate(
                  GenJnlLine1."Document Date","Currency Code");
              IF "Bal. Account Type" = "Bal. Account Type"::Customer THEN
                GenJnlLine1.VALIDATE(Amount,-GenJnlLine1.Amount);

              IF IsPaymentOrRefund(GenJnlLine1) THEN
                IF WHTPostingSetup.GET(GenJnlLine1."WHT Business Posting Group",GenJnlLine1."WHT Product Posting Group") THEN BEGIN
                  IF (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest) THEN BEGIN
                    IF GenJnlLine1.FINDFIRST THEN
                      WHTManagement.CheckApplicationGenSalesWHT(GenJnlLine1);
                    WHTAmountLCY :=
                      -CurrExchRate.ExchangeAmtFCYToLCY(
                        GenJnlLine1."Document Date",
                        GenJnlLine1."Currency Code",
                        ABS(
                          WHTManagement.CalcCustExtraWHTForEarliest(GenJnlLine1)),CurrFactor);
                  END;

                  IF (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Payment) AND
                     (NOT GLSetup."Manual Sales WHT Calc.")
                  THEN
                    WHTAmountLCY :=
                      -CurrExchRate.ExchangeAmtFCYToLCY(
                        GenJnlLine1."Document Date",
                        GenJnlLine1."Currency Code",
                        ABS(WHTManagement.ApplyCustCalcWHT(GenJnlLine1)),CurrFactor);
                END;

              IF "Document Type" = "Document Type"::Refund THEN
                WHTAmountLCY := ABS(WHTAmountLCY);
            END;
            WHTAmountLCY := ROUND(WHTAmountLCY);

            IF GLSetup."Round Amount for WHT Calc" THEN
              WHTAmountLCY := ROUND(WHTAmountLCY,1,'<');
          END ELSE
            IF "Bank Payment Type" =
               "Bank Payment Type"::"Computer Check"
            THEN BEGIN
              TESTFIELD("Check Printed",TRUE);
              CheckLedgEntry.LOCKTABLE;
              CheckLedgEntry.RESET;
              CheckLedgEntry.SETCURRENTKEY("Bank Account No.","Entry Status","Check No.");
              CheckLedgEntry.SETRANGE("Bank Account No.","Account No.");
              CheckLedgEntry.SETRANGE("Entry Status",CheckLedgEntry."Entry Status"::Printed);
              CheckLedgEntry.SETRANGE("Check No.","Document No.");
              IF CheckLedgEntry.FIND('-') THEN
                WHTAmountLCY := ABS(CheckLedgEntry."WHT Amount")
            END;
      END;
    END;

    PROCEDURE PostBankAccWHT@1240010(GenJnlLine@1240000 : Record 81;WHTPostingSetup@1240002 : Record 50003;WHTAmountLCY@1240001 : Decimal);
    BEGIN
      IF WHTAmountLCY = 0 THEN
        EXIT;

      WITH GenJnlLine DO BEGIN
        CASE TRUE OF
          IsVendAcc(GenJnlLine):
            IF ((WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest) OR
                (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Payment)) AND
               (("Applies-to Doc. No." = '') AND ("Applies-to ID" = ''))
            THEN BEGIN
              WHTPostingSetup.TESTFIELD("Payable WHT Account Code");
              CreateGLEntry(
                GenJnlLine,WHTPostingSetup."Payable WHT Account Code",-WHTAmountLCY,-WHTAmountLCY,TRUE);
            END;
          IsCustAcc(GenJnlLine):
            IF WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest THEN BEGIN
              WHTPostingSetup.TESTFIELD("Prepaid WHT Account Code");
              CreateGLEntry(
                GenJnlLine,WHTPostingSetup."Prepaid WHT Account Code",-WHTAmountLCY,-WHTAmountLCY,TRUE);
            END;
        END;
      END;
    END;

    PROCEDURE UpdateWHTEntryTransaction@1240003(DocNo@1240002 : Code[20]);
    VAR
      WHTEntry@1240000 : Record 50004;
      WHTPostingSetup@1240001 : Record 50003;
    BEGIN
      WHTEntry.RESET;
      WHTEntry.SETCURRENTKEY("Document No.","Posting Date");
      WHTEntry.SETRANGE("Document No.",DocNo);
      IF WHTEntry.FINDSET(TRUE,FALSE) THEN
        REPEAT
          WHTPostingSetup.GET(WHTEntry."WHT Bus. Posting Group",WHTEntry."WHT Prod. Posting Group");
          IF (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Invoice) OR
             (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Earliest)
          THEN BEGIN
            WHTEntry."Transaction No." := NextTransactionNo;
            WHTEntry.MODIFY;
          END;
        UNTIL WHTEntry.NEXT = 0;
    END;

    PROCEDURE ProcessSourceCode@1240006(SourceCode@1240000 : Code[10];SourceCodeSetup@1240001 : Record 242) : Boolean;
    BEGIN
      EXIT(SourceCode IN [SourceCodeSetup."Payment Journal",
                          SourceCodeSetup."Purchase Journal",
                          SourceCodeSetup."Sales Journal",
                          SourceCodeSetup."Cash Receipt Journal",
                          SourceCodeSetup."General Journal"]);
    END;

    PROCEDURE IsCustAcc@1240002(GenJnlLine@1240000 : Record 81) : Boolean;
    BEGIN
      WITH GenJnlLine DO
        EXIT(("Account Type" = "Account Type"::Customer) OR ("Bal. Account Type" = "Account Type"::Customer));
    END;

    PROCEDURE IsVendAcc@1240004(GenJnlLine@1240000 : Record 81) : Boolean;
    BEGIN
      WITH GenJnlLine DO
        EXIT(("Account Type" = "Account Type"::Vendor) OR ("Bal. Account Type" = "Account Type"::Vendor));
    END;

    PROCEDURE IsPaymentOrRefund@1240008(GenJnlLine@1240000 : Record 81) : Boolean;
    BEGIN
      WITH GenJnlLine DO
        EXIT("Document Type" IN ["Document Type"::Payment,"Document Type"::Refund]);
    END;

    PROCEDURE UnapplyWHTEntry@1240020(GenJnlLine@1240000 : Record 81;TransactionType@1240003 : Option;CVNo@1240004 : Code[20];TransactionNo@1240005 : Integer;VoidCheck@1240006 : Boolean);
    VAR
      WHTEntry@1240001 : Record 50004;
      NewWHTEntry@1240002 : Record 50004;
      UnrealizedWHTEntry@1240009 : Record 50004;
      WHTPostingSetup@1240008 : Record 50003;
      Vend@1250000 : Record 23;
      Source@1240007 : Option;
    BEGIN
      WHTEntry.RESET;
      WHTEntry.SETCURRENTKEY("Transaction Type","Bill-to/Pay-to No.","Transaction No.");
      WHTEntry.SETRANGE("Transaction Type",TransactionType);
      WHTEntry.SETRANGE("Bill-to/Pay-to No.",CVNo);
      WHTEntry.SETRANGE("Transaction No.",TransactionNo);
      WHTEntry.SETFILTER("Document Type",'<>%1',WHTEntry."Document Type"::"Credit Memo");
      IF WHTEntry.FINDSET THEN
        REPEAT
          NewWHTEntry := WHTEntry;
          NewWHTEntry."Closed by Entry No." := 0;
          NewWHTEntry.Closed := FALSE;
          NewWHTEntry."Posting Date" := GenJnlLine2."Posting Date";
          NewWHTEntry.Base := -WHTEntry.Base;
          NewWHTEntry.Amount := -WHTEntry.Amount;
          NewWHTEntry."Base (LCY)" := -WHTEntry."Base (LCY)";
          NewWHTEntry."Amount (LCY)" := -WHTEntry."Amount (LCY)";
          NewWHTEntry."Unrealized Amount" := -WHTEntry."Unrealized Amount";
          NewWHTEntry."Unrealized Base" := -WHTEntry."Unrealized Base";
          NewWHTEntry."Remaining Unrealized Amount" := -WHTEntry."Remaining Unrealized Amount";
          NewWHTEntry."Remaining Unrealized Base" := -WHTEntry."Remaining Unrealized Base";
          NewWHTEntry."Original Document No." := WHTEntry."Document No.";
          NewWHTEntry."Transaction No." := NextTransactionNo;
          NewWHTEntry."Entry No." := NextWHTEntryNo;
          NextWHTEntryNo := NextWHTEntryNo + 1;
          NewWHTEntry.INSERT;
          IF WHTEntry."Unrealized WHT Entry No." <> 0 THEN BEGIN
            WHTPostingSetup.GET(WHTEntry."WHT Bus. Posting Group",WHTEntry."WHT Prod. Posting Group");
            IF NOT VoidCheck THEN BEGIN
              GenJnlLine1.COPY(GenJnlLine);
              GenJnlLine1.Amount := -WHTEntry.Amount;
              GenJnlLine1."Amount (LCY)" := -WHTEntry."Amount (LCY)";
              CASE TransactionType OF
                WHTEntry."Transaction Type"::Sale:
                  Source := 0;
                WHTEntry."Transaction Type"::Purchase:
                  Source := 1;
              END;
              InsertWHTPostingBufferPosted(WHTEntry,GenJnlLine1,FALSE,Source);
            END ELSE BEGIN
              IF WHTEntry."Amount (LCY)" <> 0 THEN
                WHTPostingSetup.GET(WHTEntry."WHT Bus. Posting Group",WHTEntry."WHT Prod. Posting Group");
                CreateGLEntry(
                  GenJnlLine,WHTPostingSetup."Payable WHT Account Code",WHTEntry."Amount (LCY)",0,FALSE);
            END;

            UnrealizedWHTEntry.GET(WHTEntry."Unrealized WHT Entry No.");
            UnrealizedWHTEntry."Remaining Unrealized Amount" := UnrealizedWHTEntry."Remaining Unrealized Amount" + WHTEntry.Amount;
            UnrealizedWHTEntry."Remaining Unrealized Base" := UnrealizedWHTEntry."Remaining Unrealized Base" + WHTEntry.Base;
            UnrealizedWHTEntry."Rem Unrealized Amount (LCY)" := UnrealizedWHTEntry."Rem Unrealized Amount (LCY)" + WHTEntry."Amount (LCY)";
            UnrealizedWHTEntry."Rem Unrealized Base (LCY)" := UnrealizedWHTEntry."Rem Unrealized Base (LCY)" + WHTEntry."Base (LCY)";
            UnrealizedWHTEntry.Closed := FALSE;
            UnrealizedWHTEntry.MODIFY;
          END;
          WHTEntry."Original Document No." := NewWHTEntry."Document No.";
          WHTEntry.MODIFY;
        UNTIL WHTEntry.NEXT = 0;
    END;

    PROCEDURE CollectWHTAmount@1240021(DocNo@1240001 : Code[20]) WHTAmount : Decimal;
    VAR
      WHTEntry@1240000 : Record 50004;
    BEGIN
      WITH WHTEntry DO BEGIN
        RESET;
        SETCURRENTKEY("Document No.");
        SETRANGE("Document No.",DocNo);
        SETFILTER("Applies-to Entry No.",'%1',0);
        IF FINDSET THEN
          REPEAT
            WHTAmount := WHTAmount + "Unrealized Amount (LCY)";
          UNTIL NEXT = 0;
      END;
    END;

    PROCEDURE GetNextWHTEntryNo@1250000() : Integer;
    BEGIN
      EXIT(NextWHTEntryNo);
    END;

    PROCEDURE IncrNextWHTEntryNo@1250001();
    BEGIN
      NextWHTEntryNo := NextWHTEntryNo + 1;
    END;

    LOCAL PROCEDURE InsertPmtDiscGSTReport@1450000(GenJnlLine@1450001 : Record 81;VATEntry@1450000 : Record 254);
    VAR
      TempGenJnlLine@1450003 : TEMPORARY Record 81;
    BEGIN
      WITH TempGenJnlLine DO BEGIN
        INIT;
        "Line No." := GenJnlLine."Line No.";
        "Posting Date" := GenJnlLine."Posting Date";
        "Document Type" := GenJnlLine."Document Type";
        "Document No." := GenJnlLine."Document No.";
        "VAT Base Amount (LCY)" := VATEntry.Base;
        "VAT Amount (LCY)" := VATEntry.Amount;
        "VAT Calculation Type" := VATEntry."VAT Calculation Type";
        "VAT Bus. Posting Group" := VATEntry."VAT Bus. Posting Group";
        "VAT Prod. Posting Group" := VATEntry."VAT Prod. Posting Group";
        "Gen. Posting Type" := VATEntry.Type;
      END;
    END;

    LOCAL PROCEDURE UpdateGLRegister@1500059(GLEntry@1450000 : Record 17;VAR GLRegister@1450001 : Record 45;IsTransactionConsistent@1450002 : Boolean);
    BEGIN
      IF GLEntry."Entry Type" = GLEntry."Entry Type"::Definitive THEN
        GLRegister."To VAT Entry No." := NextVATEntryNo - 1;
      IF NextWHTEntryNo <> 0 THEN
        GLRegister."To WHT Entry No." := NextWHTEntryNo - 1;
      GLRegister."To Entry No." := GLEntry."Entry No.";
      IF IsTransactionConsistent THEN
        IF IsGLRegInserted THEN
          GLReg.MODIFY
        ELSE BEGIN
          GLReg.INSERT;
          IsGLRegInserted := TRUE;
        END;
    END;

    LOCAL PROCEDURE CreateVendGLEntriesForTotalAmounts@1450001(GenJnlLine@1450004 : Record 81;VAR InvPostBuf@1450003 : Record 49;VendPostingGr@1450005 : Record 93;AdjAmountBuf@1450002 : ARRAY [4] OF Decimal;SavedEntryNo@1450001 : Integer;LedgEntryInserted@1450006 : Boolean);
    VAR
      GLEntry@1005 : Record 17;
      DimMgt@1002 : Codeunit 408;
    BEGIN
      WITH InvPostBuf DO BEGIN
        RESET;
        IF FINDSET THEN
          REPEAT
            IF LedgEntryInserted OR (Amount <> 0) OR ("Amount (ACY)" <> 0) AND (AddCurrencyCode <> '') THEN BEGIN
              UseVendExchRate := GenJnlLine."Amount Including VAT (ACY)" <> 0;
              DimMgt.UpdateGenJnlLineDim(GenJnlLine,"Dimension Set ID");
              HandleDtldAdjustment(
                GenJnlLine,GLEntry,AdjAmountBuf,Amount,"Amount (ACY)",VendPostingGr.GetPayablesAccount);
              GLEntry."Bal. Account Type" := GenJnlLine."Bal. Account Type";
              GLEntry."Bal. Account No." := GenJnlLine."Bal. Account No.";
              UpdateGLEntryNo(GLEntry."Entry No.",SavedEntryNo);
              InsertGLEntry(GenJnlLine,GLEntry,TRUE);
              LedgEntryInserted := FALSE;
            END;
          UNTIL NEXT = 0;
      END;

      IF GenJnlLine."Interest Amount (LCY)" <> 0 THEN BEGIN
        VendPostingGr.TESTFIELD("Interest Account");
        CreateGLEntryBalAcc(
          GenJnlLine,VendPostingGr."Interest Account",
          GenJnlLine."Interest Amount (LCY)",GenJnlLine."Interest Amount",
          GenJnlLine."Bal. Account Type",GenJnlLine."Bal. Account No.");
      END;
    END;

    LOCAL PROCEDURE SetAddCurrForUnapplication@136(VAR DtldCVLedgEntryBuf@1000 : Record 383);
    BEGIN
      WITH DtldCVLedgEntryBuf DO
        IF NOT ("Entry Type" IN ["Entry Type"::Application,"Entry Type"::"Unrealized Loss",
                                 "Entry Type"::"Unrealized Gain","Entry Type"::"Realized Loss",
                                 "Entry Type"::"Realized Gain","Entry Type"::"Correction of Remaining Amount"])
        THEN
          IF ("Entry Type" = "Entry Type"::"Appln. Rounding") OR
             ((AddCurrencyCode <> '') AND (AddCurrencyCode = "Currency Code"))
          THEN
            "Additional-Currency Amount" := Amount
          ELSE
            "Additional-Currency Amount" := CalcAddCurrForUnapplication("Posting Date","Amount (LCY)");
    END;

    LOCAL PROCEDURE PostDeferral@125(VAR GenJournalLine@1000 : Record 81;AccountNumber@1006 : Code[20]);
    VAR
      DeferralTemplate@1001 : Record 1700;
      DeferralHeader@1002 : Record 1701;
      DeferralLine@1003 : Record 1702;
      GLEntry@1004 : Record 17;
      CurrExchRate@1012 : Record 330;
      DeferralUtilities@1005 : Codeunit 1720;
      PerPostDate@1007 : Date;
      PeriodicCount@1008 : Integer;
      AmtToDefer@1010 : Decimal;
      AmtToDeferACY@1009 : Decimal;
      EmptyDeferralLine@1011 : Boolean;
    BEGIN
      WITH GenJournalLine DO BEGIN
        IF "Source Type" IN ["Source Type"::Vendor,"Source Type"::Customer] THEN
          // Purchasing and Sales, respectively
          // We can create these types directly from the GL window, need to make sure we don't already have a deferral schedule
          // created for this GL Trx before handing it off to sales/purchasing subsystem
          IF "Source Code" <> GLSourceCode THEN BEGIN
            PostDeferralPostBuffer(GenJournalLine);
            EXIT;
          END;

        IF DeferralHeader.GET(DeferralDocType::"G/L","Journal Template Name","Journal Batch Name",0,'',"Line No.") THEN BEGIN
          EmptyDeferralLine := FALSE;
          // Get the range of detail records for this schedule
          DeferralUtilities.FilterDeferralLines(
            DeferralLine,DeferralDocType::"G/L","Journal Template Name","Journal Batch Name",0,'',"Line No.");
          IF DeferralLine.FINDSET THEN
            REPEAT
              IF DeferralLine.Amount = 0.0 THEN
                EmptyDeferralLine := TRUE;
            UNTIL (DeferralLine.NEXT = 0) OR EmptyDeferralLine;
          IF EmptyDeferralLine THEN
            ERROR(ZeroDeferralAmtErr,"Line No.","Deferral Code");
          DeferralHeader."Amount to Defer (LCY)" :=
            ROUND(CurrExchRate.ExchangeAmtFCYToLCY("Posting Date","Currency Code",
                DeferralHeader."Amount to Defer","Currency Factor"));
          DeferralHeader.MODIFY;
        END;

        DeferralUtilities.RoundDeferralAmount(
          DeferralHeader,
          "Currency Code","Currency Factor","Posting Date",AmtToDefer,AmtToDeferACY);

        DeferralTemplate.GET("Deferral Code");
        DeferralTemplate.TESTFIELD("Deferral Account");
        DeferralTemplate.TESTFIELD("Deferral %");

        // Get the Deferral Header table so we know the amount to defer...
        // Assume straight GL posting
        IF DeferralHeader.GET(DeferralDocType::"G/L","Journal Template Name","Journal Batch Name",0,'',"Line No.") THEN
          // Get the range of detail records for this schedule
          DeferralUtilities.FilterDeferralLines(
            DeferralLine,DeferralDocType::"G/L","Journal Template Name","Journal Batch Name",0,'',"Line No.")
        ELSE
          ERROR(NoDeferralScheduleErr,"Line No.","Deferral Code");

        InitGLEntry(GenJournalLine,GLEntry,
          AccountNumber,
          -DeferralHeader."Amount to Defer (LCY)",
          -DeferralHeader."Amount to Defer",TRUE,TRUE);
        GLEntry.Description := Description;
        InsertGLEntry(GenJournalLine,GLEntry,TRUE);

        InitGLEntry(GenJournalLine,GLEntry,
          DeferralTemplate."Deferral Account",
          DeferralHeader."Amount to Defer (LCY)",
          DeferralHeader."Amount to Defer",TRUE,TRUE);
        GLEntry.Description := Description;
        InsertGLEntry(GenJournalLine,GLEntry,TRUE);

        // Here we want to get the Deferral Details table range and loop through them...
        IF DeferralLine.FINDSET THEN BEGIN
          PeriodicCount := 1;
          REPEAT
            PerPostDate := DeferralLine."Posting Date";
            IF GenJnlCheckLine.DateNotAllowed(PerPostDate) THEN
              ERROR(InvalidPostingDateErr,PerPostDate);

            InitGLEntry(GenJournalLine,GLEntry,AccountNumber,DeferralLine."Amount (LCY)",
              DeferralLine.Amount,
              TRUE,TRUE);
            GLEntry."Posting Date" := PerPostDate;
            GLEntry.Description := DeferralLine.Description;
            InsertGLEntry(GenJournalLine,GLEntry,TRUE);

            InitGLEntry(GenJournalLine,GLEntry,
              DeferralTemplate."Deferral Account",-DeferralLine."Amount (LCY)",
              -DeferralLine.Amount,
              TRUE,TRUE);
            GLEntry."Posting Date" := PerPostDate;
            GLEntry.Description := DeferralLine.Description;
            InsertGLEntry(GenJournalLine,GLEntry,TRUE);
            PeriodicCount := PeriodicCount + 1;
          UNTIL DeferralLine.NEXT = 0;
        END ELSE
          ERROR(NoDeferralScheduleErr,"Line No.","Deferral Code");
      END;
    END;

    LOCAL PROCEDURE PostDeferralPostBuffer@127(GenJournalLine@1005 : Record 81);
    VAR
      DeferralPostBuffer@1004 : Record 1706;
      GLEntry@1003 : Record 17;
      PostDate@1000 : Date;
    BEGIN
      WITH GenJournalLine DO BEGIN
        IF "Source Type" = "Source Type"::Customer THEN
          DeferralDocType := DeferralDocType::Sales
        ELSE
          DeferralDocType := DeferralDocType::Purchase;

        DeferralPostBuffer.SETRANGE("Deferral Doc. Type",DeferralDocType);
        DeferralPostBuffer.SETRANGE("Document No.","Document No.");
        DeferralPostBuffer.SETRANGE("Deferral Line No.","Deferral Line No.");

        IF DeferralPostBuffer.FINDSET THEN BEGIN
          REPEAT
            PostDate := DeferralPostBuffer."Posting Date";
            IF GenJnlCheckLine.DateNotAllowed(PostDate) THEN
              ERROR(InvalidPostingDateErr,PostDate);

            // When no sales/purch amount is entered, the offset was already posted
            IF (DeferralPostBuffer."Sales/Purch Amount" <> 0) OR (DeferralPostBuffer."Sales/Purch Amount (LCY)" <> 0) THEN BEGIN
              InitGLEntry(GenJournalLine,GLEntry,DeferralPostBuffer."G/L Account",
                DeferralPostBuffer."Sales/Purch Amount (LCY)",
                DeferralPostBuffer."Sales/Purch Amount",
                TRUE,TRUE);
              GLEntry."Posting Date" := PostDate;
              GLEntry.Description := DeferralPostBuffer.Description;
              GLEntry.CopyFromDeferralPostBuffer(DeferralPostBuffer);
              InsertGLEntry(GenJournalLine,GLEntry,TRUE);
            END;

            IF DeferralPostBuffer.Amount <> 0 THEN BEGIN
              InitGLEntry(GenJournalLine,GLEntry,
                DeferralPostBuffer."Deferral Account",
                -DeferralPostBuffer."Amount (LCY)",
                -DeferralPostBuffer.Amount,
                TRUE,TRUE);
              GLEntry."Posting Date" := PostDate;
              GLEntry.Description := DeferralPostBuffer.Description;
              InsertGLEntry(GenJournalLine,GLEntry,TRUE);
            END;
          UNTIL DeferralPostBuffer.NEXT = 0;
          DeferralPostBuffer.DELETEALL;
        END;
      END;
    END;

    [External]
    PROCEDURE RemoveDeferralSchedule@128(GenJournalLine@1002 : Record 81);
    VAR
      DeferralUtilities@1000 : Codeunit 1720;
      DeferralDocType@1001 : 'Purchase,Sales,G/L';
    BEGIN
      // Removing deferral schedule after all deferrals for this line have been posted successfully
      WITH GenJournalLine DO
        DeferralUtilities.DeferralCodeOnDelete(
          DeferralDocType::"G/L",
          "Journal Template Name",
          "Journal Batch Name",0,'',"Line No.");
    END;

    LOCAL PROCEDURE GetGLSourceCode@130();
    VAR
      SourceCodeSetup@1000 : Record 242;
    BEGIN
      SourceCodeSetup.GET;
      GLSourceCode := SourceCodeSetup."General Journal";
    END;

    LOCAL PROCEDURE DeferralPosting@131(DeferralCode@1000 : Code[10];SourceCode@1001 : Code[10];AccountNo@1002 : Code[20];VAR GenJournalLine@1005 : Record 81;Balancing@1006 : Boolean);
    BEGIN
      IF DeferralCode <> '' THEN
        // Sales and purchasing could have negative amounts, so check for them first...
        IF (SourceCode <> GLSourceCode) AND
           (GenJournalLine."Account Type" IN [GenJournalLine."Account Type"::Customer,GenJournalLine."Account Type"::Vendor])
        THEN
          PostDeferralPostBuffer(GenJournalLine)
        ELSE
          // Pure GL trx, only post deferrals if it is not a balancing entry
          IF NOT Balancing THEN
            PostDeferral(GenJournalLine,AccountNo);
    END;

    LOCAL PROCEDURE GetPostingAccountNo@225(VATPostingSetup@1002 : Record 325;VATEntry@1001 : Record 254;UnrealizedVAT@1000 : Boolean) : Code[20];
    VAR
      TaxJurisdiction@1003 : Record 320;
    BEGIN
      IF VATPostingSetup."VAT Calculation Type" = VATPostingSetup."VAT Calculation Type"::"Sales Tax" THEN BEGIN
        VATEntry.TESTFIELD("Tax Jurisdiction Code");
        TaxJurisdiction.GET(VATEntry."Tax Jurisdiction Code");
        CASE VATEntry.Type OF
          VATEntry.Type::Sale:
            EXIT(TaxJurisdiction.GetSalesAccount(UnrealizedVAT));
          VATEntry.Type::Purchase:
            EXIT(TaxJurisdiction.GetPurchAccount(UnrealizedVAT));
        END;
      END;

      CASE VATEntry.Type OF
        VATEntry.Type::Sale:
          EXIT(VATPostingSetup.GetSalesAccount(UnrealizedVAT));
        VATEntry.Type::Purchase:
          EXIT(VATPostingSetup.GetPurchAccount(UnrealizedVAT));
      END;
    END;

    LOCAL PROCEDURE IsDebitAmount@137(DtldCVLedgEntryBuf@1000 : Record 383;Unapply@1001 : Boolean) : Boolean;
    VAR
      VATPostingSetup@1002 : Record 325;
      VATAmountCondition@1003 : Boolean;
      EntryAmount@1004 : Decimal;
    BEGIN
      WITH DtldCVLedgEntryBuf DO BEGIN
        VATAmountCondition :=
          "Entry Type" IN ["Entry Type"::"Payment Discount (VAT Excl.)","Entry Type"::"Payment Tolerance (VAT Excl.)",
                           "Entry Type"::"Payment Discount Tolerance (VAT Excl.)"];
        IF VATAmountCondition THEN BEGIN
          VATPostingSetup.GET("VAT Bus. Posting Group","VAT Prod. Posting Group");
          VATAmountCondition := VATPostingSetup."VAT Calculation Type" = VATPostingSetup."VAT Calculation Type"::"Full VAT";
        END;
        IF VATAmountCondition THEN
          EntryAmount := "VAT Amount (LCY)"
        ELSE
          EntryAmount := "Amount (LCY)";
        IF Unapply THEN
          EXIT(EntryAmount > 0);
        EXIT(EntryAmount <= 0);
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCode@159(VAR GenJnlLine@1000 : Record 81;CheckLine@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCheckPurchExtDocNo@153(GenJournalLine@1000 : Record 81;VendorLedgerEntry@1001 : Record 25;CVLedgerEntryBuffer@1003 : Record 382;VAR Handled@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeStartPosting@149(VAR GenJournalLine@1000 : Record 81);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeContinuePosting@150(VAR GenJournalLine@1000 : Record 81);
    BEGIN
    END;

    [Integration]
    [External]
    LOCAL PROCEDURE OnBeforePostGenJnlLine@133(VAR GenJournalLine@1000 : Record 81);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInitGLRegister@139(VAR GLRegister@1000 : Record 45;VAR GenJournalLine@1001 : Record 81);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInsertGlobalGLEntry@142(VAR GLEntry@1000 : Record 17);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterRunWithCheck@160(VAR GenJnlLine@1000 : Record 81);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterRunWithoutCheck@158(VAR GenJnlLine@1000 : Record 81);
    BEGIN
    END;

    [Integration]
    [External]
    LOCAL PROCEDURE OnBeforeInsertGLEntryBuffer@146(VAR TempGLEntryBuf@1000 : TEMPORARY Record 17;VAR GenJournalLine@1001 : Record 81);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterGLFinishPosting@144(GLEntry@1000 : Record 17;VAR GenJnlLine@1003 : Record 81;IsTransactionConsistent@1001 : Boolean;FirstTransactionNo@1002 : Integer);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnNextTransactionNoNeeded@154(GenJnlLine@1000 : Record 81;LastDocType@1003 : ' ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder';LastDocNo@1002 : Code[20];LastDate@1001 : Date;CurrentBalance@1004 : Decimal;CurrentBalanceACY@1005 : Decimal;VAR NewTransaction@1006 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPostGLAcc@161(GenJnlLine@1000 : Record 81);
    BEGIN
    END;

    PROCEDURE CreateFAInventory@1000000000(VAR GenJnlLine@1000000000 : Record 81);
    VAR
      FA@1000000001 : Record 5600;
      Text001@1000000002 : TextConst 'ENU=Post from Cylinder Disposal Journal';
      FASetup@1000000003 : Record 5603;
      FAItemJnlLineNo@1000 : Integer;
      ItemJnlLine@1001 : Record 83;
      ItemJnlPostLine@1002 : Codeunit 22;
    BEGIN
      //RKY
      FASetup.GET;
      FASetup.TESTFIELD("FA Batch Name");
      FASetup.TESTFIELD("FA Template Name");
      WITH GenJnlLine DO BEGIN
        IF "Account Type" = "Account Type"::"Fixed Asset" THEN
          IF FA.GET("Account No.") THEN BEGIN
            FAItemJnlLineNo := FAItemJnlLineNo + 10000; //Global
            ItemJnlLine."Journal Template Name" := FASetup."FA Template Name";
            ItemJnlLine."Journal Batch Name" := FASetup."FA Batch Name";
            ItemJnlLine."Line No." := FAItemJnlLineNo;
            ItemJnlLine.VALIDATE("Shortcut Dimension 1 Code","Shortcut Dimension 1 Code");
            ItemJnlLine.VALIDATE("Shortcut Dimension 2 Code","Shortcut Dimension 2 Code");
            ItemJnlLine.VALIDATE("Entry Type",ItemJnlLine."Entry Type"::"Negative Adjmt.");
            ItemJnlLine.VALIDATE("Document No.","Document No.");
            ItemJnlLine.VALIDATE("Posting Date",TODAY);

            ItemJnlLine."FA No." := "Account No."; //tag FA batch
            ItemJnlLine.VALIDATE("Item No.",FA."Item No.");
            ItemJnlLine.Description := STRSUBSTNO(Text001);
            ItemJnlLine.VALIDATE("Location Code","Location Code");
            ItemJnlLine.VALIDATE("Bin Code","Bin Code");
            ItemJnlLine.VALIDATE(Quantity,"Disposal Qty.");
            ItemJnlLine.VALIDATE("Unit Amount",0);
            ItemJnlLine.VALIDATE("Unit Cost",0);

            ItemJnlPostLine.RUN(ItemJnlLine);
          END;
      END;
    END;

    BEGIN
    {
      DQU 20181003 CR: CWT #1

      LPC-CA 080219 Added External Doc Number on the Reimbursement Line
    }
    END.
  }
}

OBJECT Codeunit 13 Gen. Jnl.-Post Batch
{
  OBJECT-PROPERTIES
  {
    Date=07/26/19;
    Time=12:00:00 PM;
    Version List=NAVW111.00.00.34561;
  }
  PROPERTIES
  {
    TableNo=81;
    Permissions=TableData 232=imd;
    OnRun=VAR
            GenJnlLine@1000 : Record 81;
          BEGIN
            GenJnlLine.COPY(Rec);
            Code(GenJnlLine);
            Rec := GenJnlLine;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot exceed %1 characters';
      PostingStateMsg@1001 : TextConst '@@@=This is a message for dialog window. Parameters do not require translation.;ENU=Journal Batch Name    #1##########\\Posting @2@@@@@@@@@@@@@\#3#############';
      CheckingLinesMsg@1003 : TextConst 'ENU=Checking lines';
      CheckingBalanceMsg@1004 : TextConst 'ENU=Checking balance';
      UpdatingBalLinesMsg@1005 : TextConst 'ENU=Updating bal. lines';
      PostingLinesMsg@1006 : TextConst 'ENU=Posting lines';
      PostingReversLinesMsg@1007 : TextConst 'ENU=Posting revers. lines';
      UpdatingLinesMsg@1036 : TextConst 'ENU=Updating lines';
      Text008@1008 : TextConst 'ENU=must be the same on all lines for the same document';
      Text009@1009 : TextConst 'ENU="%1 %2 posted on %3 includes more than one customer or vendor. "';
      Text010@1010 : TextConst 'ENU=In order for the program to calculate VAT, the entries must be separated by another document number or by an empty line.';
      Text012@1012 : TextConst 'ENU="%5 %2 is out of balance by %1. "';
      Text013@1013 : TextConst 'ENU=Please check that %3, %4, %5 and %6 are correct for each line.';
      Text014@1014 : TextConst 'ENU="The lines in %1 are out of balance by %2. "';
      Text015@1015 : TextConst 'ENU=Check that %3 and %4 are correct for each line.';
      Text016@1016 : TextConst 'ENU="Your reversing entries in %4 %2 are out of balance by %1. "';
      Text017@1017 : TextConst 'ENU=Please check whether %3 is correct for each line for this %4.';
      Text018@1018 : TextConst 'ENU="Your reversing entries for %1 are out of balance by %2. "';
      Text019@1019 : TextConst 'ENU="%3 %1 is out of balance due to the additional reporting currency. "';
      Text020@1020 : TextConst 'ENU=Please check that %2 is correct for each line.';
      Text021@1021 : TextConst 'ENU=cannot be specified when using recurring journals.';
      Text022@1022 : TextConst 'ENU=The Balance and Reversing Balance recurring methods can be used only for G/L accounts.';
      Text023@1023 : TextConst 'ENU=Allocations can only be used with recurring journals.';
      Text024@1024 : TextConst 'ENU=<Month Text>';
      Text025@1025 : TextConst 'ENU=A maximum of %1 posting number series can be used in each journal.';
      Text026@1026 : TextConst 'ENU="%5 %2 is out of balance by %1 %7. "';
      Text027@1027 : TextConst 'ENU="The lines in %1 are out of balance by %2 %5. "';
      Text028@1028 : TextConst 'ENU=The Balance and Reversing Balance recurring methods can be used only with Allocations.';
      GenJnlTemplate@1029 : Record 80;
      GenJnlBatch@1030 : Record 232;
      GenJnlLine2@1032 : Record 81;
      GenJnlLine3@1033 : Record 81;
      TempGenJnlLine4@1034 : TEMPORARY Record 81;
      GenJnlLine5@1035 : Record 81;
      GLEntry@1037 : Record 17;
      GLReg@1038 : Record 45;
      GLAcc@1039 : Record 15;
      GenJnlAlloc@1042 : Record 221;
      AccountingPeriod@1043 : Record 50;
      NoSeries@1044 : TEMPORARY Record 308;
      GLSetup@1045 : Record 98;
      FAJnlSetup@1046 : Record 5605;
      GenJnlLineTemp@1102601000 : TEMPORARY Record 81;
      WHTEntry@1500005 : Record 50004;
      CustLedgEntry@1500004 : Record 21;
      VendLedgEntry@1500003 : Record 25;
      PurchInvLine@1500001 : Record 123;
      SalesInvLine@1500000 : Record 113;
      WHTPostingSetup@1500007 : Record 50003;
      Journal@1500008 : Record 230;
      TempCustLedgerEntry@1106000003 : TEMPORARY Record 21;
      TempVendLedgerEntry@1106000002 : TEMPORARY Record 25;
      GenJnlCheckLine@1047 : Codeunit 11;
      GenJnlPostLine@1048 : Codeunit 12;
      GenJnlPostPreview@1080 : Codeunit 19;
      NoSeriesMgt@1049 : Codeunit 396;
      NoSeriesMgt2@1050 : ARRAY [10] OF Codeunit 396;
      ICOutboxMgt@1078 : Codeunit 427;
      PostingSetupMgt@1072 : Codeunit 48;
      Window@1052 : Dialog;
      GLRegNo@1053 : Integer;
      StartLineNo@1054 : Integer;
      StartLineNoReverse@1055 : Integer;
      LastDate@1056 : Date;
      LastDocType@1057 : Integer;
      Counter@1106000004 : Integer;
      LastDocNo@1058 : Code[20];
      LastPostedDocNo@1059 : Code[20];
      CurrentBalance@1060 : Decimal;
      CurrentBalanceReverse@1061 : Decimal;
      Day@1062 : Integer;
      Week@1063 : Integer;
      Month@1064 : Integer;
      MonthText@1065 : Text[30];
      NoOfRecords@1066 : Integer;
      NoOfReversingRecords@1067 : Integer;
      LineCount@1068 : Integer;
      NoOfPostingNoSeries@1069 : Integer;
      PostingNoSeriesNo@1070 : Integer;
      DocCorrection@1071 : Boolean;
      VATEntryCreated@1073 : Boolean;
      LastFAAddCurrExchRate@1074 : Decimal;
      LastCurrencyCode@1075 : Code[10];
      CurrencyBalance@1076 : Decimal;
      TotWHT@1500006 : Decimal;
      WHTAmt@1106000001 : Decimal;
      Text029@1041 : TextConst '@@@="%1 = Document Type;%2 = Document No.;%3=Posting Date";ENU=%1 %2 posted on %3 includes more than one customer, vendor or IC Partner.';
      Text030@1011 : TextConst 'ENU=You cannot enter G/L Account or Bank Account in both %1 and %2.';
      Text031@1040 : TextConst 'ENU=Line No. %1 does not contain a G/L Account or Bank Account. When the %2 field contains an account number, either the %3 field or the %4 field must contain a G/L Account or Bank Account.';
      RefPostingState@1002 : 'Checking lines,Checking balance,Updating bal. lines,Posting Lines,Posting revers. lines,Updating lines';
      PreviewMode@1051 : Boolean;
      SkippedLineMsg@1092 : TextConst 'ENU=One or more lines has not been posted because the amount is zero.';
      ConfirmPostingAfterCurrentPeriodQst@1079 : TextConst 'ENU=The posting date of one or more journal lines is after the current calendar date. Do you want to continue?';

    LOCAL PROCEDURE Code@7(VAR GenJnlLine@1000 : Record 81);
    VAR
      TempMarkedGenJnlLine@1001 : TEMPORARY Record 81;
      IntegrationService@1004 : Codeunit 5151;
      IntegrationManagement@1003 : Codeunit 5150;
    BEGIN
      // let's force Api Enabled check.
      // this will disable integration related subscribers in case of disabled Api setup
      BINDSUBSCRIPTION(IntegrationService);
      IntegrationManagement.ResetIntegrationActivated;

      WITH GenJnlLine DO BEGIN
        SETRANGE("Journal Template Name","Journal Template Name");
        SETRANGE("Journal Batch Name","Journal Batch Name");

        LOCKTABLE;
        GenJnlAlloc.LOCKTABLE;

        GenJnlTemplate.GET("Journal Template Name");
        GenJnlBatch.GET("Journal Template Name","Journal Batch Name");
        IF STRLEN(INCSTR(GenJnlBatch.Name)) > MAXSTRLEN(GenJnlBatch.Name) THEN
          GenJnlBatch.FIELDERROR(
            Name,
            STRSUBSTNO(Text000,MAXSTRLEN(GenJnlBatch.Name)));

        IF GenJnlTemplate.Recurring THEN BEGIN
          TempMarkedGenJnlLine.COPY(GenJnlLine);
          CheckGenJnlLineDates(TempMarkedGenJnlLine,GenJnlLine);
          TempMarkedGenJnlLine.SETRANGE("Posting Date",0D,WORKDATE);
          GLSetup.GET;
        END;

        IF GenJnlTemplate.Recurring THEN BEGIN
          ProcessLines(TempMarkedGenJnlLine);
          COPY(TempMarkedGenJnlLine);
        END ELSE
          ProcessLines(GenJnlLine);
      END;
    END;

    LOCAL PROCEDURE ProcessICTransaction@30(LastICTransactionNo@1000 : Integer;ICTransactionNo@1001 : Integer);
    VAR
      ICOutboxExport@1002 : Codeunit 431;
    BEGIN
      IF LastICTransactionNo = 0 THEN
        LastICTransactionNo := ICTransactionNo
      ELSE
        IF LastICTransactionNo <> ICTransactionNo THEN BEGIN
          ICOutboxExport.ProcessAutoSendOutboxTransactionNo(LastICTransactionNo);
          LastICTransactionNo := ICTransactionNo;
        END;
    END;

    LOCAL PROCEDURE ProcessLines@43(VAR GenJnlLine@1000 : Record 81);
    VAR
      TempGenJnlLine@1015 : TEMPORARY Record 81;
      GenJnlLineVATInfoSource@1014 : Record 81;
      UpdateAnalysisView@1010 : Codeunit 410;
      ICOutboxExport@1013 : Codeunit 431;
      ICLastDocNo@1009 : Code[20];
      CurrentICPartner@1005 : Code[20];
      LastLineNo@1008 : Integer;
      LastICTransactionNo@1012 : Integer;
      ICTransactionNo@1007 : Integer;
      ICLastDocType@1004 : Integer;
      ICLastDate@1006 : Date;
      VATInfoSourceLineIsInserted@1003 : Boolean;
      SkippedLine@1002 : Boolean;
      PostingAfterCurrentFiscalYearConfirmed@1001 : Boolean;
    BEGIN
      WITH GenJnlLine DO BEGIN
        IF NOT FIND('=><') THEN BEGIN
          "Line No." := 0;
          IF PreviewMode THEN
            GenJnlPostPreview.ThrowError;
          COMMIT;
          EXIT;
        END;

        Window.OPEN(PostingStateMsg);
        Window.UPDATE(1,"Journal Batch Name");

        // Check lines
        LineCount := 0;
        StartLineNo := "Line No.";
        NoOfRecords := COUNT;
        GenJnlCheckLine.SetBatchMode(TRUE);
        REPEAT
          LineCount := LineCount + 1;
          UpdateDialog(RefPostingState::"Checking lines",LineCount,NoOfRecords);
          CheckLine(GenJnlLine,PostingAfterCurrentFiscalYearConfirmed);
          TempGenJnlLine := GenJnlLine5;
          TempGenJnlLine.INSERT;
          IF NEXT = 0 THEN
            FINDFIRST;
        UNTIL "Line No." = StartLineNo;
        IF GenJnlTemplate.Type = GenJnlTemplate.Type::Intercompany THEN
          CheckICDocument(TempGenJnlLine);

        ProcessBalanceOfLines(GenJnlLine,GenJnlLineVATInfoSource,VATInfoSourceLineIsInserted,LastLineNo,CurrentICPartner);

        // Find next register no.
        Journal.GET("Source Code");
        GLEntry.LOCKTABLE;

        IF Journal.Simulation THEN BEGIN
          IF GLEntry.FINDFIRST THEN;
        END ELSE BEGIN
          IF GLEntry.FINDLAST THEN;
        END;
        FindNextGLRegisterNo;

        // Post lines
        LineCount := 0;
        LastDocNo := '';
        LastPostedDocNo := '';
        LastICTransactionNo := 0;
        TempGenJnlLine4.DELETEALL;
        NoOfReversingRecords := 0;
        FINDSET(TRUE,FALSE);
        REPEAT
          ProcessICLines(CurrentICPartner,ICTransactionNo,ICLastDocNo,ICLastDate,ICLastDocType,GenJnlLine,TempGenJnlLine);
          ProcessICTransaction(LastICTransactionNo,ICTransactionNo);
          GenJnlLine3 := GenJnlLine;
          IF NOT PostGenJournalLine(GenJnlLine3,GenJnlLine,CurrentICPartner,ICTransactionNo) THEN
            SkippedLine := TRUE;
        UNTIL NEXT = 0;

        IF LastICTransactionNo > 0 THEN
          ICOutboxExport.ProcessAutoSendOutboxTransactionNo(ICTransactionNo);

        // Post reversing lines
        PostReversingLines(TempGenJnlLine4);

        IF PreviewMode THEN
          GenJnlPostPreview.ThrowError;

        // Copy register no. and current journal batch name to general journal
        IF Journal.Simulation THEN BEGIN
          IF NOT GLReg.FINDFIRST OR (GLReg."No." <> GLRegNo) THEN
            GLRegNo := 0;
        END ELSE BEGIN
          IF NOT GLReg.FINDLAST OR (GLReg."No." <> GLRegNo) THEN
            GLRegNo := 0;
        END;

        INIT;
        "Line No." := GLRegNo;

        // Update/delete lines
        IF GLRegNo <> 0 THEN
          UpdateAndDeleteLines(GenJnlLine);

        IF GenJnlBatch."No. Series" <> '' THEN
          NoSeriesMgt.SaveNoSeries;
        IF NoSeries.FINDSET THEN
          REPEAT
            EVALUATE(PostingNoSeriesNo,NoSeries.Description);
            NoSeriesMgt2[PostingNoSeriesNo].SaveNoSeries;
          UNTIL NoSeries.NEXT = 0;

        COMMIT;
        CLEAR(GenJnlCheckLine);
        CLEAR(GenJnlPostLine);
        CLEARMARKS;
      END;
      UpdateAnalysisView.UpdateAll(0,TRUE);
      GenJnlBatch.OnMoveGenJournalBatch(GLReg.RECORDID);
      COMMIT;

      IF SkippedLine AND GUIALLOWED THEN
        MESSAGE(SkippedLineMsg);
    END;

    LOCAL PROCEDURE ProcessBalanceOfLines@42(VAR GenJnlLine@1000 : Record 81;VAR GenJnlLineVATInfoSource@1003 : Record 81;VAR VATInfoSourceLineIsInserted@1002 : Boolean;VAR LastLineNo@1004 : Integer;CurrentICPartner@1001 : Code[20]);
    VAR
      VATPostingSetup@1006 : Record 325;
      BalVATPostingSetup@1005 : Record 325;
      ErrorMessage@1007 : Text;
      ForceCheckBalance@1008 : Boolean;
    BEGIN
      WITH GenJnlLine DO BEGIN
        GLSetup.GET;
        IF ((GenJnlBatch."No. Series" = '') AND (GenJnlBatch."Posting No. Series" = '') AND GenJnlTemplate."Force Doc. Balance") OR
           IsWHTPaymentPosting(GenJnlLine)
        THEN
          SETCURRENTKEY("Document No.");
        LineCount := 0;
        LastDate := 0D;
        LastDocType := 0;
        LastDocNo := '';
        LastFAAddCurrExchRate := 0;
        GenJnlLineTemp.RESET;
        GenJnlLineTemp.DELETEALL;
        VATEntryCreated := FALSE;
        CurrentBalance := 0;
        CurrentBalanceReverse := 0;
        CurrencyBalance := 0;

        FINDSET(TRUE,FALSE);
        LastCurrencyCode := "Currency Code";

        REPEAT
          LineCount := LineCount + 1;
          UpdateDialog(RefPostingState::"Checking balance",LineCount,NoOfRecords);

          IF NOT EmptyLine THEN BEGIN
            IF NOT PreviewMode THEN
              CheckDocNoBasedOnNoSeries(LastDocNo,GenJnlBatch."No. Series",NoSeriesMgt);
            IF "Posting No. Series" <> '' THEN
              TESTFIELD("Posting No. Series",GenJnlBatch."Posting No. Series");
            IF ("Posting Date" <> LastDate) OR
               ("Document Type" <> LastDocType) OR ("Document No." <> LastDocNo)
            THEN BEGIN
              IF Correction THEN
                GenJnlTemplate.TESTFIELD("Force Doc. Balance",TRUE);
              DocCorrection := Correction;
            END ELSE
              IF Correction <> DocCorrection THEN
                FIELDERROR(Correction,Text008);
          END;
          OnBeforeIfCheckBalance(GenJnlTemplate,GenJnlLine,LastDocType,LastDocNo,LastDate,ForceCheckBalance);
          IF ForceCheckBalance OR ("Posting Date" <> LastDate) OR GenJnlTemplate."Force Doc. Balance" AND
             (("Document Type" <> LastDocType) OR ("Document No." <> LastDocNo))
          THEN BEGIN
            CheckBalance(GenJnlLine);
            CurrencyBalance := 0;
            LastCurrencyCode := "Currency Code";
            GenJnlLineTemp.RESET;
            GenJnlLineTemp.DELETEALL;
          END;

          IF Amount <> 0 THEN BEGIN
            IF LastFAAddCurrExchRate <> "FA Add.-Currency Factor" THEN
              CheckAddExchRateBalance(GenJnlLine);
            IF (CurrentBalance = 0) AND (CurrentICPartner = '') THEN BEGIN
              GenJnlLineTemp.RESET;
              GenJnlLineTemp.DELETEALL;
              IF VATEntryCreated AND VATInfoSourceLineIsInserted THEN
                UpdateGenJnlLineWithVATInfo(GenJnlLine,GenJnlLineVATInfoSource,StartLineNo,LastLineNo);
              VATEntryCreated := FALSE;
              VATInfoSourceLineIsInserted := FALSE;
              StartLineNo := "Line No.";
            END;
            IF CurrentBalanceReverse = 0 THEN
              StartLineNoReverse := "Line No.";
            UpdateLineBalance;
            CurrentBalance := CurrentBalance + "Balance (LCY)";
            IF "Recurring Method" >= "Recurring Method"::"RF Reversing Fixed" THEN
              CurrentBalanceReverse := CurrentBalanceReverse + "Balance (LCY)";

            UpdateCurrencyBalanceForRecurringLine(GenJnlLine);
          END;

          LastDate := "Posting Date";
          LastDocType := "Document Type";
          IF NOT EmptyLine THEN
            LastDocNo := "Document No.";
          LastFAAddCurrExchRate := "FA Add.-Currency Factor";
          IF GenJnlTemplate."Force Doc. Balance" THEN BEGIN
            IF NOT VATPostingSetup.GET("VAT Bus. Posting Group","VAT Prod. Posting Group") THEN
              CLEAR(VATPostingSetup);
            IF NOT BalVATPostingSetup.GET("Bal. VAT Bus. Posting Group","Bal. VAT Prod. Posting Group") THEN
              CLEAR(BalVATPostingSetup);
            VATEntryCreated :=
              VATEntryCreated OR
              (("Account Type" = "Account Type"::"G/L Account") AND ("Account No." <> '') AND
               ("Gen. Posting Type" IN ["Gen. Posting Type"::Purchase,"Gen. Posting Type"::Sale]) AND
               (VATPostingSetup."VAT %" <> 0)) OR
              (("Bal. Account Type" = "Bal. Account Type"::"G/L Account") AND ("Bal. Account No." <> '') AND
               ("Bal. Gen. Posting Type" IN ["Bal. Gen. Posting Type"::Purchase,"Bal. Gen. Posting Type"::Sale]) AND
               (BalVATPostingSetup."VAT %" <> 0));
            IF GenJnlLineTemp.IsCustVendICAdded(GenJnlLine) THEN BEGIN
              GenJnlLineVATInfoSource := GenJnlLine;
              VATInfoSourceLineIsInserted := TRUE;
            END;
            IF (GenJnlLineTemp.COUNT > 1) AND VATEntryCreated THEN BEGIN
              ErrorMessage := Text009 + Text010;
              ERROR(ErrorMessage,"Document Type","Document No.","Posting Date");
            END;
            IF (GenJnlLineTemp.COUNT > 1) AND (CurrentICPartner <> '') AND
               (GenJnlTemplate.Type = GenJnlTemplate.Type::Intercompany)
            THEN
              ERROR(
                Text029,
                "Document Type","Document No.","Posting Date");
            LastLineNo := "Line No.";
          END;
        UNTIL NEXT = 0;
        CheckBalance(GenJnlLine);
        CopyFields(GenJnlLine);
        IF VATEntryCreated AND VATInfoSourceLineIsInserted THEN
          UpdateGenJnlLineWithVATInfo(GenJnlLine,GenJnlLineVATInfoSource,StartLineNo,LastLineNo);
      END;
    END;

    LOCAL PROCEDURE ProcessICLines@49(VAR CurrentICPartner@1001 : Code[20];VAR ICTransactionNo@1002 : Integer;VAR ICLastDocNo@1006 : Code[20];VAR ICLastDate@1005 : Date;VAR ICLastDocType@1003 : Integer;VAR GenJnlLine@1000 : Record 81;VAR TempGenJnlLine@1004 : TEMPORARY Record 81);
    VAR
      HandledICInboxTrans@1007 : Record 420;
    BEGIN
      WITH GenJnlLine DO
        IF (GenJnlTemplate.Type = GenJnlTemplate.Type::Intercompany) AND NOT EmptyLine AND
           (("Posting Date" <> ICLastDate) OR ("Document Type" <> ICLastDocType) OR ("Document No." <> ICLastDocNo))
        THEN BEGIN
          CurrentICPartner := '';
          ICLastDate := "Posting Date";
          ICLastDocType := "Document Type";
          ICLastDocNo := "Document No.";
          TempGenJnlLine.RESET;
          TempGenJnlLine.SETCURRENTKEY("Journal Template Name","Journal Batch Name","Posting Date","Document No.");
          TempGenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
          TempGenJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
          TempGenJnlLine.SETRANGE("Posting Date","Posting Date");
          TempGenJnlLine.SETRANGE("Document No.","Document No.");
          TempGenJnlLine.SETFILTER("IC Partner Code",'<>%1','');
          IF TempGenJnlLine.FINDFIRST AND (TempGenJnlLine."IC Partner Code" <> '') THEN BEGIN
            CurrentICPartner := TempGenJnlLine."IC Partner Code";
            IF TempGenJnlLine."IC Direction" = TempGenJnlLine."IC Direction"::Outgoing THEN
              ICTransactionNo := ICOutboxMgt.CreateOutboxJnlTransaction(TempGenJnlLine,FALSE)
            ELSE
              IF HandledICInboxTrans.GET(
                   TempGenJnlLine."IC Partner Transaction No.",TempGenJnlLine."IC Partner Code",
                   HandledICInboxTrans."Transaction Source"::"Created by Partner",TempGenJnlLine."Document Type")
              THEN BEGIN
                HandledICInboxTrans.LOCKTABLE;
                HandledICInboxTrans.Status := HandledICInboxTrans.Status::Posted;
                HandledICInboxTrans.MODIFY;
              END
          END
        END;
    END;

    LOCAL PROCEDURE CheckBalance@8(VAR GenJnlLine@1000 : Record 81);
    BEGIN
      OnBeforeCheckBalance(
        GenJnlTemplate,GenJnlLine,CurrentBalance,CurrentBalanceReverse,CurrencyBalance,
        StartLineNo,StartLineNoReverse,LastDocType,LastDocNo,LastDate,LastCurrencyCode);

      WITH GenJnlLine DO BEGIN
        IF CurrentBalance <> 0 THEN BEGIN
          GET("Journal Template Name","Journal Batch Name",StartLineNo);
          IF GenJnlTemplate."Force Doc. Balance" THEN
            ERROR(
              Text012 +
              Text013,
              CurrentBalance,LastDocNo,FIELDCAPTION("Posting Date"),FIELDCAPTION("Document Type"),
              FIELDCAPTION("Document No."),FIELDCAPTION(Amount));
          ERROR(
            Text014 +
            Text015,
            LastDate,CurrentBalance,FIELDCAPTION("Posting Date"),FIELDCAPTION(Amount));
        END;
        IF CurrentBalanceReverse <> 0 THEN BEGIN
          GET("Journal Template Name","Journal Batch Name",StartLineNoReverse);
          IF GenJnlTemplate."Force Doc. Balance" THEN
            ERROR(
              Text016 +
              Text017,
              CurrentBalanceReverse,LastDocNo,FIELDCAPTION("Recurring Method"),FIELDCAPTION("Document No."));
          ERROR(
            Text018 +
            Text017,
            LastDate,CurrentBalanceReverse,FIELDCAPTION("Recurring Method"),FIELDCAPTION("Posting Date"));
        END;
        IF (LastCurrencyCode <> '') AND (CurrencyBalance <> 0) THEN BEGIN
          GET("Journal Template Name","Journal Batch Name",StartLineNo);
          IF GenJnlTemplate."Force Doc. Balance" THEN
            ERROR(
              Text026 +
              Text013,
              CurrencyBalance,LastDocNo,FIELDCAPTION("Posting Date"),FIELDCAPTION("Document Type"),
              FIELDCAPTION("Document No."),FIELDCAPTION(Amount),
              LastCurrencyCode);
          ERROR(
            Text027 +
            Text015,
            LastDate,CurrencyBalance,FIELDCAPTION("Posting Date"),FIELDCAPTION(Amount),LastCurrencyCode);
        END;
      END;
    END;

    LOCAL PROCEDURE CheckAddExchRateBalance@9(GenJnlLine@1000 : Record 81);
    BEGIN
      WITH GenJnlLine DO
        IF CurrentBalance <> 0 THEN
          ERROR(
            Text019 +
            Text020,
            LastDocNo,FIELDCAPTION("FA Add.-Currency Factor"),FIELDCAPTION("Document No."));
    END;

    LOCAL PROCEDURE CheckRecurringLine@1(VAR GenJnlLine2@1000 : Record 81);
    VAR
      DummyDateFormula@1001 : DateFormula;
    BEGIN
      WITH GenJnlLine2 DO BEGIN
        IF "Account No." <> '' THEN
          IF GenJnlTemplate.Recurring THEN BEGIN
            TESTFIELD("Recurring Method");
            TESTFIELD("Recurring Frequency");
            IF "Bal. Account No." <> '' THEN
              FIELDERROR("Bal. Account No.",Text021);
            CASE "Recurring Method" OF
              "Recurring Method"::"V  Variable","Recurring Method"::"RV Reversing Variable",
              "Recurring Method"::"F  Fixed","Recurring Method"::"RF Reversing Fixed":
                TESTFIELD(Amount);
              "Recurring Method"::"B  Balance","Recurring Method"::"RB Reversing Balance":
                TESTFIELD(Amount,0);
            END;
          END ELSE BEGIN
            TESTFIELD("Recurring Method",0);
            TESTFIELD("Recurring Frequency",DummyDateFormula);
          END;
      END;
    END;

    LOCAL PROCEDURE UpdateRecurringAmt@2(VAR GenJnlLine2@1000 : Record 81) : Boolean;
    BEGIN
      WITH GenJnlLine2 DO BEGIN
        IF ("Account No." <> '') AND
           ("Recurring Method" IN
            ["Recurring Method"::"B  Balance","Recurring Method"::"RB Reversing Balance"])
        THEN BEGIN
          GLEntry.LOCKTABLE;
          IF "Account Type" = "Account Type"::"G/L Account" THEN BEGIN
            GLAcc."No." := "Account No.";
            GLAcc.SETRANGE("Date Filter",0D,"Posting Date");
            IF GLSetup."Additional Reporting Currency" <> '' THEN BEGIN
              "Source Currency Code" := GLSetup."Additional Reporting Currency";
              GLAcc.CALCFIELDS("Additional-Currency Net Change");
              "Source Currency Amount" := -GLAcc."Additional-Currency Net Change";
              GenJnlAlloc.UpdateAllocationsAddCurr(GenJnlLine2,"Source Currency Amount");
            END;
            GLAcc.CALCFIELDS("Net Change");
            VALIDATE(Amount,-GLAcc."Net Change");
            EXIT(TRUE);
          END;
          ERROR(Text022);
        END;
      END;
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE CheckAllocations@3(VAR GenJnlLine2@1000 : Record 81);
    BEGIN
      WITH GenJnlLine2 DO BEGIN
        IF "Account No." <> '' THEN BEGIN
          IF "Recurring Method" IN
             ["Recurring Method"::"B  Balance",
              "Recurring Method"::"RB Reversing Balance"]
          THEN BEGIN
            GenJnlAlloc.RESET;
            GenJnlAlloc.SETRANGE("Journal Template Name","Journal Template Name");
            GenJnlAlloc.SETRANGE("Journal Batch Name","Journal Batch Name");
            GenJnlAlloc.SETRANGE("Journal Line No.","Line No.");
            IF GenJnlAlloc.ISEMPTY THEN
              ERROR(
                Text028);
          END;

          GenJnlAlloc.RESET;
          GenJnlAlloc.SETRANGE("Journal Template Name","Journal Template Name");
          GenJnlAlloc.SETRANGE("Journal Batch Name","Journal Batch Name");
          GenJnlAlloc.SETRANGE("Journal Line No.","Line No.");
          GenJnlAlloc.SETFILTER(Amount,'<>0');
          IF NOT GenJnlAlloc.ISEMPTY THEN BEGIN
            IF NOT GenJnlTemplate.Recurring THEN
              ERROR(Text023);
            GenJnlAlloc.SETRANGE("Account No.",'');
            IF GenJnlAlloc.FINDFIRST THEN
              GenJnlAlloc.TESTFIELD("Account No.");
          END;
        END;
      END;
    END;

    LOCAL PROCEDURE MakeRecurringTexts@4(VAR GenJnlLine2@1000 : Record 81);
    BEGIN
      WITH GenJnlLine2 DO BEGIN
        IF ("Account No." <> '') AND ("Recurring Method" <> 0) THEN BEGIN
          Day := DATE2DMY("Posting Date",1);
          Week := DATE2DWY("Posting Date",2);
          Month := DATE2DMY("Posting Date",2);
          MonthText := FORMAT("Posting Date",0,Text024);
          AccountingPeriod.SETRANGE("Starting Date",0D,"Posting Date");
          IF NOT AccountingPeriod.FINDLAST THEN
            AccountingPeriod.Name := '';
          "Document No." :=
            DELCHR(
              PADSTR(
                STRSUBSTNO("Document No.",Day,Week,Month,MonthText,AccountingPeriod.Name),
                MAXSTRLEN("Document No.")),
              '>');
          Description :=
            DELCHR(
              PADSTR(
                STRSUBSTNO(Description,Day,Week,Month,MonthText,AccountingPeriod.Name),
                MAXSTRLEN(Description)),
              '>');
        END;
      END;
    END;

    LOCAL PROCEDURE PostAllocations@5(VAR AllocateGenJnlLine@1000 : Record 81;Reversing@1001 : Boolean);
    BEGIN
      WITH AllocateGenJnlLine DO
        IF "Account No." <> '' THEN BEGIN
          GenJnlAlloc.RESET;
          GenJnlAlloc.SETRANGE("Journal Template Name","Journal Template Name");
          GenJnlAlloc.SETRANGE("Journal Batch Name","Journal Batch Name");
          GenJnlAlloc.SETRANGE("Journal Line No.","Line No.");
          GenJnlAlloc.SETFILTER("Account No.",'<>%1','');
          IF GenJnlAlloc.FINDSET(TRUE,FALSE) THEN BEGIN
            GenJnlLine2.INIT;
            GenJnlLine2."Account Type" := GenJnlLine2."Account Type"::"G/L Account";
            GenJnlLine2."Posting Date" := "Posting Date";
            GenJnlLine2."Document Type" := "Document Type";
            GenJnlLine2."Document No." := "Document No.";
            GenJnlLine2.Description := Description;
            GenJnlLine2."Source Code" := "Source Code";
            GenJnlLine2."Journal Batch Name" := "Journal Batch Name";
            GenJnlLine2."Line No." := "Line No.";
            GenJnlLine2."Reason Code" := "Reason Code";
            GenJnlLine2.Correction := Correction;
            GenJnlLine2."Recurring Method" := "Recurring Method";
            IF "Account Type" IN ["Account Type"::Customer,"Account Type"::Vendor] THEN BEGIN
              GenJnlLine2."Bill-to/Pay-to No." := "Bill-to/Pay-to No.";
              GenJnlLine2."Ship-to/Order Address Code" := "Ship-to/Order Address Code";
            END;
            REPEAT
              GenJnlLine2.CopyFromGenJnlAllocation(GenJnlAlloc);
              GenJnlLine2."Shortcut Dimension 1 Code" := GenJnlAlloc."Shortcut Dimension 1 Code";
              GenJnlLine2."Shortcut Dimension 2 Code" := GenJnlAlloc."Shortcut Dimension 2 Code";
              GenJnlLine2."Dimension Set ID" := GenJnlAlloc."Dimension Set ID";
              GenJnlLine2."Allow Zero-Amount Posting" := TRUE;
              PrepareGenJnlLineAddCurr(GenJnlLine2);
              IF NOT Reversing THEN BEGIN
                GenJnlPostLine.RunWithCheck(GenJnlLine2);
                IF "Recurring Method" IN
                   ["Recurring Method"::"V  Variable","Recurring Method"::"B  Balance"]
                THEN BEGIN
                  GenJnlAlloc.Amount := 0;
                  GenJnlAlloc."Additional-Currency Amount" := 0;
                  GenJnlAlloc.MODIFY;
                END;
              END ELSE BEGIN
                MultiplyAmounts(GenJnlLine2,-1);
                GenJnlLine2."Reversing Entry" := TRUE;
                GenJnlPostLine.RunWithCheck(GenJnlLine2);
                IF "Recurring Method" IN
                   ["Recurring Method"::"RV Reversing Variable",
                    "Recurring Method"::"RB Reversing Balance"]
                THEN BEGIN
                  GenJnlAlloc.Amount := 0;
                  GenJnlAlloc."Additional-Currency Amount" := 0;
                  GenJnlAlloc.MODIFY;
                END;
              END;
            UNTIL GenJnlAlloc.NEXT = 0;
          END;
        END;

      OnAfterPostAllocations(AllocateGenJnlLine,Reversing);
    END;

    LOCAL PROCEDURE MultiplyAmounts@6(VAR GenJnlLine2@1000 : Record 81;Factor@1001 : Decimal);
    BEGIN
      WITH GenJnlLine2 DO
        IF "Account No." <> '' THEN BEGIN
          Amount := Amount * Factor;
          "Debit Amount" := "Debit Amount" * Factor;
          "Credit Amount" := "Credit Amount" * Factor;
          "Amount (LCY)" := "Amount (LCY)" * Factor;
          "Balance (LCY)" := "Balance (LCY)" * Factor;
          "Sales/Purch. (LCY)" := "Sales/Purch. (LCY)" * Factor;
          "Profit (LCY)" := "Profit (LCY)" * Factor;
          "Inv. Discount (LCY)" := "Inv. Discount (LCY)" * Factor;
          Quantity := Quantity * Factor;
          "VAT Amount" := "VAT Amount" * Factor;
          "VAT Base Amount" := "VAT Base Amount" * Factor;
          "VAT Amount (LCY)" := "VAT Amount (LCY)" * Factor;
          "VAT Base Amount (LCY)" := "VAT Base Amount (LCY)" * Factor;
          "Source Currency Amount" := "Source Currency Amount" * Factor;
          IF "Job No." <> '' THEN BEGIN
            "Job Quantity" := "Job Quantity" * Factor;
            "Job Total Cost (LCY)" := "Job Total Cost (LCY)" * Factor;
            "Job Total Price (LCY)" := "Job Total Price (LCY)" * Factor;
            "Job Line Amount (LCY)" := "Job Line Amount (LCY)" * Factor;
            "Job Total Cost" := "Job Total Cost" * Factor;
            "Job Total Price" := "Job Total Price" * Factor;
            "Job Line Amount" := "Job Line Amount" * Factor;
            "Job Line Discount Amount" := "Job Line Discount Amount" * Factor;
            "Job Line Disc. Amount (LCY)" := "Job Line Disc. Amount (LCY)" * Factor;
          END;
        END;

      OnAfterMultiplyAmounts(GenJnlLine2,Factor);
    END;

    LOCAL PROCEDURE CheckDocumentNo@11(VAR GenJnlLine2@1000 : Record 81);
    BEGIN
      WITH GenJnlLine2 DO BEGIN
        IF "Posting No. Series" = '' THEN
          "Posting No. Series" := GenJnlBatch."No. Series"
        ELSE
          IF NOT EmptyLine THEN
            IF "Document No." = LastDocNo THEN
              "Document No." := LastPostedDocNo
            ELSE BEGIN
              IF NOT NoSeries.GET("Posting No. Series") THEN BEGIN
                NoOfPostingNoSeries := NoOfPostingNoSeries + 1;
                IF NoOfPostingNoSeries > ARRAYLEN(NoSeriesMgt2) THEN
                  ERROR(
                    Text025,
                    ARRAYLEN(NoSeriesMgt2));
                NoSeries.Code := "Posting No. Series";
                NoSeries.Description := FORMAT(NoOfPostingNoSeries);
                NoSeries.INSERT;
              END;
              LastDocNo := "Document No.";
              EVALUATE(PostingNoSeriesNo,NoSeries.Description);
              "Document No." :=
                NoSeriesMgt2[PostingNoSeriesNo].GetNextNo("Posting No. Series","Posting Date",TRUE);
              LastPostedDocNo := "Document No.";
            END;
      END;
    END;

    LOCAL PROCEDURE PrepareGenJnlLineAddCurr@10(VAR GenJnlLine@1000 : Record 81);
    BEGIN
      IF (GLSetup."Additional Reporting Currency" <> '') AND
         (GenJnlLine."Recurring Method" IN
          [GenJnlLine."Recurring Method"::"B  Balance",
           GenJnlLine."Recurring Method"::"RB Reversing Balance"])
      THEN BEGIN
        GenJnlLine."Source Currency Code" := GLSetup."Additional Reporting Currency";
        IF (GenJnlLine.Amount = 0) AND
           (GenJnlLine."Source Currency Amount" <> 0)
        THEN BEGIN
          GenJnlLine."Additional-Currency Posting" :=
            GenJnlLine."Additional-Currency Posting"::"Additional-Currency Amount Only";
          GenJnlLine.Amount := GenJnlLine."Source Currency Amount";
          GenJnlLine."Source Currency Amount" := 0;
        END;
      END;
    END;

    LOCAL PROCEDURE CopyFields@12(VAR GenJnlLine@1004 : Record 81);
    VAR
      GenJnlLine4@1000 : Record 81;
      GenJnlLine6@1001 : Record 81;
      TempGenJnlLine@1007 : TEMPORARY Record 81;
      JnlLineTotalQty@1002 : Integer;
      RefPostingSubState@1003 : 'Check account,Check bal. account,Update lines';
    BEGIN
      GenJnlLine6.SETCURRENTKEY("Journal Template Name","Journal Batch Name","Posting Date","Document No.");
      GenJnlLine4.FILTERGROUP(2);
      GenJnlLine4.COPY(GenJnlLine);
      GenJnlLine4.FILTERGROUP(0);
      GenJnlLine6.FILTERGROUP(2);
      GenJnlLine6.COPY(GenJnlLine);
      GenJnlLine6.FILTERGROUP(0);
      GenJnlLine6.SETFILTER(
        "Account Type",'<>%1&<>%2',GenJnlLine6."Account Type"::Customer,GenJnlLine6."Account Type"::Vendor);
      GenJnlLine6.SETFILTER(
        "Bal. Account Type",'<>%1&<>%2',GenJnlLine6."Bal. Account Type"::Customer,GenJnlLine6."Bal. Account Type"::Vendor);
      GenJnlLine4.SETFILTER(
        "Account Type",'%1|%2',GenJnlLine4."Account Type"::Customer,GenJnlLine4."Account Type"::Vendor);
      GenJnlLine4.SETRANGE("Bal. Account No.",'');
      CheckAndCopyBalancingData(GenJnlLine4,GenJnlLine6,TempGenJnlLine,FALSE);

      GenJnlLine4.SETRANGE("Account Type");
      GenJnlLine4.SETRANGE("Bal. Account No.");
      GenJnlLine4.SETFILTER(
        "Bal. Account Type",'%1|%2',GenJnlLine4."Bal. Account Type"::Customer,GenJnlLine4."Bal. Account Type"::Vendor);
      GenJnlLine4.SETRANGE("Account No.",'');
      CheckAndCopyBalancingData(GenJnlLine4,GenJnlLine6,TempGenJnlLine,TRUE);

      JnlLineTotalQty := TempGenJnlLine.COUNT;
      LineCount := 0;
      IF TempGenJnlLine.FINDSET THEN
        REPEAT
          LineCount := LineCount + 1;
          UpdateDialogUpdateBalLines(RefPostingSubState::"Update lines",LineCount,JnlLineTotalQty);
          GenJnlLine4.GET(TempGenJnlLine."Journal Template Name",TempGenJnlLine."Journal Batch Name",TempGenJnlLine."Line No.");
          CopyGenJnlLineBalancingData(GenJnlLine4,TempGenJnlLine);
          GenJnlLine4.MODIFY;
        UNTIL TempGenJnlLine.NEXT = 0;
    END;

    LOCAL PROCEDURE CheckICDocument@13(VAR TempGenJnlLine1@1001 : TEMPORARY Record 81);
    VAR
      TempGenJnlLine2@1002 : TEMPORARY Record 81;
      CurrentICPartner@1000 : Code[20];
    BEGIN
      WITH TempGenJnlLine1 DO BEGIN
        SETCURRENTKEY("Journal Template Name","Journal Batch Name","Posting Date","Document No.");
        SETRANGE("Journal Template Name","Journal Template Name");
        SETRANGE("Journal Batch Name","Journal Batch Name");
        FIND('-');
        REPEAT
          IF ("Posting Date" <> LastDate) OR ("Document Type" <> LastDocType) OR ("Document No." <> LastDocNo) THEN BEGIN
            TempGenJnlLine2 := TempGenJnlLine1;
            SETRANGE("Posting Date","Posting Date");
            SETRANGE("Document No.","Document No.");
            SETFILTER("IC Partner Code",'<>%1','');
            IF FIND('-') THEN
              CurrentICPartner := "IC Partner Code"
            ELSE
              CurrentICPartner := '';
            SETRANGE("Posting Date");
            SETRANGE("Document No.");
            SETRANGE("IC Partner Code");
            LastDate := "Posting Date";
            LastDocType := "Document Type";
            LastDocNo := "Document No.";
            TempGenJnlLine1 := TempGenJnlLine2;
          END;
          IF (CurrentICPartner <> '') AND ("IC Direction" = "IC Direction"::Outgoing) THEN BEGIN
            IF ("Account Type" IN ["Account Type"::"G/L Account","Account Type"::"Bank Account"]) AND
               ("Bal. Account Type" IN ["Bal. Account Type"::"G/L Account","Account Type"::"Bank Account"]) AND
               ("Account No." <> '') AND
               ("Bal. Account No." <> '')
            THEN
              ERROR(Text030,FIELDCAPTION("Account No."),FIELDCAPTION("Bal. Account No."));
            IF (("Account Type" IN ["Account Type"::"G/L Account","Account Type"::"Bank Account"]) AND ("Account No." <> '')) XOR
               (("Bal. Account Type" IN ["Bal. Account Type"::"G/L Account","Account Type"::"Bank Account"]) AND
                ("Bal. Account No." <> ''))
            THEN
              TESTFIELD("IC Partner G/L Acc. No.")
            ELSE
              IF "IC Partner G/L Acc. No." <> '' THEN
                ERROR(Text031,
                  "Line No.",FIELDCAPTION("IC Partner G/L Acc. No."),FIELDCAPTION("Account No."),
                  FIELDCAPTION("Bal. Account No."));
          END ELSE
            TESTFIELD("IC Partner G/L Acc. No.",'');
        UNTIL NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE UpdateIncomingDocument@15(VAR GenJnlLine@1000 : Record 81);
    VAR
      IncomingDocument@1001 : Record 130;
    BEGIN
      WITH GenJnlLine DO
        IncomingDocument.UpdateIncomingDocumentFromPosting("Incoming Document Entry No.","Posting Date","Document No.");
    END;

    PROCEDURE CheckWHTCalculationRule@1500029(TotalInvoiceAmountLCY@1500000 : Decimal;WHTPostingSetup@1500001 : Record 50003;VAR GenJnlLine5@1500003 : Record 81);
    BEGIN
      GenJnlLine5."Skip WHT" :=
        NOT CompareAmounts(TotalInvoiceAmountLCY,WHTPostingSetup) AND
        (CompareAmounts(GenJnlLine5."Amount (LCY)",WHTPostingSetup) OR GLSetup."Min. WHT Calc only on Inv. Amt");
    END;

    PROCEDURE VendorMinWHT@1500002(VAR VendLedgEntry@1500000 : Record 25;VAR GenJnlLine5@1500002 : Record 81);
    BEGIN
      REPEAT
        TempVendLedgerEntry.INIT;
        TempVendLedgerEntry."Entry No." := VendLedgEntry."Entry No.";
        TempVendLedgerEntry."Document Type" := VendLedgEntry."Document Type";
        TempVendLedgerEntry."Document No." := VendLedgEntry."Document No.";
        TempVendLedgerEntry.INSERT;
        WHTEntry.RESET;
        WHTPostingSetup.RESET;
        TotWHT := 0;
        WHTEntry.SETCURRENTKEY("Document Type","Document No.");
        WHTEntry.SETRANGE("Document Type",VendLedgEntry."Document Type");
        WHTEntry.SETRANGE("Document No.",VendLedgEntry."Document No.");
        IF WHTEntry.FINDFIRST THEN BEGIN
          WHTEntry.CALCSUMS("Unrealized Base (LCY)");
          TotWHT := TotWHT + WHTEntry."Unrealized Base (LCY)";
        END;
        IF WHTPostingSetup.GET(WHTEntry."WHT Bus. Posting Group",WHTEntry."WHT Prod. Posting Group") THEN
          CheckWHTCalculationRule(TotWHT,WHTPostingSetup,GenJnlLine5);

        IF VendLedgEntry."Document Type" = VendLedgEntry."Document Type"::Invoice THEN BEGIN
          PurchInvLine.SETRANGE("Document No.",VendLedgEntry."Applies-to Doc. No.");
          IF PurchInvLine.FINDFIRST AND (PurchInvLine."Blanket Order No." <> '') THEN
            GenJnlLine5."Skip WHT" := FALSE;
        END;
        // IF (NOT GenJnlLine5."Skip WHT") THEN
        // EXIT;
      UNTIL VendLedgEntry.NEXT = 0;
    END;

    PROCEDURE CustomerMinWHT@1500000(VAR CustLedgEntry@1500000 : Record 21;VAR GenJnlLine5@1500002 : Record 81);
    BEGIN
      REPEAT
        TempCustLedgerEntry.INIT;
        TempCustLedgerEntry."Entry No." := CustLedgEntry."Entry No.";
        TempCustLedgerEntry."Document Type" := CustLedgEntry."Document Type";
        TempCustLedgerEntry."Document No." := CustLedgEntry."Document No.";
        TempCustLedgerEntry.INSERT;
        WHTEntry.RESET;
        WHTPostingSetup.RESET;
        TotWHT := 0;
        WHTEntry.SETCURRENTKEY("Document Type","Document No.");
        WHTEntry.SETRANGE("Document Type",CustLedgEntry."Document Type");
        WHTEntry.SETRANGE("Document No.",CustLedgEntry."Document No.");
        IF WHTEntry.FINDFIRST THEN BEGIN
          WHTEntry.CALCSUMS("Unrealized Base (LCY)");
          TotWHT := TotWHT + WHTEntry."Unrealized Base (LCY)";
        END;
        IF WHTPostingSetup.GET(WHTEntry."WHT Bus. Posting Group",WHTEntry."WHT Prod. Posting Group") THEN
          CheckWHTCalculationRule(TotWHT,WHTPostingSetup,GenJnlLine5);

        IF CustLedgEntry."Document Type" = CustLedgEntry."Document Type"::Invoice THEN BEGIN
          SalesInvLine.SETRANGE("Document No.",CustLedgEntry."Applies-to Doc. No.");
          IF SalesInvLine.FINDFIRST AND (SalesInvLine."Blanket Order No." <> '') THEN
            GenJnlLine5."Skip WHT" := FALSE;
        END;
        IF NOT GenJnlLine5."Skip WHT" THEN
          EXIT;
      UNTIL CustLedgEntry.NEXT = 0;
    END;

    PROCEDURE CompareAmounts@1500005(AmountLCY@1500000 : Decimal;WHTPostSetup@1500001 : Record 50003) : Boolean;
    BEGIN
      AmountLCY := ABS(AmountLCY);
      WITH WHTPostSetup DO
        CASE "WHT Calculation Rule" OF
          "WHT Calculation Rule"::"Less than":
            EXIT(AmountLCY >= "WHT Minimum Invoice Amount");
          "WHT Calculation Rule"::"Less than or equal to":
            EXIT(AmountLCY > "WHT Minimum Invoice Amount");
          "WHT Calculation Rule"::"Equal to":
            EXIT(AmountLCY <> "WHT Minimum Invoice Amount");
          "WHT Calculation Rule"::"Greater than":
            EXIT(AmountLCY <= "WHT Minimum Invoice Amount");
          "WHT Calculation Rule"::"Greater than or equal to":
            EXIT(AmountLCY < "WHT Minimum Invoice Amount");
        END;
    END;

    LOCAL PROCEDURE CopyGenJnlLineBalancingData@18(VAR GenJnlLineTo@1000 : Record 81;VAR GenJnlLineFrom@1002 : Record 81);
    BEGIN
      GenJnlLineTo."Bill-to/Pay-to No." := GenJnlLineFrom."Bill-to/Pay-to No.";
      GenJnlLineTo."Ship-to/Order Address Code" := GenJnlLineFrom."Ship-to/Order Address Code";
      GenJnlLineTo."VAT Registration No." := GenJnlLineFrom."VAT Registration No.";
      GenJnlLineTo."Country/Region Code" := GenJnlLineFrom."Country/Region Code";
    END;

    LOCAL PROCEDURE CheckGenPostingType@19(GenJnlLine6@1000 : Record 81;AccountType@1001 : 'G/L Account,Customer,Vendor,Bank Account,Fixed Asset,IC Partner');
    BEGIN
      IF (AccountType = AccountType::Customer) AND
         (GenJnlLine6."Gen. Posting Type" = GenJnlLine6."Gen. Posting Type"::Purchase) OR
         (AccountType = AccountType::Vendor) AND
         (GenJnlLine6."Gen. Posting Type" = GenJnlLine6."Gen. Posting Type"::Sale)
      THEN
        GenJnlLine6.FIELDERROR("Gen. Posting Type");
      IF (AccountType = AccountType::Customer) AND
         (GenJnlLine6."Bal. Gen. Posting Type" = GenJnlLine6."Bal. Gen. Posting Type"::Purchase) OR
         (AccountType = AccountType::Vendor) AND
         (GenJnlLine6."Bal. Gen. Posting Type" = GenJnlLine6."Bal. Gen. Posting Type"::Sale)
      THEN
        GenJnlLine6.FIELDERROR("Bal. Gen. Posting Type");
    END;

    LOCAL PROCEDURE CheckAndCopyBalancingData@16(VAR GenJnlLine4@1002 : Record 81;VAR GenJnlLine6@1001 : Record 81;VAR TempGenJnlLine@1004 : TEMPORARY Record 81;CheckBalAcount@1003 : Boolean);
    VAR
      TempGenJournalLineHistory@1006 : TEMPORARY Record 81;
      AccountType@1005 : 'G/L Account,Customer,Vendor,Bank Account,Fixed Asset,IC Partner';
      CheckAmount@1000 : Decimal;
      JnlLineTotalQty@1007 : Integer;
      RefPostingSubState@1009 : 'Check account,Check bal. account,Update lines';
      LinesFound@1008 : Boolean;
    BEGIN
      JnlLineTotalQty := GenJnlLine4.COUNT;
      LineCount := 0;
      IF CheckBalAcount THEN
        RefPostingSubState := RefPostingSubState::"Check bal. account"
      ELSE
        RefPostingSubState := RefPostingSubState::"Check account";
      IF GenJnlLine4.FINDSET THEN
        REPEAT
          LineCount := LineCount + 1;
          UpdateDialogUpdateBalLines(RefPostingSubState,LineCount,JnlLineTotalQty);
          TempGenJournalLineHistory.SETRANGE("Posting Date",GenJnlLine4."Posting Date");
          TempGenJournalLineHistory.SETRANGE("Document No.",GenJnlLine4."Document No.");
          IF TempGenJournalLineHistory.ISEMPTY THEN BEGIN
            TempGenJournalLineHistory := GenJnlLine4;
            TempGenJournalLineHistory.INSERT;
            GenJnlLine6.SETRANGE("Posting Date",GenJnlLine4."Posting Date");
            GenJnlLine6.SETRANGE("Document No.",GenJnlLine4."Document No.");
            LinesFound := GenJnlLine6.FINDSET;
          END;
          IF LinesFound THEN BEGIN
            AccountType := GetPostingTypeFilter(GenJnlLine4,CheckBalAcount);
            CheckAmount := 0;
            REPEAT
              IF (GenJnlLine6."Account No." = '') <> (GenJnlLine6."Bal. Account No." = '') THEN BEGIN
                CheckGenPostingType(GenJnlLine6,AccountType);
                IF GenJnlLine6."Bill-to/Pay-to No." = '' THEN BEGIN
                  TempGenJnlLine := GenJnlLine6;
                  CopyGenJnlLineBalancingData(TempGenJnlLine,GenJnlLine4);
                  IF TempGenJnlLine.INSERT THEN;
                END;
                CheckAmount := CheckAmount + GenJnlLine6.Amount;
              END;
              LinesFound := (GenJnlLine6.NEXT <> 0);
            UNTIL NOT LinesFound OR (-GenJnlLine4.Amount = CheckAmount);
          END;
        UNTIL GenJnlLine4.NEXT = 0;
    END;

    LOCAL PROCEDURE UpdateGenJnlLineWithVATInfo@26(VAR GenJournalLine@1005 : Record 81;GenJournalLineVATInfoSource@1000 : Record 81;StartLineNo@1003 : Integer;LastLineNo@1004 : Integer);
    VAR
      GenJournalLineCopy@1001 : Record 81;
      Finish@1002 : Boolean;
      OldLineNo@1007 : Integer;
    BEGIN
      OldLineNo := GenJournalLine."Line No.";
      WITH GenJournalLine DO BEGIN
        "Line No." := StartLineNo;
        Finish := FALSE;
        IF GET("Journal Template Name","Journal Batch Name","Line No.") THEN
          REPEAT
            IF "Line No." <> GenJournalLineVATInfoSource."Line No." THEN BEGIN
              "Bill-to/Pay-to No." := GenJournalLineVATInfoSource."Bill-to/Pay-to No.";
              "Country/Region Code" := GenJournalLineVATInfoSource."Country/Region Code";
              "VAT Registration No." := GenJournalLineVATInfoSource."VAT Registration No.";
              MODIFY;
              IF ISTEMPORARY THEN BEGIN
                GenJournalLineCopy.GET("Journal Template Name","Journal Batch Name","Line No.");
                GenJournalLineCopy."Bill-to/Pay-to No." := "Bill-to/Pay-to No.";
                GenJournalLineCopy."Country/Region Code" := "Country/Region Code";
                GenJournalLineCopy."VAT Registration No." := "VAT Registration No.";
                GenJournalLineCopy.MODIFY;
              END;
            END;
            Finish := "Line No." = LastLineNo;
          UNTIL (NEXT = 0) OR Finish;

        IF GET("Journal Template Name","Journal Batch Name",OldLineNo) THEN;
      END;
    END;

    LOCAL PROCEDURE GetPostingTypeFilter@17(VAR GenJnlLine4@1002 : Record 81;CheckBalAcount@1000 : Boolean) : Integer;
    BEGIN
      IF CheckBalAcount THEN
        EXIT(GenJnlLine4."Bal. Account Type");
      EXIT(GenJnlLine4."Account Type");
    END;

    LOCAL PROCEDURE UpdateDialog@23(PostingState@1000 : Integer;LineNo@1001 : Integer;TotalLinesQty@1002 : Integer);
    BEGIN
      UpdatePostingState(PostingState,LineNo);
      Window.UPDATE(2,GetProgressBarValue(PostingState,LineNo,TotalLinesQty));
    END;

    LOCAL PROCEDURE UpdateDialogUpdateBalLines@22(PostingSubState@1003 : Integer;LineNo@1001 : Integer;TotalLinesQty@1002 : Integer);
    BEGIN
      UpdatePostingState(RefPostingState::"Updating bal. lines",LineNo);
      Window.UPDATE(
        2,
        GetProgressBarUpdateBalLinesValue(
          CalcProgressPercent(PostingSubState,3,LineCount,TotalLinesQty)));
    END;

    LOCAL PROCEDURE UpdatePostingState@25(PostingState@1000 : Integer;LineNo@1002 : Integer);
    BEGIN
      Window.UPDATE(3,STRSUBSTNO('%1 (%2)',GetPostingStateMsg(PostingState),LineNo));
    END;

    LOCAL PROCEDURE UpdateCurrencyBalanceForRecurringLine@44(VAR GenJnlLine@1000 : Record 81);
    BEGIN
      WITH GenJnlLine DO BEGIN
        IF "Recurring Method" <> "Recurring Method"::" " THEN
          CALCFIELDS("Allocated Amt. (LCY)");
        IF ("Recurring Method" = "Recurring Method"::" ") OR ("Amount (LCY)" <> -"Allocated Amt. (LCY)") THEN
          IF "Currency Code" <> LastCurrencyCode THEN
            LastCurrencyCode := ''
          ELSE
            IF ("Currency Code" <> '') AND (("Account No." = '') XOR ("Bal. Account No." = '')) THEN
              IF "Account No." <> '' THEN
                CurrencyBalance := CurrencyBalance + Amount
              ELSE
                CurrencyBalance := CurrencyBalance - Amount;
      END;
    END;

    LOCAL PROCEDURE GetPostingStateMsg@29(PostingState@1000 : Integer) : Text;
    BEGIN
      CASE PostingState OF
        RefPostingState::"Checking lines":
          EXIT(CheckingLinesMsg);
        RefPostingState::"Checking balance":
          EXIT(CheckingBalanceMsg);
        RefPostingState::"Updating bal. lines":
          EXIT(UpdatingBalLinesMsg);
        RefPostingState::"Posting Lines":
          EXIT(PostingLinesMsg);
        RefPostingState::"Posting revers. lines":
          EXIT(PostingReversLinesMsg);
        RefPostingState::"Updating lines":
          EXIT(UpdatingLinesMsg);
      END;
    END;

    LOCAL PROCEDURE GetProgressBarValue@21(PostingState@1002 : Integer;LineNo@1001 : Integer;TotalLinesQty@1000 : Integer) : Integer;
    BEGIN
      EXIT(ROUND(100 * CalcProgressPercent(PostingState,GetNumberOfPostingStages,LineNo,TotalLinesQty),1));
    END;

    LOCAL PROCEDURE GetProgressBarUpdateBalLinesValue@34(PostingStatePercent@1000 : Decimal) : Integer;
    BEGIN
      EXIT(ROUND((RefPostingState::"Updating bal. lines" * 100 + PostingStatePercent) / GetNumberOfPostingStages * 100,1));
    END;

    LOCAL PROCEDURE CalcProgressPercent@20(PostingState@1001 : Integer;NumberOfPostingStates@1000 : Integer;LineNo@1002 : Integer;TotalLinesQty@1003 : Integer) : Decimal;
    BEGIN
      EXIT(100 / NumberOfPostingStates * (PostingState + LineNo / TotalLinesQty));
    END;

    LOCAL PROCEDURE GetNumberOfPostingStages@33() : Integer;
    BEGIN
      IF GenJnlTemplate.Recurring THEN
        EXIT(6);

      EXIT(4);
    END;

    LOCAL PROCEDURE FindNextGLRegisterNo@24();
    BEGIN
      GLReg.LOCKTABLE;
      IF Journal.Simulation THEN BEGIN
        IF GLReg.FINDFIRST THEN BEGIN
          GLRegNo := GLReg."No." - 1;
          IF GLRegNo = 0 THEN
            GLRegNo := -1;
        END ELSE
          GLRegNo := -1;
      END ELSE BEGIN
        IF GLReg.FINDLAST THEN
          GLRegNo := GLReg."No." + 1
        ELSE
          GLRegNo := 1;
      END;
    END;

    LOCAL PROCEDURE CheckGenJnlLineDates@36(VAR MarkedGenJnlLine@1001 : Record 81;VAR GenJournalLine@1000 : Record 81);
    BEGIN
      WITH GenJournalLine DO BEGIN
        IF NOT FIND THEN
          FINDSET;
        SETRANGE("Posting Date",0D,WORKDATE);
        IF FINDSET THEN BEGIN
          StartLineNo := "Line No.";
          REPEAT
            IF IsNotExpired(GenJournalLine) AND IsPostingDateAllowed(GenJournalLine) THEN BEGIN
              MarkedGenJnlLine := GenJournalLine;
              MarkedGenJnlLine.INSERT;
            END;
            IF NEXT = 0 THEN
              FINDFIRST;
          UNTIL "Line No." = StartLineNo
        END;
        MarkedGenJnlLine := GenJournalLine;
      END;
    END;

    LOCAL PROCEDURE IsNotExpired@50(GenJournalLine@1000 : Record 81) : Boolean;
    BEGIN
      EXIT((GenJournalLine."Expiration Date" = 0D) OR (GenJournalLine."Expiration Date" >= GenJournalLine."Posting Date"));
    END;

    LOCAL PROCEDURE IsPostingDateAllowed@48(GenJournalLine@1000 : Record 81) : Boolean;
    BEGIN
      EXIT(NOT GenJnlCheckLine.DateNotAllowed(GenJournalLine."Posting Date"));
    END;

    [External]
    PROCEDURE SetPreviewMode@27(NewPreviewMode@1000 : Boolean);
    BEGIN
      PreviewMode := NewPreviewMode;
    END;

    LOCAL PROCEDURE PostReversingLines@28(VAR TempGenJnlLine@1000 : TEMPORARY Record 81);
    VAR
      GenJournalLine1@1001 : Record 81;
      GenJournalLine2@1002 : Record 81;
    BEGIN
      LineCount := 0;
      LastDocNo := '';
      LastPostedDocNo := '';
      IF TempGenJnlLine.FIND('-') THEN
        REPEAT
          GenJournalLine1 := TempGenJnlLine;
          WITH GenJournalLine1 DO BEGIN
            LineCount := LineCount + 1;
            UpdateDialog(RefPostingState::"Posting revers. lines",LineCount,NoOfReversingRecords);
            CheckDocumentNo(GenJournalLine1);
            GenJournalLine2.COPY(GenJournalLine1);
            PrepareGenJnlLineAddCurr(GenJournalLine2);
            GenJnlPostLine.RunWithCheck(GenJournalLine2);
            PostAllocations(GenJournalLine1,TRUE);
          END;
        UNTIL TempGenJnlLine.NEXT = 0;
    END;

    LOCAL PROCEDURE UpdateAndDeleteLines@31(VAR GenJnlLine@1003 : Record 81);
    VAR
      TempGenJnlLine2@1002 : TEMPORARY Record 81;
      OldVATAmount@1000 : Decimal;
      OldVATPct@1001 : Decimal;
    BEGIN
      OnBeforeUpdateAndDeleteLines(GenJnlLine);

      ClearDataExchEntries(GenJnlLine);
      IF GenJnlTemplate.Recurring THEN BEGIN
        // Recurring journal
        LineCount := 0;
        GenJnlLine2.COPY(GenJnlLine);
        GenJnlLine2.SETCURRENTKEY("Journal Template Name","Journal Batch Name","Line No.");
        GenJnlLine2.FINDSET(TRUE,FALSE);
        REPEAT
          LineCount := LineCount + 1;
          UpdateDialog(RefPostingState::"Updating lines",LineCount,NoOfRecords);
          OldVATAmount := GenJnlLine2."VAT Amount";
          OldVATPct := GenJnlLine2."VAT %";
          IF GenJnlLine2."Posting Date" <> 0D THEN
            GenJnlLine2.VALIDATE(
              "Posting Date",CALCDATE(GenJnlLine2."Recurring Frequency",GenJnlLine2."Posting Date"));
          IF NOT
             (GenJnlLine2."Recurring Method" IN
              [GenJnlLine2."Recurring Method"::"F  Fixed",
               GenJnlLine2."Recurring Method"::"RF Reversing Fixed"])
          THEN
            MultiplyAmounts(GenJnlLine2,0)
          ELSE
            IF (GenJnlLine2."VAT %" = OldVATPct) AND (GenJnlLine2."VAT Amount" <> OldVATAmount) THEN
              GenJnlLine2.VALIDATE("VAT Amount",OldVATAmount);
          GenJnlLine2.MODIFY;
        UNTIL GenJnlLine2.NEXT = 0;
      END ELSE BEGIN
        // Not a recurring journal
        GenJnlLine2.COPY(GenJnlLine);
        GenJnlLine2.SETFILTER("Account No.",'<>%1','');
        IF GenJnlLine2.FINDLAST THEN; // Remember the last line
        GenJnlLine3.COPY(GenJnlLine);
        GenJnlLine3.SETCURRENTKEY("Journal Template Name","Journal Batch Name","Line No.");
        GenJnlLine3.DELETEALL;
        GenJnlLine3.RESET;
        GenJnlLine3.SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
        GenJnlLine3.SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
        IF NOT GenJnlLine3.FINDLAST THEN
          IF INCSTR(GenJnlLine."Journal Batch Name") <> '' THEN BEGIN
            GenJnlBatch.DELETE;
            IF GenJnlTemplate.Type = GenJnlTemplate.Type::Assets THEN
              FAJnlSetup.IncGenJnlBatchName(GenJnlBatch);
            GenJnlBatch.Name := INCSTR(GenJnlLine."Journal Batch Name");
            IF GenJnlBatch.INSERT THEN;
            GenJnlLine."Journal Batch Name" := GenJnlBatch.Name;
          END;

        GenJnlLine3.SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
        IF (GenJnlBatch."No. Series" = '') AND NOT GenJnlLine3.FINDLAST THEN BEGIN
          GenJnlLine3.INIT;
          GenJnlLine3."Journal Template Name" := GenJnlLine."Journal Template Name";
          GenJnlLine3."Journal Batch Name" := GenJnlLine."Journal Batch Name";
          GenJnlLine3."Line No." := 10000;
          GenJnlLine3.INSERT;
          TempGenJnlLine2 := GenJnlLine2;
          TempGenJnlLine2."Balance (LCY)" := 0;
          GenJnlLine3.SetUpNewLine(TempGenJnlLine2,0,TRUE);
          GenJnlLine3.MODIFY;
        END;
      END;
    END;

    [Internal]
    PROCEDURE Preview@32(VAR GenJournalLine@1000 : Record 81);
    VAR
      GenJnlLine@1001 : Record 81;
    BEGIN
      PreviewMode := TRUE;
      GenJnlLine.COPY(GenJournalLine);
      Code(GenJnlLine);
    END;

    LOCAL PROCEDURE CheckRestrictions@35(VAR GenJournalLine@1000 : Record 81);
    BEGIN
      IF NOT PreviewMode THEN
        GenJournalLine.OnCheckGenJournalLinePostRestrictions;
    END;

    LOCAL PROCEDURE ClearDataExchEntries@37(VAR PassedGenJnlLine@1000 : Record 81);
    VAR
      GenJnlLine@1001 : Record 81;
    BEGIN
      GenJnlLine.COPY(PassedGenJnlLine);
      IF GenJnlLine.FINDSET THEN
        REPEAT
          GenJnlLine.ClearDataExchangeEntries(TRUE);
        UNTIL GenJnlLine.NEXT = 0;
    END;

    LOCAL PROCEDURE PostGenJournalLine@39(VAR GenJournalLine@1000 : Record 81;CurrGenJnlLine@1450000 : Record 81;CurrentICPartner@1001 : Code[20];ICTransactionNo@1002 : Integer) : Boolean;
    BEGIN
      WITH GenJournalLine DO BEGIN
        IF NeedCheckZeroAmount AND (Amount = 0) AND IsRecurring THEN
          EXIT(FALSE);

        LineCount := LineCount + 1;
        IF CurrentICPartner <> '' THEN
          "IC Partner Code" := CurrentICPartner;
        UpdateDialog(RefPostingState::"Posting Lines",LineCount,NoOfRecords);
        MakeRecurringTexts(GenJournalLine);
        CheckDocumentNo(GenJournalLine);
        GenJnlLine5.COPY(GenJournalLine);
        PrepareGenJnlLineAddCurr(GenJnlLine5);
        // Update/delete lines
        IF GLSetup."Enable WHT" AND (NOT GenJnlLine5."Skip WHT") THEN
          IF GenJnlLine5."Applies-to Doc. No." <> '' THEN BEGIN
            WHTEntry.SETCURRENTKEY("Document Type","Document No.");
            WHTEntry.SETRANGE("Document No.",GenJnlLine5."Applies-to Doc. No.");
            WHTEntry.SETRANGE("Document Type",GenJnlLine5."Applies-to Doc. Type");
            IF WHTEntry.FINDFIRST THEN
              IF WHTPostingSetup.GET(WHTEntry."WHT Bus. Posting Group",WHTEntry."WHT Prod. Posting Group") THEN BEGIN
                WHTEntry.CALCSUMS("Unrealized Base (LCY)");
                CheckWHTCalculationRule(WHTEntry."Unrealized Base (LCY)",WHTPostingSetup,GenJnlLine5);
              END;
            IF GenJnlLine5."Applies-to Doc. Type" = GenJnlLine5."Applies-to Doc. Type"::Invoice THEN BEGIN
              PurchInvLine.SETRANGE("Document No.",GenJnlLine5."Applies-to Doc. No.");
              IF PurchInvLine.FINDFIRST THEN
                IF PurchInvLine."Blanket Order No." <> '' THEN
                  GenJnlLine5."Skip WHT" := FALSE;
            END;
          END ELSE
            IF GenJnlLine5."Applies-to ID" <> '' THEN
              CASE GenJnlLine5."Account Type" OF
                GenJnlLine5."Account Type"::Customer:
                  BEGIN
                    TotWHT := 0;
                    CustLedgEntry.SETRANGE("Applies-to ID",CurrGenJnlLine."Document No.");
                    IF CustLedgEntry.FINDFIRST THEN
                      CustomerMinWHT(CustLedgEntry,GenJnlLine5);
                  END;
                GenJnlLine5."Account Type"::Vendor:
                  BEGIN
                    TotWHT := 0;
                    VendLedgEntry.SETRANGE("Applies-to ID",CurrGenJnlLine."Document No.");
                    IF VendLedgEntry.FINDFIRST THEN
                      VendorMinWHT(VendLedgEntry,GenJnlLine5);
                  END;
                GenJnlLine5."Account Type"::Vendor:
                  BEGIN
                    IF Counter = 0 THEN BEGIN
                      IF TempCustLedgerEntry.FINDFIRST THEN
                        Counter := 1;
                      IF TempVendLedgerEntry.FINDFIRST THEN
                        Counter := 2;
                    END;
                    WHTAmt := 0;
                  END;
              END;

        UpdateIncomingDocument(GenJnlLine5);
        OnBeforePostGenJnlLine(GenJnlLine5);
        GenJnlPostLine.RunWithoutCheck(GenJnlLine5);
        OnAfterPostGenJnlLine(GenJnlLine5);
        IF (GenJnlTemplate.Type = GenJnlTemplate.Type::Intercompany) AND (CurrentICPartner <> '') AND
           ("IC Direction" = "IC Direction"::Outgoing) AND (ICTransactionNo > 0)
        THEN
          ICOutboxMgt.CreateOutboxJnlLine(ICTransactionNo,1,GenJnlLine5);
        IF ("Recurring Method" >= "Recurring Method"::"RF Reversing Fixed") AND ("Posting Date" <> 0D) THEN BEGIN
          "Posting Date" := "Posting Date" + 1;
          "Document Date" := "Posting Date";
          MultiplyAmounts(GenJournalLine,-1);
          TempGenJnlLine4 := GenJournalLine;
          TempGenJnlLine4."Reversing Entry" := TRUE;
          TempGenJnlLine4.INSERT;
          NoOfReversingRecords := NoOfReversingRecords + 1;
          "Posting Date" := "Posting Date" - 1;
          "Document Date" := "Posting Date";
        END;
        PostAllocations(GenJournalLine,FALSE);
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE IsWHTPaymentPosting@1500001(VAR GenJournalLine@1500000 : Record 81) : Boolean;
    VAR
      GenJournalLineWHT@1500001 : Record 81;
      WHTPostingSetup@1500002 : Record 50003;
    BEGIN
      IF NOT GLSetup."Enable WHT" THEN
        EXIT(FALSE);

      GenJournalLineWHT.COPY(GenJournalLine);
      GenJournalLineWHT.SETRANGE("Document Type",GenJournalLine."Document Type"::Payment);
      GenJournalLineWHT.SETRANGE("Skip WHT",FALSE);
      GenJournalLineWHT.SETFILTER("Applies-to Doc. No.",'<>%1','');
      IF GenJournalLineWHT.FINDSET THEN
        REPEAT
          IF WHTPostingSetup.GET(GenJournalLineWHT."WHT Business Posting Group",GenJournalLineWHT."WHT Product Posting Group") AND
             (WHTPostingSetup."Realized WHT Type" = WHTPostingSetup."Realized WHT Type"::Payment)
          THEN
            EXIT(TRUE);
        UNTIL GenJournalLineWHT.NEXT = 0;

      EXIT(FALSE);
    END;

    LOCAL PROCEDURE CheckLine@38(VAR GenJnlLine@1000 : Record 81;VAR PostingAfterCurrentFiscalYearConfirmed@1002 : Boolean);
    VAR
      GenJournalLineToUpdate@1001 : Record 81;
      IsModified@1003 : Boolean;
    BEGIN
      GenJournalLineToUpdate.COPY(GenJnlLine);
      CheckRecurringLine(GenJournalLineToUpdate);
      IsModified := UpdateRecurringAmt(GenJournalLineToUpdate);
      CheckAllocations(GenJournalLineToUpdate);
      GenJnlLine5.COPY(GenJournalLineToUpdate);
      IF NOT PostingAfterCurrentFiscalYearConfirmed THEN
        PostingAfterCurrentFiscalYearConfirmed :=
          PostingSetupMgt.ConfirmPostingAfterCurrentCalendarDate(
            ConfirmPostingAfterCurrentPeriodQst,GenJnlLine5."Posting Date");
      PrepareGenJnlLineAddCurr(GenJnlLine5);
      GenJnlCheckLine.RunCheck(GenJnlLine5);
      CheckRestrictions(GenJnlLine5);
      GenJnlLine.COPY(GenJournalLineToUpdate);
      IF IsModified THEN
        GenJnlLine.MODIFY;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPostGenJnlLine@41(VAR GenJournalLine@1000 : Record 81);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCheckBalance@51(GenJnlTemplate@1000 : Record 80;GenJnlLine@1001 : Record 81;CurrentBalance@1002 : Decimal;CurrentBalanceReverse@1003 : Decimal;CurrencyBalance@1004 : Decimal;StartLineNo@1005 : Integer;StartLineNoReverse@1006 : Integer;LastDocType@1010 : Option;LastDocNo@1007 : Code[20];LastDate@1008 : Date;LastCurrencyCode@1009 : Code[10]);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeIfCheckBalance@54(GenJnlTemplate@1004 : Record 80;GenJnlLine@1003 : Record 81;LastDocType@1005 : Option;LastDocNo@1002 : Code[20];LastDate@1001 : Date;VAR CheckIfBalance@1000 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostGenJnlLine@40(VAR GenJournalLine@1000 : Record 81);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeUpdateAndDeleteLines@47(VAR GenJournalLine@1000 : Record 81);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPostAllocations@46(GenJournalLine@1000 : Record 81;Reversing@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterMultiplyAmounts@45(VAR GenJournalLine@1000 : Record 81;Factor@1001 : Decimal);
    BEGIN
    END;

    BEGIN
    END.
  }
}

